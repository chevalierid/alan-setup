*$
* TPS57114C-Q1
*****************************************************************************
* (C) Copyright 2020 Texas Instruments Incorporated. All rights reserved.                                            
*****************************************************************************
** This model is designed as an aid for customers of Texas Instruments.
** TI and its licensors and suppliers make no warranties, either expressed
** or implied, with respect to this model, including the warranties of 
** merchantability or fitness for a particular purpose.  The model is
** provided solely on an "as is" basis.  The entire risk as to its quality
** and performance is with the customer
*****************************************************************************
*
* This model is subject to change without notice. Texas Instruments
* Incorporated is not responsible for updating this model.
*
*****************************************************************************
*
** Released by: Texas Instruments Inc.
* Part: TPS57114C-Q1
* Date: 11DEC2020
* Model Type: AVERAGE
* Simulator: PSPICE 
* Simulator Version: 17.4-2019 S012
* EVM Order Number: TPS57114EVM
* EVM Users Guide: SLVU963–November 2013
* Datasheet: SLVSDQ7A –OCTOBER 2016–REVISED DECEMBER 2019
* Topologies Supported: Buck
*
* Model Version: Final 1.00
*
*****************************************************************************
*
* Updates:
*
* Final 1.00
* Release to Web
*
*****************************************************************************
*
* Model Usage Notes:
* A. Features modelled
*	1. Average loop implementaion of peak current mode control.
*   2. Only VIN,PH,COMP and VSENSE pins are modelled.
*
* B. Features not modelled
*	1. Device does not support PFM cases.
*   2. Temperature dependent characteristics.
*	3. Operating Quiescent Current.
*   4. Shutdown Current.
*
* C. Application Notes
*   1. Following parameter is added in the model
*      a. The parameter "L" is used to pass the inductor value in the model.
*      b. The parameter "FS" is used to pass the switching frequency value in the model.
*
*****************************************************************************
.SUBCKT TPS57114C-Q1_AVG VIN_0 VIN_1 GND_0 GND_1 AGND VSENSE COMP RT_CLK SS_TR 
+ PH_0 PH_1 PH_2 BOOT PWRGD EN VIN_2 GND_2 PARAMS: L=0.75u FS=1000K
R_R19         AGND COMP  2.3e6
C_C13         AGND COMP  8e-12
V_V28         N1165059 AGND 0.8
G_G2         AGND COMP N1165059 VSENSE  2.45E-04
X_U2         VIN_0 PH_0 AGND COMP N1165537 CCM PARAMS:  L={L} FS={FS} Ri=40e-3
+ Se=8.25 Acs=1            
.ENDS TPS57114C-Q1_AVG
*$
.SUBCKT CCM a c p vc dc params: L=4.7u FS=500k Ri=3.89E-02 Se=8.56 Acs=1 
ESe Sa 0 value = {{Se}*{Fs}*{Ri}}
Edc dcx 0 value = { v(cx,p)/(v(a,p)+1u) }
Xdc dcx dc limit params: clampH={1-{60n*{FS}}} clampL={75n*{FS}}
Ed2 d2X 0 value = {(1 - V(dc))} 
Xd2 d2X dc d2 limit2
GIap a p value =  {V(dc)*I(VM)}
GIpc p cx value = { V(vc)/({Acs}*{Ri}) }
GImju cx p value = {V(Sa)*V(dc)/({Acs}*{Ri}*{Fs})+(v(cx,p))/({L}*{Fs})*((1-V(dc))/2) }
XC1 cx p mode varicap 
Emode mode 0 Value = { IF ((2*{L}*{Fs}*I(VM)/(V(dc)*V(a,cx)+1u))
+ < 1, {4/({L}*(6.28*{Fs})**2)}, {4/({L}*(6.28*{Fs})**2)}) } 
VM cx c
.ENDS
*$
.SUBCKT VARICAP 1 2 CTRL
R1 1 3 1u
VC 3 4
EBC 4 2 Value = { (1/v(ctrl))*v(int) }
GINT 0 INT Value = { I(VC) }
CINT INT 0 1
Rdum INT 0 10E10
.ENDS
*$
.subckt limit d dc params: clampH=0.99 clampL=48m
Gd 0 dcx VALUE = { V(d)*100u }
Rdc dcx 0 10k
V1 clpn 0 {clampL}
V2 clpp 0 {clampH}
D1 clpn dcx dclamp
D2 dcx clpp dclamp
Edc dc 0 value={ V(dcx) }
.model dclamp d n=0.01 rs=100m
.ENDS
*$
.subckt limit2 d2nc d d2c
Gd 0 d2cx d2nc 0 100u
Rdc d2cx 0 10k
V1 clpn 0 7m
E2 clpp 0 Value = { 1-V(d) }
D1 clpn d2cx dclamp
D2 d2cx clpp dclamp
Edc d2c 0 value={ V(d2cx) }
.model dclamp d n=0.01 rs=100m
.ENDS
*$
.SUBCKT LDCR IN OUT
+ PARAMs:  L=1u DCR=0.01 
L             IN 1  {L} 
RDCR	1 OUT {DCR}
.ENDS LDCR
*$
.SUBCKT CESR IN OUT
+ PARAMs:  C=100u ESR=0.01 X=2 
C	IN 1  {C*X} 
RESR	1 OUT {ESR/X}
.ENDS CESR
*$