* LMH6523
*****************************************************************************
* (C) Copyright 2011 Texas Instruments Incorporated. All rights reserved.                                            
*****************************************************************************
** This model is designed as an aid for customers of Texas Instruments.
** TI and its licensors and suppliers make no warranties, either expressed
** or implied, with respect to this model, including the warranties of 
** merchantability or fitness for a particular purpose.  The model is
** provided solely on an "as is" basis.  The entire risk as to its quality
** and performance is with the customer.
*****************************************************************************
*
** Released by: WEBENCH Design Center, Texas Instruments Inc.
* Part: LMH6523
* Date: 1/15/13
* Model Type: All In One
* Simulator: TINA-TI
* Simulator Version: 9.3.50.40
* EVM Order Number: N/A 
* EVM Users Guide: N/A
* Datasheet:
*
* Model Version: 1.0
*
*****************************************************************************
*
* Updates:
*
* Version 1.0 : Release to Web
*
*****************************************************************************
* Notes: 
* 1. The model conforms to the published specs for:
*    - Nominal max gain, min gain and gain steps
*    - Noise figure with a 100 ohm signal source
*    - Input impedance vs frequency
*    - Output impedance at DC
*    - Enabled & disabled supply current at +5V supply
*         NOTE: Model is disabled if the power supply is outside 
*         of the max supply range: 4.75V < Vsupp < 5.25V
*    - HD2/3 vs frequency and vs Output Power for a 200 hm load
* 2. Digital functions: This model provides only a nominal, idealized 
*      gain values.
*      This model does not support the SERIAL MODE feature, only the
*      equivalent, nominal gain settins via a 5 bit parallel 
*      gain control word [B4 B3 B2 B1 B0].
*    - This model does not provide many of the digital functions 
*      described in the device data sheet. 
*    - This model does not conform to many of the digital performance 
*      metrics described in the device data sheet, such as setup time or 
*      hold time. 
* 3. Output: This model does not offer the same distortion performance 
*      versus output load or gain as described in the device data sheet.
*      The OIP3 of the macromodel is generally 4dB to 5dB better than the
*      actual device. 
*      This model can gives the same harmonic distortion behavior as 
*      shown in the data sheet for maximum gain and a 200 ohm resistive load.
* 4. Rejection ratios: this model does not conform to the published 
*      specs for PSRR and CMRR. 
*****************************************************************************
*$
.SUBCKT LMH6523 OUTP OUTM INP INM EN B4 B3 B2 B1 B0 VCC GND 
R_R_E20         GND N2596414  40 TC=0,0 
R_LMH688x_GAIN_STAGE_R10         LMH688x_GAIN_STAGE_P1N LMH688x_GAIN_STAGE_P1P 
+  2 TC=0,0 
G_LMH688x_GAIN_STAGE_G14         LMH688x_GAIN_STAGE_N2597723 GND
+  LMH688x_GAIN_STAGE_OUT2P LMH688x_GAIN_STAGE_OUT2N 1
G_LMH688x_GAIN_STAGE_G11         LMH688x_GAIN_STAGE_N2331516
+  LMH688x_GAIN_STAGE_N2331162 LMH688x_GAIN_STAGE_OUT1P_GAIN2
+  LMH688x_GAIN_STAGE_OUT1N_GAIN2 -1
E_LMH688x_GAIN_STAGE_E20         LMH688x_GAIN_STAGE_GAIN_INP GND
+  LMH688x_GAIN_STAGE_N2331162 LMH688x_GAIN_STAGE_FBN 1
R_LMH688x_GAIN_STAGE_R6         LMH688x_GAIN_STAGE_GCN LMH688x_GAIN_STAGE_GCP 
+  2 TC=0,0 
C_LMH688x_GAIN_STAGE_C10         LMH688x_GAIN_STAGE_N2619483
+  LMH688x_GAIN_STAGE_N2647675  226pF  TC=0,0 
R_LMH688x_GAIN_STAGE_R28         GND LMH688x_GAIN_STAGE_N2597723  1 TC=0,0 
R_LMH688x_GAIN_STAGE_R31         GND LMH688x_GAIN_STAGE_N2566464  1 TC=0,0 
R_LMH688x_GAIN_STAGE_R7         GND N2564708  1 TC=0,0 
G_LMH688x_GAIN_STAGE_G13         LMH688x_GAIN_STAGE_N2566464 GND
+  LMH688x_GAIN_STAGE_OUT3P LMH688x_GAIN_STAGE_OUT3N 0.007
C_LMH688x_GAIN_STAGE_C14         LMH688x_GAIN_STAGE_N2692016
+  LMH688x_GAIN_STAGE_N2691178  150pF  TC=0,0 
G_LMH688x_GAIN_STAGE_G15         LMH688x_GAIN_STAGE_N2331516
+  LMH688x_GAIN_STAGE_N2331162 LMH688x_GAIN_STAGE_N2565789 GND 1
R_LMH688x_GAIN_STAGE_R14         N2564314 GND  1 TC=0,0 
R_LMH688x_GAIN_STAGE_R16         GND LMH688x_GAIN_STAGE_N2331516  1 TC=0,0 
R_LMH688x_GAIN_STAGE_R34         GND LMH688x_GAIN_STAGE_N2691178  1 TC=0,0 
E_LMH688x_GAIN_STAGE_E17         LMH688x_GAIN_STAGE_GAIN_INN GND
+  LMH688x_GAIN_STAGE_N2331516 LMH688x_GAIN_STAGE_FBP 1
G_LMH688x_GAIN_STAGE_G2         LMH688x_GAIN_STAGE_P2P LMH688x_GAIN_STAGE_P2N
+  LMH688x_GAIN_STAGE_P1P LMH688x_GAIN_STAGE_P1N 1
R_LMH688x_GAIN_STAGE_R15         N2564314 N2564708  2 TC=0,0 
R_LMH688x_GAIN_STAGE_R30         GND LMH688x_GAIN_STAGE_N2647675  1 TC=0,0 
X_LMH688x_GAIN_STAGE_U1         LMH688x_GAIN_STAGE_OUT1P_GAIN2
+  LMH688x_GAIN_STAGE_OUT1N_GAIN2 LMH688x_GAIN_STAGE_OUT2P
+  LMH688x_GAIN_STAGE_OUT2N LMH688x_GAIN_STAGE_OUT3P LMH688x_GAIN_STAGE_OUT3N
+  N2596374 N2596362 EN LMH6523_GAIN2 
G_LMH688x_GAIN_STAGE_G18         LMH688x_GAIN_STAGE_N2331516
+  LMH688x_GAIN_STAGE_N2331162 LMH688x_GAIN_STAGE_N2566464 GND 1
R_LMH688x_GAIN_STAGE_R17         LMH688x_GAIN_STAGE_N2331162 GND  1 TC=0,0 
R_LMH688x_GAIN_STAGE_R11         GND LMH688x_GAIN_STAGE_P2P  1 TC=0,0 
G_LMH688x_GAIN_STAGE_G12         LMH688x_GAIN_STAGE_N2565789 GND
+  LMH688x_GAIN_STAGE_OUT2P LMH688x_GAIN_STAGE_OUT2N 0.005
E_LMH688x_GAIN_STAGE_E18         LMH688x_GAIN_STAGE_FBP GND
+  LMH688x_GAIN_STAGE_N2331516 OSTG_FBP 0.045
G_LMH688x_GAIN_STAGE_G1         LMH688x_GAIN_STAGE_P1P LMH688x_GAIN_STAGE_P1N
+  LMH688x_GAIN_STAGE_GCP LMH688x_GAIN_STAGE_GCN 1
X_LMH688x_GAIN_STAGE_U2         LMH688x_GAIN_STAGE_OUT1P_GAIN
+  LMH688x_GAIN_STAGE_OUT1N_GAIN LMH688x_GAIN_STAGE_GAIN_INP
+  LMH688x_GAIN_STAGE_GAIN_INN EN LMH6523_GAIN 
R_LMH688x_GAIN_STAGE_R18         LMH688x_GAIN_STAGE_N2331162
+  LMH688x_GAIN_STAGE_N2331516  2 TC=0,0 
C_LMH688x_GAIN_STAGE_C4         LMH688x_GAIN_STAGE_P2N LMH688x_GAIN_STAGE_P2P 
+  10p  TC=0,0 
R_LMH688x_GAIN_STAGE_R32         GND LMH688x_GAIN_STAGE_N2663531  1 TC=0,0 
G_LMH688x_GAIN_STAGE_G19         LMH688x_GAIN_STAGE_N2331516
+  LMH688x_GAIN_STAGE_N2331162 LMH688x_GAIN_STAGE_N2691178 GND 1
E_LMH688x_GAIN_STAGE_E19         LMH688x_GAIN_STAGE_FBN GND
+  LMH688x_GAIN_STAGE_N2331162 OSTG_FBN 0.045
R_LMH688x_GAIN_STAGE_R27         GND LMH688x_GAIN_STAGE_N2565789  1 TC=0,0 
R_LMH688x_GAIN_STAGE_R12         LMH688x_GAIN_STAGE_P2N GND  1 TC=0,0 
G_LMH688x_GAIN_STAGE_G17         LMH688x_GAIN_STAGE_N2663531 GND
+  LMH688x_GAIN_STAGE_OUT3P LMH688x_GAIN_STAGE_OUT3N 1
G_LMH688x_GAIN_STAGE_G16         LMH688x_GAIN_STAGE_N2331516
+  LMH688x_GAIN_STAGE_N2331162 LMH688x_GAIN_STAGE_N2647675 GND 1
R_LMH688x_GAIN_STAGE_R8         GND LMH688x_GAIN_STAGE_P1P  1 TC=0,0 
R_LMH688x_GAIN_STAGE_R13         LMH688x_GAIN_STAGE_P2N LMH688x_GAIN_STAGE_P2P 
+  2 TC=0,0 
G_LMH688x_GAIN_STAGE_G6         N2564708 N2564314 LMH688x_GAIN_STAGE_P2P
+  LMH688x_GAIN_STAGE_P2N 1
C_LMH688x_GAIN_STAGE_C7         LMH688x_GAIN_STAGE_N2597723
+  LMH688x_GAIN_STAGE_N2619483  226pF  TC=0,0 
G_LMH688x_GAIN_STAGE_G10         LMH688x_GAIN_STAGE_GCP LMH688x_GAIN_STAGE_GCN
+  LMH688x_GAIN_STAGE_OUT1P_GAIN LMH688x_GAIN_STAGE_OUT1N_GAIN 1
C_LMH688x_GAIN_STAGE_C1         LMH688x_GAIN_STAGE_P1N LMH688x_GAIN_STAGE_P1P 
+  75p  TC=0,0 
R_LMH688x_GAIN_STAGE_R4         GND LMH688x_GAIN_STAGE_GCP  1 TC=0,0 
L_LMH688x_GAIN_STAGE_L3         GND LMH688x_GAIN_STAGE_N2619483  113pH  
R_LMH688x_GAIN_STAGE_R9         LMH688x_GAIN_STAGE_P1N GND  1 TC=0,0 
C_LMH688x_GAIN_STAGE_C13         LMH688x_GAIN_STAGE_N2663531
+  LMH688x_GAIN_STAGE_N2692016  150pF  TC=0,0 
R_LMH688x_GAIN_STAGE_R5         LMH688x_GAIN_STAGE_GCN GND  1 TC=0,0 
C_LMH688x_GAIN_STAGE_C2         LMH688x_GAIN_STAGE_GCN LMH688x_GAIN_STAGE_GCP 
+  3.3n  TC=0,0 
L_LMH688x_GAIN_STAGE_L8         GND LMH688x_GAIN_STAGE_N2692016  75pH  
X_U1         N2596346 N2596354 INP INM B4 B3 B2 B1 B0 EN VCC GND
+  LMH6523_DIG_ATTEN 
E_E20         N2596346 N2596374 N2596414 GND 1
E_E21         N2596354 N2596362 N2596394 GND 1
R_LMH6881_OUTSTAGE_R52         GND EN  6k TC=0,0 
X_LMH6881_OUTSTAGE_U10         LMH6881_OUTSTAGE_N2383162 EN GND LMH6523_OUT_BIAS 
R_LMH6881_OUTSTAGE_R41         LMH6881_OUTSTAGE_N2385401
+  LMH6881_OUTSTAGE_N2385319  4 TC=0,0 
X_LMH6881_OUTSTAGE_U8         LMH6881_OUTSTAGE_OUT_LIM2P
+  LMH6881_OUTSTAGE_OUT_LIM2N N2564708 N2564314 VCC GND LMH6881_OUTSTAGE_IOUTN EN
+  LMH6523_OUT_STG 
R_LMH6881_OUTSTAGE_R51         OSTG_FBN LMH6881_OUTSTAGE_N2366245  0.1 TC=0,0 
R_LMH6881_OUTSTAGE_R49         VCC GND  100k TC=0,0 
E_LMH6881_OUTSTAGE_E5         LMH6881_OUTSTAGE_VOUT GND OUTP OUTM 1
I_LMH6881_OUTSTAGE_I1         GND EN DC 225uAdc  
L_LMH6881_OUTSTAGE_L12         LMH6881_OUTSTAGE_OUT_E2P
+  LMH6881_OUTSTAGE_N2383174  8.50nH  
X_LMH6881_OUTSTAGE_H1    LMH6881_OUTSTAGE_N2366105 OUTP LMH6881_OUTSTAGE_IOUTP
+  GND LMH6523_OUTPUT_STAGE_H1 
X_LMH6881_OUTSTAGE_U9         LMH6881_OUTSTAGE_N2385319 EN GND LMH6523_OUT_BIAS 
R_LMH6881_OUTSTAGE_R37         LMH6881_OUTSTAGE_N2383162
+  LMH6881_OUTSTAGE_N2383174  10.00 TC=0,0 
R_LMH6881_OUTSTAGE_R27         GND LMH6881_OUTSTAGE_VOUT  1k TC=0,0 
C_LMH6881_OUTSTAGE_C11         LMH6881_OUTSTAGE_N2388445
+  LMH6881_OUTSTAGE_OUT_E2P  24pF  TC=0,0 
R_LMH6881_OUTSTAGE_R39         LMH6881_OUTSTAGE_N2388445
+  LMH6881_OUTSTAGE_N2383162  4 TC=0,0 
X_LMH6881_OUTSTAGE_U7         LMH6881_OUTSTAGE_OUT_LIM1P
+  LMH6881_OUTSTAGE_OUT_LIM1N N2564708 N2564314 VCC GND LMH6881_OUTSTAGE_IOUTP EN
+  LMH6523_OUT_STG 
R_LMH6881_OUTSTAGE_R50         OSTG_FBP LMH6881_OUTSTAGE_N2366105  0.1 TC=0,0 
L_LMH6881_OUTSTAGE_L4         LMH6881_OUTSTAGE_OUT_E1N
+  LMH6881_OUTSTAGE_N2385395  8.50nH  
R_LMH6881_OUTSTAGE_R40         LMH6881_OUTSTAGE_N2385395
+  LMH6881_OUTSTAGE_N2385319  10.00 TC=0,0 
E_LMH6881_OUTSTAGE_E15         OSTG_FBP LMH6881_OUTSTAGE_OUT_E1N
+  LMH6881_OUTSTAGE_OUT_LIM1P LMH6881_OUTSTAGE_OUT_LIM1N 1
X_LMH6881_OUTSTAGE_H2    LMH6881_OUTSTAGE_N2366245 OUTM LMH6881_OUTSTAGE_IOUTN
+  GND LMH6523_OUTPUT_STAGE_H2 
X_LMH6881_OUTSTAGE_U11         VCC EN LMH6881_OUTSTAGE_IOUTP GND LMH6523_OUT_SW_IQ 
E_LMH6881_OUTSTAGE_E16         LMH6881_OUTSTAGE_OUT_E2P OSTG_FBN
+  LMH6881_OUTSTAGE_OUT_LIM2P LMH6881_OUTSTAGE_OUT_LIM2N 1
C_LMH6881_OUTSTAGE_C12         LMH6881_OUTSTAGE_OUT_E1N
+  LMH6881_OUTSTAGE_N2385401  24pF  TC=0,0 
R_R_E21         GND N2596394  40 TC=0,0 
X_SUPP_CURR VCC EN GND LMH6523_SUPP_CURR
.ENDS
*$
.subckt LMH6523_OUTPUT_STAGE_H1 1 2 3 4  
H_LMH6881_OUTSTAGE_H1         3 4 VH_LMH6881_OUTSTAGE_H1 -1
VH_LMH6881_OUTSTAGE_H1         1 2 0V
.ends LMH6523_OUTPUT_STAGE_H1
*$
.subckt LMH6523_OUTPUT_STAGE_H2 1 2 3 4  
H_LMH6881_OUTSTAGE_H2         3 4 VH_LMH6881_OUTSTAGE_H2 -1
VH_LMH6881_OUTSTAGE_H2         1 2 0V
.ends LMH6523_OUTPUT_STAGE_H2
*$
.SUBCKT LMH6523_GAIN OUT1P OUT1N INP INN EN
.PARAM a0 = 0
.PARAM a1 = 500
.PARAM LL = -1000
.PARAM UL = 1000
.PARAM VDIS = 0.401
EDIS N_DIS 0 VALUE = {STP(V(EN)) - STP(V(EN) - VDIS)}
Eid DIFF_IN 0 VALUE = {V(INP) - V(INN)}
E1 OUT1P OUT1N VALUE = {MAX(MIN(a1*(1-V(N_DIS))*V(DIFF_IN),UL),LL)}
RE1P OUT1P 0 200
RE1N 0 OUT1N 200
RE1d OUT1P OUT1N 400
.ENDS LMH6523_GAIN
*$
.SUBCKT LMH6523_GAIN2 OUT1P OUT1N OUT2P OUT2N OUT3P OUT3N INP INN EN
.PARAM a0 = 0
.PARAM a1 = 1
.PARAM a2 = {0.08}		
.PARAM a3 = {-1.0}
.PARAM LL = -1000
.PARAM UL = 1000
.PARAM VDIS = 0.401
.PARAM VLPL = 0.599
.PARAM VLPH = 1.901
EDIS N_DIS 0 VALUE = {STP(V(EN)) - STP(V(EN) - VDIS)}
ELP  N_LP  0 VALUE = {STP(V(EN)-VLPL) - STP(V(EN)-VLPH)}
Eid DIFF_IN 0 VALUE = {V(INP) - V(INN)}
E1 OUT1P OUT1N VALUE = {MAX(MIN(a1*(1-V(N_DIS))*V(DIFF_IN),UL),LL)}
E2 OUT2P OUT2N VALUE = {MAX(MIN(a2*(1-0.5*V(N_LP))*(1-V(N_DIS))*V(DIFF_IN)*V(DIFF_IN),UL),LL)}
E3 OUT3P OUT3N VALUE = {MAX(MIN(a3*(1+2*V(N_LP))*(1-V(N_DIS))*V(DIFF_IN)*V(DIFF_IN)*V(DIFF_IN),UL),LL)}
RE1P OUT1P 0 200
RE1N 0 OUT1N 200
RE1d OUT1P OUT1N 400
RE2P OUT2P 0 200
RE2N 0 OUT2N 200
RE2d OUT2P OUT2N 400
RE3P OUT3P 0 200
RE3N 0 OUT3N 200
RE3d OUT3P OUT3N 400
.ENDS LMH6523_GAIN2
*$
.SUBCKT LMH6523_DIG_ATTEN VOUTP VOUTM INPD INMD B4 B3 B2 B1 B0 EN VCC GND
* B4-B0 are active low digital inputs; Max gain is at [00000]
* B4 -  16dB Gain Step  
* B3 -  8dB Gain Step
* B2 -  4dB Gain Step
* B1 -  2dB Gain Step
* B0 -  1dB Gain Step
.PARAM VCM_OUT = 2.5
.PARAM VTHRESH_DIG = 0.5
.PARAM SCALE = {1/1.3}
.PARAM G_B4 = -16
.PARAM G_B3 = -8
.PARAM G_B2 = -4
.PARAM G_B1 = -2
.PARAM G_B0 = -1
.PARAM RINT = {5/260e-6}
.PARAM VINT = {5*60e-6/260e-6}
.PARAM VCC_MIN = 4.75
.PARAM VCC_MAX = 5.25
.PARAM ATTEN_GAIN_MIN = -32
.PARAM a3 = -3.98e-5
.PARAM a2 = -2.82e-4
.PARAM a0 = 0
EVCC_TEST N_VCC_TEST GND VALUE = {0.5*(1 - SGN(V(VCC,GND)-VCC_MIN)*SGN(V(VCC,GND)-VCC_MAX))}
EVMID N_VMID_RAIL GND VALUE = {0.5*(V(VCC) - V(GND)) + V(GND)}
EVIN_MAX N_VIN_MAX GND VALUE = {V(VCC) - 1.125}
EVIN_MIN N_VIN_MIN GND VALUE = {V(GND) + 1.125}
* -- Sources & Resistances for Digital Bias Currents -----
VB4 B4n GND {VINT}
VB3 B3n GND {VINT}
VB2 B2n GND {VINT}
VB1 B1n GND {VINT}
VB0 B0n GND {VINT}
RB4 B4 B4n {RINT}
RB3 B3 B3n {RINT}
RB2 B2 B2n {RINT}
RB1 B1 B1n {RINT}
RB0 B0 B0n {RINT}
VEN ENn GND {VINT}
REN EN ENn {RINT}
* CONVERT DIGITAL INPUT VOLTAGES TO LOGIC LEVELS
EB4 N_B4 GND VALUE = {MIN(MAX(SCALE*(V(B4)-VTHRESH_DIG),0),1)}
EB3 N_B3 GND VALUE = {MIN(MAX(SCALE*(V(B3)-VTHRESH_DIG),0),1)}
EB2 N_B2 GND VALUE = {MIN(MAX(SCALE*(V(B2)-VTHRESH_DIG),0),1)}
EB1 N_B1 GND VALUE = {MIN(MAX(SCALE*(V(B1)-VTHRESH_DIG),0),1)}
EB0 N_B0 GND VALUE = {MIN(MAX(SCALE*(V(B0)-VTHRESH_DIG),0),1)}
EEN N_EN GND VALUE = {MIN(MAX(10*(V(EN)-VTHRESH_DIG),0),1)}
* CALC ATTENUATOR GAIN
E_ATTEN_GAIN_DB N_GAIN_DB GND VALUE = {MAX(G_B4*V(N_B4)+G_B3*V(N_B3)
+ +G_B2*V(N_B2)+G_B1*V(N_B1)+G_B0*V(N_B0),ATTEN_GAIN_MIN)}
E_ATTEN_GAIN a1 GND VALUE = {(V(N_EN))*V(N_VCC_TEST)*(10^(V(N_GAIN_DB)/20))}
*------  Attenuator diff input resistance and input VCM bias ---
EinCM CM_INT GND VALUE = {v(N_VMID_RAIL)}
RINPD INPD INPDmid 51
LINPD INPDmid CM_INT 1.5nH
RINMD INMD INMDmid 51
LINMD INMDmid CM_INT 1.5nH
CIN2 INmid1 INMD 0.954p
LIN2 INmid1 INmid2 73.75n
RIN2 INmid2 INPD 510
*-------------------------------------
*----- Polynomial Distortion -----
E_LIM_INPD INPD_LIM GND VALUE = {MIN(MAX(V(INPD),V(N_VIN_MIN)),V(N_VIN_MAX))}
E_LIM_INMD INMD_LIM GND VALUE = {MIN(MAX(V(INMD),V(N_VIN_MIN)),V(N_VIN_MAX))}
E_ID DIFF_IN GND        VALUE = {V(INPD_LIM) - V(INMD_LIM)}
G_DIFF_IN VOUTP VOUTM VALUE = {(a0 + V(a1)*V(DIFF_IN) + a2*PWR(V(DIFF_IN),2) + a3*PWRS(V(DIFF_IN),3))}
*--------------------
* --- Current Drain ---
*GQ_EN VCC GND VALUE = {(V(N_EN))*V(N_VCC_TEST)*0.085}
*GQ_DIS VCC GND VALUE = {(1-V(N_EN))*V(N_VCC_TEST)*(0.005)}
* --- Output Load & CM voltage -----
ROUT1P VOUTP N2 1
ROUT1M VOUTM N2 1
ROUTp  VOUTP VOUTM 2
E1CM N2 GND VALUE = {V(N_VMID_RAIL)}
* -------------------
.ENDS LMH6523_DIG_ATTEN
*$
.SUBCKT LMH6523_OUT_BIAS OUT EN GND
.PARAM VDIS = 0.45
.PARAM VLPL = 0.6
.PARAM VLPH = 1.95
.PARAM VHP =  2.2
EDIS N_DIS GND VALUE = {STP(V(EN)) - STP(V(EN) - VDIS)}
ELP  N_LP GND VALUE = {STP(V(EN)-VLPL) - STP(V(EN)-VLPH)}
EHP  N_HP GND VALUE = {STP(V(EN)-VHP) - STP(V(EN)-5)}
EOS N_EOS GND VALUE = {0.29*V(N_LP) + 0.40*V(N_HP)}
*ROS N_EOS OUT VALUE = {1e5*V(N_DIS)}
*ROS N_EOS OUT 1
GOS OUT N_EOS VALUE = {(1 - V(N_DIS))*V(OUT,N_EOS)}
.ENDS LMH6523_OUT_BIAS
*$
.SUBCKT LMH6523_OUT_STG OUTP OUTN INP INN VCC GND VC EN
.PARAM VCM_OUT = 0
.PARAM GAIN = 0.5
.PARAM DV = {2/3}
.PARAM N = 5
.PARAM VDIS = 0.401
.PARAM VLPL = 0.599
.PARAM VLPH = 1.901
.PARAM VHP =  2.19
* Placeholder component to terminate control voltage VC
R_VC VC GND 1k
* ENABLE PIN DC Components
*IEN GND EN 0.000225
*REN EN GND 6e3
* ENABLE pin states to support DISABLE, LOW POWER, HIGH POWER modes
* If ENABLE pin is unterminated the model will default to LOW POWER mode
EDV  N_DV  GND VALUE = {DV + 1*DV*V(N_LP)}
EDIS N_DIS GND VALUE = {STP(V(EN)) - STP(V(EN) - VDIS)}
ELP  N_LP  GND VALUE = {STP(V(EN)-VLPL) - STP(V(EN)-VLPH)}
EHP  N_HP  GND VALUE = {STP(V(EN)-VHP) - STP(V(EN)-5)}
EMR N_MR GND VALUE = {0.5*(V(VCC) - V(GND)) +V(GND)}
EIN N_IN GND VALUE = {V(INP) - V(INN)}
EV1 N_V1 GND VALUE = {V(VCC) - 1 - 1*V(N_LP)}
EV2 N_V2 GND VALUE = {V(N_V1) + V(N_DV)}
EV3 N_V3 GND VALUE = {V(N_V1) + 2*V(N_DV)}
EV4 N_V4 GND VALUE = {V(N_V1) + 3*V(N_DV)}
EV5 N_V5 GND VALUE = {V(N_V1) + 4*V(N_DV)}
EG1 N_G1 GND VALUE = {GAIN*(1 - V(N_DIS))}
EG2 N_G2 GND VALUE = {0.75*V(N_G1)}
EG3 N_G3 GND VALUE = {0.5*V(N_G1)}
EG4 N_G4 GND VALUE = {0.25*V(N_G1)}
EG5 N_G5 GND VALUE = {0}
EB1 N_B1 GND VALUE = {0}
EB2 N_B2 GND VALUE = {0.25*GAIN*(V(N_V1))}
EB3 N_B3 GND VALUE = {0.25*GAIN*(V(N_V1)+V(N_V2))}
EB4 N_B4 GND VALUE = {0.25*GAIN*(V(N_V1)+V(N_V2)+V(N_V3))}
EB5 N_B5 GND VALUE = {0.25*GAIN*(V(N_V1)+V(N_V2)+V(N_V3)+V(N_V4))}
E1U N_1U GND VALUE = {V(N_G1)*V(N_IN)}
E2U N_2U GND VALUE = {V(N_G2)*V(N_IN) + V(N_B2)}
E3U N_3U GND VALUE = {V(N_G3)*V(N_IN) + V(N_B3)}
E4U N_4U GND VALUE = {V(N_G4)*V(N_IN) + V(N_B4)}
E5U N_5U GND VALUE = {V(N_G5)*V(N_IN) + V(N_B5)}
E2L N_2L GND VALUE = {V(N_G2)*V(N_IN) - V(N_B2)}
E3L N_3L GND VALUE = {V(N_G3)*V(N_IN) - V(N_B3)}
E4L N_4L GND VALUE = {V(N_G4)*V(N_IN) - V(N_B4)}
E5L N_5L GND VALUE = {V(N_G5)*V(N_IN) - V(N_B5)}
EX1 N_X1 GND VALUE = {MIN(MAX(V(N_1U),V(N_2L)),V(N_2U))}
EX2 N_X2 GND VALUE = {MIN(MAX(V(N_X1),V(N_3L)),V(N_3U))}
EX3 N_X3 GND VALUE = {MIN(MAX(V(N_X2),V(N_4L)),V(N_4U))}
EX4 OUTP OUTN VALUE = {MIN(MAX(V(N_X3),V(N_5L)),V(N_5U))}
V1CM N2 GND {VCM_OUT}
ROUTP OUTP N2 100
ROUTN OUTN N2 100
ROUTD OUTP OUTN 200
.ENDS LMH6523_OUT_STG
*$
.SUBCKT LMH6523_OUT_SW_IQ VCC EN IOUT GND
.PARAM VDIS = 0.45
.PARAM VLPL = 0.6
.PARAM VLPH = 1.95
.PARAM VHP =  2.2
EDIS N_DIS GND VALUE = {STP(V(EN)) - STP(V(EN) - VDIS)}
ELP  N_LP GND VALUE = {STP(V(EN)-VLPL) - STP(V(EN)-VLPH)}
EHP  N_HP GND VALUE = {STP(V(EN)-VHP) - STP(V(EN)-5)}
EOS N_EOS GND VALUE = {0.026*V(N_LP) + 0.036*V(N_HP)}
GIQ VCC GND VALUE = {ABS(V(IOUT) + V(N_EOS))}
.ENDS LMH6523_OUT_SW_IQ
*$
.SUBCKT LMH6523_SUPP_CURR VCC EN GND
.PARAM VDIS = 0.45
.PARAM VLPL = 0.6
.PARAM VLPH = 1.95
.PARAM VHP =  2.2
EDIS N_DIS GND VALUE = {STP(V(EN)) - STP(V(EN) - VDIS)}
ELP  N_LP GND VALUE = {STP(V(EN)-VLPL) - STP(V(EN)-VLPH)}
EHP  N_HP GND VALUE = {STP(V(EN)-VHP) - STP(V(EN)-V(VCC))}
GIQ VCC GND VALUE = {0.0185*V(N_DIS) + 0.0925*V(N_LP) + 0.116*V(N_HP)}
.ENDS LMH6523_SUPP_CURR
*$

