.SUBCKT TPA3111D1 GAIN0 GAIN1 SD INN INP OUTP OUTN PVCC PLIMIT GND
V3          19 GND 6
V1          20 GND 26
XU1 GAIN0 INN INP GND VREF GAIN1 AVCC 17 OUTN SD PLIMIT 18 OUTP TPA3110D2_HALF_0 
R2          17 GND 200M 
R1          PVCC_16 18 200M 
XU6         PVCC_16 19 AVCC SCLAMP_0
XU2         PLIMIT 20 PVCC_16 SCLAMP_0
XU7         AVCC GND VREF TRI_GEN_0
.ENDS

.SUBCKT TPA3110D2_HALF_0 GAIN0 INN INP AGND VREF GAIN1 AVCC PGND OUTN SD PLIMIT 
+ PVCC OUTP 
XU4         G0 G1 INN 24 PRHEO_0
XU6         G0 G1 INP 35 PRHEO_0
XU3         PVCC PLIMIT PLIM PLIMIT_0
C10         25 26 5P  
C9          27 28 5P  
C8          26 29 25P 
C5          28 30 25P
XU11        AVCC 28 26 29 30 AGND STD_OPA_0
R2          25 26 110K 
R1          27 28 110K 
XU10        AVCC PLIM 31 32 34 33 OUTP OUTN SD AVCC PGND HBRIDGE_0
XU12        AVCC 36 37 AGND 31 32 34 33 HDRIVER_0
R4          35 OUTN 600K 
R3          24 OUTP 600K 
C7          24 27 8.84P 
C6          35 25 8.84P 
XU1         AVCC 35 24 27 25 AGND STD_OPA_0
XU8         AVCC 29 30 VREF 36 37 AGND BD_COMP_0
XU5         GAIN0 G0 TOLOGIC_0
XU2         GAIN1 G1 TOLOGIC_0
.ENDS


.SUBCKT VCVS_0  VP VN CP CN PARAMS: G = 0.5

E1 VP VN VALUE = {V(CP,CN)*G}

.ENDS VCVS_0 

.SUBCKT SCLAMP_0   VIN VLIM VOUT 

ES1  VIN  VOUT  VALUE = { IF(V(VIN,0)>V(VLIM,0),V(VIN) - V(VLIM),0) }

.ENDS SCLAMP_0 

.SUBCKT TRI_GEN_0  VDD VSS VOTR PARAMS: DIV = 6

VTR 1 0 PULSE (-1 1 0 1.6129US 1.6129US 0 3.2258US)

EADJUST VOTR VCM VALUE = {V(1,0)*V(VDD,0)/DIV}
EVCM VCM 0 VALUE = {(V(VDD,0)+V(VSS,0))*0.5}

.ENDS TRI_GEN_0 

.SUBCKT PRHEO_0  G0 G1 VP VN  PARAMS: R1 = 9K R2 = 6K R3 = 15K R4 = 30K 

ES1  VS1  0  VALUE  =  { IF(V(G0) > 0.5,IF(V(G1) > 0.5,1,0),0) }
ES2  VS2  0  VALUE  =  { IF(V(G0) < 0.5,IF(V(G1) > 0.5,1,0),0) }
ES3  VS3  0  VALUE  =  { IF(V(G0) > 0.5,IF(V(G1) < 0.5,1,0),0) }
ES4  VS4  0  VALUE  =  { IF(V(G0) < 0.5,IF(V(G1) < 0.5,1,0),0) }

ERES  VP  2   VALUE  =  { I(VSEI)*(V(VS2)*R2 + V(VS3)*(R3+R2) + V(VS4)*(R4+R3+R2)) }
VSEI  2   3  DC 0V
RSEI  3  VN  {R1}


.ENDS PRHEO_0 

.SUBCKT PLIMIT_0  PVCC VPLIMIT VSUPP

ELIMIT  PVCC  VSUPP  VALUE = { IF(V(PVCC)>4*V(VPLIMIT),V(PVCC) - 4*V(VPLIMIT),0) }

.ENDS PLIMIT_0 

.SUBCKT STD_OPA_0  VDD VIP VIN VOP VON VSS

EIP  FP1  VCM  VIP  VIN  200E3
EIN  VCM  FP2  VIP  VIN  200E3

RP1  FP1  VIBP  5.0E+6
CP1  VIBP 0  3.18E-10

RP2  FP2  VIBN  5.0E+6
CP2  VIBN 0 3.18E-10

EVCM VCM 0 VALUE = {(V(VDD,0)+V(VSS,0))*0.5}


EBP  VOP  0  VALUE = {LIMIT(V(VIBP),V(VDD) - 0.1,V(VSS) + 0.1)}
EBN  VON  0  VALUE = {LIMIT(V(VIBN),V(VDD) - 0.1,V(VSS) + 0.1)}


.ENDS STD_OPA_0 


.SUBCKT HBRIDGE_0  AVDD PVDD VHA VLA VHB VLB VOA VOB SDB SSS VSS 


ESD  VSDBX  0  VALUE = {IF(V(SDB)>1.3,1,0)}
ESS  VSSSX  0  VALUE = {IF(V(SSS)>1.3,1,0)}

EBETA  VBETA  0  POLY(1) (AVDD,0) 0 100



*                            VDS       VG      VBETA     SHUTDOWN   SPECIAL
GAH  PVDD  VOA  VALUE = { V(PVDD,VOA)*V(VHA,0)*V(VBETA,0)*V(VSDBX,0)*V(VSSSX,0) }
RAH  PVDD  VOA  2E3
DAH  VOA  PVDD  IDEAL_DIODE

GAL  VOA  VSS  VALUE = { V(VOA,VSS)*(V(VLA,0)*V(VSSSX,0) + V(AVDD,0)*(1 - V(VSSSX,0)))*V(VBETA,0)*V(VSDBX,0) }
RAL  VOA  VSS  2E3
DAL  VSS  VOA  IDEAL_DIODE



GBH  PVDD  VOB  VALUE = { V(PVDD,VOB)*V(VHB,0)*V(VBETA,0)*V(VSDBX,0)*V(VSSSX,0) }
RBH  PVDD  VOB  2E3
DBH  VOB  PVDD  IDEAL_DIODE

GBL  VOB  VSS  VALUE = { V(VOB,VSS)*(V(VLB,0)*V(VSSSX,0) + V(AVDD,0)*(1 - V(VSSSX,0)))*V(VBETA,0)*V(VSDBX,0) }
RBL  VOB  VSS  2E3
DBL  VSS  VOB  IDEAL_DIODE

.MODEL IDEAL_DIODE D ( N = 0.01 )

.ENDS HBRIDGE_0 



.SUBCKT HDRIVER_0  VDD VIA VIB VSS VHA VLA VHB VLB



X0  VIA  VXA  DELAY_LINE
X1  VDD  VIA  VXA  VHA  VSS AND

X2  VDD  VIA  VNA  VSS  NOT
X3  VNA  VYA  DELAY_LINE
X4  VDD  VNA  VYA  VLA  VSS AND 


X5  VIB  VXB  DELAY_LINE
X6  VDD  VIB  VXB  VHB  VSS AND

X7  VDD  VIB  VNB  VSS  NOT
X8  VNB  VYB  DELAY_LINE
X9  VDD  VNB  VYB  VLB  VSS AND 

.ENDS HDRIVER_0 


.SUBCKT NOT VDD VI VO VSS 


ESLICE VO 0  VALUE = {IF(V(VI)>V(VCM),V(VSS),V(VDD))}
ECM  VCM  0  VALUE = {(V(VDD,0)+V(VSS,0))*0.5}

.ENDS NOT

.SUBCKT DELAY_LINE VI VO 


E1 VIX 0 VI 0 1 
T1 VIX 0 VOX 0 Z0 = 75 TD = 25NS
RL VOX 0 75
E2 VO 0 VOX 0 1

.ENDS DELAY_LINE


.SUBCKT AND VDD A B Y VSS 


EA  VA  0  VALUE = {IF(V(A)>V(VCM),1,0)}
EB  VB  0  VALUE = {IF(V(B)>V(VCM),1,0)}
ECM  VCM  0  VALUE = {(V(VDD,0)+V(VSS,0))*0.5}

EY  Y  0  VALUE = {IF(V(VA)*V(VB)>0.5,V(VDD),V(VSS))}

.ENDS AND



.SUBCKT BD_COMP_0  VDD VIP VIN REF VOP VON VSS


XCP  VDD  VIP  REF  VOP  VSS  COMP
XCN  VDD  VIN  REF  VON  VSS  COMP

.ENDS BD_COMP_0 

.SUBCKT COMP VDD VP VN VO VSS


ECOMP  VO  0  VALUE = { LIMIT(V(VP,VN)*1E5,V(VDD),V(VSS)) }

.ENDS COMP


.SUBCKT TOLOGIC_0  VI VO 


ESLICE VO 0 VALUE = {IF(V(VI)>2,1,0)}
RSLICE VO 0 1G

.ENDS TOLOGIC_0