***********************************************************************
*  (C) Copyright 2014 Texas Instruments Incorporated. All rights reserved.
***********************************************************************
***********************************************************************
** This model is designed as an aid for customers of Texas Instruments.
** TI and its licensors and suppliers make no warranties, either expressed
** or implied, with respect to this model, including the warranties of
** merchantability or fitness for a particular purpose. The model is
** provided solely on an "as is" basis. The entire risk as to its quality
** and performance is with the customer
********************************************************************************
*
* Released by: WEBENCH(R) Design Center, Texas Instruments Inc.
* Part: CSD17577Q5A
* Date: 09/05/2014
* Model Type: TRANSIENT
* Simulator: PSPICE
* Simulator Version: 16.2
* EVM Order Number: 
* EVM Users Guide: 
* Datasheet: SLPS516 - AUGUST 2014
* Model Version: Final 1.00
*
*****************************************************************************
*
* Updates:
*
* Final 1.00
* Release to Web
*
*****************************************************************************
*
* Model Usage Notes:
*
*****************************************************************************
**********************************************************************
**********************************************************************
*                                                                    *
* CSD17577Q5A   -  PSpice Model for use with OrCAD                   *
*                                                                    *
**********************************************************************
* SUBCKT Definition:  1=D  2=G  3=S
.SUBCKT CSD17577Q5A  1 2 3
* PARAMETER (local) DEFINITIONS SECTION
 .param	 AA17570  11.16
 .param	 AA17577  1.82
 .PARAM  ptrc1    4.00e-3  
 .PARAM  ptrc2    5.00e-6
 .PARAM  pwidthP  {AA17577/0.8*2}
 .PARAM  pwidth   {pwidthP*1e6}
 .PARAM  perimP   {2.1*pwidthP}
 .PARAM  perim    {perimP*1e6}
****** MODEL SUBCIRCUIT BEGINS HERE     ******************************
M1   10 11 12 12  NMOS	W={pwidthP} L=0.5u  PS={perimP} PD={perimP}
DBD   8  7	 DBD	{AA17577/AA17570}
CBD0  8  7   3e-12
RBD0  8  7   1e10
DDG1  10 101 DD		{AA17577/AA17570}
DDG2  11 101 DD		{AA17577/AA17570}
* Note:  gate oxide capacitance included in NMOS below
LGG   2  5	 2.1e-9
cgg   5	11	 1e-10
RGG   5 11	 1.8
RSB  12  9	 RTEMP 1.00e-6
RSS   9  8 	 RTEMP 1.00e-6
RSP   8  6 	 0.03e-6
LSS   6  3	 0.47e-9
RSL   6  3       0.5
RDD   7 10 	 RTEMP {0.43e-3*AA17570/AA17577+1.6e-3}
RDP   4  7	 0.05e-3
LDD   1  4	 0.05e-9
******************************************************************* 
.MODEL  NMOS   NMOS (
* CONTROL PARAMETERS
+ LEVEL  = 7             VERSION = 3.2         CAPMOD = 2
* DC PARAMETERS
+ NLX    = 1.74E-7       VBM    = -5           DVT0   = 2.2
+ DVT1   = 0.53          DVT2   = -0.032       U0     = 550
+ UA     = 3.0e-9        UB     = 0.0e-19      A0     = 0.9
+ AGS    = 0.0           A1     = 0.0          A2     = 0.9
+ LINT   = 0.055e-6      VOFF   = -0.665       NFACTOR= 1.7
+ ETA0   = 0.005         ETAB   = -0.07        DSUB   = 1.4
+ PCLM   = 3.00          PDIBLC1= 0.02         PDIBLC2= 0.004
+ DROUT  = 0.90          PSCBE1 = 3.0e8        PSCBE2 = 1e-6
+ PVAG   = 0.1           DELTA  = 0.0220       JS     = 0
* AC AND CAPACITANCE PARAMETERS
+ CGSO   = 1.00e-18	 CGDO   = 1e-18        CGBO   = 1e-18
+ CJ     = 1e-18         CJSW   = 1e-18        CF     = 0
* TEMPERATURE PARAMETERS
+ TNOM   = 25            UTE    = -1.5         KT1    = -0.80
+ KT1L   = 1.0e-15       KT2    = 0.022        UA1    = -01.00e-9
+ UB1    = -0.0e-17      
* PROCESS PARAMETERS
+ TOX    = 3.00e-8       XJ     = 3e-7         NCH    = 1.435e17
+ NSUB   = 1.0e16)
*******************************************************************
.MODEL DBD D (CJO=3500e-12 VJ=1.0 M=0.625 TNOM=25
+ FC=0.5 TT=2e-09 XTI=3.3 BV=31
+ IS=6.0e-11 N=1.07 RS=0.25e-3 TRS1=4.0e-3)
.MODEL DD  D (CJO=3.75e-09 VJ=0.7 M=0.625 IS=1e-13 RS=0.1 TNOM=25)
.MODEL DDx D (CJO=1e-09 VJ=0.9 M=0.9 IS=1e-13 RS=1.0 TNOM=25)
*******************************************************************
.MODEL RTEMP RES (TC1={ptrc1} TC2={ptrc2})
*******************************************************************
.ENDS CSD17577Q5A
*$