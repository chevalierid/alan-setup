***********************************************************************
**  v1.7.5 PSpice Models Library - TI NexFET Power N/P Channel FET's
*  (C) Copyright 2010 Texas Instruments Incorporated. All rights reserved.
***********************************************************************
***********************************************************************
* Notice:
* The data and models provided herein are intended to be used by 
* customers as an aid in designing electrical circuits using 
* semiconductor devices manufactured &/or sold by Texas Instruments.
* The models and associated information contained herein are not to
* be construed as a guarantee of electrical performance and/or device 
* specification.  Therefore, Texas Instruments does not assume any 
* liability arising out of the use of said data or models, nor from
* the devices or products manufactured therefrom.  Texas Instruments
* does not convey any license under its patent rights nor the rights
* of others.  Texas Instruments reserves the right to change models 
* without prior notice.  
*
* TI is a registered trademark of Texas Instruments Inc.
*************************************************************************
********************************************************************************
*
* Released by: Analog e-Lab Design Center, Texas Instruments Inc.
* Part: CSD86311W1723
* Date: 12/08/2010
* Model Type: TRANSIENT
* Simulator: PSPICE
* Datasheet: SLPS251 - May 2010
*
*****************************************************************************
*$
**********************************************************************
**********************************************************************
*                                                                    *
* CSD86311w1723   -  P-Spice Model -  STATUS:  PRELIMINARY           *
*                                                                    *
* n-channel 25/10 Source Down technology                             *
*  model is based in part on measurements from part csd36301q5,      *
*                                                                    *
**********************************************************************
**********************************************************************
* Device is dual common source:  (2) drains  (2) gates  (1) source
* SUBCKT Definition:  1=D1  2=D2  3=G1  4=G2  5=S
**********************************************************************
.SUBCKT CSD86311W1723 1 2 3 4 5
*Xnfet1  1 3 7  n86311w
*Xnfet2  2 4 7  n86311w
Xnfet1  1 3 7  nfet
Xnfet2  2 4 7  nfet
RSX     7 6    2.5E-3
LSS     6 5    0.05E-9
.ENDS CSD86311W1723
*$
**********************************************************************
* SUBCKT Block Definition:  1=D  2=G  3=S
*.SUBCKT n86311w  1 2 3
.SUBCKT nfet  1 2 3
* PARAMETER (local) DEFINITIONS SECTION
.PARAM  pWidth  0.600368	; m, total gate width
.PARAM  perim   {2*pWidth}	; m, gate/drain perim (rough est)
.PARAM  ptrc1   3.8e-3		; temp co of ext Rd / Rs
.PARAM  ptrc2   5.5e-6
****** MODEL SUBCIRCUIT BEGINS HERE     ******************************
M1   10 11 12 12  NMOS	W={pWidth} L=0.4u  PS={perim} PD={perim}
DBD   8  7	 DBD
CGD0  5  7	 1E-12
CGS0  5  8	 4e-12
M2   12 11 12 10  PMOSd W={pWidth} L=0.082u PS={perim} PD={perim}
DDG  14  5	 DD
DGD  14 10	 DD
* Note:  gate oxide capacitance included in NMOS below
CGS   5 13	 102.3e-12
RGS  13  8	 528.6
LGG   2  5	 0.5e-9
RGG   5 11	 1.9
RSB  12  9	 RTEMP 4.5e-3
RSS   9  8 	 RTEMP 14.2e-3
*RSP   8  6 	 0.01e-3
RSP   8  6   2.61e-3
LSS   6  3	 0.01e-9
RDD   7 10 	 RTEMP 4.65e-3
RDP   4  7	 0.6e-3
LDD   1  4	 0.2e-9
******************************************************************* 
.MODEL  NMOS   NMOS (    LEVEL = 7             Version = 3.2  
+ TNOM   = 27            LINT   = 0.055e-6      CAPMOD = 2
+ TOX    = 1.76e-8       NCH    = 1.470e17      NSUB   = 1.0e16
* Vth=0.9
*+ TOX    = 1.76e-8       NCH    = 1.394e17      NSUB   = 1.0e16
+ AGS    = 0.0           PVAG   = 1.5
+ A0     = 1.10          A1     = 0.0           A2     = 1.0
+ UA     = 3.75e-9       UB     = 4.0e-19       VBM    = -5
+ UA1    = 3.45e-9       UB1    = -6.5e-18      UTE    = -1.50
+ KT1    = -0.59         KT2    =-0.022         KT1L   = 1.0e-12
+ DVT0   = 2.3           DVT1   = 0.55	      DVT2   = -0.033
+ ETA0   = 0.045         ETAB   = -0.07         XJ     = 3e-7
+ PDIBLC1= 0.08          PDIBLC2= 0.006         NLX    = 1.7e-7
+ PSCBE1 = 2.5e8         PSCBE2 = 1e-7          PCLM   = 1.3
+ VOFF   = -0.12         NFACTOR= 2.0           JS     = 0
+ DROUT  = 0.8           DSUB   = 1.3           DELTA  = 0.022
+ CJ     = 1e-18         CJSW   = 1e-18         CF     = 0
+ CGSO   = 167e-12       CGDO   = 0.1e-12       CGBO   = 0  )
******************************************************************* 
.MODEL  PMOSd   PMOS (   LEVEL  = 7            Version = 3.2
+ TNOM   = 27            CAPMOD = 2
+ JS     = 0             VBM    = -10
+ TOX    = 2.8e-8        NCH    = 1.6e17        NSUB   = 5e16
+ CJ     = 1e-18         CJSW   = 1e-18         CF     = 0
+ CGSO   = 0             CGDO   = 0             CGBO   = 0 )
******************************************************************* 
.MODEL DBD D (CJO=595e-12 VJ=0.75 M=0.32 TNOM=27
+ FC=0.5 TT=5e-09 XTI=2.5 BV=27
+ IS=1.91e-12 N=1.015 RS=2.0e-3 TRS1=4.9e-3)
.MODEL DD  D (CJO=7.15e-12 VJ=1.0 M=0.7 IS=1e-12 RS=0.1 TNOM=27)
*******************************************************************
.MODEL RTEMP RES (TC1={ptrc1} TC2={ptrc2})
*******************************************************************
.ENDS nfet
*.ENDS n86311w
