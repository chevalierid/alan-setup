*CSD23381F4
*$
***********************************************************************
* (C) Copyright 2015 Texas Instruments Incorporated. All rights reserved.
***********************************************************************
***********************************************************************
** This model is designed as an aid for customers of Texas Instruments.
** TI and its licensors and suppliers make no warranties, either expressed
** or implied, with respect to this model, including the warranties of
** merchantability or fitness for a particular purpose. The model is
** provided solely on an "as is" basis. The entire risk as to its quality
** and performance is with the customer
********************************************************************************
**Released by: WEBENCH(R) Design Center, Texas Instruments Inc.
* Part: CSD23381F4
* Date: 23JUNE2015
* Model Type: TRANSIENT
* Simulator: PSPICE
* Simulator Version: 16.2.0.p001
* EVM Order Number:
* EVM Users Guide:
* Datasheet: SLPS450E –OCTOBER 2013–REVISED MAY 2015
* Model Version: 1.1
**
* Update:
* Subcircuit updated
**
**********************************************************************
**********************************************************************
*                                                                    *
*  CSD23381F4 -  P-Channel 20/12 QFN3x3 2.5x1.8 mm2                  *
*                                                                    *
**********************************************************************
**********************************************************************
*   PSpice Model "CSD23381F4"  - case NOM typical
*
*   OrCAD PSpice MOS model level 7 = BSIM3 v3.3.2
*
*	revision:
*    initial model from csd25481f4 10/23/2013
*********************************************************************
* SUBCKT Definition:  1=D  2=G  3=S
*******************************************************************
.SUBCKT CSD23381F4 1 2 3
.param	refAA	1.71701
.param	pitch	1.53
.param	AA	0.3045
.PARAM  pWidth	{AA/pitch}
.PARAM  perim	{pWidth*2.1}
.PARAM  pSclf	{AA/refAA}
M1  10 11 12 12 PMOS W={pWidth} L=0.5u PD={perim} PS={perim}
DBD  7  8	DBD	{pSclf}
RBD  7  8	1E9
CBD  7  8	{12e-12*pSclf}
*CGD  5  7	{2e-12*pSclf}
*ccc 11 10	{35e-12*pSclf}
DGD 11 13	DD	{pSclf}
DDG 10 13	DD	{pSclf}
*M2   12 11 12 10  NMOSd W={pwidth} L=0.045u  PS={perim} PD={perim}
CRGS	5	101	50e-12
RRGS	101	8	5000
CGS  5  8	{2E-12*pSclf}
LGG  2  5	0.87e-9
RGG  5 11	{4.0/pSclf}
RSB 12  9	RTEMP {1.4E-3/pSclf}
RSS  9  8   RTEMP {7.6e-3/pSclf}
RSX  8  6	{0.25E-3/pSclf}
LSS  6  3	0.08E-9
RDD  7 10   RTEMP {1.5e-3/pSclf}
RDX  4  7	{0.45E-3/pSclf}
LDD  1  4	0.43E-9
DESD	2	3	DZ
******************************************************************* 
.MODEL  PMOS   PMOS (    LEVEL   = 7             Version = 3.2
+ TNOM    = 27           CAPMOD  = 2             LINT   = 0.02e-6
+ TOX     = 2.00E-8      NCH     = 1.255e17      NSUB   = 5.0e15
+ A0      = 1.15         A1      = 0.1           A2     = 1.5
+ UA      = 2.02E-09     UB      = 4.5E-19       VBM    = -5
+ UA1     = 2.47e-9      UB1     = -1.0e-18      UTE    = -1.50
+ KT1     = -0.65        KT2     = 0.02          KT1L   = 1e-14
+ Dvt0    = 2.3          Dvt1    = 0.55          Dvt2   = -0.03
+ PDIBLC1 = 0.05         PDIBLC2 = 0.015         B0     = 0.02
+ VOFF    = -0.11        NFACTOR = 2.10          NLX    = 1.70e-7
+ ETA0    = 0.03         ETAB    = -0.05         XJ     = 6.0e-7
+ PSCBE1  = 8.0e8        PSCBE2  = 3.0e-4		 U0	  = 250
+ DELTA   = 0.08         DSUB    = 1.0           DROUT  = 0.7
+ CJ      = 1e-18        CJSW    = 1e-18         CF     = 0
+ CGSO    = 250e-12      CGDO    = 40e-12        CGBO   = 0 )
******************************************************************* 
.MODEL  NMOSd   NMOS (   LEVEL  = 7             Version = 3.2
+ TNOM   = 27            CAPMOD = 2
+ JS     = 0             VBM    = -10
+ TOX    = 2e-8          NCH    = 1.6e17        NSUB   = 1e16
+ CJ     = 1e-18         CJSW   = 1e-18         CF     = 0
+ CGSO   = 0             CGDO   = 0             CGBO   = 0 )
*******************************************************************
.MODEL DBD D (CJO=947E-12 VJ=0.85 M=0.37 RS=5.0e-3 TRS1=5e-3 
+ TRS2=1e-7 FC=0.5 TT=3E-9 IS=7.6e-13 N=1.01 XTI=4.5 BV=22.5)
*******************************************************************
.MODEL DD D (CJO=150E-12 VJ=0.40 M=0.95 FC=0.5 IS=1E-14 RS=0.1)
******************************************************************* 
.MODEL DZ D (CJO=150E-12 VJ=0.40 M=0.95 FC=0.5 IS=1E-14 RS=0.1 BV=13.2)
******************************************************************* 
.MODEL RTEMP RES (TC1=1.33E-3  TC2=1.0E-07)
******************************************************************* 
.ENDS CSD23381F4 
*$
