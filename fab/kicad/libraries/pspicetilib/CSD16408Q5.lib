***********************************************************************
**  v1.7.5 PSpice Models Library - TI NexFET Power N/P Channel FET's
*  (C) Copyright 2010 Texas Instruments Incorporated. All rights reserved.
***********************************************************************
***********************************************************************
* Notice:
* The data and models provided herein are intended to be used by 
* customers as an aid in designing electrical circuits using 
* semiconductor devices manufactured &/or sold by Texas Instruments.
* The models and associated information contained herein are not to
* be construed as a guarantee of electrical performance and/or device 
* specification.  Therefore, Texas Instruments does not assume any 
* liability arising out of the use of said data or models, nor from
* the devices or products manufactured therefrom.  Texas Instruments
* does not convey any license under its patent rights nor the rights
* of others.  Texas Instruments reserves the right to change models 
* without prior notice.  
*
* TI is a registered trademark of Texas Instruments Inc.
*************************************************************************
********************************************************************************
*
* Released by: Analog e-Lab Design Center, Texas Instruments Inc.
* Part: CSD16408Q5
* Date: 12/08/2010
* Model Type: TRANSIENT
* Simulator: PSPICE
* Datasheet: SLPS228A - October 2009 - Revised September 2010
*
*****************************************************************************
*$
**********************************************************************
**********************************************************************
*                                                                    *
* CSD16408Q5  -  PSpice Model for use with OrCAD                     *
*                                                                    *
**********************************************************************
**********************************************************************
* SUBCKT Definition:  1=D  2=G  3=S
.SUBCKT CSD16408Q5  1 2 3
M1  4 5 6 6 NMOS W=1.42906 L=0.5u 
DBD  6  4	DBD
RBD  6  4	1E11
CGD  5  4	1e-12
DGD  5 10	DGD
RGD  5 10	1E13
DDG  4 10	DDG
RDG  4 10	1E13
DGD2 5 10	DGD2
* Note:  gate oxide capacitance included in model NMOS below
CGS  5  6	2e-12
LGG  2 11	1.18e-9
RGG  11 5	1.1
* Package Resistance given in Rs0
RS0  8  6	0.22e-3
RSS  7  8	RTEMP 0.10E-3
LSS  3  7	0.24E-9
RDD  9  4	RTEMP 0.20E-3
LDD  1  9	0.04E-9
******************************************************************* 
.MODEL  NMOS   NMOS (     LEVEL   = 7      
+ TOX     = 3.0E-8       NCH     = 1.915e17     NSUB    = 5e15
+ RS      = 0.05e-3      RD      = 1.50e-3      VBM     = -10
+ A0      = 1.15         A1      = 0.10         A2      = 0.55
+ UA      = 0.40e-09     UB      = 8.5e-18 
+ UA1     = 3.50e-09     UB1     = -1.0e-19     UTE     = -1.35
+ KT1     = -0.82        KT2     = 0.022        KT1L    = 1e-10
+ Dvt0    = 2.0          Dvt1    = 0.8	      Dvt2    = -0.033
+ ETA     = 0.3          ETA0    = 0.04         ETAB    = -0.07
+ PDIBLC1 = 0.35         PDIBLC2 = 0.005        NLX     = 1.75e-7
+ PSCBE1  = 7.5e8        PSCBE2  = 2.4e-6       AGS     = 1.1
+ VOFF    = -0.100       NFACTOR = 2.0          IS      = 0
+ B0      = 0.01         DSUB    = 0.75         DROUT   = 0.77
+ CBD     = 0            CBS     = 0            CF      = 0
+ CJ      = 0            CJSW    = 0            DELTA   = 0.0025
+ CGSO    = 138e-12      CGDO    = 18e-12       CGBO    = 0  )
******************************************************************* 
.MODEL DBD D (CJO=2410E-12 VJ=1.2 M=0.50 FC=0.5
+ RS=8.3e-4 TRS1=-1.1e-3 IS=1.3E-12 N=1.02 XTI=4.3 TT=5E-9 BV=26)
*******************************************************************
.MODEL DGD D (CJO=280E-12 VJ=0.50 M=0.68 FC=0.5 IS=1E-15 RS=600)
.MODEL DGD2 D (CJO=100E-12 VJ=0.50 M=0.68 FC=0.5 IS=1E-14 RS=0.1)
*******************************************************************
.MODEL DDG D (CJO=725E-12 VJ=0.5 M=0.65 FC=0.5 IS=1E-12)
******************************************************************* 
.MODEL RTEMP RES (TC1=8.6E-3  TC2=7.6E-5)
******************************************************************* 
.ENDS CSD16408Q5
