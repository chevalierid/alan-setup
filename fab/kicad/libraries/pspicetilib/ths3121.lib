* PSpice Model Editor - Version 9.2

*$
* [Disclaimer] (C) Copyright Texas Instruments Incorporated 2001 All rights reserved 
* Texas Instruments Incorporated hereby grants the user of this SPICE Macro-model a 
* non-exclusive, nontransferable license to use this SPICE Macro-model under the following 
* terms. Before using this SPICE Macro-model, the user should read this license. If the 
* user does not accept these terms, the SPICE Macro-model should be returned to Texas 
* Instruments within 30 days. The user is granted this license only to use the SPICE 
* Macro-model and is not granted rights to sell, load, rent, lease or license the SPICE 
* Macro-model in whole or in part, or in modified form to anyone other than user. User may 
* modify the SPICE Macro-model to suit its specific applications but rights to derivative 
* works and such modifications shall belong to Texas Instruments. This SPICE Macro-model is 
* provided on an "AS IS" basis and Texas Instruments makes absolutely no warranty with 
* respect to the information contained herein. TEXAS INSTRUMENTS DISCLAIMS AND CUSTOMER 
* WAIVES ALL WARRANTIES, EXPRESS OR IMPLIED, INCLUDING WARRANTIES OF MERCHANTABILITY OR 
* FITNESS FOR A PARTICULAR PURPOSE. The entire risk as to quality and performance is with 
* the Customer. ACCORDINGLY, IN NO EVENT SHALL THE COMPANY BE LIABLE FOR ANY DAMAGES, 
* WHETHER IN CONTRACT OR TORT,INCLUDING ANY LOST PROFITS OR OTHER INCIDENTAL, CONSEQUENTIAL,
* EXEMPLARY, OR PUNITIVE DAMAGES ARISING OUT OF THE USE OR APPLICATION OF THE SPICE 
* Macro-model provided in this package. Further, Texas Instruments reserves the right to 
* discontinue or make changes without notice to any product herein to improve reliability, 
* function, or design. Texas Instruments does not convey any license under patent rights or 
* any other intellectual property rights, including those of third parties.
*  
*
* THS3121 SUBCIRCUIT 
* WRITTEN 2/10/06
* CONNECTIONS:      NON-INVERTING INPUT
*                   | INVERTING INPUT
*                   | | POSITIVE POWER SUPPLY
*                   | | | NEGATIVE POWER SUPPLY
*                   | | | | OUTPUT
*                   | | | | | 
*                   | | | | | 
*                   | | | | | 
.SUBCKT THS3121     1 2 3 4 5 
*

 Q62          17   1  16 NPN_IN 0.625
 Q61          18   1  12 PNP_IN 0.625
 Q68          13  12   2 NPN_IN 0.625
 Q69          11  16   2 PNP_IN 0.625
 I8           17  12     DC 90u  
 I10          16  18     DC 90u  


 Q124         11  11  31 NPN 0.625
 Q125         32  32  15 NPN 1
 Q126         09  11  33 NPN 0.625
 Q127         13  13  35 PNP 0.625
 Q128         34  34  14 PNP 1
 Q129         09  13  06 PNP 0.625
 R386         18  15  50  
 R387         32  31  600  
 R388         32  33  600  
 R389         35  34  600  
 R390         06  34  600  
 R391         14  17  50 

 C80           0  09  1.5p  

 Q101         18  09  21 PNP 0.3
 Q102         17  09  22 NPN 0.3
 Q122         21  34  19 PNP 3
 Q123         22  32  28 NPN 3
 R385         18  28  60  
 R392         19  17  60  

 Q93          50  30  18 NPN 50
 Q94          30  30  27 NPN 30
 Q97          50  29  17 PNP 65
 Q98          29  29  26 PNP 40
 Q103         30  22  25 PNP 4
 Q104         29  21  24 NPN 2.2
 R367         50  24  50  
 R366         25  50  50  
 R356         26  17  50  
 R352         18  27  50 
 Lout		  50   5 5n
 Cout		  50  51 15p
 Rout    	  51   5 5  
 
 V43           4  18 0.15
 V44          17   3 0.15
 Rbias         3   4  Rtc 80k

.MODEL Rtc RES TC1=-0.0055

.MODEL NPN NPN
+ IS=170E-18 BF=100 NF=1 VAF=100 IKF=0.0389 ISE=7.6E-18
+ NE=1.13489 BR=1.11868 NR=1 VAR=4.46837 IKR=8 ISC=8E-15
+ NC=1.8 RB=250 RE=0.1220 RC=200 CJE=120.2E-15 VJE=1.0888 MJE=0.381406
+ VJC=0.589703 MJC=0.265838 FC=0.1 CJC=133.8E-15 XTF=272.204 TF=12.13E-12
+ VTF=10 ITF=0.147 TR=3E-09 XTB=1 XTI=5

.MODEL NPN_IN NPN
+ IS=170E-18 BF=400 NF=1 VAF=100 IKF=0.0389 ISE=7.6E-18
+ NE=1.13489 BR=1.11868 NR=1 VAR=4.46837 IKR=8 ISC=8E-15
+ NC=1.8 RB=10 RE=0.1220 RC=200 CJE=120.2E-15 VJE=1.0888 MJE=0.381406
+ VJC=0.589703 MJC=0.265838 FC=0.1 CJC=133.8E-15 XTF=272.204 TF=12.13E-12
+ VTF=10 ITF=0.147 TR=3E-09 XTB=1 XTI=5 KF=1.1E-14

.MODEL PNP PNP
+ IS=296E-18 BF=100 NF=1 VAF=100 IKF=0.021 ISE=494E-18
+ NE=1.49168 BR=0.491925 NR=1 VAR=2.35634 IKR=8 ISC=8E-15
+ NC=1.8 RB=250 RE=0.1220 RC=200 CJE=120.2E-15 VJE=0.940007 MJE=0.55
+ VJC=0.588526 MJC=0.55 FC=0.1 CJC=133.8E-15 XTF=141.135 TF=12.13E-12
+ VTF=6.82756 ITF=0.267 TR=3E-09 XTB=1 XTI=5

.MODEL PNP_IN PNP 
+ IS=296E-18 BF=100 NF=1 VAF=100 IKF=0.021 ISE=494E-18
+ NE=1.49168 BR=0.491925 NR=1 VAR=2.35634 IKR=8 ISC=8E-15
+ NC=1.8 RB=10 RE=0.1220 RC=200 CJE=120.2E-15 VJE=0.940007 MJE=0.55
+  VJC=0.588526 MJC=0.55 FC=0.1 CJC=133.8E-15 XTF=141.135 TF=12.13E-12 
+ VTF=6.82756 ITF=0.267 TR=3E-09 XTB=1 XTI=5 KF=1.1E-14

.ENDS

*$
