* CIP: NDA is required as of 08/29/2019
*****************************************************************************
* PSpice Model Editor - Version 16.2.0
*$
* LM5069-2
*****************************************************************************
*  (C) Copyright 2012 Texas Instruments Incorporated. All rights reserved.
*****************************************************************************
** This model is designed as an aid for customers of Texas Instruments.
** TI and its licensors and suppliers make no warranties, either expressed
** or implied, with respect to this model, including the warranties of 
** merchantability or fitness for a particular purpose.  The model is
** provided solely on an "as is" basis.  The entire risk as to its quality
** and performance is with the customer
*****************************************************************************
*
** Released by: WEBENCH Design Center, Texas Instruments Inc.
* Part: LM5069-2
* Date: 6 MAR 2013
* Model Type: Transient
* Simulator: PSPICE
* Simulator Version: 16.2.0.s003
* EVM Order Number: AN-1522
* EVM Users Guide: SNVA184A, 07MAR2008
* Datasheet: SNVS452B, 26SEP2012
*
* Model Version: Final 1.00
*
*****************************************************************************
*
* Updates:
*
* Final 1.00
* Release to Web.
*
*****************************************************************************
.SUBCKT LM5069-2_TRANS VIN UVLO OVLO TIMER GND PWR PGD OUT GATE SENSE
+ PARAMS: Rsense = 0.008 Rpwr = 56k
GB1 GND GATE Value = { IF(V(SHUTDOWN) < 1.75 , 20u , 0)}
D1 GATE 4 _D1_MOD 1 IC=0
.MODEL _D1_MOD D TT=0.1NS VJ=0.6 M=1
E20 OVLODET GND  Value = { IF(V(OVLO) > 2.5, 3.5, 0)}
V1 4 5 DC=11.452V
E1 5 GND VIN GND 1
D9 PWR VDDINT1 _D1_MOD 1 IC=0
D2 VIN SENSE _D2_MOD 1.73 IC=0
.MODEL _D2_MOD D N=1.09 TT=0.1NS VJ=0.6
D3 SENSE VIN _D2_MOD 1.73 IC=0
R2 SENSE OUT 1MEG
VA GND ISUM DC=0
V12 SENSEY SENSE DC=0 AC=0
D4 9 GATE _D1_MOD 1 IC=0
V5 9 GND DC=0.776V
D5 12 GATE _D3_MOD 9.65 IC=0
.MODEL _D3_MOD D N=1.054 RS=2 TT=0.1NS VJ=0.6
D6 OUT 12 _D3_MOD 9.65 IC=0
D7 GATE 13 _D1_MOD 1 IC=0
V6 13 OUT DC=11.452V
GB2 GATE GND Value = { IF(V(SHUTDOWN) > 1.75, 2m,0)}
VX GND 14 DC=0
GB3 GATE GND Value= { IF(I(VA) < 33u, I(VA),33u)}
V9 VIN ILIMREF DC=55M
GB4 ISUM GND Value = { IF(I(VX) > 0 , I(VX) , 0 )}
G1 14 GND ILIMREF SENSEY 8.6E-3
GB6 GND PWR Value = { IF(V(SHUTDOWN) < 1.75 , 20U ,0)}
V11 VDDINT1 GND DC=5V
EB5 ACTIVEILIM GND Value = { IF(I(VX) > 2.3U , 3.5 , 0)}
X2 PWR GND SHUTDOWN GND SSWITCH_0
D8 GND OUT _D1_MOD 1 IC=0
*EB7 PFETLIM GND Value = {0.9*150.00K/5.0000M/(1.25E5)}
EB7 PFETLIM GND Value = {{Rpwr}/{Rsense}/(1.25E5)}
R5 CBDET CBDETDLY 62.5K
GB11 GATE GND Value = { IF(V(CBDETDLY) < 1.75
+ & V(ITPOR) < 1.75 , 0 , 230M )}
VY GND 25 DC=0
GB8 ISUM GND Value= { IF(I(VY) > 0 ,I(VY),0 )}
G2 25 GND PSENSE PFETLIM  1.9456E-6
EB9 ACTIVEPLIM GND Value = { IF (I(VY) > 2.3U , 3.5, 0 )}
EB10 PSENSE GND Value={ V(VIN,SENSE)/{Rsense}*V(SENSE,OUT) }
EB17 UVLODET GND Value = { IF(V(UVLO) > 2.5, 0 ,3.5 )}
C1 CBDETDLY GND 10P
E12 CBDET GND Value = {IF(V(VIN,SENSE) > 105M , 3.5, 0 )}
X3 VIN 32 ENPOR COMPHYST_0
E13 ACTIVE GND Value = { IF ( (V(ACTIVEILIM)>1.75
+| V(ACTIVEPLIM) > 1.75)
+ & (V(SHUTDOWN) < 1.75), 3.5, 0 )}
V13 32 GND DC=8.4
EB15 SHUTDOWN GND Value = {IF ( V(ITPOR) < 1.75
+& V(ENPOR) < 1.75 & V(UVLOCK) < 1.75 &
+ V(OVLOCK) < 1.75 & V(TIMEOUT) < 1.75 , 0 , 3.5 )}
V15 VDDINT2 GND DC=3.5
C12 TIMEOUT GND 1F
EB14 ITPORDET GND Value= { IF ( V(VIN) > 7.6 ,0 ,3.5 )}
X4 ITPORDLY GND ITPORDET GND SSWITCH_1
C2 ITPORDLY GND 144P
R6 VDDINT2 ITPORDLY 1MEG
EB16 ITPOR GND Value =  {IF( V(ITPORDLY) < 1.75 , 3.5 ,0 )}
C3 UVLODETX GND 1P
R7 UVLODETX UVLODET 1K
D10 GND UVLO _D1_MOD 1 IC=0
GB18 UVLO GND Value = { IF( V(UVLODETX) > 1.75 , 21U ,0 )}
X5 VDDINT2 UVLODETDLY VDDINT2 UVLODETX SSWITCH_2
X6 UVLODETDLY GND UVLODETX GND SSWITCH_3
C4 UVLODETDLY GND 15.8P
EB19 UVLOCK GND Value = { IF(V(UVLODETDLY) > 1.75 , 0 , 3.5 )}
C5 OVLODETX GND 1e-12
R10 OVLODETX OVLODET 1K
D11 OVLO VDDINT1 _D1_MOD 1 IC=0
GB21 VDDINT1 OVLO Value = { IF( V(OVLODETX) > 1.75 , 21U, 0 )}
X7 VDDINT2 OVLODETDLY VDDINT2 OVLODETX SSWITCH_2
X8 OVLODETDLY GND OVLODETX GND SSWITCH_3
C6 OVLODETDLY GND 15.8P
EB22 OVLOCK GND Value = { IF ( V(OVLODETDLY) > 1.75 , 0 ,3.5 )}
X9 51 SENSE NPG COMPHYST_1
*V4 OUT 51 DC=2.5V
V4 51 OUT DC=2.5V
EB23 VINDET GND Value = { IF ( V(VIN) > 5 , 3.5 ,0 )}
EB24 NOTPGD GND Value = { IF ( ( ( V(NPG)>1.75 | V(ITPOR)>1.75
+ | V(SHUTDOWN)>1.75)
+ & V(VINDET)>1.75),3.5,0 )}
X10 PGD GND NOTPGD GND SSWITCH_6
D12 80 TIMER _D1_MOD 1 IC=0
D13 TIMER VDDINT1 _D1_MOD 1 IC=0
V3 80 GND DC=0.665586V
GB25 TIMER GND Value= { IF( V(ITIMERFINISH) > 1.75 ,2.5U ,0 )}
GB26 VDDINT1 TIMER Value= { IF ( V(ITIMERFINISH) > 1.75 ,0 ,5.5U )}
GB27 TIMER GND Value = { IF( V(ENDITIMER) > 1.75 , 1.5M ,0 )}
GB28 VDDINT1 TIMER Value = { IF( V(FTIMERON) > 1.75 ,87.5U ,0 )}
EB29 RESTART GND Value = { IF( V(TIMER) < 0.3 , 3.5 ,0 )}
X11 TIMER 81 TIMERCHARGEDET COMPHYST_2
V8 81 GND DC=4
X12 VDDINT2 TIMERCHARGEBAR VDDINT2 TIMERCHARGEDET SSWITCH_3
X13 TIMERCHARGEBAR GND TIMERCHARGEDET GND SSWITCH_3
C7 TIMERCHARGEBAR GND 17.3P
EB30 TIMERCHARGE GND 
+ Value = { IF( V(TIMERCHARGEBAR) < 1.75 , 3.5 ,0 )}
EB31 TIMERISLOW GND Value = { IF( V(RESTART) > 1.75
+ & V(TIMERCHARGE) > 1.75 , 3.5  , 0 )}
XA1 ITPOR  82  83 NOR2
XA2  83 TIMERISLOW   82 NOR2
XA3 83 RESETTIMER INVERTER
XA4  RESETTIMER 84  ITIMERALMFINISH NOR2
XA5  ITIMERALMFINISH TIMERDISCH  84 NOR2
XA6 TIMERCHARGE  TIMERDISCH INVERTER
XA7  RESETTIMER IHOLDOFF  ITIMERFINISH   NOR2
XA8  ITIMERFINISH  85  IHOLDOFF NOR2
XA9  ITIMERALMFINISH TIMERISLOW   85 AND2
C8 ITIMERFINISH GND 1F
XA10  RESETTIMER 86  ENDITIMER   OR2
XA11  ITIMERALMFINISH IHOLDOFF  86 AND2
C9 ENDITIMER GND 1F
XA12  FAULTR 87  FSHUTDOWN NOR2
XA13  FSHUTDOWN FAULTS  87 NOR2
XA14 FSHUTDOWN RESETCOUNTER INVERTER
XA15  TIMERISLOW  COUNTERCOMPLETE  88 AND2
XA16  88 RESETTIMER FAULTR OR2
XA17 ITIMERFINISH  TIMERDISCH FAULTS AND2
XA18  FSHUTDOWN IHOLDOFF  RUNNING NOR2
XA19 RUNNING TIMEOUT   INVERTER
XA20 89 FAULTS VDDINT2  RESETCOUNTBAR Q0 89 DFF
XA22 90 89 VDDINT2  RESETCOUNTBAR Q1 90 DFF
XA23 91 90 VDDINT2  RESETCOUNTBAR Q2 91 DFF
XA25 COUNT111 CHECKTIMER VDDINT2  RESETCOUNTBAR
+ COUNTERCOMPLETE COUNTERNC DFF
XA21 RESETCOUNTER RESETCOUNTBAR INVERTER
XA26 FAULTS CHECKTIMER INVERTER
XA24  Q2 Q1 Q0  COUNT111 AND3
XA27  COUNTERNC FSHUTDOWN TIMERCHARGE   92 AND3
XA28  92 93  FTIMERON   OR2
XA29  RUNNING ACTIVE   93 AND2
C10 FTIMERON GND 1F
.ENDS
*$
.SUBCKT INV_DELAY_BASIC_GEN A  Y PARAMS: VDD=3.5 VSS=0
+ VTHRESH=1.75 DELAY = 1n
E_ABMGATE1 YINT1 0 VALUE {IF(V(A)>{VTHRESH},{VDD},{VSS})}
RINT YINT1 YINT2 1
CINT YINT2 0 {DELAY*1.3}
E_ABMGATE2 YINT3 0 VALUE {IF(V(YINT2)>{VTHRESH},{VSS},{VDD})}
RINT2 YINT3 Y 1
CINT2 Y 0 1n
.ENDS
*$
.SUBCKT BUF_DELAY_BASIC_GEN A  Y PARAMS: VDD=3.5 VSS=0
+  VTHRESH=1.75 DELAY = 1n
E_ABMGATE1    YINT1 0 VALUE {{IF(V(A) > {VTHRESH} ,
+ {VDD},{VSS})}}
RINT YINT1 YINT2 1
CINT YINT2 0 {DELAY*1.3}
E_ABMGATE2    YINT3 0 VALUE {{IF(V(YINT2) > {VTHRESH} ,
+ {VDD},{VSS})}}
RINT2 YINT3 Y 1
CINT2 Y 0 1n
.ENDS
*$
.SUBCKT AND2_BASIC_GEN A B Y PARAMS: VDD=3.5 VSS=0 VTHRESH= 1.75
E_ABMGATE YINT 0 VALUE = {IF(V(A) > {VTHRESH} & V(B) > {VTHRESH},{VDD},
+ IF(V(A) < {VTHRESH} | V(B) < {VTHRESH},{VSS}, V(Y)))}
RINT YINT Y 1
CINT Y 0 1n
.ENDS
*$
.SUBCKT NOR2 1 2 3
EB1 4 0 VALUE ={IF(V(1)>1.75 | V(2)>1.75, 0.3, 3.5)}
RD 4 3 1
CD 3 0 0.2nF
.ENDS
*$
.SUBCKT INVERTER 1 2
EB1 3 0 VALUE ={IF(V(1)>1.75 , 0.3, 3.5)}
RD 3 2 1
CD 2 0 0.2nF
.ENDS
*$
.SUBCKT AND2 1 2 3
EB1 4 0 VALUE ={IF(V(1)>1.75 & V(2)>1.75, 3.5, 0.3)}
RD 4 3 1
CD 3 0 0.2nF
.ENDS
*$
.SUBCKT OR2 1 2 3
EB1 4 0 VALUE ={IF(V(1)>1.75 | V(2)>1.75, 3.5, 0.3)}
RD 4 3 1
CD 3 0 0.2nF
.ENDS
*$
.SUBCKT AND3 1 2 3 4
EB1 5 0 VALUE ={IF(V(1)>1.75 & V(2)>1.75 & V(3)>1.75 , 3.5, 0.3)}
RD 5 4 1
CD 4 0 0.2nF
.ENDS
*$
.SUBCKT  DFF D CLK SB RB Q QB
+ PARAMS: VDD=5 VSS=0 VTHRESH=1.75
XLAT_M SB RB CLK D Q_M DLAT_0
XLAT_S SB RB NCLK Q_M Q_S DLAT_0
ENCLK NCLK 0 VALUE = {IF(V(CLK) > VTHRESH, 0,VDD)}
EQB_S QB_S 0 VALUE = {IF(V(Q_S) > VTHRESH,0,VDD)}
RQB QB_S QB 1
Cdummy1 QB 0 1nF
RQ Q_S Q  1
Cdummy2 Q 0 1nF
.ENDS
*$
.SUBCKT SSWITCH_0 1 2 3 4
+ PARAMS:RON=160 ROFF=1G VON=1.8  VOFF=1.7
*CONNECTIONS    + - NC+ NC-
*PARAMETERS: RON ON RESISTANCE IN OHMS, ROFF OFF RESISTANCE IN OHMS
* VON ON CURRENT IN AMPS, VOFF OFF CURRENT IN AMPS
* IF V(3,4) > VON THEN RSWITCH=RON,
* IF V(3,4) < VOFF THEN RSWITCH=ROFF, ELSE
* RSWITCH WILL SMOOTHLY TRANSISTION BETWEEN THE ON AND OFF STATES
S_A1 1 2 3 4 SMOOTH
.MODEL SMOOTH VSWITCH RON={RON} ROFF={ROFF} VON={VON}  VOFF={VOFF}
.ENDS
*$
.SUBCKT SSWITCH_1 1 2 3 4
+ PARAMS:RON=1 ROFF=1G VON=1.8  VOFF=1.7
*CONNECTIONS    + - NC+ NC-
*PARAMETERS: RON ON RESISTANCE IN OHMS, ROFF OFF RESISTANCE IN OHMS
* VON ON CURRENT IN AMPS, VOFF OFF CURRENT IN AMPS
* IF V(3,4) > VON THEN RSWITCH=RON,
* IF V(3,4) < VOFF THEN RSWITCH=ROFF, ELSE
* RSWITCH WILL SMOOTHLY TRANSISTION BETWEEN THE ON AND OFF STATES
S_A1 1 2 3 4 SMOOTH
.MODEL SMOOTH VSWITCH RON={RON} ROFF={ROFF} VON={VON}  VOFF={VOFF}
.ENDS
*$
.SUBCKT SSWITCH_2 1 2 3 4
+ PARAMS:RON=5MEG ROFF=1G VON=1.8  VOFF=1.7
*CONNECTIONS    + - NC+ NC-
*PARAMETERS: RON ON RESISTANCE IN OHMS, ROFF OFF RESISTANCE IN OHMS
* VON ON CURRENT IN AMPS, VOFF OFF CURRENT IN AMPS
* IF V(3,4) > VON THEN RSWITCH=RON, 
* IF V(3,4) < VOFF THEN RSWITCH=ROFF, ELSE
* RSWITCH WILL SMOOTHLY TRANSISTION BETWEEN THE ON AND OFF STATES
S_A1 1 2 3 4 SMOOTH
.MODEL SMOOTH VSWITCH RON={RON} ROFF={ROFF} VON={VON}  VOFF={VOFF}
.ENDS
*$
.SUBCKT SSWITCH_3 1 2 3 4
+ PARAMS:RON=1MEG ROFF=1G VON=1.8  VOFF=1.7
*CONNECTIONS    + - NC+ NC-
*PARAMETERS: RON ON RESISTANCE IN OHMS, ROFF OFF RESISTANCE IN OHMS
* VON ON CURRENT IN AMPS, VOFF OFF CURRENT IN AMPS
* IF V(3,4) > VON THEN RSWITCH=RON,
* IF V(3,4) < VOFF THEN RSWITCH=ROFF, ELSE
* RSWITCH WILL SMOOTHLY TRANSISTION BETWEEN THE ON AND OFF STATES
S_A1 1 2 3 4 SMOOTH
.MODEL SMOOTH VSWITCH RON={RON} ROFF={ROFF} VON={VON}  VOFF={VOFF}
.ENDS
*$
.SUBCKT SSWITCH_6 1 2 3 4
+ PARAMS:RON=30 ROFF=100MEG VON=1.8  VOFF=1.7
*CONNECTIONS    + - NC+ NC-
*PARAMETERS: RON ON RESISTANCE IN OHMS, ROFF OFF RESISTANCE IN OHMS
* VON ON CURRENT IN AMPS, VOFF OFF CURRENT IN AMPS
* IF V(3,4) > VON THEN RSWITCH=RON,
* IF V(3,4) < VOFF THEN RSWITCH=ROFF, ELSE
* RSWITCH WILL SMOOTHLY TRANSISTION BETWEEN THE ON AND OFF STATES
S_A1 1 2 3 4 SMOOTH
.MODEL SMOOTH VSWITCH RON={RON} ROFF={ROFF} VON={VON}  VOFF={VOFF}
.ENDS
*$
.SUBCKT COMPHYST_0 LO HI OUT
*FAMILY = ANALOG
*FAMILY = ANALOG
*FAMILY = ANALOG
S1 2 OUT HI LO _S1_MOD
R1 OUT 0 1K
EB2 2 0 Value= {3.5}
R2 LO 0 1E12
R3 HI 0 1E12
.MODEL _S1_MOD VSWITCH VON=90.000M VOFF=0.000M ROFF=1MEG RON=1m
.ENDS
*$
.SUBCKT COMPHYST_1 LO HI OUT
*FAMILY = ANALOG
*FAMILY = ANALOG
*FAMILY = ANALOG
S1 2 OUT HI LO _S1_MOD
R1 OUT 0 1K
EB2 2 0 Value= { 3.5 }
R2 LO 0 1E12
R3 HI 0 1E12
.MODEL _S1_MOD VSWITCH VT=625.00M VH=625.00M ROFF=1MEG
.ENDS
*$
.SUBCKT COMPHYST_2 LO HI OUT
*FAMILY = ANALOG
*FAMILY = ANALOG
*FAMILY = ANALOG
S1 2 OUT HI LO _S1_MOD
*S_A3 2 OUT HI LO SMOOTH
R1 OUT 0 1K
EB2 2 0 Value= {3.5 }
R2 LO 0 1E12
R3 HI 0 1E12
.MODEL _S1_MOD VSWITCH VT=1.3750  VH=1.3750  ROFF=1MEG
*.MODEL _S1_MOD VSWITCH VON=2.74 VOFF=0 ROFF=1MEG RON=1m
.ENDS
*$
.SUBCKT DLAT_0 SET RST CLK D Q
+ PARAMS: VDD=5 VSS=0 VTHRESH=1.75
EN1 N1 0 VALUE = {IF((V(N3)<VTHRESH | V(RST)< VTHRESH) , 0, VDD)}
EN2 N2 0 Value = {IF((V(N1)>VTHRESH | V(SET) < VTHRESH), VDD, 0)}
EN3 N3 0 VALUE = {IF(V(CLK)>VTHRESH , V(N2), V(D))}
RQ N3 Q 1
CQ Q 0 1nF
.ENDS
*$
.SUBCKT C_load 13 2 PARAMS: IC = 60.0
* Steve's Tantalum Model w/Initial Conditions
* C=1.9999999999999998E-4 F
* ESR@1K=0.0050 Ohm
* ESL=8.0E-9 H
* RLEAK=1000000.0 Ohm
* IC=60.0 V
C3 13 11 2.4999999999999998E-5 IC={IC}
C4 13 3 1.2499999999999999E-5 IC={IC}
C5 13 4 6.2499999999999995E-6 IC={IC}
R1 4 5 0.00625
R2 3 6 0.0045000000000000005
R3 11 7 0.00875
R4 12 8 0.0116
C6 13 9 9.999999999999999E-5 IC={IC}
R5 13 0 1000000.0
R6 9 10 0.03125
L1 5 2 8.0E-9
L2 6 2 8.0E-9
L3 7 2 8.0E-9
L4 8 2 8.0E-9
L5 10 2 8.0E-9
R8 2 0 1000000.0
C2 13 12 4.9999999999999996E-5 IC={IC}
.ENDS
*$
.SUBCKT Z1 A K
D1 A K Z1
.MODEL Z1 D ( BV=500 CJO=470E-12 EG=0.69 IBV=1E-6
+ IS=9E-6 M=0.8 N=1.442 RS=60E-3 VJ=1 XTI=2 )
.ENDS
*$
.SUBCKT SUM40N15-38 drain gate source
* Cgd = 1.67e-9
* F -> Gate to drain Capacitance fit to approximate gate charge
* Cgs = 0.98e-9 F -> Gate to source Capacitance
* Cj0 = 1.74e-9 F -> Approximated
* Source to Drain Capacitance at Vds=0 (by given Coss at certain Vds)
* VdsMax = 100 V -> Maximum drain to source Voltage
* RdsON = (2 * 0.0022 ) Ohms -> MOSFET on resistance
* Rgate = 1 Ohms -> Gate Resistance
* Vth = 2 V -> Threshold Voltage
Cgd g d 1.67e-9
M1 d g s s _M1_modX L=1u W=1u
.MODEL _M1_modX NMOS CGDO=0 CGSO=0 KP=100 RD=0 RS=0
+ TOX=1E-7 UO=600 VTO= 2
Rds2 source s 0.0022
Rds1 drain d 0.0022
RG gate g 1
Cgs g s 0.98e-9
Dbd source drain Dbt
.MODEL Dbt D(BV= 100 CJO=1.74e-9 M= .35 VJ= .9 )
.ENDS
*$
