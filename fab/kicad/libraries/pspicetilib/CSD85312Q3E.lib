***********************************************************************
*  (C) Copyright 2013 Texas Instruments Incorporated. All rights reserved.
***********************************************************************
***********************************************************************
** This model is designed as an aid for customers of Texas Instruments.
** TI and its licensors and suppliers make no warranties, either expressed
** or implied, with respect to this model, including the warranties of
** merchantability or fitness for a particular purpose. The model is
** provided solely on an "as is" basis. The entire risk as to its quality
** and performance is with the customer
********************************************************************************
*
* Released by: WEBENCH(R) Design Center, Texas Instruments Inc.
* Part: CSD85312Q3E
* Date: 12/12/2013
* Model Type: TRANSIENT
* Simulator: PSPICE
* Simulator Version: 16.2
* EVM Order Number: 
* EVM Users Guide: 
* Datasheet: SLPS457 - NOVEMBER 2013
* Model Version: Final 1.00
*
*****************************************************************************
*
* Updates:
*
* Final 1.00
* Release to Web
*
*****************************************************************************
*
* Model Usage Notes:
*
*****************************************************************************

**********************************************************************
**********************************************************************
*                                                                    *
*   csd85312q3e   -  PSpice Model                                    *
*                                                                    *
*   csd85312q3e is the silicon die model for use with csd85312q3e    *
*                                                                    *
**********************************************************************
**********************************************************************
* SUBCKT Definition:  1=D1  2=D2	3=G  3=S
**********************************************************************
.subckt csd85312q3e D1 D2 G S
.PARAM  ptrc1    2.425e-3		; temp co of ext Rd / Rs
.PARAM  ptrc2    6.0e-6
x1 D1 G1 S1	csd85312q3eq1
x2 D2 G2 S2	csd85312q3eq2
RS1	  S1	S	0.26e-3
RS2	  S2	S	0.26e-3
RG1	G	G1	5.0
RG2	G	G2	16.81
.ends
.SUBCKT CSD85312Q3EQ1 1 2 3
* PARAMETER (local) DEFINITIONS SECTION
.PARAM  pWidthP  0.5474252
.PARAM  pWidth   {pWidthP*1e6}
.PARAM  pLen     0.4u
.PARAM  perimP   {2.1*pWidthP}
.PARAM  perim    {perimP*1e6}
.PARAM  ptrc1    2.300e-3		; temp co of ext Rd / Rs
.PARAM  ptrc2    5.0e-6
****** MODEL SUBCIRCUIT BEGINS HERE     ******************************
M1   10 11 12 12  NMOS	W={pWidthP} L={pLen} PS={perimP} PD={perimP}
DBD   3  1	 DBD
CBD0  3  1   148e-12
RBD0  3  1   9e9
CGD0  2 13	 2E-12
RGD  13  1   2e6
CGS0  2  3	 1e-18
CGS   2 99 5e-12
RGS   99 3 7.5
DDG  14  11	 DD
DGD  14 10	 DD
RGG   2 11	 0.85
RSB  12  9	 rtemp {(0.2094067+0.1322350)*1e-3}
RSS   9  3 	 rtemp {(0.2094067+0.1322350)*1e-3}
RDD   1 10 	 rtemp {(2.0940667+1.3223500)*1e-3}
******************************************************************* 
.MODEL  NMOS   NMOS (    LEVEL  = 7             Version = 3.2  
+ TNOM   = 27            LINT   = 0.050e-6      CAPMOD = 2
+ TOX    = 1.75e-8       NCH    = 2.125e17      NSUB   = 1.5e16
+ AGS    = 0.0           PVAG   = 0.0           U0     = 1250.0
+ A0     = 1.10          A1     = 0.0           A2     = 1.0
+ UA     = 2.25e-9       UB     = 0.10e-19      VBM    = -10
+ UA1    = 0.01e-9       UB1    = -0.01e-18     UTE    = -1.50
+ KT1    = -0.700        KT2    = 0.022         KT1L   = 1.0e-15
+ DVT0   = 2.2           DVT1   = 0.53	      DVT2   = -0.033
+ ETA0   = 0.020         ETAB   = -0.07         XJ     = 4e-7
+ PDIBLC1= 0.1           PDIBLC2= 0.005         NLX    = 1.75e-7
+ PSCBE1 = 7.0e8         PSCBE2 = 2e-7          PCLM   = 0.75
+ VOFF   = -0.158        NFACTOR= 1.83          JS     = 0
+ DROUT  = 0.9           DSUB   = 1.3           DELTA  = 0.045
+ CJ     = 1e-18         CJSW   = 1e-18         CF     = 0
+ CGSO   = 29.00e-10    CGDO   = 0.1e-12       CGBO   = 1e-18  )
******************************************************************* 
.MODEL  PMOSd   PMOS (   LEVEL  = 1
+ TNOM   = 27            VTO    = -0.90         IS     = 7e-14
+ TOX    = 1.75e-8       NSUB   = 5.0e17	      gamma  = 4.0
+ CJ     = 1e-18         CJSW   = 1e-18
+ CGSO   = 1e-18         CGDO   = 1e-18         CGBO   = 1e-18  )
******************************************************************* 
.MODEL DBD D (CJO=1.00e-9 VJ=1.00 M=0.5 TNOM=27 FC=0.5 TT=1e-09 
+ XTI=-1.0 BV=21 IS=1.0e-10 N=1.15 RS=2.50e-3 TRS1=3.5e-4)
.MODEL DD  D (CJO=3.0e-10 VJ=0.50 M=0.75 IS=1e-13 TNOM=27)
*******************************************************************
.model rtemp res (tc1={ptrc1} tc2={ptrc2})
.ENDS CSD85312Q3EQ1 
*$
.SUBCKT CSD85312Q3EQ2 1 2 3
* PARAMETER (local) DEFINITIONS SECTION
* PARAMETER (local) DEFINITIONS SECTION
.PARAM  pWidthP  0.5474252
.PARAM  pWidth   {pWidthP*1e6}
.PARAM  pLen     0.4u
.PARAM  perimP   {2.1*pWidthP}
.PARAM  perim    {perimP*1e6}
.PARAM  ptrc1    2.300e-3		; temp co of ext Rd / Rs
.PARAM  ptrc2    5.0e-6
****** MODEL SUBCIRCUIT BEGINS HERE     ******************************
M1   10 11 12 12  NMOS	W={pWidthP} L={pLen} PS={perimP} PD={perimP}
DBD   3  1	 DBD
CBD0  3  1   148e-12
RBD0  3  1   9e9
CGD0  2 13	 2E-12
RGD  13  1   2e6
CGS0  2  3	 1e-18
CGS   2 99 5e-12
RGS   99 3 7.5
DDG  14  11	 DD
DGD  14 10	 DD
RGG   2 11	 0.85
RSB  12  9	 rtemp {(0.2094067+0.1322350)*1e-3}
RSS   9  3 	 rtemp {(0.2094067+0.1322350)*1e-3}
RDD   1 10 	 rtemp {(2.0940667+1.3223500)*1e-3}
******************************************************************* 
.MODEL  NMOS   NMOS (    LEVEL  = 7             Version = 3.2  
+ TNOM   = 27            LINT   = 0.050e-6      CAPMOD = 2
+ TOX    = 1.75e-8       NCH    = 2.100e17      NSUB   = 1.5e16
+ AGS    = 0.0           PVAG   = 0.0           U0     = 1250.0
+ A0     = 1.10          A1     = 0.0           A2     = 1.0
+ UA     = 2.25e-9       UB     = 0.10e-19      VBM    = -10
+ UA1    = 0.01e-9       UB1    = -0.01e-18     UTE    = -1.50
+ KT1    = -0.700        KT2    = 0.022         KT1L   = 1.0e-15
+ DVT0   = 2.2           DVT1   = 0.53	      DVT2   = -0.033
+ ETA0   = 0.020         ETAB   = -0.07         XJ     = 4e-7
+ PDIBLC1= 0.1           PDIBLC2= 0.005         NLX    = 1.75e-7
+ PSCBE1 = 7.0e8         PSCBE2 = 2e-7          PCLM   = 0.75
+ VOFF   = -0.158        NFACTOR= 1.83          JS     = 0
+ DROUT  = 0.9           DSUB   = 1.3           DELTA  = 0.045
+ CJ     = 1e-18         CJSW   = 1e-18         CF     = 0
+ CGSO   = 29.00e-10    CGDO   = 0.1e-12       CGBO   = 1e-18  )
******************************************************************* 
.MODEL  PMOSd   PMOS (   LEVEL  = 1
+ TNOM   = 27            VTO    = -0.90         IS     = 7e-14
+ TOX    = 1.75e-8       NSUB   = 5.0e17	      gamma  = 4.0
+ CJ     = 1e-18         CJSW   = 1e-18
+ CGSO   = 1e-18         CGDO   = 1e-18         CGBO   = 1e-18  )
******************************************************************* 
.MODEL DBD D (CJO=1.00e-9 VJ=1.00 M=0.5 TNOM=27 FC=0.5 TT=1e-09 
+ XTI=-1.0 BV=21 IS=1.0e-10 N=1.15 RS=2.50e-3 TRS1=3.5e-4)
.MODEL DD  D (CJO=3.0e-10 VJ=0.50 M=0.75 IS=1e-13 TNOM=27)
*******************************************************************
.model rtemp res (tc1={ptrc1} tc2={ptrc2})
.ENDS CSD85312Q3EQ2 
*$
