* PSpice Model Editor - Version 16.0.0

*$
* TPS61020
*****************************************************************************
*  (C) Copyright 2009Texas Instruments Incorporated. All rights reserved.  
*****************************************************************************
** Thismodelis designed as an aid for customers of Texas Instruments.     
**TI and itslicensors and suppliers makeno warranties, either expressed  
** or implied, with respect to thismodel, including thewarranties of     
** merchantability or fitness fora particular purpose. The model is      
** provided solely on an "as is" basis. The entirerisk as to its quality 
** and performance is with the customer                                  
*****************************************************************************
*
** Released by: Analog eLab Design Center, Texas Instruments Inc.
* Part: TPS61020
* Date: 27APR2009
* Model Type: TRANSIENT
* Simulator: PSPICE
* Simulator Version:16.0.0.s001  
* EVM Order Number: TPS61020EVM - HPA025RevA
* EVM Users Guide: SLVU100- NOV 2003
* Datasheet: SLVS451D-REVISED FEBRUARY 2006
*
*****************************************************************************
*
* Updates:
*
* Final 1.00
* Release to Web.
*
*****************************************************************************
.SUBCKT TPS61020 SW VBAT EN LBI PS GND VOUT FB LBO PGND 
X1 SW VBAT EN LBI PS GND VOUT FB LBO PGND TPS6102X PARAMS: Isw=1500m
.ENDS TPS61020
*$
.SUBCKT TPS6102X SW VBAT EN LBI PS GND VOUT FB LBO PGND 
+ PARAMS: Isw=1500m
*
V1          Vref GND 500M
Vref5       Ref5 GND 5
XErrAmp      Vref FBflt VRT GND Itimer   TRANSCOND_AMP
+ PARAMS: RIN=1E12 ROUT=250MEG IOUTMAXSOURCE=20E-6 IOUTMAXSINK=20E-6 K=10 
CSensor     FBflt GND 5P IC=497M
Rfilt       FB FBflt 180K 
XAntiNegRinging  VOUT DRV PSMEn AROn Ref5 SW VBAT GND PMOSOn SWOn VDD  Anti-Ringing
XContrLog    EN UVLO SWOn VRT DRV Ref5 PMOSOn PS SW VOUT ShDwn_ NMOSOn GND Itimer Startup PSMOn FB VBAT VDD Vref 
+ CurrLimN DM AROn Steady PSMEn   Control_Logic__Oscillator__Gate_Drive
XLBIComp     Vref LBI ShDwn_ GND SLBO GND   COMP2INPAND
+ PARAMS: VTHRES1=0 VTHRES2=2.5 VHYST1=10M VHYST2=10M VOUTH=5 RIN=1E12 DELAY=0
+ VOUTL=100M
SWLBO       LBO GND SLBO GND  S_VSWITCH_1
XGateContr   DRVP VDD NMOSOn GND PMOSOn VOUT DM Startup DRVN VBAT SW   MOSDrv
XCurrSense   CurrLimN VOUT PMOSOn GND SWOn Steady Ref5 NMOSOn SW   Current_Sense__Current_Limit
+ PARAMS: Isw={Isw}
XBackgate    Startup SW BG PM_S DM VOUT ShDwn_ GND   Backgate_Control
XMN1         SW DRVN PGND   DRVNMOS
XMP1         SW DRVP PM_S BG   DRVPMOS
Ccomp       35 FB 20P 
Rcomp       VOUT 35 10K 
RPS         PS GND 9MEG 
RPGND       GND PGND 1M 
VISD_in     36 GND
FISD        VBAT GND VISD_in     1
RSD         Vref 36 2.332MEG 
RGnd        GND 0 1G 
RFB         FB GND 250MEG 
RLBI        LBI GND 50MEG 
REn         EN GND 9MEG 
XUVLO        GND VBAT UVLO   UVLO
.MODEL S_VSWITCH_1 VSWITCH (RON=400 ROFF=33MEG VON=2 VOFF=1)
.ENDS TPS6102X
*$
.SUBCKT Anti-Ringing VOUT DRV PSMEn AROn Ref5 SW VBAT GND PMOSOn SWOn VDD
CdAR        37 GND 10P 
RdAR        PMOSOn 37 7.215007215K 
XARComp      VDD SW GND SWOn GND DRV PSMEn GND AROn GND   COMP4INPAND
+ PARAMS: VTHRES1=-10M VTHRES2=-2.5 VTHRES3=-2.5 VTHRES4=2.5 VOUTH=5.0 VOUTL=0.0
+ DELAY=50N RIN=1E12 VHYST1=5M VHYST2=0 VHYST3=0 VHYST4=0
SARSW       VBAT SW AROn 37  S_VSWITCH_1
.MODEL S_VSWITCH_1 VSWITCH (RON=80 ROFF=100MEG VON=2 VOFF=1)
.ENDS 
*$
.SUBCKT Control_Logic__Oscillator__Gate_Drive EN UVLO_ SWOn IERR DRV Ref5 PMOSOn PS SW VOUT ShDwn_ 
+ NMOSOn GND Itimer Startup PSMOn FB VBAT VDD Vref CurrLimN DM AROn Steady PSMEn 
*
VReset      RESET GND DC 0 AC 1 0
+ PWL ( 0 5 100n 5 101n 0 1u 0 )
VT          41 VBAT 1
XU7          OscOn DRV1 DRV Ref5 GND   AND2
+ PARAMS: DLH=0 DHL=0
XPMFF        OSC PRES PMFF PMFFQ_ Ref5 GND   RSFF
+ PARAMS: VOUTH=5.0 VOUTL=0 RIN=1E12 DELAY=1N ROUT=10 VTHRESS=2.5 VTHRESR=2.5 
XNMFF        DRVP NRES NMFF NMOSOn_B Ref5 GND   RSFF
+ PARAMS: VOUTH=5.0 VOUTL=0 RIN=1E12 DELAY=1N ROUT=10 VTHRESS=2.5 VTHRESR=2.5 
XU3          OSC PMFFQ_ PMOSOnFF Ref5 GND   NOR2
+ PARAMS: DLH=0 DHL=0
XU6          DRVP NMOSOn_B NMOSOn Ref5 GND   NOR2
+ PARAMS: DLH=0 DHL=0
XU4          DRV DRV_ Ref5 GND   INV
+ PARAMS: DLH=40N DHL=40N 
XU2          DRV DRV_ DRVP Ref5 GND   AND2
+ PARAMS: DLH=0 DHL=0
XU1          OscOn GND Ramp OSC   OSCILLATOR
+ PARAMS: FREQ=600K DUTYCYC=0.024 RISETIME=1E-9 FALLTIME=1E-9 VOUTH=5 VOUTL=0
+ VENTHRES=2.5
XCMPT        VILRsw IERR OSC GND DRV1 GND   COMP2INPOR
+ PARAMS: RIN=1E12 DELAY=0 VOUTH=5 VOUTL=100M VTHRES1=-60M VHYST1=1M VTHRES2=2.5
+ VHYST2=0
XVNOMComp    Vref FBf Steady GND   HYSTCOMP
+ PARAMS: VTHRES=100M VHYST=70M VOUTH=5 VOUTL=100M ROUT=0 TDEL=1N 
SWILRsw     SW VILRsw SWOn NMOSOn_B  S_VSWITCH_1
CIL         VILRsw GND 10P 
XPSM         FBf RESET Vref ShDwn_ VBAT VOUT DM GND AROn PS Ref5 PSMOn PSMEn   Power_Save_Mode
CSensor     58 GND 100P 
RfOut       VOUT 58 28.860029K 
CfFB        FBf GND 1.85185185185P 
RfFB        FB FBf 1800K 
XPMOSOnComp  PMOSOnFF GND GND DM GND Steady GND Startup PMOSOn GND   COMP4INPANDS
+ PARAMS: VTHRES1=2.5 VTHRES2=-2.5 VTHRES3=-2.5 VTHRES4=-2.5 VOUTH=5.0 VOUTL=0.0
+ DELAY=0 RIN=1E12 VHYST1=0 VHYST2=0 VHYST3=0 VHYST4=0
XPRESComp    GND OscOn DRVP GND AROn GND RESET GND PRES GND   COMP4INPORS
+ PARAMS: VTHRES1=-2.5 VTHRES2=2.5 VTHRES3=2.5 VTHRES4=2.5 VOUTH=5.0 VOUTL=0.0
+ DELAY=0N RIN=1E12 VHYST1=0 VHYST2=0 VHYST3=0 VHYST4=0
XNRESComp    GND OscOn OSC GND CurrLimN GND RESET GND NRES GND   COMP4INPORS
+ PARAMS: VTHRES1=-2.5 VTHRES2=2.5 VTHRES3=2.5 VTHRES4=2.5 VOUTH=5.0 VOUTL=0.0
+ RIN=1E12
XD1          GND IERR   D_D
GTCGEN      IERR Itimer VALUE = {IF(V(DM,GND)>2.5,V(41,GND)*6U,V(VOUT,GND)*6U)}
C           IERR GND 10P 
SWRT        VBAT IERR OSC GND  S_VSWITCH_2
XSWOnComp    VBAT SW SWOn GND   COMPGNDREFS
+ PARAMS: VTHRES=200M VHYST=100M VOUTH=5 DELAY=100N ROUT=0 VOUTL=100M 
XVoViComp    58 VBAT VoutuVin GND   COMPGNDREFS
+ PARAMS: VTHRES=-125M VHYST=25M VOUTH=5 VOUTL=100M ROUT=0 DELAY=0 
XINTEnComp   ShDwn_ GND GND PSMOn GND Startup OscOn GND   COMP3INPANDS
+ PARAMS: RIN=1E12 DELAY=0 VOUTH=5 VOUTL=-100M VTHRES1=2.5 VHYST1=0 VTHRES2=-2.5
+ VHYST2=0 VTHRES3=-2.5 VHYST3=0
SWVOUT      VDD VOUT Vout_Vin GND  S_VSWITCH_3
SWVBAT      VDD VBAT Vout_Vin GND  S_VSWITCH_4
XDMComp      GND Steady Vout_Vin GND ShDwn_ GND DM GND   COMP3INPANDS
+ PARAMS: RIN=1E12 VOUTH=5 VOUTL=100M VTHRES1=-2.5 VTHRES2=2.5 VTHRES3=2.5 
XStartupComp  ShDwn_ GND GND VoutuVin Steady GND Startup GND   COMP3INPANDS
+ PARAMS: RIN=1E12 VOUTH=5 VOUTL=100M VTHRES1=2.5 VTHRES2=-2.5 VTHRES3=2.5 
XViVoComp    VBAT 58 Vout_Vin GND   COMPGNDREFS
+ PARAMS: VTHRES=-25M VHYST=25M VOUTH=5 VOUTL=100M ROUT=0 DELAY=0
GIQ         VBAT GND OscOn GND  6.16U
RTrigger    OSC Vref 1MEG 
XShDComp     UVLO_ GND IEN GND ShDwn_ GND   COMP2INPANDS
+ PARAMS: VTHRES1=2.5 VTHRES2=2.5 VOUTH=5.0 VOUTL=100M RIN=1E12 
R2          VBAT 59 30MEG 
R1          59 GND 60MEG 
XENComp      EN 59 IEN GND   COMPGNDREFS
+ PARAMS: VTHRES=10.0M VHYST=1.0M VOUTH=5 VOUTL=100M ROUT=0 DELAY=0
.MODEL S_VSWITCH_1 VSWITCH (RON=100K ROFF=1G VON=3 VOFF=2)
.MODEL S_VSWITCH_2 VSWITCH (RON=200 ROFF=1G VON=3 VOFF=2)
.MODEL S_VSWITCH_3 VSWITCH (RON=10M ROFF=100MEG VON=2 VOFF=3)
.MODEL S_VSWITCH_4 VSWITCH (RON=10M ROFF=100MEG VON=3 VOFF=2)
.ENDS
*$
.SUBCKT Power_Save_Mode FBf Reset Vref ShDwn_ VBAT VOUT DM GND AROn PS Ref5 PSMOn PSMEn
XPSMRComp    Vref FBf Reset GND GND PSMEn  PSMR GND   COMP3INPOR
+ PARAMS: RIN=1E12 VOUTH=5 VOUTL=0 VTHRES1=7M VTHRES2=2.5 VTHRES3=-2.5 DELAY=50N
+ VHYST1=1.5M VHYST2=0 VHYST3=0
XPFMLatch    AROn  PSMR PSMOn 60 Ref5 GND   RSFF
+ PARAMS: VOUTH=5.0 VOUTL=0 RIN=1E12 DELAY=1N ROUT=10 VTHRESS=2.5 VTHRESR=2.5
XPSMEnCmp    GND PS DM GND PSMEn GND   COMP2INPORS
+ PARAMS: VTHRES1=-0.5 VTHRES2=2.5 VOUTH=5 RIN=1E12 VOUTL=0 
.ENDS
*$
.SUBCKT MOSDrv DRVP VDD NMOSOn GND PMOSOn VOUT DM Startup DRVN VBAT SW
XScVgs       VBAT VDD VOUT SW DM Startup VG GND   SCVGS
SWPL        VG DRVP PMOSOn GND  S_VSWITCH_1
SWPH        GND DRVP PMOSOn GND  S_VSWITCH_2
SWNL        GND DRVN NMOSOn GND  S_VSWITCH_3
SWNH        VDD DRVN NMOSOn GND  S_VSWITCH_4
.MODEL S_VSWITCH_1 VSWITCH (RON=10 ROFF=100MEG VON=2 VOFF=3)
.MODEL S_VSWITCH_2 VSWITCH (RON=10 ROFF=100MEG VON=3 VOFF=2)
.MODEL S_VSWITCH_3 VSWITCH (RON=10 ROFF=100MEG VON=2 VOFF=3)
.MODEL S_VSWITCH_4 VSWITCH (RON=10 ROFF=100MEG VON=3 VOFF=2)
.ENDS
*$
*
.SUBCKT SCVGS  VBAT VDD VOUT SW DM STARTUP VG GND
*EVGS VGS GND VALUE = { IF (V(STARTUP,GND) > 2.5, (SQRT( (V(VBAT,GND)-883M)*19.32M + IF (V(VOUT,GND) > 700M, (V(VOUT,GND)-700M)*5M, 0) ) + 800M), 0 ) }
*EVGS VGS GND VALUE = { IF (V(STARTUP,GND) > 2.5, (SQRT( (V(VBAT,GND)-883M)*17.32M + IF (V(VOUT,GND) > 700M, 
*+ (V(VOUT,GND)-700M)*10M, 0) ) + 800M), 0 ) }
EVgs VGS GND Value = { If (V(Startup,GND) > 2.5, Limit( (Sqrt( (V(VBAT,GND)-883m)*17.32m + If (V(VOUT,GND) > 700m, 
+ (V(VOUT,GND)-700m)*10m, 0) ) + 800m), V(VBAT,GND), 0), 0 ) }
EVG VG GND Value = { If ( (V(Startup,GND) > 2.5) | (V(DM,GND) > 2.5), V(VBAT,GND) - V(VGS,GND),  V(VDD,GND) ) }
.ENDS
*$
.SUBCKT Current_Sense__Current_Limit CurrLimN VOUT PMOSOn GND SWOn Steady Ref5 NMOSOn SW
+ PARAMS: Isw=1500m
.Param RswN = 260m
.Param Vlim = {Isw*RswN}
.Param Vlimsu = {0.4*Vlim}
XU4          Steady 10 Ref5 GND   INV
+ PARAMS: DLH=100U DHL=100N
ECLNV       11 GND VALUE = {IF((V(10,GND))<2.5, Vlimsu, Vlim)}
XNLimComp    SWOn GND NMOSOn GND SW 11 CurrLimN GND   COMP3INPAND
+ PARAMS: RIN=1E12 DELAY=50N VOUTH=5 VOUTL=100M VTHRES1=2.5 VHYST1=0 VTHRES2=2.5
+ VHYST2=0 VTHRES3=0 VHYST3=0
.ENDS
*$
.SUBCKT Backgate_Control Startup D BG S DM VOUT ShDwn_ GND
XBGComp      GND Startup GND DM ShDwn_ GND BGSW2On GND   COMP3INPANDS
+ PARAMS: RIN=1E12 VOUTH=5 VOUTL=100M VTHRES1=-2.5 VTHRES2=-2.5 VTHRES3=2.5 
RPMS        S VOUT 150M 
XDPMOS       D BG   D_D
CDPM       D BG  10P
CBGSW      BG S 10P 
SBGSW2      BG S BGSW2On GND  S_VSWITCH_1
.MODEL S_VSWITCH_1 VSWITCH (RON=100M ROFF=100MEG VON=1.2 VOFF=800M)
.ENDS
*$
.SUBCKT UVLO GND VBAT UVLO_
XUVLOComp    VBAT GND UVLO_ GND   COMPGNDREFS
+ PARAMS: VTHRES=800.0M VHYST=10.0M VOUTH=5 VOUTL=100M ROUT=0 DELAY=0 
.ENDS
*$
.SUBCKT TRANSCOND_AMP   INP INM  OUT GND IT
+ PARAMS: K=10 RIN=250MEG ROUT=250MEG IOUTMAXSOURCE=20.0E-6 IOUTMAXSINK=20.0E-6 
*
G1  OUT  GND  VALUE = { LIMIT(V(INP,INM)*V(IT,GND), IOUTMAXSOURCE, -IOUTMAXSINK) }
RIT IT GND {K}
R2   OUT GND  {ROUT}
.ENDS
*$
.SUBCKT DRVNMOS  D G S
*                D G S
M1 D G S S DRVNMOS
C1 G S 400P   
C2 D S 100P   
.MODEL DRVNMOS NMOS LEVEL=1 L=1.5U W=1500U KP=5.75M VTO=0.5 RD=0.195 LAMBDA=10E-3 IS=1E-12
.ENDS DRVNMOS 
*$
.SUBCKT DRVPMOS  D G S B
*                D G S B
*M1 D G S B DRVPMOS
M1 D G S S DRVPMOS
C1 G S 400P   
C2 D S 100P  
*.MODEL DRVPMOS PMOS LEVEL=1 L=1.5U W=1500U KP=5.75M VTO=-0.8 RS=0.15 LAMBDA=10E-3
*+ IS=1E-14 N=1 RB=0
.MODEL DRVPMOS PMOS LEVEL=1 L=1.5U W=1500U KP=5.75M VTO=-0.8 LAMBDA=10E-3
+ IS=0 
.ENDS DRVPMOS 
**********************************************************
*$
.SUBCKT CAPRLC 1 2 PARAMS: C=47u INIT=0 ESR=110m ESL=2.2n 
C1 1 3 {C} IC = {INIT}
R1 3 4 {ESR}
L1 4 2 {ESL} 
.ENDS CAPRLC
*$
.Subckt CAPM1 1 2 Params: C=10u ESL=0 IR=1000MEG ESR=1M
* C = Capacitance ESL = Equivalent Series Inductance 
* IR = Insulation Resistance ESR = Equivalent Series Resistance
LS  1 11 {ESL}
RLS 1 11 1
RS 11 12 {ESR}
C  12  2 {C}   
RI 12  2 {IR}
.Ends
*$
.Subckt INDM1 1 2 Params: L=10u ESR=1M 
* L = Inductance ESR = Equivalent Series Resistance
L  1 11 {L}   
RS 11 2 {ESR}
.Ends
*$
.Subckt Ampli IN+ IN- OUT  V+ V-
+ Params: GAIN=1000 RIN=1E30 ROUT=0 ROUTSOURCE=0 ROUTSINK=0 IOUTMAX=0 IOUTMAXSOURCE=0 
+    IOUTMAXSINK=0 SLEWRATE=0 SLEWRATERISE=0 SLEWRATEFALL=0 FPOLE1=0 FPOLE2=0 VDROPOH=0 VDROPOL=0 
+    VOFFSNOM=0 TCOVOFFS=0 IBIASNOM=0 IOFFSNOM=0 CURRDOUB=0 VOUTOFFS=0
X1 In+ In- V+ V- Out  OPAMP Params: 
+    GAIN={GAIN} RIN={RIN} ROUT={ROUT} ROUTSOURCE={ROUTSOURCE} ROUTSINK={ROUTSINK} IOUTMAX={IOUTMAX} 
+    IOUTMAXSOURCE={IOUTMAXSOURCE} IOUTMAXSINK={IOUTMAXSINK} SLEWRATE={SLEWRATE} SLEWRATERISE={SLEWRATERISE} 
+    SLEWRATEFALL={SLEWRATEFALL} FPOLE1={FPOLE1} FPOLE2={FPOLE2} VDROPOH={VDROPOH} VDROPOL={VDROPOL} VOFFS={VOFFSNOM} 
+    TCOVOFFS={TCOVOFFS} IBIAS={IBIASNOM} IOFFS={IOFFSNOM} CURRDOUB={CURRDOUB} VOUTOFFS={VOUTOFFS}  );
.Ends Ampli 
*$
.Subckt AmpliGndRef IN+ IN- OUT GND
+ Params: GAIN=1000 RIN=1E30 ROUT=0 ROUTSOURCE=0 ROUTSINK=0 IOUTMAX=0 IOUTMAXSOURCE=0 
+    IOUTMAXSINK=0 SLEWRATE=0 SLEWRATERISE=0 SLEWRATEFALL=0 FPOLE1=0 FPOLE2=0 VOUTH=0 VOUTL=0 
+    VOFFSNOM=0 TCOVOFFS=0 IBIASNOM=0 IOFFSNOM=0 CURRDOUB=0 VOUTOFFS=0
X1 In+ In- GND Out  OPAMP Params: 
+    GAIN={GAIN} RIN={RIN} ROUT={ROUT} ROUTSOURCE={ROUTSOURCE} ROUTSINK={ROUTSINK} IOUTMAX={IOUTMAX} 
+    IOUTMAXSOURCE={IOUTMAXSOURCE} IOUTMAXSINK={IOUTMAXSINK} SLEWRATE={SLEWRATE} SLEWRATERISE={SLEWRATERISE} 
+    SLEWRATEFALL={SLEWRATEFALL} FPOLE1={FPOLE1} FPOLE2={FPOLE2} VOUTH={VOUTH} VOUTL={VOUTL} VOFFS={VOFFSNOM} 
+    TCOVOFFS={TCOVOFFS} IBIAS={IBIASNOM} IOFFS={IOFFSNOM} CURRDOUB={CURRDOUB} VOUTOFFS={VOUTOFFS}  );
.Ends AmpliGndRef 
*$
.Subckt Comp IN+ IN- OUT  V+ V-
+ Params: GAIN=1000 RIN=1E30 ROUT=0 ROUTSOURCE=0 ROUTSINK=0 IOUTMAX=0 IOUTMAXSOURCE=0 
+    IOUTMAXSINK=0 SLEWRATE=0 SLEWRATERISE=0 SLEWRATEFALL=0 DELAY=0 DELAYHL=0 DELAYLH=0 
+    VHYST=0 VDROPOH=0 VDROPOL=0  VOFFSNOM=0 TCOVOFFS=0 IBIASNOM=0 IOFFSNOM=0 
+    CURRDOUB=0 VOUTOFFS=0 VTHRES=0 
X1 In+ In- V+ V- Out  COMPARATOR Params: 
+    GAIN={GAIN} RIN={RIN} ROUT={ROUT} ROUTSOURCE={ROUTSOURCE} ROUTSINK={ROUTSINK} IOUTMAX={IOUTMAX} 
+    IOUTMAXSOURCE={IOUTMAXSOURCE} IOUTMAXSINK={IOUTMAXSINK} SLEWRATE={SLEWRATE} SLEWRATERISE={SLEWRATERISE} 
+    SLEWRATEFALL={SLEWRATEFALL} DELAY={DELAY} DELAYHL={DELAYHL} DELAYLH={DELAYLH} VHYST={VHYST} VDROPOH={VDROPOH}
+    VDROPOL={VDROPOL}  VOFFS={VOFFSNOM} TCOVOFFS={TCOVOFFS} IBIAS={IBIASNOM} IOFFS={IOFFSNOM} 
+    CURRDOUB={CURRDOUB} VOUTOFFS={VOUTOFFS}  VTHRES={VTHRES}   );
.Ends Comp
*$
.Subckt CompGndRefS  IN+ IN- OUT GND
+ Params: Vthres=1 Vhyst=0 VoutH=5 VoutL=0 Rout=0 DELAY=1N
X1 In+ In- Out Gnd   HYSTCOMP  Params: 
+    ROUT={ROUT} Tdel={DELAY} VHYST={VHYST} VOUTH={VOUTH} VOUTL={VOUTL} VTHRES={VTHRES}
.Ends CompGndRefS 
*$
.Subckt CompGndRef  IN+ IN- OUT GND
+ Params: GAIN=1000 RIN=1E30 ROUT=0 ROUTSOURCE=0 ROUTSINK=0 IOUTMAX=0 IOUTMAXSOURCE=0 
+    IOUTMAXSINK=0 SLEWRATE=0 SLEWRATERISE=0 SLEWRATEFALL=0 DELAY=0 DELAYHL=0 DELAYLH=0 
+    VHYST=0 VOUTH=0 VOUTL=0 VOFFSNOM=0 TCOVOFFS=0 IBIASNOM=0 IOFFSNOM=0 
+    CURRDOUB=0 VOUTOFFS=0 VTHRES=0
X1 In+ In- Out Gnd  ComparGndRef  Params: 
+    GAIN={GAIN} RIN={RIN} ROUT={ROUT} ROUTSOURCE={ROUTSOURCE} ROUTSINK={ROUTSINK} IOUTMAX={IOUTMAX} 
+    IOUTMAXSOURCE={IOUTMAXSOURCE} IOUTMAXSINK={IOUTMAXSINK} SLEWRATE={SLEWRATE} SLEWRATERISE={SLEWRATERISE} 
+    SLEWRATEFALL={SLEWRATEFALL} DELAY={DELAY} DELAYHL={DELAYHL} DELAYLH={DELAYLH} VHYST={VHYST} VOUTH={VOUTH} 
+    VOUTL={VOUTL} VOFFSNOM={VOFFSNOM} TCOVOFFS={TCOVOFFS} IBIASNOM={IBIASNOM} IOFFSNOM={IOFFSNOM} CURRDOUB={CURRDOUB}
+    VOUTOFFS={VOUTOFFS}  VTHRES={VTHRES}   );
.Ends CompGndRef 
*$
.Subckt Comp2InpAndS IN1+ IN1- IN2+ IN2- OUT GND
+ Params: RIN=1E12 VOUTH=5 VOUTL=0 VTHRES1=2.5 VTHRES2=2.5
*
Rinp1 IN1+ GND {RIN}
Rinm1 IN1- GND {RIN}
Rinp2 IN2+ GND {RIN}
Rinm2 IN2- GND {RIN}
Eo OUT gnd Value= { IF ( (V(IN1+,IN1-) > {VTHRES1}) & (V(IN2+,IN2-) > {VTHRES2}), VOUTH, VOUTL ) }
.Ends Comp2InpAndS
*$
.Subckt CompSD IN1+ IN1- OUT GND
+ PARAMS: RIN=1E12 ROUT=10 DELAY=10N VOUTH=5 VOUTL=0 VTHRES=2.5
*
.Param Tdel = {IF ( (DELAY < 1e-9) , 1e-9, DELAY ) } 
.Param Rdel = {IF ( (Tdel > 1E-15) & (Rout < 1), 1, Rout ) } 
.Param Cout={Tdel/(0.693*(Rdel+1u))}
RINP1 IN1+ GND {RIN}
RINM1 IN1- GND {RIN}
EOUT OUT GND VALUE= { IF ( (V(IN1+,IN1-) > {VTHRES}), 
+ VOUTH + RDEL*I(EOUT), VOUTL + RDEL*I(EOUT) ) }
COUT OUT GND {COUT}
.Ends CompSD
*$
.Subckt CompInvS IN1+ IN1- OUT GND
+ PARAMS: RIN=1E12 VOUTH=5 VOUTL=0 VTHRES=2.5
*
RINP1 IN1+ GND {RIN}
RINM1 IN1- GND {RIN}
EOUT OUT GND VALUE= { IF ( (V(IN1+,IN1-) > {VTHRES}), VOUTL, VOUTH ) }
.Ends CompInvS
*$
.Subckt CompInvSD IN1+ IN1- OUT GND
+ PARAMS: RIN=1E12 ROUT=10 DELAY=10N VOUTH=5 VOUTL=0 VTHRES=2.5
*
.PARAM RDEL = {IF ( (DELAY > 1E-15) & (ROUT < 1), 1, ROUT ) } 
.PARAM COUT={DELAY/(0.693*(RDEL+1U))}
RINP1 IN1+ GND {RIN}
RINM1 IN1- GND {RIN}
EOUT OUT GND VALUE= { IF ( (V(IN1+,IN1-) > {VTHRES}), 
+ VOUTL + RDEL*I(EOUT), VOUTH + RDEL*I(EOUT) ) }
COUT OUT GND {COUT}
.Ends CompInvSD
*$
.Subckt Comp2InpAndSD IN1+ IN1- IN2+ IN2- OUT GND
+ PARAMS: RIN=1E12 ROUT=10 DELAY=10N VOUTH=5 VOUTL=0 VTHRES1=2.5 VTHRES2=2.5
*
.Param Tdel = {IF ( (DELAY < 1e-9) , 1e-9, DELAY ) } 
.Param Rdel = {IF ( (Tdel > 1E-15) & (Rout < 1), 1, Rout ) } 
.Param Cout={Tdel/(0.693*(Rdel+1u))}
RINP1 IN1+ GND {RIN}
RINM1 IN1- GND {RIN}
RINP2 IN2+ GND {RIN}
RINM2 IN2- GND {RIN}
EOUT OUT GND VALUE= { IF ( (V(IN1+,IN1-) > {VTHRES1}) & (V(IN2+,IN2-) > {VTHRES2}), 
+ VOUTH + RDEL*I(EOUT), VOUTL + RDEL*I(EOUT) ) }
COUT OUT GND {COUT}
.Ends Comp2InpAndSD
*$
.Subckt Comp2InpAnd IN1+ IN1- IN2+ IN2- OUT GND
+ Params: RIN=1E12 DELAY=10N VOUTH=5 VOUTL=0 VTHRES1=2.5 VHYST1=0 VTHRES2=2.5 VHYST2=0 
.Param VoutM={(VoutH-VoutL)/2}
X1 IN1+ IN1- IN2+ IN2- ST1 ST2 GND Hyst2Inp Params: 
+    RIN={RIN} DELAY={DELAY} VOUTH={VOUTH} VOUTL={VOUTL} 
+    VTHRES1={VTHRES1} VHYST1={VHYST1} VTHRES2={VTHRES2} VHYST2={VHYST2}
Eo OUT gnd Value= { IF ( (V(st1,GND) > {VoutM}) & (V(st2,GND) > {VoutM}), VOUTH, VOUTL ) }
.Ends Comp2InpAnd
*$
.Subckt Comp2InpOrS IN1+ IN1- IN2+ IN2- OUT GND
+ Params: RIN=1E12 VOUTH=5 VOUTL=0 VTHRES1=2.5 VTHRES2=2.5
*
Rinp1 IN1+ GND {RIN}
Rinm1 IN1- GND {RIN}
Rinp2 IN2+ GND {RIN}
Rinm2 IN2- GND {RIN}
Eo OUT gnd Value= { IF ( (V(IN1+,IN1-) > {VTHRES1}) | (V(IN2+,IN2-) > {VTHRES2}), VOUTH, VOUTL ) }
.Ends Comp2InpOrS
*$
.Subckt Comp2InpNOrS IN1+ IN1- IN2+ IN2- OUT GND
+ Params: RIN=1E12 VOUTH=5 VOUTL=0 VTHRES1=2.5 VTHRES2=2.5
*
Rinp1 IN1+ GND {RIN}
Rinm1 IN1- GND {RIN}
Rinp2 IN2+ GND {RIN}
Rinm2 IN2- GND {RIN}
Eo OUT gnd Value= { IF ( (V(IN1+,IN1-) > {VTHRES1}) | (V(IN2+,IN2-) > {VTHRES2}), VOUTL, VOUTH ) }
.Ends Comp2InpNOrS
*$
.Subckt Comp2InpOr IN1+ IN1- IN2+ IN2- OUT GND
+ Params: RIN=1E12 DELAY=10N VOUTH=5 VOUTL=0 VTHRES1=0 VHYST1=0 VTHRES2=0 VHYST2=0 
.Param VoutM={(VoutH-VoutL)/2}
X1 IN1+ IN1- IN2+ IN2- ST1 ST2 GND Hyst2Inp Params: 
+    RIN={RIN} DELAY={DELAY} VOUTH={VOUTH} VOUTL={VOUTL} 
+    VTHRES1={VTHRES1} VHYST1={VHYST1} VTHRES2={VTHRES2} VHYST2={VHYST2}
Eo OUT gnd Value= { IF ( (V(st1,GND) > {VoutM}) | (V(st2,GND) > {VoutM}), VOUTH, VOUTL ) }
.Ends Comp2InpOr 
*$
.Subckt Comp2InpNAndSD IN1+ IN1- IN2+ IN2- OUT GND
+ PARAMS: RIN=1E12 ROUT=0 DELAY=0 VOUTH=0 VOUTL=0 VTHRES1=0 VTHRES2=0
*
.PARAM RDEL = {IF ( (DELAY > 1E-15) & (ROUT < 1), 1, ROUT ) } 
.PARAM COUT={DELAY/(0.693*(RDEL+1U))}
RINP1 IN1+ GND {RIN}
RINM1 IN1- GND {RIN}
RINP2 IN2+ GND {RIN}
RINM2 IN2- GND {RIN}
EOUT OUT GND VALUE= { IF ( (V(IN1+,IN1-) > {VTHRES1}) & (V(IN2+,IN2-) > {VTHRES2}), 
+ VOUTL + RDEL*I(EOUT), VOUTH + RDEL*I(EOUT) ) }
COUT OUT GND {COUT}
.Ends Comp2InpNAndSD
*$
.Subckt Comp2InpNOrSD IN1+ IN1- IN2+ IN2- OUT GND
+ Params: RIN=1E12 ROUT=0 DELAY=0 VOUTH=0 VOUTL=0 VTHRES1=0 VTHRES2=0
*
.Param Tdel = {IF ( (DELAY < 1e-9) , 1e-9, DELAY ) } 
.Param Rdel = {IF ( (Tdel > 1E-15) & (Rout < 1), 1, Rout ) } 
.Param Cout={Tdel/(0.693*(Rdel+1u))}
Rinp1 IN1+ GND {RIN}
Rinm1 IN1- GND {RIN}
Rinp2 IN2+ GND {RIN}
Rinm2 IN2- GND {RIN}
Eout OUT gnd Value= { IF ( (V(IN1+,IN1-) > {VTHRES1}) | (V(IN2+,IN2-) > {VTHRES2}), 
+ VOUTL + Rdel*I(Eout), VOUTH + Rdel*I(Eout) ) }
Cout OUT gnd {Cout}
.Ends Comp2InpNOrSD
*$
.Subckt Hyst2Inp IN1+ IN1- IN2+ IN2- ST1 ST2 GND
+ Params: RIN=1E12 DELAY=0 VOUTH=0 VOUTL=0 VTHRES1=0 VHYST1=0 VTHRES2=0 VHYST2=0 
.Param VoutM={(VoutH-VoutL)/2}
.Param VthH1={Vthres1+Vhyst1}
.Param VthL1={Vthres1-Vhyst1}
.Param VthH2={Vthres2+Vhyst2}
.Param VthL2={Vthres2-Vhyst2}
.Param Rdel=10
.Param Tdel = {IF ( (DELAY < 1e-9) , 1e-9, DELAY ) } 
.Param Cdel={Tdel/(0.693*Rdel)}
*
Rinp1 IN1+ GND {RIN}
Rinm1 IN1- GND {RIN}
Rinp2 IN2+ GND {RIN}
Rinm2 IN2- GND {RIN}
Ethr1 thr1 gnd Value= { IF ( V(st1,gnd) < {VoutM}, {VthH1}, {VthL1}) }
Est1 st1 gnd Value= { IF ( V(IN1+,IN1-) > V(thr1,gnd), VoutH + Rdel*I(Est1), VoutL + Rdel*I(Est1) ) }
Cst1 st1 gnd {Cdel} 
Ethr2 thr2 gnd Value= { IF ( V(st2,gnd) < {VoutM}, {VthH2}, {VthL2}) }
Est2 st2 gnd Value= { IF ( V(IN2+,IN2-) > V(thr2,gnd), VoutH + Rdel*I(Est2), VoutL + Rdel*I(Est2) ) }
Cst2 st2 gnd {Cdel} 
.Ends Hyst2Inp
*$
.Subckt Comp2Inp IN1+ IN1- IN2+ IN2- OUT GND
+ Params: GAIN=1000 RIN=1E30 ROUT=0 ROUTSOURCE=0 ROUTSINK=0 IOUTMAX=0 IOUTMAXSOURCE=0 
+    IOUTMAXSINK=0 SLEWRATE=0 SLEWRATERISE=0 SLEWRATEFALL=0 DELAY=0 
+    DELAYHL=0 DELAYLH=0 VOUTH=0 VOUTL=0 VOFFSNOM=0 TCOVOFFS=0 IBIASNOM=0 IOFFSNOM=0 
+    CURRDOUB=0 VOUTOFFS=0 DCTRANSFER=0 LOGICFUNC=0 VTHRES1=0 VHYST1=0 VTHRES2=0 VHYST2=0
X1 In1+ In1- In2+ In2- Out Gnd  Comp2InpAnd  Params: 
+    GAIN={GAIN} RIN={RIN} ROUT={ROUT} ROUTSOURCE={ROUTSOURCE} ROUTSINK={ROUTSINK} IOUTMAX={IOUTMAX} 
+    IOUTMAXSOURCE={IOUTMAXSOURCE} IOUTMAXSINK={IOUTMAXSINK} SLEWRATE={SLEWRATE} SLEWRATERISE={SLEWRATERISE} 
+    SLEWRATEFALL={SLEWRATEFALL} DELAY={DELAY} DELAYHL={DELAYHL} DELAYLH={DELAYLH} VOUTH={VOUTH} 
+    VOUTL={VOUTL} VOFFSNOM={VOFFSNOM} TCOVOFFS={TCOVOFFS} IBIASNOM={IBIASNOM} IOFFSNOM={IOFFSNOM} CURRDOUB={CURRDOUB}
+    VOUTOFFS={VOUTOFFS} DCTRANSFER={DCTRANSFER} LOGICFUNC={LOGICFUNC} 
+    VTHRES1={VTHRES1} VHYST1={VHYST1} VTHRES2={VTHRES2} VHYST2={VHYST2}
.Ends Comp2InpAnd
*$
.Subckt Comp3InpAndS IN1+ IN1- IN2+ IN2- IN3+ IN3- OUT GND
+ Params: RIN=1E12 DELAY=0 VOUTH=0 VOUTL=0 VTHRES1=0 VHYST1=0 VTHRES2=0 VHYST2=0 VTHRES3=0 VHYST3=0  
*
Rinp1 IN1+ GND {RIN}
Rinm1 IN1- GND {RIN}
Rinp2 IN2+ GND {RIN}
Rinm2 IN2- GND {RIN}
Rinp3 IN3+ GND {RIN}
Rinm3 IN3- GND {RIN}
Eo OUT gnd Value= { IF ( (V(IN1+,IN1-) > {VTHRES1}) & (V(IN2+,IN2-) > {VTHRES2}) & (V(IN3+,IN3-) > {VTHRES3}), VOUTH, VOUTL ) }
.Ends Comp3InpAndS
*$
.Subckt Comp3InpAnd IN1+ IN1- IN2+ IN2- IN3+ IN3- OUT GND
+ Params: RIN=1E12 DELAY=0 VOUTH=0 VOUTL=0 VTHRES1=0 VHYST1=0 VTHRES2=0 VHYST2=0 VTHRES3=0 VHYST3=0  
.Param VoutM={(VoutH-VoutL)/2}
X1 IN1+ IN1- IN2+ IN2- IN3+ IN3- ST1 ST2  ST3 GND Hyst3Inp Params: 
+    RIN={RIN} DELAY={DELAY} VOUTH={VOUTH} VOUTL={VOUTL} 
+    VTHRES1={VTHRES1} VHYST1={VHYST1} VTHRES2={VTHRES2} VHYST2={VHYST2} VTHRES3={VTHRES3} VHYST3={VHYST3}
Eo OUT gnd Value= { IF ( (V(st1,GND) > {VoutM}) & (V(st2,GND) > {VoutM}) & (V(st3,GND) > {VoutM}), VOUTH, VOUTL ) }
.Ends Comp3InpAnd
*$
.Subckt Comp3InpOr IN1+ IN1- IN2+ IN2- IN3+ IN3- OUT GND
+ Params: RIN=1E12 DELAY=0 VOUTH=0 VOUTL=0 VTHRES1=0 VHYST1=0 VTHRES2=0 VHYST2=0 VTHRES3=0 VHYST3=0  
.Param VoutM={(VoutH-VoutL)/2}
X1 IN1+ IN1- IN2+ IN2- IN3+ IN3- ST1 ST2  ST3 GND Hyst3Inp Params: 
+    RIN={RIN} DELAY={DELAY} VOUTH={VOUTH} VOUTL={VOUTL} 
+    VTHRES1={VTHRES1} VHYST1={VHYST1} VTHRES2={VTHRES2} VHYST2={VHYST2} VTHRES3={VTHRES3} VHYST3={VHYST3}
Eo OUT gnd Value= { IF ( (V(st1,GND) > {VoutM}) | (V(st2,GND) > {VoutM}) | (V(st3,GND) > {VoutM}), VOUTH, VOUTL ) }
.Ends Comp3InpOr
*$
.Subckt Hyst3Inp IN1+ IN1- IN2+ IN2- IN3+ IN3- ST1 ST2 ST3 GND
+ Params: RIN=1E12 DELAY=0 VOUTH=0 VOUTL=0 VTHRES1=0 VHYST1=0 VTHRES2=0 VHYST2=0 VTHRES3=0 VHYST3=0 
.Param VoutM={(VoutH-VoutL)/2}
.Param VthH1={Vthres1+Vhyst1}
.Param VthL1={Vthres1-Vhyst1}
.Param VthH2={Vthres2+Vhyst2}
.Param VthL2={Vthres2-Vhyst2}
.Param VthH3={Vthres3+Vhyst3}
.Param VthL3={Vthres3-Vhyst3}
.Param Rdel=10
.Param Tdel = {IF ( (DELAY < 1e-9) , 1e-9, DELAY ) } 
.Param Cdel={Tdel/(0.693*Rdel)}
*
Rinp1 IN1+ GND {RIN}
Rinm1 IN1- GND {RIN}
Rinp2 IN2+ GND {RIN}
Rinm2 IN2- GND {RIN}
Rinp3 IN3+ GND {RIN}
Rinm3 IN3- GND {RIN}
Ethr1 thr1 gnd Value= { IF ( V(st1,gnd) < {VoutM}, {VthH1}, {VthL1}) }
Est1 st1 gnd Value= { IF ( V(IN1+,IN1-) > V(thr1,gnd), VoutH + Rdel*I(Est1), VoutL + Rdel*I(Est1) ) }
Cst1 st1 gnd {Cdel} 
Ethr2 thr2 gnd Value= { IF ( V(st2,gnd) < {VoutM}, {VthH2}, {VthL2}) }
Est2 st2 gnd Value= { IF ( V(IN2+,IN2-) > V(thr2,gnd), VoutH + Rdel*I(Est2), VoutL + Rdel*I(Est2) ) }
Cst2 st2 gnd {Cdel} 
Ethr3 thr3 gnd Value= { IF ( V(st3,gnd) < {VoutM}, {VthH3}, {VthL3}) }
Est3 st3 gnd Value= { IF ( V(IN3+,IN3-) > V(thr3,gnd), VoutH + Rdel*I(Est3), VoutL + Rdel*I(Est3) ) }
Cst3 st3 gnd {Cdel} 
.Ends Hyst3Inp
*$
.Subckt Comp3Inp IN1+ IN1- IN2+ IN2- IN3+ IN3- OUT GND
+ Params: GAIN=1000 RIN=1E30 ROUT=0 ROUTSOURCE=0 ROUTSINK=0 IOUTMAX=0 IOUTMAXSOURCE=0 
+    IOUTMAXSINK=0 SLEWRATE=0 SLEWRATERISE=0 SLEWRATEFALL=0 DELAY=0 
+    DELAYHL=0 DELAYLH=0 VOUTH=0 VOUTL=0 VOFFSNOM=0 TCOVOFFS=0 IBIASNOM=0 IOFFSNOM=0 
+    CURRDOUB=0 VOUTOFFS=0 DCTRANSFER=0 LOGICFUNC=0 VTHRES1=0 VHYST1=0 VTHRES2=0 VHYST2=0 VTHRES3=0 VHYST3=0
X1 In1+ In1- In2+ In2- In3+ In3- Out Gnd  Comp3InpAnd
.Model _Comp COMP_GR_3INP (GAIN={GAIN} RIN={RIN} ROUT={ROUT} ROUTSOURCE={ROUTSOURCE} ROUTSINK={ROUTSINK} IOUTMAX={IOUTMAX} 
+    IOUTMAXSOURCE={IOUTMAXSOURCE} IOUTMAXSINK={IOUTMAXSINK} SLEWRATE={SLEWRATE} SLEWRATERISE={SLEWRATERISE} 
+    SLEWRATEFALL={SLEWRATEFALL} DELAY={DELAY} DELAYHL={DELAYHL} DELAYLH={DELAYLH} VOUTH={VOUTH} 
+    VOUTL={VOUTL} VOFFSNOM={VOFFSNOM} TCOVOFFS={TCOVOFFS} IBIASNOM={IBIASNOM} IOFFSNOM={IOFFSNOM} CURRDOUB={CURRDOUB}
+    VOUTOFFS={VOUTOFFS} DCTRANSFER={DCTRANSFER} LOGICFUNC={LOGICFUNC}
+    VTHRES1={VTHRES1} VHYST1={VHYST1} VTHRES2={VTHRES2} VHYST2={VHYST2} VTHRES3={VTHRES3} VHYST3={VHYST3} );
.Ends Comp3InpAnd
*$
.Subckt Comp4InpAndS IN1+ IN1- IN2+ IN2- IN3+ IN3- IN4+ IN4- OUT GND
+ Params: RIN=1E12 DELAY=0 VOUTH=0 VOUTL=0 VTHRES1=0 VHYST1=0 VTHRES2=0 VHYST2=0 VTHRES3=0 VHYST3=0 VTHRES4=0 VHYST4=0 
*
Rinp1 IN1+ GND {RIN}
Rinm1 IN1- GND {RIN}
Rinp2 IN2+ GND {RIN}
Rinm2 IN2- GND {RIN}
Rinp3 IN3+ GND {RIN}
Rinm3 IN3- GND {RIN}
Rinp4 IN4+ GND {RIN}
Rinm4 IN4- GND {RIN}
Eo OUT gnd Value= { IF ( (V(IN1+,IN1-) > {VTHRES1}) & (V(IN2+,IN2-) > {VTHRES2}) & (V(IN3+,IN3-) > {VTHRES3}) & 
+ (V(IN4+,IN4-) > {VTHRES4}), VOUTH, VOUTL ) }
.Ends Comp4InpAndS
*$
.Subckt Comp4InpAnd IN1+ IN1- IN2+ IN2- IN3+ IN3- IN4+ IN4- OUT GND
+ Params: RIN=1E12 DELAY=0 VOUTH=0 VOUTL=0 VTHRES1=0 VHYST1=0 VTHRES2=0 VHYST2=0 VTHRES3=0 VHYST3=0 VTHRES4=0 VHYST4=0  
.Param VoutM={(VoutH-VoutL)/2}
X1 IN1+ IN1- IN2+ IN2- IN3+ IN3- IN4+ IN4- ST1 ST2 ST3 ST4 GND Hyst4Inp Params: 
+    RIN={RIN} DELAY={DELAY} VOUTH={VOUTH} VOUTL={VOUTL} 
+    VTHRES1={VTHRES1} VHYST1={VHYST1} VTHRES2={VTHRES2} VHYST2={VHYST2}
+    VTHRES3={VTHRES3} VHYST3={VHYST3} VTHRES4={VTHRES4} VHYST4={VHYST4}
Eo OUT gnd Value= { IF ( (V(st1,GND) > {VoutM}) & (V(st2,GND) > {VoutM}) & (V(st3,GND) > {VoutM}) & 
+ (V(st4,GND) > {VoutM}), VOUTH, VOUTL ) }
.Ends Comp4InpAnd
*$
.Subckt Comp4InpOrS IN1+ IN1- IN2+ IN2- IN3+ IN3- IN4+ IN4- OUT GND
+ Params: RIN=1E12 DELAY=0 VOUTH=0 VOUTL=0 VTHRES1=0 VHYST1=0 VTHRES2=0 VHYST2=0 VTHRES3=0 VHYST3=0 VTHRES4=0 VHYST4=0 
*
Rinp1 IN1+ GND {RIN}
Rinm1 IN1- GND {RIN}
Rinp2 IN2+ GND {RIN}
Rinm2 IN2- GND {RIN}
Rinp3 IN3+ GND {RIN}
Rinm3 IN3- GND {RIN}
Rinp4 IN4+ GND {RIN}
Rinm4 IN4- GND {RIN}
Eo OUT gnd Value= { IF ( (V(IN1+,IN1-) > {VTHRES1}) | (V(IN2+,IN2-) > {VTHRES2}) | (V(IN3+,IN3-) > {VTHRES3}) | 
+ (V(IN4+,IN4-) > {VTHRES4}), VOUTH, VOUTL ) }
.Ends Comp4InpOrS
*$
.Subckt Hyst4Inp IN1+ IN1- IN2+ IN2- IN3+ IN3- IN4+ IN4- ST1 ST2 ST3 ST4 GND
+ Params: RIN=1E12 DELAY=0 VOUTH=0 VOUTL=0 VTHRES1=0 VHYST1=0 VTHRES2=0 VHYST2=0 VTHRES3=0 VHYST3=0 VTHRES4=0 VHYST4=0
.Param VoutM={(VoutH-VoutL)/2}
.Param VthH1={Vthres1+Vhyst1}
.Param VthL1={Vthres1-Vhyst1}
.Param VthH2={Vthres2+Vhyst2}
.Param VthL2={Vthres2-Vhyst2}
.Param VthH3={Vthres3+Vhyst3}
.Param VthL3={Vthres3-Vhyst3}
.Param VthH4={Vthres4+Vhyst4}
.Param VthL4={Vthres4-Vhyst4}
.Param Rdel=10
.Param Tdel = {IF ( (DELAY < 1e-9) , 1e-9, DELAY ) } 
.Param Cdel={Tdel/(0.693*Rdel)}
*
Rinp1 IN1+ GND {RIN}
Rinm1 IN1- GND {RIN}
Rinp2 IN2+ GND {RIN}
Rinm2 IN2- GND {RIN}
Rinp3 IN3+ GND {RIN}
Rinm3 IN3- GND {RIN}
Rinp4 IN4+ GND {RIN}
Rinm4 IN4- GND {RIN}
Ethr1 thr1 gnd Value= { IF ( V(st1,gnd) < {VoutM}, {VthH1}, {VthL1}) }
Est1 st1 gnd Value= { IF ( V(IN1+,IN1-) > V(thr1,gnd), VoutH + Rdel*I(Est1), VoutL + Rdel*I(Est1) ) }
Cst1 st1 gnd {Cdel} 
Ethr2 thr2 gnd Value= { IF ( V(st2,gnd) < {VoutM}, {VthH2}, {VthL2}) }
Est2 st2 gnd Value= { IF ( V(IN2+,IN2-) > V(thr2,gnd), VoutH + Rdel*I(Est2), VoutL + Rdel*I(Est2) ) }
Cst2 st2 gnd {Cdel} 
Ethr3 thr3 gnd Value= { IF ( V(st3,gnd) < {VoutM}, {VthH3}, {VthL3}) }
Est3 st3 gnd Value= { IF ( V(IN3+,IN3-) > V(thr3,gnd), VoutH + Rdel*I(Est3), VoutL + Rdel*I(Est3) ) }
Cst3 st3 gnd {Cdel} 
Ethr4 thr4 gnd Value= { IF ( V(st4,gnd) < {VoutM}, {VthH4}, {VthL4}) }
Est4 st4 gnd Value= { IF ( V(IN4+,IN4-) > V(thr4,gnd), VoutH + Rdel*I(Est4), VoutL + Rdel*I(Est4) ) }
Cst4 st4 gnd {Cdel} 
.Ends Hyst4Inp
*$
.Subckt Comp4InpAndF IN1+ IN1- IN2+ IN2- IN3+ IN3- IN4+ IN4- OUT GND
+ Params: GAIN=1000 RIN=1E30 ROUT=0 ROUTSOURCE=0 ROUTSINK=0 IOUTMAX=0 IOUTMAXSOURCE=0 
+    IOUTMAXSINK=0 SLEWRATE=0 SLEWRATERISE=0 SLEWRATEFALL=0 DELAY=0 
+    DELAYHL=0 DELAYLH=0 VOUTH=0 VOUTL=0 VOFFSNOM=0 TCOVOFFS=0 IBIASNOM=0 IOFFSNOM=0 
+    CURRDOUB=0 VOUTOFFS=0 DCTRANSFER=0 LOGICFUNC=0 
+    VTHRES1=0 VHYST1=0 VTHRES2=0 VHYST2=0 VTHRES3=0 VHYST3=0 VTHRES4=0 VHYST4=0 
Y1 In1+ In1- In2+ In2- In3+ In3- In4+ In4- Out Gnd  _Comp
.Model _Comp COMP_GR_4INP (GAIN={GAIN} RIN={RIN} ROUT={ROUT} ROUTSOURCE={ROUTSOURCE} ROUTSINK={ROUTSINK} IOUTMAX={IOUTMAX} 
+    IOUTMAXSOURCE={IOUTMAXSOURCE} IOUTMAXSINK={IOUTMAXSINK} SLEWRATE={SLEWRATE} SLEWRATERISE={SLEWRATERISE} 
+    SLEWRATEFALL={SLEWRATEFALL} DELAY={DELAY} DELAYHL={DELAYHL} DELAYLH={DELAYLH} VOUTH={VOUTH} 
+    VOUTL={VOUTL} VOFFSNOM={VOFFSNOM} TCOVOFFS={TCOVOFFS} IBIASNOM={IBIASNOM} IOFFSNOM={IOFFSNOM} CURRDOUB={CURRDOUB}
+    VOUTOFFS={VOUTOFFS} DCTRANSFER={DCTRANSFER} LOGICFUNC={LOGICFUNC} 
+    VTHRES1={VTHRES1} VHYST1={VHYST1} VTHRES2={VTHRES2} VHYST2={VHYST2} VTHRES3={VTHRES3} VHYST3={VHYST3} VTHRES4={VTHRES4}
+    VHYST4={VHYST4} );
.Ends Comp4InpAndF
*$
.Subckt Comp4InpNOrS IN1+ IN1- IN2+ IN2- IN3+ IN3- IN4+ IN4- OUT GND
+ Params: RIN=1E12 DELAY=0 VOUTH=0 VOUTL=0 VTHRES1=0 VHYST1=0 VTHRES2=0 VHYST2=0 VTHRES3=0 VHYST3=0 VTHRES4=0 VHYST4=0 
*
Rinp1 IN1+ GND {RIN}
Rinm1 IN1- GND {RIN}
Rinp2 IN2+ GND {RIN}
Rinm2 IN2- GND {RIN}
Rinp3 IN3+ GND {RIN}
Rinm3 IN3- GND {RIN}
Rinp4 IN4+ GND {RIN}
Rinm4 IN4- GND {RIN}
Eo OUT gnd Value= { IF ( (V(IN1+,IN1-) > {VTHRES1}) | (V(IN2+,IN2-) > {VTHRES2}) | (V(IN3+,IN3-) > {VTHRES3}) | 
+ (V(IN4+,IN4-) > {VTHRES4}), VOUTL, VOUTH ) }
.Ends Comp4InpNOrS
*$
.Subckt VCO InP InM Gnd OutSin OutTri OutSqr PARAMS: CENTFREQ=1E3 CONVGAIN=1E3 PHI0=0 OUTAMPLI=1 OUTOFFS=0 INLLIM=-1E30 INULIM=1E30
+ LIMRNG=1E-6 DUTYCYC=0.5 RISETIME=1E-9 FALLTIME=1E-9 MODE=1
Y1 InP InM Gnd OutSin OutTri OutSq VCO_
.Model VCO_ VCO (CENTFREQ ={ CENTFREQ} CONVGAIN ={CONVGAIN} PHI0 ={PHI0} OUTAMPLI ={OUTAMPLI} OUTOFFS ={OUTOFFS} 
+ INLLIM ={INLLIM} INULIM ={INULIM} LIMRNG ={LIMRNG} DUTYCYC ={DUTYCYC} RISETIME ={RISETIME} FALLTIME ={FALLTIME} MODE ={MODE})
.Ends
*$
.Subckt SINEVCO InP InM Gnd Out PARAMS: CENTFREQ=1E3 CONVGAIN=1E3 PHI0=0 OUTAMPLI=1 OUTOFFS=0 INLLIM=-1E30 INULIM=1E30
+ LIMRNG=1E-6  MODE=1
Y1 InP InM Gnd Out sinVCO
.Model sinVCO SINE_VCO (CENTFREQ ={ CENTFREQ} CONVGAIN ={CONVGAIN} PHI0 ={PHI0} OUTAMPLI ={OUTAMPLI} OUTOFFS ={OUTOFFS} 
+ INLLIM ={INLLIM} INULIM ={INULIM} LIMRNG ={LIMRNG} MODE ={MODE})
.Ends
*$
.Subckt TRIVCO InP InM Gnd Out PARAMS: CENTFREQ=1E3 CONVGAIN=1E3 PHI0=0 OUTAMPLI=1 OUTOFFS=0 INLLIM=-1E30 INULIM=1E30
+ LIMRNG=1E-6 DUTYCYC=0.5 MODE=1
Y1 InP InM Gnd Out triVCO
.Model triVCO TRI_VCO (CENTFREQ ={ CENTFREQ} CONVGAIN ={CONVGAIN} PHI0 ={PHI0} OUTAMPLI ={OUTAMPLI} OUTOFFS ={OUTOFFS} 
+ INLLIM ={INLLIM} INULIM ={INULIM} LIMRNG ={LIMRNG} DUTYCYC ={DUTYCYC} MODE ={MODE})
.Ends
*$
.Subckt SQRVCO InP InM Gnd Out PARAMS: CENTFREQ=1E3 CONVGAIN=1E3 PHI0=0 OUTAMPLI=1 OUTOFFS=0 INLLIM=-1E30 INULIM=1E30
+ LIMRNG=1E-6 DUTYCYC=0.5 RISETIME=1E-9 FALLTIME=1E-9 MODE=1
Y1 InP InM Gnd Out sqrVCO
.Model sqrVCO SQUARE_VCO (CENTFREQ ={ CENTFREQ} CONVGAIN ={CONVGAIN} PHI0 ={PHI0} OUTAMPLI ={OUTAMPLI} OUTOFFS ={OUTOFFS} 
+ INLLIM ={INLLIM} INULIM ={INULIM} LIMRNG ={LIMRNG} DUTYCYC ={DUTYCYC} RISETIME ={RISETIME} FALLTIME ={FALLTIME} MODE ={MODE})
.Ends
*$
.Subckt NCOUNTER_UDSR   CLK UD CE S R MIN MAX  GND VCC PARAMS: INTYP=0 OUTTYP=0 DEL=0 IOMODEL=0 
+  DELL2H=10E-9 DELH2L=10E-9 LATCH=1 MAXCOUNT=15
Y1  UD CLK CE R MIN MAX S GND VCC  _CNTN
.Model _CNTN  CNTN_UDSR (INTYP ={INTYP} OUTTYP ={OUTTYP} DEL ={DEL} IOMODEL ={IOMODEL} DELL2H ={DELL2H} 
+ DELH2L ={DELH2L} LATCH ={LATCH} MAXCOUNT ={MAXCOUNT} 
.Ends
*$
* Standard operational amplifier macromodel subcircuit
* Created using 08/05/06
* (REV 1.1 29/07/06 )
.SUBCKT StdOpamp INP INM VP VM Out
+ Params: GAIN=200K RIN=2MEG RINC=1E9 ROUT=75 SLEWRATE=500K FPOLE1=5 FPOLE2=1MEG 
+         VDROPOH=1.9 VDROPOL=1.9 VOFFS=1m IBIAS=80n IOFFS=20n 
*
.PARAM VOSW = 15
.PARAM pi = 3.141592
.PARAM IS = 1.0E-12
.PARAM VT = 0.02585
.PARAM C1 = {VOSW/ROUT/SLEWRATE}
.PARAM IMAX = {C1*SLEWRATE}
.PARAM R1 = {1/(2*pi*C1*FPOLE1)}
.PARAM GM1 = {GAIN/R1}
.PARAM R2 = 100
.PARAM G2 = {1/R2}
.PARAM GOUT = {1/ROUT}
.PARAM C2 = {1/(2*pi*R2*FPOLE2)}
.PARAM VDF = {VT*Log(1 + IMAX/IS)}
*
Ibiasm      INM 0  {IBIAS - IOFFS}
RINM      INM  8  {2*RINC}
RINP      INP  8  {2*RINC}
Ibias       10 0   {IBIAS}
Voffs       10 INP  {VOFFS}
Vc          VP 11  {VDROPOH + VDF}
Ve          12 VM  {VDROPOL + VDF}
D1          VM VP  D_1
Rp          VP VM  15E3
ROut        OUT 8  {ROUT}
GmO         8 OUT Value =  {V(9,8)*GOUT}
C2          9 8  {C2}
R2          9 8  {R2}
Gm2         8 9  Value =  {V(7,8)*G2}
Rin         INM 10  {RIN}
EGND        8  0  POLY(2) (VP,0) (VM,0) 0 .5 .5
D3          12 7  D_1
D2          7 11  D_1
C1          7 8  {C1}
R1          7 8  {R1}
Gm1         8 7 Value = { Limit( GM1*V(10,INM), -IMAX, IMAX) }
.MODEL D_1 D( IS={IS} )
.ENDS
*$
.SUBCKT DFFLOP  PREB CLRB CLK D Q QB
+ OPTIONAL: DPWR=$G_DPWR DGND=$G_DGND
+ PARAMS:    DLH = 0 DHL = 0
U1 DFF(1) DPWR DGND PREB CLRB CLK
+ D Q QB
+ T_DFF IO_HC
.MODEL T_DFF UEFF( TPPCQLHTY = {DLH} TPCLKQLHTY = {DLH}
+                   TPPCQHLTY = {DHL} TPCLKQHLTY = {DHL} )
.ENDS
*$
.SUBCKT DFFLOP_P PREB CLRB CLK D Q QB DPWR DGND 
+ PARAMS:    DLH = 0 DHL = 0
U1 DFF(1) DPWR DGND PREB CLRB CLK
+ D Q QB
+ T_DFF IO_HC
.MODEL T_DFF UEFF( TPPCQLHTY = {DLH} TPCLKQLHTY = {DLH}
+                   TPPCQHLTY = {DHL} TPCLKQHLTY = {DHL} )
.ENDS
*$
.SUBCKT RSFFLOP  S  R  Q  QB
+ OPTIONAL: DPWR=$G_DPWR DGND=$G_DGND
+ PARAMS:    DLH = 0 DHL = 0
U1 INV DPWR DGND R CLRBAR 
+ T_INV IO_HC
.MODEL T_INV UGATE()
U2 INV DPWR DGND S PREBAR 
+ T_INV2 IO_HC
.MODEL T_INV2 UGATE()
U3 DFF(1) DPWR DGND PREBAR CLRBAR DGND DGND Q QB
+ T_DFF IO_HC
.MODEL T_DFF UEFF( TPPCQLHTY = {DLH} TPCLKQLHTY = {DLH}
+                   TPPCQHLTY = {DHL} TPCLKQHLTY = {DHL} )
.ENDS
*$
.SUBCKT RSFFLOP_P   S  R  Q  QB  DPWR  DGND
+ PARAMS:    DLH = 0 DHL = 0
U1 INV DPWR DGND R CLRBAR 
+ T_INV IO_HC
.MODEL T_INV UGATE()
U2 INV DPWR DGND S PREBAR 
+ T_INV2 IO_HC
.MODEL T_INV2 UGATE()
U3 DFF(1) DPWR DGND PREBAR CLRBAR DGND DGND Q QB
+ T_DFF IO_HC
.MODEL T_DFF UEFF( TPPCQLHTY = {DLH} TPCLKQLHTY = {DLH}
+                   TPPCQHLTY = {DHL} TPCLKQHLTY = {DHL} )
.ENDS
*$
.SUBCKT R_S_FFLOP_P   S_  R_  Q  QB  DPWR  DGND
+ PARAMS:    DLH = 0 DHL = 0
U3 DFF(1) DPWR DGND S_ R_ DGND DGND Q QB
+ T_DFF IO_HC
.MODEL T_DFF UEFF( TPPCQLHTY = {DLH} TPCLKQLHTY = {DLH}
+                   TPPCQHLTY = {DHL} TPCLKQHLTY = {DHL} )
.ENDS
*$
.SUBCKT R_S_FF S_ R_ Q  Q_ DPWR DGND
+ PARAMS: VTHRESS=2.5 VTHRESR=2.5 VOUTH=5.0 VOUTL=0 RIN=1E12 DELAY=10N ROUT=10
*
XU1     Q DGND R_ DGND Q_ DGND   COMP2INPNANDSD
+ PARAMS: RIN={RIN} DELAY={1.1*DELAY} VOUTH={VOUTH} VOUTL={VOUTL} VTHRES1={(VOUTH-VOUTL)/2} VTHRES2={VTHRESR}
XU2     S_ DGND Q_ DGND Q DGND   COMP2INPNANDSD
+ PARAMS: RIN={RIN} DELAY={DELAY} VOUTH={VOUTH} VOUTL={VOUTL} VTHRES1={VTHRESS} VTHRES2={(VOUTH-VOUTL)/2}
.ENDS R_S_FF
*$
.SUBCKT RSFF S  R  Q  Q_ DPWR DGND
+ PARAMS:VTHRESS=2.5 VTHRESR=2.5 VOUTH=5.0 VOUTL=0 RIN=1E12 DELAY=10N ROUT=10
*
XU1     Q DGND S DGND Q_ DGND   Comp2InpNOrSD
+ PARAMS: RIN={RIN} DELAY={1.1*DELAY} VOUTH={VOUTH} VOUTL={VOUTL} VTHRES1={(VOUTH-VOUTL)/2} VTHRES2={VTHRESS}
XU2     R DGND Q_ DGND Q DGND   Comp2InpNOrSD
+ PARAMS: RIN={RIN} DELAY={DELAY} VOUTH={VOUTH} VOUTL={VOUTL} VTHRES1={VTHRESR} VTHRES2={(VOUTH-VOUTL)/2}
.ENDS  RSFF
*$
.SUBCKT TFFLOP  PREB CLRB CLKB T Q QB 
+ OPTIONAL: DPWR=$G_DPWR DGND=$G_DGND
+ PARAMS:    DLH = 0 DHL = 0
U1 JKFF(1) DPWR DGND PREB CLRB CLKB T T Q QB 
+ T_DFF IO_HC
.MODEL T_DFF UEFF( TPPCQLHTY = {DLH} TPCLKQLHTY = {DLH}
+                   TPPCQHLTY = {DHL} TPCLKQHLTY = {DHL} )
.ENDS
*$
.SUBCKT TFFLOP_P  PREB CLRB CLKB T Q QB DPWR DGND 
+ PARAMS:    DLH = 0 DHL = 0
U1 JKFF(1) DPWR DGND PREB CLRB CLKB T T Q QB 
+ T_DFF IO_HC
.MODEL T_DFF UEFF( TPPCQLHTY = {DLH} TPCLKQLHTY = {DLH}
+                   TPPCQHLTY = {DHL} TPCLKQHLTY = {DHL} )
.ENDS
*$
.SUBCKT JKFFLOP  PREB CLRB CLKB J K Q QB 
+ OPTIONAL: DPWR=$G_DPWR DGND=$G_DGND
+ PARAMS:    DLH = 0 DHL = 0
U1 JKFF(1) DPWR DGND PREB CLRB CLKB J K Q QB 
+ T_DFF IO_HC
.MODEL T_DFF UEFF( TPPCQLHTY = {DLH} TPCLKQLHTY = {DLH}
+                   TPPCQHLTY = {DHL} TPCLKQHLTY = {DHL} )
.ENDS
*$
.SUBCKT JKFFLOP_P  PREB CLRB CLKB J K Q QB DPWR DGND 
+ PARAMS:    DLH = 0 DHL = 0
U1 JKFF(1) DPWR DGND PREB CLRB CLKB J K Q QB 
+ T_DFF IO_HC
.MODEL T_DFF UEFF( TPPCQLHTY = {DLH} TPCLKQLHTY = {DLH}
+                   TPPCQHLTY = {DHL} TPCLKQHLTY = {DHL} )
.ENDS
*$
.SUBCKT INV_P  IN OUT DPWR DGND 
+ PARAMS:    DLH = 0 DHL = 0
*
U1 INV DPWR DGND IN OUT 
+ T_GATE IO_HC
.MODEL T_GATE UGATE( TPLHTY={DLH} TPHLTY={DHL} )
.ENDS
*$
.SUBCKT AND2_P  IN1 IN2 OUT DPWR DGND 
+ PARAMS:    DLH = 0 DHL = 0
*
U1 AND(2) DPWR DGND IN1 IN2 OUT 
+ T_GATE IO_HC
.MODEL T_GATE UGATE( TPLHTY={DLH} TPHLTY={DHL} )
.ENDS
*$
.SUBCKT INVS  IN OUT DPWR DGND  
*
XU1     IN DGND OUT DGND   CompInvS 
+ PARAMS: RIN=1E12 VOUTH=5 VOUTL=0 VTHRES=2.5
.ENDS
*$
.SUBCKT AND2  IN1 IN2 OUT DPWR DGND 
+ PARAMS:    DLH = 0 DHL = 0
*
XU1     IN1 DGND IN2 DGND OUT DGND   Comp2InpAndSD  
+ PARAMS: RIN=1E12 ROUT=10 DELAY={(DLH + DHL)/2} VOUTH=5 VOUTL=0 VTHRES1=2.5 VTHRES2=2.5
.ENDS
*$
.SUBCKT ANDS2  IN1 IN2 OUT DPWR DGND 
*
XU1     IN1 DGND IN2 DGND OUT DGND   Comp2InpAndS
+ PARAMS: RIN=1E12 VOUTH=5 VOUTL=0 VTHRES1=2.5 VTHRES2=2.5
.ENDS
*$
.SUBCKT NOR2  IN1 IN2 OUT DPWR DGND 
+ PARAMS:    DLH = 0 DHL = 0
*
XU1     IN1 DGND IN2 DGND OUT DGND   Comp2InpNORSD 
+ PARAMS: RIN=1E12 ROUT=10 DELAY={(DLH + DHL)/2} VOUTH=5 VOUTL=0 VTHRES1=2.5 VTHRES2=2.5
.ENDS
*$
.SUBCKT NORS2  IN1 IN2 OUT DPWR DGND 
*
XU1     IN1 DGND IN2 DGND OUT DGND   Comp2InpNORS 
+ PARAMS: RIN=1E12 VOUTH=5 VOUTL=0 VTHRES1=2.5 VTHRES2=2.5
.ENDS
*$
.SUBCKT INV  IN OUT DPWR DGND  
+ PARAMS:    DLH = 0 DHL = 0
*
XU1     IN DGND OUT DGND   CompInvD 
+ PARAMS: RIN=1E12 ROUT=10 DELAYLH={DLH} DELAYHL={DHL} VOUTH=5 VOUTL=0 VTHRES=2.5
.ENDS
*$
.Subckt CompInvD IN1+ IN1- OUT GND
+ PARAMS: RIN=1E12 ROUT=10 DELAYLH=10N DELAYHL=10N VOUTH=5 VOUTL=0 VTHRES=2.5
*
.PARAM RDEL = {IF ( (ROUT < 1), 1, ROUT ) } 
.PARAM COUT={(DELAYLH + DELAYHL)/2/(0.693*RDEL)}
.PARAM RDELHL = {DELAYHL/(0.693*COUT)}
.PARAM RDELLH = {DELAYLH/(0.693*COUT)}
RINP1 IN1+ GND {RIN}
RINM1 IN1- GND {RIN}
EOUT OUT GND VALUE= { IF ( (V(IN1+,IN1-) > {VTHRES}), 
+ VOUTL + RDELHL*I(EOUT), VOUTH + RDELLH*I(EOUT) ) }
COUT OUT GND {COUT}
.Ends CompInvD
*$
* OPERATIONAL AMPLIFIER MACROMODEL SUBCIRCUIT
* CREATED 08/05/06
* (REV 1.3 26/02/08 )
.SUBCKT OPAMP  INP INM VP VM OUT
+ PARAMS: GAIN=200K RIN=2MEG RINC=1E9 ROUT=75 SLEWRATE=500K FPOLE1=5 FPOLE2=1MEG 
+         VDROPOH=1.9 VDROPOL=1.9 VOFFS=1M IBIAS=80N IOFFS=20N 
*
.PARAM PI = 3.141592
.PARAM IS = 1.0E-12
.PARAM VT = 0.02585
.PARAM IMAX = 100.0E-2
.PARAM C1 = {IMAX/SLEWRATE}
.PARAM R1 = {1/(2*PI*C1*FPOLE1)}
.PARAM GM1 = {GAIN/R1}
.PARAM R2 = 100
.PARAM G2 = {1/R2}
.PARAM GOUT = {1/ROUT}
.PARAM C2 = {1/(2*PI*R2*FPOLE2)}
.PARAM VDF = {VT*LOG(1 + IMAX/IS)}
*
IBIASM      INM 8  {IBIAS - IOFFS}
RINM      INM  8  {2*RINC}
RINP      INP  8  {2*RINC}
IBIAS       10 8   {IBIAS}
VOFFS       10 INP  {VOFFS}
EVP VPI 0 VP 0 1
EVM VMI 0 VM 0 1
VC          VPI 11  {VDROPOH + VDF}
VE          12 VMI  {VDROPOL + VDF}
RP          VP VM  15E3
ROUT        OUT 8  {ROUT}
GMO         8 OUT 9 8 {GOUT}
C2          9 8  {C2}
R2          9 8  {R2}
GM2         8 9 7 8 {G2}
RIN         INM 10  {RIN}
EGND        8  0  POLY(2) (VP,0) (VM,0) 0 .5 .5
D3         12 7  D_1
D2          7 11  D_1
C1          7 8  {C1}
R1          7 8  {R1}
GM1         8 7 VALUE = { LIMIT( GM1*V(10,INM), -IMAX, IMAX) }
.MODEL D_1 D( IS={IS} )
.ENDS
*$
* OPERATIONAL AMPLIFIER MACROMODEL SUBCIRCUIT
* CREATED 15/09/08
* (REV 1.0 15/09/08 )
.SUBCKT OPAMPGndRef  INP INM GND OUT
+ PARAMS: GAIN=200K RIN=2MEG RINC=1E9 ROUT=75 SLEWRATE=500K FPOLE1=5 FPOLE2=1MEG 
+         VDROPOH=1.9 VDROPOL=1.9 VOFFS=1M IBIAS=80N IOFFS=20N 
*
.PARAM PI = 3.141592
.PARAM IS = 1.0E-12
.PARAM VT = 0.02585
.PARAM IMAX = 100.0E-2
.PARAM C1 = {IMAX/SLEWRATE}
.PARAM R1 = {1/(2*PI*C1*FPOLE1)}
.PARAM GM1 = {GAIN/R1}
.PARAM R2 = 100
.PARAM G2 = {1/R2}
.PARAM GOUT = {1/ROUT}
.PARAM C2 = {1/(2*PI*R2*FPOLE2)}
.PARAM VDF = {VT*LOG(1 + IMAX/IS)}
*
IBIASM      INM GND  {IBIAS - IOFFS}
RINM      INP  GND  {2*RINC}
RINP      INP  GND  {2*RINC}
IBIAS       10 GND   {IBIAS}
VOFFS       10 INP  {VOFFS}
VC          11 GND {VOUTH - VDF}
VE          GND 12  {VOUTL - VDF}
ROUT        OUT GND  {ROUT}
GMO         GND OUT 9 GND {GOUT}
C2          9 GND  {C2}
R2          9 GND  {R2}
GM2         GND 9 7 GND {G2}
RIN         INM 10  {RIN}
D3         12 7  D_1
D2          7 11  D_1
C1          7 GND  {C1}
R1          7 GND  {R1}
GM1         GND 7 VALUE = { LIMIT( GM1*V(10,INM), -IMAX, IMAX) }
.MODEL D_1 D( IS={IS} )
.ENDS
*$
.SUBCKT HYSTCOMP in+ in- out gnd 
+ Params: Vthres=0 Vhyst=1 VoutH=5 VoutL=0 Rout=1 Tdel=1N
*
.Param Tdelay = {IF ( (Tdel < 1e-9) , 1e-9, Tdel ) } 
.Param Rdel = {IF ( (Tdelay > 1E-15) & (Rout < 1), 1, Rout ) } 
.Param VoutM={(VoutH-VoutL)/2}
.Param VthH={Vthres+Vhyst}
.Param VthL={Vthres-Vhyst}
.Param Cout={Tdelay/(0.693*(Rdel+1u))}
*
Ethr thr gnd Value= { IF ( V(out,gnd) < {VoutM}, {VthH}, {VthL}) }
Eout out gnd Value= { IF ( V(in+,in-) > V(thr,gnd), VoutH + Rdel*I(Eout) , VoutL + Rdel*I(Eout)  ) }
Cout out gnd {Cout}
.ENDS
*$
* COMPARATOR MACROMODEL SUBCIRCUIT
* CREATED 15/09/08
* (REV 1.0 15/09/08 )
.SUBCKT COMPARATOR  INP INM VP VM OUT
+ PARAMS: GAIN=200K RIN=2MEG RINC=1E9 ROUT=75 SLEWRATE=500K DELAY=0
+         VDROPOH=1.9 VDROPOL=1.9 VOFFS=1M IBIAS=80N IOFFS=20N VTHRES=0 VHYST=0
*
.PARAM PI = 3.141592
.PARAM IS = 1.0E-12
.PARAM VT = 0.02585
.PARAM IMAX = 100.0E-2
.PARAM G_SRMULT = 10
.PARAM DEL = {IF ( DELAY < 1N, 1N, DELAY ) } 
.PARAM SR = { IF ( SLEWRATE < 1e-6, G_SRMULT*5/DEL, SLEWRATE ) }
.PARAM C1 = {IMAX/SR}
.PARAM G_PMULT = 100
.PARAM FPOLE1:= G_PMULT*SR/(2*pi*5)/Gain;
.PARAM R1 = {1/(2*PI*C1*FPOLE1)}
.PARAM GM1 = {GAIN/R1}
.PARAM GOUT = {1/ROUT}
.PARAM VDF = {VT*LOG(1 + IMAX/IS)}
.PARAM VDH = {VDROPOH - IMAX*DEL/C1}
.PARAM VDL = {VDROPOL + IMAX*DEL/C1}
.Param VthH={Vthres+Vhyst}
.Param VthL={Vthres-Vhyst}
*
IBIASM      INM 8  {IBIAS - IOFFS}
RINM      INM  8  {2*RINC}
RINP      INP  8  {2*RINC}
IBIAS       10 8   {IBIAS}
VOFFS       10 INP  {VOFFS}
EVP VPI 0 VP 0 1
EVM VMI 0 VM 0 1
VC          VPI 11  {VDH + VDF}
VE          12 VMI  {VDL + VDF}
RP          VP VM  15E3
ROUT        OUT 8  {ROUT}
GMO         8 OUT VALUE = {GOUT*LIMIT( V(7,8), V(VMI,0)+VDL, V(VPI,0)-VDH)}
RIN         INM 10  {RIN}
EGND        8  0  POLY(2) (VP,0) (VM,0) 0 .5 .5
D3         12 7  D_1
D2          7 11  D_1
C1          7 8  {C1}
R1          7 8  {R1}
Ethr thr 0 Value= { IF ( V(out,8) < V(8,0), {VthH}, {VthL}) }
GM1         8 7 VALUE = { LIMIT( GM1*(V(10,INM)-V(thr,0)), -IMAX, IMAX) }
.MODEL D_1 D( IS={IS} )
.ENDS
*$
* COMPARATOR MACROMODEL SUBCIRCUIT
* CREATED 15/09/08
* (REV 1.0 15/09/08 )
.SUBCKT COMPARGndRef  INP INM OUT GND
+ PARAMS: GAIN=200K RIN=2MEG RINC=1E9 ROUT=75 SLEWRATE=500K DELAY=0
+         VOUTH=5 VOUTL=0 VOFFS=1M IBIAS=80N IOFFS=20N VTHRES=0 VHYST=0
*
*.PARAM PI = 3.141592
.PARAM IS = 1.0E-12
*.PARAM VT = 0.02585
.PARAM IMAX = 100.0E-2
.PARAM G_SRMULT = 10
.PARAM DEL = {IF ( DELAY < 1N, 1N, DELAY ) } 
.PARAM SR = { IF ( SLEWRATE < 1e-6, G_SRMULT*5/DEL, SLEWRATE ) }
.PARAM C1 = {IMAX/SR}
.PARAM G_PMULT = 100
.PARAM FPOLE1= {G_PMULT*SR/(2*PI*5)/GAIN};
.PARAM R1 = {1/(2*PI*C1*FPOLE1)}
.PARAM GM1 = {GAIN/R1}
.PARAM GOUT = {1/ROUT}
.PARAM VDF = {VT*LOG(1 + IMAX/IS)}
.PARAM VOH = {VOUTH + IMAX*DEL/C1}
.PARAM VOL = {VOUTL - IMAX*DEL/C1}
.Param VthH={Vthres+Vhyst}
.Param VthL={Vthres-Vhyst}
.Param VoutM={(VoutH-VoutL)/2}
*
IBIASM      INM GND  {IBIAS - IOFFS}
RINM      INM  GND  {2*RINC}
RINP      INP  GND  {2*RINC}
IBIAS       10 GND   {IBIAS}
VOFFS       10 INP  {VOFFS}
VC          11 GND {VOH - VDF}
VE          12 GND  {VOL + VDF}
ROUT        OUT GND  {ROUT}
GMO         GND OUT VALUE = {GOUT*LIMIT( V(7,GND), VOUTL, VOUTH)}
RIN         INM 10  {RIN}
D3         12 7  D_1
D2          7 11  D_1
C1          7 GND  {C1}
R1          7 GND  {R1}
Ethr        thr 0 Value= { IF ( V(out,GND) < {VoutM}, {VthH}, {VthL}) }
GM1         GND 7 VALUE = { LIMIT( GM1*(V(10,INM)-V(thr,0)), -IMAX, IMAX) }
.MODEL D_1 D( IS={IS} )
.ENDS
*$
********************************************
**        Oscillator SW                   **
**  This file was created by TINA         **
**    (c) 2008 DesignSoft, Inc.           **
********************************************
.SUBCKT Oscillator En Gnd OutRamp OutSqr
+ PARAMS: FREQ=600K DUTYCYC=0.018 RISETIME=1E-9 FALLTIME=1E-9 
+ VOUTH=5 VOUTL=0 VENTHRES=2.5
.PARAM VHYST = 1 
.PARAM COsc = 1000P
.PARAM ROsc = { 1/(FREQ*COsc)*(1-DUTYCYC) }
.PARAM IOsc = { 2*VHYST/ROsc }
.PARAM SWRon = { -DUTYCYC/(FREQ*(COsc*log(1-2*VHYST/(2*VHYST+100m)))) }
*
GIOsc       Gnd OutRamp VALUE = {IF( V(En,Gnd)>2.5, IOsc, 0)}
XOscComp     OutRamp Gnd En Gnd OutSqr Gnd   COMP2INPAND
+ PARAMS: VTHRES1={VHYST+100m} VTHRES2={VENTHRES} VOUTH={VOUTH} VOUTL={VOUTL} RIN=1E12 
+ DELAY=10N VHYST1={VHYST} VHYST2=0
SWOsc       OutRamp Gnd OutSqr Gnd  S_VSWITCH_1
COsc        OutRamp Gnd {COsc} IC=0
.MODEL S_VSWITCH_1 VSWITCH (RON={SWRon} ROFF=10MEG VON={0.6*VOUTH + 0.4*VOUTL} 
+ VOFF={0.4*VOUTH + 0.6*VOUTL} )
.ENDS
*$
*
* CONNECTIONS:   A
*                             |    C
*                             |    |
.SUBCKT D_D       1    2
D1 1 2  DD
.MODEL DD D( IS=1P N=1.0)
.ENDS D_D
*$
.Subckt CAPM1_IC 1 2 Params: C=10u ESL=0 IR=1000MEG ESR=1M IC=0
* C = Capacitance ESL = Equivalent Series Inductance 
* IR = Insulation Resistance ESR = Equivalent Series Resistance
LS  1 11 {ESL}
RLS 1 11 1
RS 11 12 {ESR}
C  12  2 {C}   IC={IC}
RI 12  2 {IR}
.Ends
*$
.Subckt INDM1_IC 1 2 Params: L=10u ESR=1M IC=0
* L = Inductance ESR = Equivalent Series Resistance
L  1 11 {L}   IC={IC}
RS 11 2 {ESR}
.Ends
*$
.SUBCKT TPSW476K010R0125_IC 1 9 PARAMS: IC=0
*parasitic inductance
Lesl 1 2 2.200000e-009
Rels 1 2 10
*leakage current & reverse bias effects
Rp 2 9 2.340426e+006
Dp 9 2 DFWD
*RC-ladder model of frequency effects
R1 2 3 RMOD1 1.758251e-001
C1 2 3 CMOD1 3.151478e-003 
R2 3 4 RMOD2 5.187693e-002
C2 4 9 CMOD2 1.519077e-006 IC={IC}
R3 4 5 RMOD3 2.793282e-002
C3 5 9 CMOD3 3.038153e-006 IC={IC}
R4 5 6 RMOD4 1.050000e-004
C4 6 9 CMOD4 6.076306e-006 IC={IC}
R5 6 7 RMOD5 1.050000e-004
C5 7 9 CMOD5 1.215261e-005 IC={IC}
R6 7 8 RMOD6 1.509521e-001
C6 8 9 CMOD6 2.430523e-005 IC={IC}
.MODEL CMOD1 CAP (T_MEASURED=25 TC1=1.463615e-003 TC2=-4.147800e-005)
.MODEL CMOD2 CAP (T_MEASURED=25 TC1=3.749220e-004 TC2=2.806000e-006)
.MODEL CMOD3 CAP (T_MEASURED=25 TC1=3.749220e-004 TC2=2.806000e-006)
.MODEL CMOD4 CAP (T_MEASURED=25 TC1=3.749220e-004 TC2=2.806000e-006)
.MODEL CMOD5 CAP (T_MEASURED=25 TC1=3.749220e-004 TC2=2.806000e-006)
.MODEL CMOD6 CAP (T_MEASURED=25 TC1=3.749220e-004 TC2=2.806000e-006)
.MODEL RMOD1 RES (T_MEASURED=25 TC1=5.948893e-003 TC2=5.337100e-005)
.MODEL RMOD2 RES (T_MEASURED=25 TC1=-1.770874e-003 TC2=1.281300e-005)
.MODEL RMOD3 RES (T_MEASURED=25 TC1=-7.138201e-003 TC2=2.153200e-005)
.MODEL RMOD4 RES (T_MEASURED=25 TC1=-7.138201e-003 TC2=2.153200e-005)
.MODEL RMOD5 RES (T_MEASURED=25 TC1=-7.138201e-003 TC2=2.153200e-005)
.MODEL RMOD6 RES (T_MEASURED=25 TC1=-7.138201e-003 TC2=2.153200e-005)
.MODEL DFWD D (RS=0.1 IS=8e-10 N=2.5 XTI=0 EG=0.1)
.ENDS TPSW476K010R0125_IC 
*$
