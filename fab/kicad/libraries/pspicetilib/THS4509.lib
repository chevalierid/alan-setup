***************************************************************************** 
* THS4509 
* WIDEBAND, LOW-NOISE, LOW DISTORTION, FULLY DIFFERENTIAL AMPLIFIER
*
*****************************************************************************
* (C) Copyright 2011 Texas Instruments Incorporated. All rights reserved.    
*****************************************************************************
** This model is designed as an aid for customers of Texas Instruments.      
** TI and its licensors and suppliers make no warranties, either expressed   
** or implied, with respect to this model, including the warranties of       
** merchantability or fitness for a particular purpose. The model is         
** provided solely on an "as is" basis. The entire risk as to its quality    
** and performance is with the customer                                      
*****************************************************************************
*
* Released by: Analog eLab Design Center, Texas Instruments Inc.
* Part:        THS4509
* WRITTEN:     01/24/2005
* Simulator:   PSPICE
* Simulator Version: 16.2.0.p001
* EVM Order Number:  N/A 
* EVM Users Guide:   N/A
* Datasheet:   SLOS454H –JANUARY 2005–REVISED NOVEMBER 2009
* Model Version: 1.0
*
*****************************************************************************
*
* Updates:
*
* Version 1.0 : Released on 01/24/2005
* Version 2.0 : Released on 02/07/2006
* Version 3.0 : 
*      It was reported that the model was only functional for split-supply operation
*      Correction for both split & single-supply operation on 7/20/2011
*
*****************************************************************************	   
* THIS MODEL SIMULATES TYPICAL VALUES FOR THE FOLLOWING:
* FREQUENCY RESPONSE OF THE MAIN DIFFERENTIAL AMP, OUTPUT VOLTAGE LIMIT,   
* INPUT VOLTAGE NOISE, INPUT CURRENT NOISE, INPUT BIAS CURRENT, INPUT OFFSET VOLTAGE, 
* CM SET POINT, OFFSET, AND BANDWIDTH
* THIS MODEL WILL NOT PROVIDE ACCURATE SIMULATION OF:  OUTPUT LOADING EFFECTS, SLEW RATE, 
* SETTLING TIME, OUTPUT IMPEDANCE, DISTORTION, INPUT OFFSET vs INPUT COMMON-MODE VOLTAGE, 
* CMRR AND PSRR
* IN PSPICE THIS MODEL WILL NOT CONVERGE IN TRANSIENT ANALYSIS USING PULSES THAT CAUSE 
* GREATER THAN 4000V/us SLEW RATE
*****************************************************************************	 
* 
*
*$
.SUBCKT THS4509 IN+ IN- VS+ VS- OUT- OUT+ CM
*INPUT*
Q9          VS- 001 005 PNP_IN  5
Q10         VS- IN- 031 PNP_IN  5
Q11         VS+ IN- 006 NPN_IN  5
Q12         VS+ 001 002 NPN_IN  5
Q19         039 005 008 NPN  5
Q20         038 031 009 NPN  5
Q21         033 006 009 PNP  5
Q22         035 002 008 PNP  5
R1          009 008  90
I1          002 003 DC 750u
I2          004 005 DC 750u
I3          006 007 DC 750u
I10         036 031 DC 750u
R20         036 VS+  500
R21         004 VS+  500
R22         VS- 003  500
R23         VS- 007  500
C1          0 001   0.3p
C2          0 IN-    0.3p
V1          001 IN+ 300u
IB+         IN+ 0 6u
IB-         IN- 0 6u
Q23         041 041 039 PNP  5
Q24         027 041 038 PNP  5
Q17         037 037 038 PNP  5
Q18         029 037 039 PNP  5
Q15         032 032 033 NPN  5
Q16         034 034 035 NPN  5
Q14         029 032 035 NPN  5
Q13         027 034 033 NPN  5
I8          VS+ 032 DC 750u
*I8          VS+ 032 DC 2.3m
*I9          VS+ 034 DC 2.3m
*I11         037 VS- DC 2.3m
*I12         041 VS- DC 2.3m

I9          VS+ 034 DC 750u
I11         037 VS- DC 750u
I12         041 VS- DC 750u
R30         029 027  1meg
C5          029 027  0.5p
R16         038 VS+  500
R17         039 VS+  500
R18         038 VS+  500
R19         039 VS+  500
R24         VS- 035  500
R25         VS- 033  500
R26         VS- 035  500
R27         VS- 033  500
Q116        027 027 128 NPN  5
V116        VS+ 128   1.75
Q118        027 027 138 PNP  5
V118        138 VS-   1.75
Q216        029 029 228 NPN  5
V216        VS+ 228   1.75
Q218        029 029 238 PNP  5
V218        238 VS-   1.75
RQ          VS+ VS- 555
E2          028 0 027 0 1
E3          030 0 029 0 1
C3          0 024  3p
C4          0 017  3p
L1          025 028  2n
L2          030 026  2n
R10         024 025  25
R11         017 026  25
Q1          VS+ 012 013 NPN  50
Q2          VS- 015 016 PNP  50
Q3          VS+ 017 015 NPN  13
Q4          VS- 017 012 PNP  13
Q5          VS+ 019 020 NPN  50
Q6          VS- 021 023 PNP  50
Q7          VS+ 024 021 NPN  13
Q8          VS- 024 019 PNP  13
I4          011 012 DC 2m
I5          015 014 DC 2m
I6          018 019 DC 2m
I7          021 022 DC 2m
R4          VS- 014  100
R7          018 VS+  100
R12         011 VS+  100
R13         VS- 022  100
R5          OUT- 013  1
R6          016 OUT-  1
R8          OUT+ 020  1
R9          023 OUT+  1
R2          OUT+ 010  10k
R3          010 OUT-  10k
*C102       OUT+ 010 100p
*C103       010 OUT- 100p
*R100       042 0 1meg
*C100       042 0 10p
R114        CM CM2  100
C114        CM2 0 2.3p
R14         VS+ CM2  50k
R15         VS- CM2  50k
V3          043 CM2 3m
F2          041 VS- VF2 1
VF2         040 VS- 0V
F1          037 VS- VF1 1
VF1         042 040 0V
G1          042 VS+ 010 043 .0001
.MODEL NPN_IN NPN KF=1E-11
.MODEL PNP_IN PNP KF=1E-11
.MODEL NPN NPN
.MODEL PNP PNP
.ENDS
*$