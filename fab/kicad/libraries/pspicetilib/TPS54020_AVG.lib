* PSpice Model Editor - Version 16.2.0
*$
* TPS54020
*****************************************************************************
* (C) Copyright 2012 Texas Instruments Incorporated. All rights reserved.
*****************************************************************************
** This model is designed as an aid for customers of Texas Instruments.
** TI and its licensors and suppliers make no warranties, either expressed
** or implied, with respect to this model, including the warranties of
** merchantability or fitness for a particular purpose. The model is
** provided solely on an "as is" basis. The entire risk as to its quality
** and performance is with the customer
*****************************************************************************
*
** Released by: WEBENCH Design Center, Texas Instruments Inc.
* Part: TPS54020
* Date: 24MAY2012
* Model Type: Average
* Simulator: PSpice
* Simulator Version: 16.2
* EVM Order Number: TPS54020EVM-082
* Datasheet: SLVSB10
* Model Version: Final 1.00
*
*****************************************************************************
*
* Updates:
*
* Final 1.00
* Release to Web
*
*****************************************************************************
.SUBCKT TPS54020_AVG VSNS RTN PH COMP VIN ILIM GND PARAMS: L=1u FS=500K
G_G2         GND COMP N16635916 VSNS 1300u
R_R19         GND COMP  2.38MEG TC=0,0 
V_V28         N16635916 GND 0.6
R_R21         ILIM RTN  100MEG TC=0,0 
I_I1         N16655371 N16635396 DC 1u  
C_C13         GND COMP  20.7p  TC=0,0 
E_ABM5         SE 0 VALUE {  IF(V(SHORT) > 0.5, 9,   
+ IF(V(500K) > 0.5, 8,10))   }
V_V31         N16635392 GND 600m
V_V30         N16655371 RTN 2.5
E_ABM4         SHORT 0 VALUE { IF(V(N16660987) > V(N16635396), 1,0)    }
X_U9         VIN PH GND COMP N16639779 RI SE CCM PARAMS:  L={L} FS={FS} ACS=1
E_ABM3         500K 0 VALUE { IF(V(N16635392) > V(N16635396), 1,0)    }
E_ABM2         RI 0 VALUE {  IF(V(SHORT) > 0.5, 54m,   
+ IF(V(500K) > 0.5, 62m,41m))   }
V_V32         N16660987 GND 100m
X_D1         N16635396 N16655371 d_d1 PARAMS: 
R_R20         N16635396 ILIM  10k TC=0,0 
.ENDS TPS54020_AVG
*$
.SUBCKT CCM a c p vc dc ri slope_comp params: L=4.7u FS=500k ACS=1 
ESe Sa 0 value = { (V(slope_comp)*{Fs}*v(ri)) }
Edc dcx 0 value = { v(cx,p)/(v(a,p)+1u) }
Xdc dcx dc limit params: clampH=0.99 clampL=48m
Ed2 d2X 0 value = {(1 - V(dc))} 
Xd2 d2X dc d2 limit2
GIap a p value =  {V(dc)*I(VM)}
GIpc p cx value = { V(vc)/({Acs}*v(ri)) }
GImju cx p value = {V(Sa)*V(dc)/({Acs}*v(ri)*{Fs})+(v(cx,p))/({L}*{Fs})*(((1-V(dc))/2)) }
XC1 cx p mode varicap ; 
Emode mode 0 Value = { IF ((2*{L}*{Fs}*I(VM)/(V(dc)*V(a,cx)+1u))
+ < 1, {4/({L}*(6.28*{Fs})^2)}, {4/({L}*(6.28*{Fs})^2)}) } ;
VM cx c
.ENDS
*$
.SUBCKT VARICAP 1 2 CTRL
R1 1 3 1u
VC 3 4
EBC 4 2 Value = { (1/v(ctrl))*v(int) }
GINT 0 INT Value = { I(VC) }
CINT INT 0 1
Rdum INT 0 10E10
.ENDS
*$
.subckt limit d dc params: clampH=0.99 clampL=48m
Gd 0 dcx VALUE = { V(d)*100u }
Rdc dcx 0 10k
V1 clpn 0 {clampL}
V2 clpp 0 {clampH}
D1 clpn dcx dclamp
D2 dcx clpp dclamp
Edc dc 0 value={ V(dcx) }
.model dclamp d n=0.01 rs=100m
.ENDS
*$
*
.subckt limit2 d2nc d d2c
Gd 0 d2cx d2nc 0 100u
Rdc d2cx 0 10k
V1 clpn 0 7m
E2 clpp 0 Value = { 1-V(d) }
D1 clpn d2cx dclamp
D2 d2cx clpp dclamp
Edc d2c 0 value={ V(d2cx) }
.model dclamp d n=0.01 rs=100m
.ENDS
*
*$
.SUBCKT LDCR IN OUT
+ PARAMs:  L=1u DCR=0.01 
L             IN 1  {L} 
RDCR	1 OUT {DCR}
.ENDS LDCR
*$
*
.SUBCKT CESR IN OUT
+ PARAMs:  C=100u ESR=0.01 X=2 
C	IN 1  {C*X} 
RESR	1 OUT {ESR/X}
.ENDS CESR
*$
*$
.SUBCKT D_D1 1 2
D1 1 2 DD1
.MODEL DD1 D( IS=1e-15 TT=10p Rs=0.05 N=.1  )
.ENDS D_D1
*$
.SUBCKT COMP_BASIC_GEN INP INM Y PARAMS: VDD=1 VSS=0 VTHRESH=0.5	
E_ABM Yint 0 VALUE {IF (V(INP) > 
+ V(INM), {VDD},{VSS})}
R1 Yint Y 1
C1 Y 0 1n
.ENDS COMP_BASIC_GEN
*$

