* INA114
*****************************************************************************
* (C) Copyright 2011 Texas Instruments Incorporated. All rights reserved.                                            
*****************************************************************************
** This model is designed as an aid for customers of Texas Instruments.
** TI and its licensors and suppliers make no warranties, either expressed
** or implied, with respect to this model, including the warranties of 
** merchantability or fitness for a particular purpose.  The model is
** provided solely on an "as is" basis.  The entire risk as to its quality
** and performance is with the customer.
*****************************************************************************
*
* This model is subject to change without notice. Texas Instruments
* Incorporated is not responsible for updating this model.
*
*****************************************************************************
*
** Released by: Analog eLab Design Center, Texas Instruments Inc.
* Part: INA114
* Date: 21JUL2011
* Model Type: ALL IN ONE
* Simulator: PSPICE
* Simulator Version: 16.0.0.p001
* EVM Order Number: N/A
* EVM Users Guide: N/A
* Datasheet: SBOS014
*
* Model Version: 1.0
*
*****************************************************************************
*
* Updates:
*
* Version 1.0 : 
* Release to Web
*
*****************************************************************************
*
* INA114 = A1_114 + A2_114 + A3_114 OP AMPS + PRECISION RESISTOR NETWORK
*
***** INA114 SUB-CIRCUIT
* CONNECTIONS:    NON-INVERTING INPUT
*                 |   INVERTING INPUT
*                 |   |   POSITIVE POWER SUPPLY
*                 |   |   |   NEGATIVE POWER SUPPLY
*                 |   |   |   |   OUTPUT
*                 |   |   |   |   |   FEEDBACK
*                 |   |   |   |   |   |    REFERENCE
*                 |   |   |   |   |   |   |   GAIN SET 1
*                 |   |   |   |   |   |   |   |   GAIN SET 2
*                 |   |   |   |   |   |   |   |   |
.SUBCKT INA114    1   2   3   4   5   6   8   9   10
*
***** A1_114 SUBCIRCUIT
* CONNECTIONS:    NON-INVERTING INPUT
*                 |   INVERTING INPUT
*                 |   |   POSITIVE POWER SUPPLY
*                 |   |   |   NEGATIVE POWER SUPPLY
*                 |   |   |   |   OUTPUT
*                 |   |   |   |   |
X1                2   9   3   4   11   A1_114
*
* CONNECTIONS:    NON-INVERTING INPUT
*                 |   INVERTING INPUT
*                 |   |   POSITIVE POWER SUPPLY
*                 |   |   |   NEGATIVE POWER SUPPLY
*                 |   |   |   |   OUTPUT
*                 |   |   |   |   |
X2                1   10  3   4   12   A2_114
*
* CONNECTIONS:    NON-INVERTING INPUT
*                 |   INVERTING INPUT
*                 |   |   POSITIVE POWER SUPPLY
*                 |   |   |   NEGATIVE POWER SUPPLY
*                 |   |   |   |   OUTPUT
*                 |   |   |   |   |
X3                14  13  3   4   5   A3_114
*
R1 11 13 25K
R2 13 6 24.9985K
C2 13 6 5P
R3 12 14 25K
R4 14 8 25K
RFB1 9 11 25K
CC1 9 11 5P
RFB2 10 12 25K
CC2 10 12 5P
*
.ENDS
*
* CONNECTIONS:   NON-INVERTING INPUT
*                |   INVERTING INPUT
*                |   |   POSITIVE POWER SUPPLY
*                |   |   |   NEGATIVE POWER SUPPLY
*                |   |   |   |   OUTPUT
*                |   |   |   |   |
.SUBCKT A1_114  1   2   3   4   5
*
C1 11 12 6.060E-12
C2 6 7 30.00E-12
DC 5 53 DX
DE 54 5 DX
DLP 90 91 DX
DLN 92 90 DX
DP 4 3 DX
EGND 99 0 POLY(2) (3,0) (4,0) 0 .5 .5
FB 7 99 POLY(5) VB VC VE VLP VLN 0 165.8E6 -20E6 20E6 20E6 -20E6
GA 6 0 11 12 188.5E-6
GCM 0 6 10 99 188.5E-12
IEE 10 4 DC 27E-6
HLIM 90 0 VLIM 1K
Q1 11 2 13 QX
Q2 12 1 14 QX
R2 6 9 100.0E3
RC1 3 11 5.305E3
RC2 3 12 5.305E3
RE1 13 10 4.348E3
RE2 14 10 4.348E3
REE 10 99 7.4074E6
RO1 8 5 40
RO2 7 99 320
VB 9 0 DC 0
VC 3 53 DC 1.672
VE 54 4 DC 1.672
VLIM 7 8 DC 0
VLP 91 0 DC 2
VLN 0 92 DC 2
****************************
* A1_114  - ENHANCEMENTS
****************************
* OUTPUT SUPPLY MIRROR
FQ3 0 20 POLY(1) VLIM 0 1
DQ1 20 21 DX
DQ2 22 20 DX
VQ1 21 0 0
VQ2 22 0 0
FQ1 3 0 POLY(1) VQ1 0 1
FQ2 0 4 POLY(1) VQ2 0 -1
* QUIESCIENT CURRENT
RQ 3 4 52.53E3
* DIFF INPUT CAPACITANCE
CDIF 1 2 3E-12
* COMMON MODE INPUT CAPACITANCE
C1CM 1 0 3E-12
C2CM 2 0 3E-12
* INPUT PROTECTION
VS11 3 31 0

FS11 0 3 VS11 1
S11 31 0 1 0 SP
FS12 1 4 VS11 1
VS21 32 4 0
FS21 4 0 VS21 1
S21 0 32 1 0 SM
FS22 3 1 VS21 1
****************************
.MODEL DX D(IS=800.0E-18)
.MODEL QX NPN(IS=800.0E-18 BF=36.00E3 NC=100 NR=100)
.MODEL SP VSWITCH(RON=10K ROFF=100G VON=15.6 VOFF=15.5)
.MODEL SM VSWITCH(RON=10K ROFF=100G VON=-15.6 VOFF=-15.5)
.ENDS
*
* CONNECTIONS:   NON-INVERTING INPUT
*                |   INVERTING INPUT
*                |   |   POSITIVE POWER SUPPLY
*                |   |   | NEGATIVE POWER SUPPLY
*                |   |   |   |   OUTPUT
*                |   |   |   |   |
.SUBCKT A2_114  1   2   3   4   5
*
C1 11 12 6.060E-12
C2 6 7 30.00E-12
DC 5 53 DX
DE 54 5 DX
DLP 90 91 DX
DLN 92 90 DX
DP 4 3 DX
EGND 99 0 POLY(2) (3,0) (4,0) 0 .5 .5
FB 7 99 POLY(5) VB VC VE VLP VLN 0 165.8E6 -20E6 20E6 20E6 -20E6
GA 6 0 11 12 188.5E-6
GCM 0 6 10 99 188.5E-12
IEE 10 4 DC 27E-6
HLIM 90 0 VLIM 1K
Q1 11 2 13 QX
Q2 12 1 14 QX
R2 6 9 100.0E3
RC1 3 11 5.305E3
RC2 3 12 5.305E3
RE1 13 10 4.348E3
RE2 14 10 4.348E3
REE 10 99 7.4074E6
RO1 8 5 40
RO2 7 99 320
VB 9 0 DC 0
VC 3 53 DC 1.672
VE 54 4 DC 1.672
VLIM 7 8 DC 0
VLP 91 0 DC 2
VLN 0 92 DC 2
****************************
* A2_114  - ENHANCEMENTS
****************************
* OUTPUT SUPPLY MIRROR
FQ3 0 20 POLY(1) VLIM 0 1
DQ1 20 21 DX
DQ2 22 20 DX
VQ1 21 0 0
VQ2 22 0 0
FQ1 3 0 POLY(1) VQ1 0 1
FQ2 0 4 POLY(1) VQ2 0 -1
* QUIESCIENT CURRENT
RQ 3 4 52.53E3
* DIFF INPUT CAPACITANCE
CDIF 1 2 3E-12
* COMMON MODE INPUT CAPACITANCE
C1CM 1 0 3E-12
C2CM 2 0 3E-12
* INPUT PROTECTION
VS11 3 31 0
FS11 0 3 VS11 1
S11 31 0 1 0 SP
FS12 1 4 VS11 1
VS21 32 4 0
FS21 4 0 VS21 1
S21 0 32 1 0 SM
FS22 3 1 VS21 1
****************************
.MODEL DX D(IS=800.0E-18)
.MODEL QX NPN(IS=800.0E-18 BF=36.00E3 NC=100 NR=100)
.MODEL SP VSWITCH(RON=10K ROFF=100G VON=15.6 VOFF=15.5)
.MODEL SM VSWITCH(RON=10K ROFF=100G VON=-15.6 VOFF=-15.5)
.ENDS
*
* CONNECTIONS:   NON-INVERTING INPUT
*                | INVERTING INPUT
*                | | POSITIVE POWER SUPPLY
*                | | | NEGATIVE POWER SUPPLY
*                | | | | OUTPUT
*                | | | | |
.SUBCKT A3_114  1 2 3 4 5
*
C1 11 12 13.51E-12
C2 6 7 30.00E-12
DC 5 53 DX
DE 54 5 DX
DLP 90 91 DX
DLN 92 90 DX
DP 4 3 DX
EGND 99 0 POLY(2) (3,0) (4,0) 0 .5 .5
FB 7 99 POLY(5) VB VC VE VLP VLN 0 251.9E6 -30E6 30E6 30E6 -30E6
GA 6 0 11 12 339.3E-6
GCM 0 6 10 99 339.3E-12
IEE 10 4 DC 18E-6
HLIM 90 0 VLIM 1K
Q1 11 2 13 QX
Q2 12 1 14 QX
R2 6 9 100.0E3
RC1 3 11 2.947E3
RC2 3 12 2.947E3
RE1 13 10 74.778
RE2 14 10 74.778
REE 10 99 11.111E6
RO1 8 5 13
RO2 7 99 117
VB 9 0 DC 0
VC 3 53 DC 1.972
VE 54 4 DC 1.972
VLIM 7 8 DC 0
VLP 91 0 DC 12
VLN 0 92 DC 12
****************************
* A3_114 - ENHANCEMENTS
****************************
* OUTPUT SUPPLY MIRROR
FQ3 0 20 POLY(1) VLIM 0 1
DQ1 20 21 DX
DQ2 22 20 DX
VQ1 21 0 0
VQ2 22 0 0
FQ1 3 0 POLY(1) VQ1 0 1
FQ2 0 4 POLY(1) VQ2 0 -1
* QUIESCIENT CURRENT
RQ 3 4 30.123E3
* DIFF INPUT CAPACITANCE
CDIF 1 2 10E-12
* COMMON MODE INPUT CAPACITANCE
C1CM 1 0 5E-12
C2CM 2 0 5E-12
****************************
.MODEL DX D(IS=800.0E-18)
.MODEL QX NPN(IS=800.0E-18 BF=30.00E3)
.ENDS
*.ENDS INA114
*