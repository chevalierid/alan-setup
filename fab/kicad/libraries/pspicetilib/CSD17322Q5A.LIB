* PSpice Model Editor - Version 16.2.0
*$
* CSD17322Q5A
*****************************************************************************
* (C) Copyright 2016 Texas Instruments Incorporated. All rights reserved.
*****************************************************************************
** This model is designed as an aid for customers of Texas Instruments.
** TI and its licensors and suppliers make no warranties, either expressed
** or implied, with respect to this model, including the warranties of
** merchantability or fitness for a particular purpose. The model is
** provided solely on an "as is" basis. The entire risk as to its quality
** and performance is with the customer.
*****************************************************************************
*
* Released by: WEBENCH Design Center, Texas Instruments Inc.
* Part: CSD17322Q5A
* Date: 08NOV2016
* Model Type: Transient
* Simulator: PSPICE
* Simulator Version: 16.2.0.p001
* EVM Order Number: 
* EVM Users Guide:   
* Datasheet: SLPS330 â€“ JUNE 2011
*
* Model Version: Final 1.00
*
*****************************************************************************
*
* Updates:
*
* Final 1.00
* Release to Web
*
*****************************************************************************
* Model Usage Notes:
*
*****************************************************************************
**********************************************************************
**********************************************************************
*                                                                    *
* CSD17322Q5A   -  PSpice Model for use with OrCAD                   *
*                                                                    *
**********************************************************************
**********************************************************************
* Characteristics:
*   Technology:   NCH 30/20
*   Die Size:	10mm2
*   Part Rdson:	3mOhms @4.5Vgs  (25A)
*   Target Vth:	1.3V
*
**********************************************************************
* Model History
*   Initial model scaled from n17301
*   Also from meas. n17501
*   tr, 05/17/2011, 07:14:26 AM
*    
**********************************************************************
* SUBCKT Definition:  1=D  2=G  3=S
.SUBCKT CSD17322Q5A  1 2 3
* PARAMETER (local) DEFINITIONS SECTION
 .PARAM  ptrc1    3.5e-3  
 .PARAM  ptrc2    5.0e-6
 .PARAM  pwidthP  0.8435272727
 .PARAM  pwidth   {pwidthP*1e6}
 .PARAM  perimP   {2.1*pwidthP}
 .PARAM  perim    {perimP*1e6}
****** MODEL SUBCIRCUIT BEGINS HERE     ******************************
M1   10 11 12 12  NMOS	W={pwidthP} L=0.5u  PS={perimP} PD={perimP}
DBD   8  7	 DBD
CBD0  8  7   3e-12
RBD0  8  7   1e10
CGD0  5  7	 1E-12
CGS0  5  8	 1e-18
M2   12 11 12 20  PMOSd W={pwidthP} L=0.08u PS={perimP} PD={perimP}
DDx  20  10  DDx
DDG  14  5	 DD
DGD  14 10	 DD
* Note:  gate oxide capacitance included in NMOS below
CGS   5 13	 200E-12
LGG   2  5	 2.1e-9
LSS   6  3	 0.47e-9
LDD   1  4	 0.05e-9
RGG   5 11	 10
RGS  13  8	 35
RSL   6  3   0.5
.param a 1e-3
RSB  12  9	 RTEMP {0.04*a}
RSS   9  8 	 RTEMP {0.10*a}
RSP   8  6 	 {0.45*a}
RDD   7 10 	 RTEMP {4.675*a}
RDP   4  7	 {0.15*a}
******************************************************************* 
.MODEL  NMOS   NMOS (
* CONTROL PARAMETERS
+ LEVEL  = 7             VERSION = 3.2         CAPMOD = 2
* DC PARAMETERS
+ NLX    = 1.74E-7       VBM    = -5           DVT0   = 2.2
+ DVT1   = 0.53          DVT2   = -0.032       U0     = 650
+ UA     = 1.0e-9        UB     = 2.4e-18      A0     = 1.10
+ AGS    = 0.0           A1     = 0.0          A2     = 1.0
+ LINT   = 0.055e-6      VOFF   = -0.00        NFACTOR= 2.0
+ ETA0   = 0.005         ETAB   = -0.07        DSUB   = 1.4
+ PCLM   = 1.30		 PDIBLC1= 0.40         PDIBLC2= 0.0086
+ DROUT  = 0.90          PSCBE1 = 3.0e8        PSCBE2 = 1e-6
+ PVAG   = 0.1           DELTA  = 0.0220       JS     = 0
* AC AND CAPACITANCE PARAMETERS
+ CGSO   = 80.00e-12      CGDO   = 8e-12        CGBO   = 1e-12
+ CJ     = 1e-18         CJSW   = 1e-18        CF     = 0
* TEMPERATURE PARAMETERS
+ TNOM   = 25            UTE    = -1.5         KT1    = -0.80
+ KT1L   = 1.0e-15       KT2    = 0.022        UA1    = 0.25e-9
+ UB1    = -0.8e-18      
* PROCESS PARAMETERS
+ TOX    = 3.30e-8       XJ     = 3e-7          NCH    = 1.230e17
+ NSUB   = 1.0e16)
******************************************************************* 
.MODEL  PMOSd   PMOS (   LEVEL  = 1
+ TNOM   = 25            VTO    = -0.3          IS     = 6.0e-14
+ TOX    = 3.3e-8        NSUB   = 9.0e18        gamma  = 6.0
+ CJ     = 1e-18         CJSW   = 1e-18
+ CGSO   = 1e-18         CGDO   = 1e-18         CGBO   = 0  )
*******************************************************************
.MODEL DBD D (CJO=1.50e-9 VJ=1.0 M=0.5175 TNOM=25
+ FC=0.5 TT=2e-09 XTI=1.0 BV=31
+ IS=3.0e-12 N=1.045 RS=3.0e-3 TRS1=4.0e-3)
.MODEL DD  D (CJO=0.240e-09 VJ=0.400 M=.67 IS=1e-13 RS=0.1 TNOM=25)
.MODEL DDx D (CJO=0.085e-09 VJ=0.9 M=0.9 IS=1e-13 RS=1.0 TNOM=25)
*******************************************************************
.MODEL RTEMP RES (TC1={ptrc1} TC2={ptrc2})
*******************************************************************
.ENDS CSD17322Q5A  
*$