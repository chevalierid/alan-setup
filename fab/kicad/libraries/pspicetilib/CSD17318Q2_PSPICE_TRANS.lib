
*$
* CSD17318Q2
*****************************************************************************
* (C) Copyright 2017 Texas Instruments Incorporated. All rights reserved.
*****************************************************************************
** This model is designed as an aid for customers of Texas Instruments.
** TI and its licensors and suppliers make no warranties, either expressed
** or implied, with respect to this model, including the warranties of
** merchantability or fitness for a particular purpose. The model is
** provided solely on an "as is" basis. The entire risk as to its quality
** and performance is with the customer.
*****************************************************************************
*
** Released by: WEBENCH Design Center, Texas Instruments Inc.
* Part: CSD17318Q2
* Date: 14AUG2017
* Model Type: Transient
* Simulator: PSPICE
* Simulator Version: 16.2.0.s003
* EVM Order Number:
* EVM Users Guide:
* Datasheet: SLPS667A –FEBRUARY 2017–REVISED JULY 2017
*
* Model Version: Final 1.00
*
*****************************************************************************
*
* Updates:
*
* Final 1.00
* Release to Web

* PSpice Model Editor - Version 16.6.0

**********************************************************************
**********************************************************************
*                                                                    *
* csd17318q2   -  PSpice Model for use with OrCAD/Allegro            *
*                                                                    *
**********************************************************************
**********************************************************************
* Characteristics:
*   Technology:  n-channel 30/10
*   Assembly:    Q2
*
**********************************************************************
* Model History
*   08/04/2016 - Strawman
*   08/09/2017 - v1.0
*
* SUBCKT Definition:  1=D  2=G  3=S
**********************************************************************
*$
.SUBCKT  csd17318q2_pspice_trans  1 2 3
.PARAM  ptrc1   4.45e-3  
.PARAM  ptrc2   5.30e-6
.PARAM  pAA	    0.507049
.PARAM  pwidth  {pAA/0.8*2}
.PARAM  perim   {2.1*pwidth}
.PARAM  pRgate  0.1
M1   10 11 12 12  NMOS	W={pwidth} L=0.55u  PS={perim} PD={perim}
M2   12 11 12 20  PMOSd W={pwidth} L=0.042u  PS={perim} PD={perim}
DDx  20  10	 DD	{pwidth}
DBD   8  7	 DBD	{pwidth}
RDB   8  7   2e10
CGD0  5  7	 0.5e-12
CGS0  5  8	 1e-12
CDS0 10 12	 2e-12
DDG  14  5   DD	{pwidth}
DGD  14 10   DD 	{pwidth}
LGG   2  5	 0.1e-9
RGG   5 11	 {0.5 + pRgate}
RSB  12  9	 RTEMP 0.01e-3
RSS   9  8 	 RTEMP 0.15e-3
RSP   8  6 	 RTEMP 2.55e-3
LSS   6  3	 0.05E-9
RDP   4  7	 RTEMP 2.15e-3
RDD   7 10 	 RTEMP 5.80e-3
LDD   1  4	 0.05E-9
******************************************************************* 
.MODEL  NMOS   NMOS (    LEVEL  = 7             Version = 3.2
+ TNOM   = 27            LINT   = 0.05e-6       CAPMOD = 3
+ TOX    = 2.5e-8        NCH    = 3.4e17        NSUB   = 1.5e16
+ AGS    = 0.0           PVAG   = 0.1           U0     = 380
+ A0     = 1.0           A1     = 0.0           A2     = 1.0
+ UA     = 4.2e-9        UB     = 9.0e-19       VBM    = -10.0
+ UA1    = 5.5e-9        UB1    = 9.0e-18       UTE    = -0.50
+ KT1    = -0.62         KT2    = 0.022         KT1L   = 1.0e-12
+ DVT0   = 2.3           DVT1   = 0.55	      DVT2   = -0.033
+ ETA0   = 0.075         ETAB   = -0.07         XJ     = 3e-7
+ PDIBLC1= 0.08          PDIBLC2= 0.005         NLX    = 1.78e-7
+ PSCBE1 = 1.0e9         PSCBE2 = 1e-8          PCLM   = 1.1
+ VOFF   = -1.1e-2       NFACTOR= 1.21          JS     = 0
+ DROUT  = 0.9           DSUB   = 0.95          DELTA  = 0.022
+ XPART  = 0.0           VOFFCV = 0.02          VSAT   = 7.5e4
+ K1     = 1.260         K2=0      K3 = 10 
+ CJ     = 1e-18         CJSW   = 1e-18         CF     = 0
+ CGSO   = 9.0e-12       CGDO   = 22e-12        CGBO   = 0    )
******************************************************************* 
.MODEL  PMOSd   PMOS (   LEVEL  = 7			Version= 3.2
+ TNOM   = 27		 CAPMOD = 0			VOFFCV =-0.1
+ TOX    = 2.5e-8	 	 VTO    = -1.0		XPART  = 0.0
+ K1	   = 5.0		 K2	  = -0.10		U0	 = 250
+ CJ     = 1e-18         CJSW   = 1e-18		JS	 = 0
+ CGSO   = 1e-20         CGDO   = 1e-20         CGBO   = 1e-18 )
******************************************************************* 
.MODEL DBD D (CJO=395e-12 VJ=0.46 M=0.821 FC=0.5 TT=4e-09 TNOM=27
+  IS=8.0e-11 N=1.17 RS=2.4e-3 TRS1=2.0e-4 XTI=1.0 BV=32.9
+  TBV1=7.9e-4 TBV2=-1.0e-6 NR=4.5 ISR=1.0e-10 )
******************************************************************
.MODEL DD  D (CJO=252e-12 VJ=0.40 M=1.34 IS=5e-16 RS=40 TNOM=27
+  FC=0.9 )
******************************************************************* 
.MODEL RTEMP RES (TC1={ptrc1} TC2={ptrc2})
*******************************************************************
.ENDS csd17318q2_pspice_trans
*
*$
