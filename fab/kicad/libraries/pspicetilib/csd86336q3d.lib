* CSD86336Q3D  
*****************************************************************************
* (C) Copyright 2019 Texas Instruments Incorporated. All rights reserved.
*****************************************************************************
** This model is designed as an aid for customers of Texas Instruments.
** TI and its licensors and suppliers make no warranties, either expressed
** or implied, with respect to this model, including the warranties of
** merchantability or fitness for a particular purpose. The model is
** provided solely on an "as is" basis. The entire risk as to its quality
** and performance is with the customer.
*****************************************************************************
*
** Released by: WEBENCH Design Center, Texas Instruments Inc.
* Part: CSD86336Q3D  
* Date: 11JAN2019
* Model Type: 
* Simulator: PSPICE
* Simulator Version: 16.2.0.s003
* EVM Order Number:
* EVM Users Guide: 
* Datasheet: 
* Model Version: Final 1.00
*
*****************************************************************************
*
* Updates:
*
* Final 1.00
* Release to Web
* PSpice Model Editor - Version 16.6.0
*$

************************************************************************
* PSpice model for TI NexFET n-channel power block CSD86336Q3D
* PSpice Model Editor - Version 16.2.0
************************************************************************
* Notice:
* The data and models provided herein are intended to be used by 
* customers as an aid in designing electrical circuits using 
* semiconductor devices manufactured &/or sold by Texas Instruments.
* The models and associated information contained herein are not to
* be construed as a guarantee of electrical performance and/or device 
* specification.  Therefore, Texas Instruments does not assume any 
* liability arising out of the use of said data or models, nor from
* the devices or products manufactured therefrom.  Texas Instruments
* does not convey any license under its patent rights nor the rights
* of others.  Texas Instruments reserves the right to change models 
* without prior notice.  
*
* TI is a registered trademark of Texas Instruments Inc.
*************************************************************************
*$
**********************************************************************
**********************************************************************
*                                                                    *
* CSD86336Q3D  -  P-Spice Model                                      *
*   model verified in Cadence OrCAD / Allegro Simulation Environment *
*                                                                    *
* Model Status:   model based on measured results                    *
*                                                                    *
**********************************************************************
* Subcircuit Instantiation Notes:
*  nFET SUBCKT Definition:  1=D  2=G  3=S
*  SUBCKT Definition:  1=2=Vin 3=TG 4=TGR 5=BG 6=7=8=VSW 9=PGND
*  - see csd86336q3d data sheet for further details
**********************************************************************
.SUBCKT csd86336q3d  1 2 3 4 5 6 7 8 9
XnfetHS  18  17  19  n36332
XnfetLS  19  16  15  n36331
RGls      5  10      10e-3
RGhs      3  11      14e-3
RDhs      1  12      1.1e-3
RDhs2     2  12      1.1e-3
RGlr      4  13      10e-3
Rpsn      8  14      0.95e-3
Rpsn2     7  14      0.95e-3
Rpsn3     6  14      0.95e-3
RSls      9  15      0.001e-3
LGls     10  16      0.61e-9
LGhs     11  17      0.99e-9
LDhs     12  18      0.45e-9
RLDhs    12  18      1
LGlr     13  19      0.61e-9
Lpsn     14  19      0.35e-9
.ENDS csd86336q3d
*$
**********************************************************************
**********************************************************************
*                                                                    *
*   n36331   -  PSpice Model -  STATUS:  PRELIMINARY                 *
*                                                                    *
*   n36331 is the LS silicon die model for use with 86336q3d         *
*                                                                    *
**********************************************************************
**********************************************************************
* SUBCKT Definition:  1=D  2=G  3=S
**********************************************************************
.SUBCKT n36331  1 2 3
* PARAMETER (local) DEFINITIONS SECTION
.PARAM  pWidth  1.25587		; m, total gate width
.PARAM  pLen    0.35u
.PARAM  perim   {2*pWidth}	; m, gate/drain perim (rough est)
.PARAM  ptrc1   8.3e-4		; temp co of ext Rd / Rs
.PARAM  ptrc2   1.9e-6
****** MODEL SUBCIRCUIT BEGINS HERE     ******************************
M1   10 11 12 12  NMOS	W={pWidth} L={pLen}  PS={perim} PD={perim}
DBD   3  1	 DBD
CBD0  3  1   10e-12
RBD0  3  1   9e9
CGD0  2 13	 5E-12
RGD  13  1   2e6
CGS0  2  3	 10e-12
M2   12 11 12 20  PMOSd W={pWidth} L=0.099u PS={perim} PD={perim}
RDx  20 12   1e14
CDx  20 12   2p
DDx  20 10   DD
RGG   2 11	 0.625
RSB  12  9	 RTEMP 0.24e-3
RSS   9  3 	 RTEMP 0.51e-3
RDD   1 10 	 RTEMP 1.24e-3
*******************************************************************
.MODEL  NMOS   NMOS (    LEVEL  = 7             Version = 3.2  
+ TNOM   = 27            LINT   = 0.045e-6      CAPMOD = 2
+ TOX    = 1.75e-8       NCH    = 2.642e17      NSUB   = 5.6e16
+ AGS    = 0.0           PVAG   = 0.2           U0     = 400
+ A0     = 1.0           A1     = 0.0           A2     = 1.0
+ UA     = 2.70e-9       UB     = 1.0e-18       VBM    = -10
+ UA1    = 1.4e-9        UB1    = -0.9e-18      UTE    = -1.50
+ KT1    = -0.79         KT2    =-0.022         KT1L   = 1.0e-15
+ DVT0   = 2.1           DVT1   = 0.45	      DVT2   = -0.033
+ ETA0   = 0.020         ETAB   = -0.07         XJ     = 3e-7
+ PDIBLC1= 0.08          PDIBLC2= 0.006         NLX    = 1.75e-7
+ PSCBE1 = 2.5e8         PSCBE2 = 1e-7          PCLM   = 1.3
+ VOFF   = -0.22         NFACTOR= 1.95          JS     = 0
+ DROUT  = 1.2           DSUB   = 1.4           DELTA  = 0.05
+ CJ     = 1e-18         CJSW   = 1e-18         CF     = 0
+ CGSO   = 14e-12        CGDO   = 0.2e-12       CGBO   = 0  
+ VOFFCV = -0.49 )
******************************************************************* 
.MODEL  PMOSd   PMOS (   LEVEL  = 7		     Version = 3.2
+ TNOM   = 27            CAPMOD = 0
+ VTH0    = -0.1         IS     = 1.0e-16		U0	 = 250
+ TOX    = 1.75e-8       K1	  = 7.0		K2     = -0.1
+ CJ     = 1e-18         CJSW   = 1e-18
+ CGSO   = 1e-18         CGDO   = 1e-18         CGBO   = 0  )
******************************************************************* 
.MODEL DBD D (CJO=1.245e-9 VJ=0.77 M=0.347 TNOM=27 FC=0.5 TT=0.5e-09 
+ XTI=3.1 BV=26.05 TBV1=7.35e-4 IS=4.2e-12 N=1.060 
+ RS=1.2e-3 TRS1=1.9e-3  ISR=3.2e-9 NR=2.0)
.MODEL DD  D (CJO=10.27e-10 VJ=0.55 M=1.28 IS=1e-13 RS=1 FC=0.9 
+ TNOM=27)
*******************************************************************
.MODEL RTEMP RES (TC1={ptrc1} TC2={ptrc2})
*******************************************************************
.ENDS n36331
*$
**********************************************************************
**********************************************************************
*                                                                    *
*   n36332   -  PSpice Model -  STATUS:  PRELIMINARY                 *
*                                                                    *
*   n36332 is the HS die silicon model for use with 86336q3d         *
*                                                                    *
**********************************************************************
**********************************************************************
* SUBCKT Definition:  1=D  2=G  3=S
**********************************************************************
.SUBCKT n36332  1 2 3
* PARAMETER (local) DEFINITIONS SECTION
.PARAM  pWidth  0.65141		; m, total gate width
.PARAM  pLen    0.35u
.PARAM  perim   {2*pWidth}	; m, gate/drain perim (rough est)
.PARAM  ptrc1   7.2e-4		; temp co of ext Rd / Rs
.PARAM  ptrc2   2.0e-6
****** MODEL SUBCIRCUIT BEGINS HERE     ******************************
M1   10 11 12 12  NMOS	W={pWidth} L={pLen}  PS={perim} PD={perim}
DBD   3  1	 DBD
CBD0  3  1   10e-12
RBD0  3  1   9e9
CGD0  2 13	 1E-12
RGD  13  1   2e6
CGS0  2  3	 5e-12
M2   12 11 12 20  PMOSd3 W={pWidth} L=0.103u PS={perim} PD={perim}
RDx  20 12   1e13
CDx  20 12   2p
DDx  20 10   DDG
* Note:  gate oxide capacitance included in NMOS below
RGG   2 11	 4.1
RSB  12  9	 RTEMP 0.40e-3
RSS   9  3 	 RTEMP 0.90e-3
RDD   1 10 	 RTEMP 2.16e-3
******************************************************************* 
.MODEL  NMOS   NMOS (    LEVEL  = 7             Version = 3.2  
+ TNOM   = 27            LINT   = 0.045e-6      CAPMOD = 2
+ TOX    = 1.75e-8       NCH    = 2.765e17      NSUB   = 5.5e16
+ AGS    = 0.0           PVAG   = 0.2           U0     = 380
+ A0     = 1.00          A1     = 0.0           A2     = 1.0
+ UA     = 2.00e-9       UB     = 1.5e-18       VBM    = -10
+ UA1    = 1.40e-9       UB1    = -0.9e-18      UTE    = -1.50
+ KT1    = -0.85         KT2    =-0.022         KT1L   = 1.0e-15
+ DVT0   = 2.1           DVT1   = 0.48	      DVT2   = -0.033
+ ETA0   = 0.020         ETAB   = -0.07         XJ     = 3e-7
+ PDIBLC1= 0.05          PDIBLC2= 0.005         NLX    = 1.75e-7
+ PSCBE1 = 2.5e8         PSCBE2 = 1e-7          PCLM   = 1.3
+ VOFF   = -0.26         NFACTOR= 2.0           JS     = 0
+ DROUT  = 1.2           DSUB   = 1.4           DELTA  = 0.05
+ CJ     = 1e-18         CJSW   = 1e-18         CF     = 0
+ CGSO   = 135e-12        CGDO   = 0.1e-12       CGBO   = 0  
+ VOFFCV = -0.02)
*******************************************************************
.MODEL  PMOSd3   PMOS (   LEVEL  = 3
+ TNOM   = 27
+ VTO    = -25         IS     = 1.0e-16		U0	 = 150
+ TOX    = 1.75e-8
+ CJ     = 1e-18         CJSW   = 1e-18
+ CGSO   = 1e-11         CGDO   = 1e-18         CGBO   = 0  )
******************************************************************* 
.MODEL DBD D (CJO=6.52e-10 VJ=0.87 M=0.37 TNOM=27 FC=0.5 TT=1e-09 
+ XTI=3.5 BV=26.15 TBV1=7.45e-4 IS=2.25e-12 N=1.070 
+ RS=1.13e-3 TRS1=2.5e-3  ISR=1.23e-09 NR=2.0)
.MODEL DDG  D (CJO=4.3e-10 VJ=0.38 M=1.057 IS=1e-13 FC=0.9 RS=1 
+ TNOM=27)
*******************************************************************
.MODEL RTEMP RES (TC1={ptrc1} TC2={ptrc2})
*******************************************************************
.ENDS n36332
*
*$
