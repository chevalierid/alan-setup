*$ 
* LOG101
*****************************************************************************
* (C) Copyright 2022 Texas Instruments Incorporated. All rights reserved.                                            
*****************************************************************************
** This model is designed as an aid for customers of Texas Instruments.
** TI and its licensors and suppliers make no warranties, either expressed
** or implied, with respect to this model, including the warranties of 
** merchantability or fitness for a particular purpose.  The model is
** provided solely on an "as is" basis.  The entire risk as to its quality
** and performance is with the customer
*****************************************************************************
*
* This model is subject to change without notice. Texas Instruments
* Incorporated is not responsible for updating this model.
*
*****************************************************************************
*
** Released by: Online Design Tools, Texas Instruments Inc.
* Part: LOG101
* Date: 02NOV2022
* EVM Order Number: N/A
* EVM Users Guide: N/A
* Datasheet: SBOS242B - MAY 2002 - REVISED JUNE 2004
*
* Model Version: Final 1.1
*
*****************************************************************************
*
* Final 1.1
* Updated PSpice symbol.
* Added Unique SUBCKT name.
*
* Final 1.0
* Release to Web.
* 
*****************************************************************************
* Model Usage Notes:
* 1. Model file is updated with latest header.
* 2. Updated the resistor(R2) syntax with new resistor model.
* 3. This model uses two subcircuit levels, ie the LOG101 subcircuit is one
*    level and there is one additional level inside that subcircuit. One 
*    component (R2) has different syntax for different simulators - see R2 
*    below.
* 4. Model functional temperature range is -40 to 85 c. High accuracy 
*    temperature range is  -5 to 75 c. Not all parameters track with 
*    temperature.
* 5. Convergance, dc sweeps, and time domain analysis will usually require
*    setting analysis options such as ITL1, ITL2, and ITL4 respectively.
*    Convergance can also be enhanced by using nodeset to set the output 
*    node (vout).For accurate results at input currents below one nanaoamp 
*    set gmin to 2e-13.
* 6. Supports DC, AC, TRANSIENT, and NOISE ANALYSIS. Logging and auxiliary 
*    amplifiers are macromodels
* 7. The following parameters are modeled: 
*    OPEN LOOP GAIN AND PHASE, CMRR W FREQ EFFECTS, PSRR W FREQ EFFECTS,
*    INPUT VOLTAGE AND CURRENT, NOISE W FREQ EFFECTS, INPUT BIAS CURRENT 
*    AND INPUT CLAMPS, OUTPUT CLAMPS, INPUT CAPACITANCE, OUTPUT SWING VS IO, 
*    OUTPUT CURRENT THRU THE SUPPLIES, SLEW RATE, CURRENT LIMIT, AND INPUT 
*    COMMON MODE RANGE. LOGGING TRANSISTORS HAVE NON-IDEAL PARAMETERS. 
*    LOGGING TRANSISTORS HAVE NOISE EFFECTS.LOG CONFORMITY ERRORS ARE MODELED.
*    TEMPERATURE EFFECTS OF LOG ERRORS ARE MODELED. TEMPERATURE EFFECT OF 
*    GAIN ERROR IS MODELED. BANDWIDTH AND DYNAMICS ARE REALISTIC.
*    COMPENSATION IS REALISTIC. UPPER LIMIT ON I1+I2 AT LOW VCC IS MODELED. 
*****************************************************************************
* PINOUT ORDER I1 I2 V+ V- VOUT GND 
* PINOUT ORDER 1  8  4  5  3   6  
*****************************************************************************
.SUBCKT LOG101 1 8 4 5 3 6 
R1 9 3 3340
* PSPICE AND PSPICE DERIVED SYNTAX FOR R2
* R2 6 9 212 TC1=3.47M
* END PSPICE SYNTAX R2
* BERKELEY 3X.X SYNTAX FOR R2
R2 6 9 R2M 212
.MODEL R2M RES TC1=3.47M
* END BERKELEY SYNTAX R2
Q1 10 9 11 QL101
Q2 12 6 11 QL101
X1 16 8 4 5 3 LA101
X2 16 1 4 5 13 LA101
RPI 16 6 10
R5 0 8 1E12
R6 0 1 1E12
R7 14 11 670
C1 1 13 154E-12
D3 14 13 DS
R8 14 13 1E8
R9 11 1 1E12
R10 8 11 1E12
R11 8 12 82
R12 10 1 82
.MODEL DS D
.MODEL QL101 NPN BF=390 IS=1E-15 NF=1
+ EG=1.2056 ISE=1E-17 NE=1.5 ISC=1E-17
+ NC=1.5 VAF=125 RB=49 RBM=2.65
+ IRB=60E-6 RE=0.0404 IKF=0.6
+ CJE=12.2E-12 VJE=0.77 MJE=0.3
+ CJC=1.5E-12 VJC=0.64 MJC=0.425
+ XCJC=1 FC=0 TF=300E-12 XTF=1 VTF=0
+ PTF=5 ITF=10E-3 TR=1E-6 CJS=55E-12
+ MJS=0.5 VJS=0.58 XTI=2.3 XTB=1.5
+ NR=1 KF=1.6E-16 AF=1
.ENDS LOG101
*
.SUBCKT LA101 3 2 7 4 6
C00 6 0 0.1E-12
Q26 8 9 10 QON
Q27 11 9 12 QOP
Q28 4 13 14 QOP
Q29 7 15 16 QON
I4 8 15 110E-6
I5 13 11 110E-6
R35 14 17 1
C6 18 19 60E-12
R36 19 18 1K
G1 18 19 20 0 -1E-3
G2 21 19 22 23 -900E-6
R37 19 21 1E10
C7 20 24 30E-12
R38 20 21 8E3
E1 25 19 20 19 1
D2 25 21 DD
R39 19 24 1.5E3
R41 26 23 300
R42 26 22 300
V7 27 11 0.65
R47 6 17 50
D1 26 27 DD
D3 21 25 DD
D4 28 8 DD
D5 11 29 DD
V8 28 21 1.3
V9 21 29 0.9
E6 30 0 8 0 1
E7 31 0 11 0 1
E8 32 0 33 0 1
R48 30 34 1E4
R49 31 35 1E4
R50 32 36 1E4
R51 0 34 1
R52 0 35 1
R53 0 36 1
E11 37 38 36 0 0.03
R54 39 33 1E3
R55 33 40 1E3
C10 30 34 0.5E-6
C11 31 35 100E-9
C12 32 36 1E-12
E12 41 37 35 0 0.025
E13 42 41 34 0 0.1
R60 41 42 1E9
R61 37 41 1E9
R62 38 37 1E9
E22 39 0 2 0 1
E23 40 0 42 0 1
E24 43 44 45 46 0.067
I7 0 46 100E-6
D6 46 0 DVN
I8 0 45 100E-6
D7 45 0 DVN
E25 11 0 4 0 1
E26 8 0 7 0 1
I9 7 4 342E-6
R65 4 7 400E3
E27 19 0 8 11 0.001
C13 2 0 2E-12
C14 42 0 2E-12
C15 42 2 1E-12
V10 44 2 0
R34 12 15 1
R67 13 10 1
R68 17 16 1
I13 42 0 1E-12
I14 2 0 1E-12
J1 22 47 48 JIP
J2 23 49 50 JIP
R72 50 51 100
R73 48 51 100
Q30 52 52 53 QIP
R74 53 54 220
R75 55 54 220
Q31 51 52 55 QIP
V12 8 54 3.5
I15 52 11 230E-6
V13 49 42 0
C17 9 19 30E-12
R76 19 9 1K
G3 9 19 18 19 -1E-3
I16 0 56 100E-6
D8 56 0 DIN
I17 0 57 100E-6
D9 57 0 DIN
G4 2 42 57 56 1E-6
E29 58 0 57 56 230
R77 0 58 220
C18 58 49 1E-12
C19 58 47 1E-12
R81 38 3 400
R82 43 47 400
J3 8 49 8 JIN
J4 8 47 8 JIN
J5 47 11 47 JIN
J6 49 11 49 JIN
D10 6 7 DD
D11 4 6 DD
.MODEL DVN D KF=1.6E-13
.MODEL DIN D
.MODEL DD D
.MODEL QIP PNP
.MODEL QON NPN BF=164 RC=175
.MODEL QOP PNP BF=164 RC=175
.MODEL JIP PJF BETA=1.5E-3 IS=1E-15
.MODEL JIN NJF IS=1E-18
.ENDS LA101
