*$
* CSD18510KCS  
*****************************************************************************
* (C) Copyright 2018 Texas Instruments Incorporated. All rights reserved.
*****************************************************************************
** This model is designed as an aid for customers of Texas Instruments.
** TI and its licensors and suppliers make no warranties, either expressed
** or implied, with respect to this model, including the warranties of
** merchantability or fitness for a particular purpose. The model is
** provided solely on an "as is" basis. The entire risk as to its quality
** and performance is with the customer.
*****************************************************************************
*
** Released by: WEBENCH Design Center, Texas Instruments Inc.
* Part: CSD18510KCS  
* Date: 29OCT2018
* Model Type: 
* Simulator: PSPICE
* Simulator Version: 16.2.0.s003
* EVM Order Number:
* EVM Users Guide: 
* Datasheet: SLPS663A - MARCH 2017 - REVISED JULY 2017
* Model Version: Final 1.00
*
*****************************************************************************
*
* Updates:
*
* Final 1.00
* Release to Web

* PSpice Model Editor - Version 16.6.0
*$
**********************************************************************
**********************************************************************
*                                                                    *
* csd18510kcs   -  PSpice Model for use with OrCAD/Allegro           *
*                                                                    *
**********************************************************************
* Characteristics:
*   Technology:  n-channel 40/20
*   Assembly:    TO-220
*
**********************************************************************
* Model History
*
*   Initial model.     gd 01/12/2017
*   
* SUBCKT Definition:  1=D  2=G  3=S
**********************************************************************
.SUBCKT csd18510kcs   1 2 3
.PARAM  ptrc1   5.5e-3  
.PARAM  ptrc2   1.31e-5
.PARAM  pAA	    10.856327
.PARAM  pwidth  {pAA/0.8*2}
.PARAM  perim   {2.1*pwidth}
.PARAM  pRgate  0.90
.PARAM  pCJO    58e-12
M1   10 11 12 12  NMOS	W={pwidth} L=0.70u  PD={perim} PS={perim}
M2   12 11 12 20  PMOSd W={pWidth} L=0.048u PS={perim} PD={perim}
DDx  20  10  DD
DBD   8  7	 DBD	{pwidth}
RDB   8  7   9e10
CGD0  5  7	 0.5e-12
CGS0  5  8	 4e-12
CDS0 10 12	 {5.63e-12 * pwidth}
DGD  14 10	 DD	{pwidth}
DDG  14  5	 DD	{pwidth}
RDC  14  5   1e10
LGG   2  5	 2.5e-9
RGG   5 11	 {0.6 + pRgate}
RSB  12  9	 RTEMP 0.01e-3
RSS   9  8 	 RTEMP 0.30e-3
RSP   8  6 	 0.01e-4
LSS   6  3	 1.7E-9
RSL   6  3   2.5
RDP   4  7	 0.02e-3
RDD   7 10 	 RTEMP 0.90e-3
LDD   1  4	 0.1E-9
*******************************************************************
.MODEL  NMOS   NMOS (    LEVEL  = 7             Version = 3.2
+ TNOM   = 27            LINT   = 0.080e-6      CAPMOD = 2
+ TOX    = 5.50e-8       NCH    = 1.365e17      NSUB   = 1.0e16
+ AGS    = 0.0           PVAG   = 0.3           
+ U0     = 370		 VSAT   = 4.2e4
+ A0     = 1.25          A1     = 0.1           A2     = 0.8
+ UA     = 8.0e-10       UB     = 1.0e-19       VBM    = -10.0
+ UA1    = 0.2e-9        UB1    = -1e-17        UTE    = -0.84
+ KT1    = -0.82         KT2    = 0.022         KT1L   = 1.0e-09
+ DVT0   = 2.5           DVT1   = 0.53	      DVT2   = -0.033
+ ETA0   = 0.08          ETAB   = -0.07         XJ     = 3e-7
+ PDIBLC1= 0.40          PDIBLC2= 0.040         NLX    = 1.64e-7
+ PSCBE1 = 4.0e8         PSCBE2 = 1e-6          PCLM   = 0.5
+ VOFF   = -0.28         NFACTOR= 1.20          JS     = 0
+ DROUT  = 1.0           DSUB   = 1.01          DELTA  = 0.05
+ CJ     = 1e-18         CJSW   = 1e-18         CF     = 0
+ CGSO   = 17e-12        CGDO   = 13.2e-12      CGBO   = 0   )
*******************************************************************
.MODEL  PMOSd   PMOS (   LEVEL  = 3
+ TNOM   = 27            VTO    = -10           IS     = 1.0e-13
+ TOX    = 5.5e-8        NSUB   = 2.0e17		GAMMA  = 15
+ CJ     = 1e-18         CJSW   = 1e-18
+ CGSO   = 1e-18         CGDO   = 1e-18         CGBO   = 0  )
*******************************************************************
.MODEL DBD D (CJO=274e-12 VJ=0.99 M=1.038 FC=0.5 TT=3e-09 TNOM=27
+  IS=3.4e-12 N=1.070 RS=2.4e-2 TRS1=3.4e-3 XTI=3.3 
+  BV=44 TBV1=9.0e-4 TBV2=-2.3e-6  NR=5.5 ISR=7.0e-11)
*******************************************************************
.MODEL DD  D (CJO={2*pCJO} VJ=0.65 M=1.55 IS=1e-12 RS=10 TNOM=27 
+  FC=0.90 BV=50)
*******************************************************************
.MODEL RTEMP RES (TC1={ptrc1} TC2={ptrc2})
*******************************************************************
.ENDS csd18510kcs
*
*$
