***********************************************************************
**  v1.7.5 PSpice Models Library - TI NexFET Power N/P Channel FET's
*  (C) Copyright 2010 Texas Instruments Incorporated. All rights reserved.
***********************************************************************
***********************************************************************
* Notice:
* The data and models provided herein are intended to be used by 
* customers as an aid in designing electrical circuits using 
* semiconductor devices manufactured &/or sold by Texas Instruments.
* The models and associated information contained herein are not to
* be construed as a guarantee of electrical performance and/or device 
* specification.  Therefore, Texas Instruments does not assume any 
* liability arising out of the use of said data or models, nor from
* the devices or products manufactured therefrom.  Texas Instruments
* does not convey any license under its patent rights nor the rights
* of others.  Texas Instruments reserves the right to change models 
* without prior notice.  
*
* TI is a registered trademark of Texas Instruments Inc.
*************************************************************************
********************************************************************************
*
* Released by: Analog e-Lab Design Center, Texas Instruments Inc.
* Part: CSD16411Q3
* Date: 12/08/2010
* Model Type: TRANSIENT
* Simulator: PSPICE
* Datasheet: SLPS206A - August 2009 - Revised September 2010
*
*****************************************************************************
*$
**********************************************************************
**********************************************************************
*                                                                    *
* CSD16411Q3   -  P-SPice Model, for use with OrCAD P-Spice          *
*                                                                    *
**********************************************************************
**********************************************************************
*
* SUBCKT Definition:  1=D  2=G  3=S
*
**********************************************************************
.SUBCKT CSD16411Q3  1 2 3
**********************************************************************
* PARAMETER (local) DEFINITIONS SECTION
****** Package Contributions *****************************************
.PARAM  pRclip    0.5e-3	 ; Ohms (source clip / bond wire res)
.PARAM  pRGwire   0.25		 ; Ohms  (gate wire resistance)
****** Device Geometry       *****************************************
.PARAM  pWidth 0.59858        ; m, total scale width (W = 0.617826)
.PARAM  perim  {2*pWidth}
.PARAM  ptrc1  2.8e-3		 ; temp coef of extr Rd / Rs
.PARAM  ptrc2  5.5e-6
**********************************************************************
M1   10 11 12 12  NMOS	W={pWidth} L=0.5u  PS={perim} PD={perim}
DBD   8  7	 DBD
CGD0  5  7	 1E-12
CGS0  5  8	 6e-12
M2   12 11 12 10  PMOSd W={pWidth} L=0.1763u PS={perim} PD={perim}
DDG  14  5	 DD
DGD  14 10	 DD
* Note:  gate oxide capacitance included in NMOS below
CGS   5 13	 86.26e-12
RGS  13  8	 666.2
LGG   2  5	 1.45e-9
RGG   5 11	 {0.575 + pRGwire}
RSB  12  9	 RTEMP 1.63e-3
RSS   9  8 	 0.05e-3
RSP   8  6 	 {1e-6 + pRclip}
LSS   6  3	 0.25e-9
RDP   4  7   0.3644e-3
RDD   7 10 	 RTEMP 3.672e-3
LDD   1  4	 0.05E-9
******************************************************************* 
.MODEL  NMOS   NMOS (    LEVEL = 7             Version = 3.2  
+ TNOM   = 27            LINT   = 0.070e-6      CAPMOD = 2
+ TOX    = 3.0E-8        NCH    = 1.40e17       NSUB   = 5e15
+ AGS    = 0.0           PVAG   = 1.1
+ A0     = 1.00          A1     = 0.0           A2     = 0.90
+ UA     = 1.35e-9       UB     = 8.3e-19       VBM    = -5
+ UA1    = 2.4e-9        UB1    = -5.5e-18      UTE    = -1.50
+ KT1    = -0.81         KT2    =-0.022         KT1L   = 1.0e-12
+ DVT0   = 2.3           DVT1   = 0.55	      DVT2   = -0.033
+ ETA0   = 0.045         ETAB   = -0.07         XJ     = 3e-7
+ PDIBLC1= 0.08          PDIBLC2= 0.006         NLX    = 1.7e-7
+ PSCBE1 = 0.3e8         PSCBE2 = 2.5e-8        PCLM   = 1.25
+ VOFF   = -0.12         NFACTOR= 2.2           JS     = 0
+ DROUT  = 0.8           DSUB   = 1.6           DELTA  = 0.01
+ CJ     = 1e-18         CJSW   = 1e-18         CF     = 0
+ CGSO   = 189e-12       CGDO   = 5.0e-12       CGBO   = 0  )
******************************************************************* 
.MODEL  PMOSd   PMOS (   LEVEL  = 7            Version = 3.2
+ TNOM = 27              CAPMOD = 2
+ JS     = 0             VBM    = -10
+ TOX    = 3.0e-8        NCH    = 1.3e17        NSUB   = 5e16
+ CJ     = 1e-18         CJSW   = 1e-18         CF     = 0
+ CGSO   = 0             CGDO   = 0             CGBO   = 0 )
******************************************************************* 
.MODEL DBD D (CJO=0.9911e-9 VJ=1.0 M=0.46 TNOM=27 FC=0.5 TT=5e-09 
+ XTI=4.4 BV=27 IS=3.2689e-13 N=1.02 RS=7.4888e-4 TRS1=7.8e-3)
.MODEL DD  D (CJO=19.069e-12 VJ=0.40 M=0.45 IS=1e-12 RS=0.1 TNOM=27)
*******************************************************************
.MODEL RTEMP RES (TC1={ptrc1} TC2={ptrc2})
******************************************************************* 
.ENDS CSD16411Q3
