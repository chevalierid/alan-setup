***********************************************************************
**  v1.7.5 PSpice Models Library - TI NexFET Power N/P Channel FET's
*  (C) Copyright 2010 Texas Instruments Incorporated. All rights reserved.
***********************************************************************
***********************************************************************
* Notice:
* The data and models provided herein are intended to be used by 
* customers as an aid in designing electrical circuits using 
* semiconductor devices manufactured &/or sold by Texas Instruments.
* The models and associated information contained herein are not to
* be construed as a guarantee of electrical performance and/or device 
* specification.  Therefore, Texas Instruments does not assume any 
* liability arising out of the use of said data or models, nor from
* the devices or products manufactured therefrom.  Texas Instruments
* does not convey any license under its patent rights nor the rights
* of others.  Texas Instruments reserves the right to change models 
* without prior notice.  
*
* TI is a registered trademark of Texas Instruments Inc.
*************************************************************************
********************************************************************************
*
* Released by: Analog e-Lab Design Center, Texas Instruments Inc.
* Part: CSD16340Q3
* Date: 12/08/2010
* Model Type: TRANSIENT
* Simulator: PSPICE
* Datasheet: SLPS247C - December 2009 - Revised September 2010
*
*****************************************************************************
*$
**********************************************************************
**********************************************************************
*                                                                    *
* CSD16340Q3   -  PSpice Model for use with OrCAD                    *
*                                                                    *
**********************************************************************
**********************************************************************
* SUBCKT Definition:  1=D  2=G  3=S
**********************************************************************
.SUBCKT CSD16340Q3  1 2 3
.PARAM  ptrc1  3.25e-3
.PARAM  ptrc2  9.0e-6
.PARAM  pwidth 1.3392864
.PARAM  perim  2.7
M1   10 11 12 12  NMOS	W={pwidth} L=0.4u  PS={perim} PD={perim}
DBD   8  7	 DBD
CGD0  5  7	 1E-12
CGS0  5  8	 4e-12
CDS0 10 12   6e-12
M2   12 11 12 10  PMOSd W={pwidth} L=0.069u  PS={perim} PD={perim}
DDG  14  5	 DD
DGD  14 10	 DD
* Note:  gate oxide capacitance included in NMOS below
CGS   5 13	 185E-12
RGS  13  8	 330
LGG   2  5	 1.31e-9
RGG   5 11	 1.69
RSB  12  9	 RTEMP 0.78E-3
RSS   9  8 	 0.05e-3
RSP   8  6 	 0.50e-3
LSS   6  3	 0.20E-9
RDP   4  7	 0.19e-3
RDD   7 10 	 RTEMP 1.52e-3
LDD   1  4	 0.05E-9
******************************************************************* 
.MODEL  NMOS   NMOS (  LEVEL = 7       Version = 3.2  
+ TNOM   = 27            LINT   = 0.055e-6      CAPMOD = 2
+ TOX    = 1.75e-8       NCH    = 1.395e17      NSUB   = 1.0e16
+ AGS    = 0.0           PVAG   = 0.0
+ A0     = 1.0           A1     = 0.0           A2     = 1.0
+ UA     = 3.9e-9        UB     = 5.0e-19       VBM    = -5
+ UA1    = 1.0e-9        UB1    = -9.5e-18      UTE    = -1.50
+ KT1    = -0.46         KT2    = 0.022         KT1L   = 1.0e-12
+ DVT0   = 2.3           DVT1   = 0.55	        DVT2   = -0.033
+ ETA0   = 0.045         ETAB   = -0.07         XJ     = 4e-7
+ PDIBLC1= 0.08          PDIBLC2= 0.006         NLX    = 1.75e-7
+ PSCBE1 = 8.0e8         PSCBE2 = 1e-7          PCLM   = 1.3
+ VOFF   = -0.115        NFACTOR= 2.0           JS     = 0
+ DROUT  = 1.0           DSUB   = 1.3           DELTA  = 0.01
+ CJ     = 1e-18         CJSW   = 1e-18         CF     = 0
+ CGSO   = 253e-12       CGDO   = 1.2e-12       CGBO   = 0  )
******************************************************************* 
.MODEL  PMOSd   PMOS (  LEVEL  = 7      Version = 3.2
+ TNOM   = 27            CAPMOD = 2
+ JS     = 0             VBM    = -10
+ TOX    = 2.75e-8       NCH    = 1.4e17        NSUB   = 5e16
+ CJ     = 1e-18         CJSW   = 1e-18         CF     = 0
+ CGSO   = 0             CGDO   = 0             CGBO   = 0 )
******************************************************************* 
.MODEL DBD D (CJO=2250e-12 VJ=0.98 M=0.46 FC=0.5 TT=5e-09 TNOM=27
+ IS=2.23e-12 N=1.02 RS=3.6e-4 TRS1=5e-3 XTI=2.15 BV=27)
.MODEL DD  D (CJO=150e-12 VJ=0.52 M=0.50 IS=1e-12 TNOM=27)
*******************************************************************
.MODEL RTEMP RES (TC1={ptrc1} TC2={ptrc2})
******************************************************************* 
.ENDS CSD16340Q3
