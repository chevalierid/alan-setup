* OPA846
*****************************************************************************
* (C) Copyright 2011 Texas Instruments Incorporated. All rights reserved.                                            
*****************************************************************************
** This model is designed as an aid for customers of Texas Instruments.
** TI and its licensors and suppliers make no warranties, either expressed
** or implied, with respect to this model, including the warranties of 
** merchantability or fitness for a particular purpose.  The model is
** provided solely on an "as is" basis.  The entire risk as to its quality
** and performance is with the customer.
*****************************************************************************
*
* This model is subject to change without notice. Texas Instruments
* Incorporated is not responsible for updating this model.
*
*****************************************************************************
*
** Released by: Analog eLab Design Center, Texas Instruments Inc.
* Part: OPA846
* Date: 24JUN2011
* Model Type: ALL IN ONE
* Simulator: PSPICE
* Simulator Version: 16.0.0.p001
* EVM Order Number: N/A
* EVM Users Guide: N/A
* Datasheet: SBOS250E – JULY 2002 – REVISED DECEMBER 2008
*
* Model Version: 1.0
*
*****************************************************************************
* 
* Updates:
*
* Version 1.0 : 
* Release to Web
*
*****************************************************************************
* CONNECTIONS:
*              Non-Inverting Input
*              | Inverting Input
*              | |  Positive Power Supply
*              | |  |  Negative Power Supply
*              | |  |  |  Output
*              | |  |  |  |
*              | |  |  |  |
*****************************************************************************
.SUBCKT OPA846 + -  V+ V- Out
V_V1         V+ 10 DC 1.4
R_R10         11 10  200
R_R11         12 10  200
R_R12         15 13  5
R_R13         15 14  5
G_G1         + 0 POLY(2) 26 0 27 0 0 1m 1m
G_G3         0 17 POLY(1) 12 11 0 30.51M 0 0
G_G4         0 18 POLY(1) 17 0 0 1M 0 0
E_E1         19 0 POLY(1) V- V+ .9 0.5 0 0
E_E2         20 0 POLY(1) V+ V- -.9 0.5 0 0
E_E3         21 22 POLY(2) 0 18  V+ V- -785m 1 0.5
E_E4         24 25 POLY(2) 18 0  V+ V- -785M 1 0.5
D_D1         16 15 DX 1
D_D3         18 20 DX 1
D_D4         22 23 DX 1
D_D5         23 24 DX 1
D_D6         26 0 DN1 .5
D_D7         0 27 DN1 .5
G_G2         2 0 POLY(2) 28 0 29 0 0 1m 1m
I_I1         15 V- DC 1.475mA
V_V2         16 V- DC 1.2
I_I2         V+ V- DC 2.0mA
I_I3         27 26 DC 100uA
I_I4         29 28 DC 100uA
Q_Q1         11 + 13 NPN8 10
D_D2         19 18 DX 1
R_R14         17 0  503K
R_R15         18 0  1K
R_R16         21 V+  45
R_R17         V- 25  45
R_R18         23 Out  5
C_C1         0 -  .35p
C_C2         17 0  7.95p
C_C3         0 18  80f
C_C4         V- Out  1p
L_L1         2 -  2.35nH
Q_Q2         12 2 14 NPN8 10.049
D_D8         28 0 DN1 .5
D_D9         0 29 DN1 .5
*
.MODEL DN1 D(IS=2F AF=1 KF=10.5E-17)
.MODEL DX D(IS=1.0000E-15)
*
.MODEL NPN8 NPN
+ IS = 7.604E-18 BF = 1.570E+02 NF = 1.000E+00 VAF= 7.871E+01
+ IKF= 3.975E-02 ISE= 3.219E-14 NE = 2.000E+00 BR = 7.614E-01
+ NR = 1.000E+00 VAR= 1.452E+00 IKR= 8.172E-02 ISC= 7.618E-21
+ NC = 1.847E+00 RB = 1.060E+02 IRB= 0.000E+00 RBM= 2.400E+00
+ RE = 2.520E+00 RC = 1.270E+02 CJE= 1.120E-13 VJE= 7.591E-01
+ MJE= 5.406E-01 TF = 1.213E-11 XTF= 2.049E+00 VTF= 1.813E+00
+ ITF= 4.293E-02 PTF= 0.000E+00 CJC= 3.052E-14 VJC= 6.666E-01
+ MJC= 4.509E-01 XCJC=8.450E-02 TR = 4.000E-11 CJS= 1.160E-13
+ VJS= 5.286E-01 MJS= 4.389E-01 XTB= 1.022E+00 EG = 1.120E+00
+ XTI= 1.780E+00 KF = 24.5E-15 AF = 1.0 FC = 8.273E-01
.ENDS OPA846
*$