* PSpice Model Editor - Version 16.0.0
*$
* TPS61031
*****************************************************************************
*  (C) Copyright 2010 Texas Instruments Incorporated. All rights reserved.
*****************************************************************************
** This model is designed as an aid for customers of Texas Instruments.
** TI and its licensors and suppliers make no warrenties, either expressed
** or implied, with respect to this model, including the warranties of 
** merchantability or fitness for a particular purpose.  The model is
** provided solely on an "as is" basis.  The entire risk as to its quality
** and performance is with the customer
*****************************************************************************
*
** Released by: Analog eLab Design Center, Texas Instruments Inc.
* Part: TPS61031
* Date: 18FEB2010
* Model Type: TRANSIENT		
* Simulator: PSPICE	
* Simulator Version: 16.0.0.001p	
* EVM Order Number: TPS61030EVM-HPA029 RevA
* EVM Users Guide: SBVU006 - OCT 2003
* Datasheet: SLUS534D – SEPTEMBER 2002 – REVISED APRIL 2004
*
* Model Version: Final 1.00
*
*****************************************************************************
*
* Updates:
*
* Final 1.00
* Release to Web.
*
*****************************************************************************
.SUBCKT TPS61031 SW VBAT EN LBI SYNC GND VOUT FB LBO PGND 
X1 SW VBAT EN LBI SYNC GND VOUT N1 LBO PGND TPS6103X PARAMS: Isw=4000m
RFB1 FB N1 1.125MEG
RFB2 N1 GND 200k
.ENDS TPS61031
*$
.SUBCKT TPS6103X SW VBAT EN LBI PS GND VOUT FB LBO PGND 
+ PARAMS: Isw=4000m
*
V1          Vref GND 500M
Vref5       Ref5 GND 5
XErrAmp      Vref FBflt VRT GND Itimer   TRANSCOND_AMP
+ PARAMS: RIN=1E12 ROUT=250MEG IOUTMAXSOURCE=20E-6 IOUTMAXSINK=20E-6 K=10 
CSensor     FBflt GND 5P IC=497M
Rfilt       FB FBflt 180K 
XAntiNegRinging  VOUT DRV PSMEn AROn Ref5 SW VBAT GND PMOSOn SWOn VDD  Anti-Ringing
XContrLog    EN UVLO SWOn VRT DRV Ref5 PMOSOn PS SW VOUT ShDwn_ NMOSOn GND
+ Itimer Startup PSMOn FB VBAT VDD Vref 
+ CurrLimN DM AROn Steady PSMEn   Control_Logic__Oscillator__Gate_Drive
XLBIComp     Vref LBI ShDwn_ GND SLBO GND   COMP2INPAND
+ PARAMS: VTHRES1=0 VTHRES2=2.5 VHYST1=10M VHYST2=10M VOUTH=5 RIN=1E12 DELAY=0
+ VOUTL=100M
SWLBO       LBO GND SLBO GND  S_VSWITCH_1
XGateContr   DRVP VDD NMOSOn GND PMOSOn VOUT DM Startup DRVN VBAT SW   MOSDrv
XCurrSense   CurrLimN VOUT PMOSOn GND SWOn Steady Ref5 NMOSOn SW   Current_Sense__Current_Limit
+ PARAMS: Isw={Isw}
XBackgate    Startup SW BG PM_S DM VOUT ShDwn_ GND   Backgate_Control
XMN1         SW DRVN PGND   DRVNMOS
XMP1         SW DRVP PM_S BG   DRVPMOS
Ccomp       35 FB 10P 
Rcomp       VOUT 35 100K 
RPS         PS GND 9MEG 
RPGND       GND PGND 1M 
VISD_in     36 GND
FISD        VBAT GND VISD_in     1
RSD         Vref 36 5MEG 
RGnd        GND 0 1G 
RFB         FB GND 250MEG 
RLBI        LBI GND 50MEG 
REn         EN GND 9MEG 
XUVLO        GND VBAT UVLO   UVLO
.MODEL S_VSWITCH_1 VSWITCH (RON=800 ROFF=33MEG VON=2 VOFF=1)
.ENDS TPS6103X
*$
.SUBCKT Anti-Ringing VOUT DRV PSMEn AROn Ref5 SW VBAT GND PMOSOn SWOn VDD
CdAR        37 GND 10P 
RdAR        PMOSOn 37 7.215007215K 
XARComp      VDD SW GND SWOn GND DRV PSMEn GND AROn GND   COMP4INPAND
+ PARAMS: VTHRES1=-10M VTHRES2=-2.5 VTHRES3=-2.5 VTHRES4=2.5 VOUTH=5.0 VOUTL=0.0
+ DELAY=50N RIN=1E12 VHYST1=5M VHYST2=0 VHYST3=0 VHYST4=0
SARSW       VBAT SW AROn 37  S_VSWITCH_1
.MODEL S_VSWITCH_1 VSWITCH (RON=80 ROFF=100MEG VON=2 VOFF=1)
.ENDS 
*$
.SUBCKT Control_Logic__Oscillator__Gate_Drive EN UVLO_ SWOn IERR DRV Ref5 PMOSOn PS SW VOUT ShDwn_ 
+ NMOSOn GND Itimer Startup PSMOn FB VBAT VDD Vref CurrLimN DM AROn Steady PSMEn 
*
VReset      RESET GND DC 0 AC 1 0
+ PWL ( 0 5 100n 5 101n 0 1u 0 )
VT          41 VBAT 1
XU7          OscOn DRV1 DRV Ref5 GND   AND2
+ PARAMS: DLH=0 DHL=0
XPMFF        OSC PRES PMFF PMFFQ_ Ref5 GND   RSFF
+ PARAMS: VOUTH=5.0 VOUTL=0 RIN=1E12 DELAY=1N ROUT=10 VTHRESS=2.5 VTHRESR=2.5 
XNMFF        DRVP NRES NMFF NMOSOn_B Ref5 GND   RSFF
+ PARAMS: VOUTH=5.0 VOUTL=0 RIN=1E12 DELAY=1N ROUT=10 VTHRESS=2.5 VTHRESR=2.5 
XU3          OSC PMFFQ_ PMOSOnFF Ref5 GND   NOR2
+ PARAMS: DLH=0 DHL=0
XU6          DRVP NMOSOn_B NMOSOn Ref5 GND   NOR2
+ PARAMS: DLH=0 DHL=0
XU4          DRV DRV_ Ref5 GND   INV
+ PARAMS: DLH=40N DHL=40N 
XU2          DRV DRV_ DRVP Ref5 GND   AND2
+ PARAMS: DLH=0 DHL=0
*XU1          OscOn GND Ramp OSC   OSCILLATOR
*+ PARAMS: FREQ=600K DUTYCYC=0.024 RISETIME=1E-9 FALLTIME=1E-9 VOUTH=5 VOUTL=0
*+ VENTHRES=2.5
XU1 OscOn GND PS OSC TCmul1 OSCILLATOR
XCMPT        VILRsw IERR OSC GND DRV1 GND   COMP2INPOR
+ PARAMS: RIN=1E12 DELAY=0 VOUTH=5 VOUTL=100M VTHRES1=-60M VHYST1=1M VTHRES2=2.5
+ VHYST2=0
XVNOMComp    Vref FBf Steady GND   HYSTCOMP
+ PARAMS: VTHRES=50M VHYST=20M VOUTH=5 VOUTL=100M ROUT=0 TDEL=1N 
SWILRsw     SW VILRsw SWOn NMOSOn_B  S_VSWITCH_1
CIL         VILRsw GND 10P 
XPSM         FBf RESET Vref ShDwn_ VBAT VOUT DM GND AROn PS Ref5 PSMOn PSMEn   Power_Save_Mode
CSensor     58 GND 100P 
RfOut       VOUT 58 28.860029K 
CfFB        FBf GND 1.85185185185P 
RfFB        FB FBf 1800K 
XPMOSOnComp  PMOSOnFF GND GND DM GND Steady GND Startup PMOSOn GND   COMP4INPANDS
+ PARAMS: VTHRES1=2.5 VTHRES2=-2.5 VTHRES3=-2.5 VTHRES4=-2.5 VOUTH=5.0 VOUTL=0.0
+ DELAY=0 RIN=1E12 VHYST1=0 VHYST2=0 VHYST3=0 VHYST4=0
XPRESComp    GND OscOn DRVP GND AROn GND RESET GND PRES GND   COMP4INPORS
+ PARAMS: VTHRES1=-2.5 VTHRES2=2.5 VTHRES3=2.5 VTHRES4=2.5 VOUTH=5.0 VOUTL=0.0
+ DELAY=0N RIN=1E12 VHYST1=0 VHYST2=0 VHYST3=0 VHYST4=0
XNRESComp    GND OscOn OSC GND CurrLimN GND RESET GND NRES GND   COMP4INPORS
+ PARAMS: VTHRES1=-2.5 VTHRES2=2.5 VTHRES3=2.5 VTHRES4=2.5 VOUTH=5.0 VOUTL=0.0
+ RIN=1E12
XD1          GND IERR   D_D
GTCGEN      IERR Itimer VALUE = {IF(V(DM,GND)>2.5,V(41,GND)*6U/V(TCmul1,GND),
+ V(VOUT,GND)*6U/V(TCmul1,GND))}
*GTCGEN      IERR Itimer VALUE = {IF(V(DM,GND)>2.5,0,V(VOUT,GND)*6U/V(TCmul1,GND))}
C           IERR GND 10P 
SWRT        VBAT IERR OSC GND  S_VSWITCH_2
XSWOnComp    VBAT SW SWOn GND   COMPGNDREFS
+ PARAMS: VTHRES=80m VHYST=20M VOUTH=5 DELAY=100N ROUT=0 VOUTL=100M 
XVoViComp    58 VBAT VoutuVin GND   COMPGNDREFS
+ PARAMS: VTHRES=-80M VHYST=30M VOUTH=5 VOUTL=100M ROUT=0 DELAY=0 
XINTEnComp   ShDwn_ GND GND PSMOn GND Startup GND DM OscOn GND   COMP4INPANDS
+ PARAMS: RIN=1E12 DELAY=0 VOUTH=5 VOUTL=-100M VTHRES1=2.5 VHYST1=0 VTHRES2=-2.5
+ VHYST2=0 VTHRES3=-2.5 VHYST3=0 VTHRES4=-2.5 VHYST4=0
SWVOUT      VDD VOUT Vout_Vin GND  S_VSWITCH_3
SWVBAT      VDD VBAT Vout_Vin GND  S_VSWITCH_4
XDMComp      GND Steady Vout_Vin GND ShDwn_ GND DM GND   COMP3INPANDS
+ PARAMS: RIN=1E12 VOUTH=5 VOUTL=100M VTHRES1=-2.5 VTHRES2=2.5 VTHRES3=2.5 
* RDM DM GND 1
XStartupComp  ShDwn_ GND GND VoutuVin Steady GND Startup GND   COMP3INPANDS
+ PARAMS: RIN=1E12 VOUTH=5 VOUTL=100M VTHRES1=2.5 VTHRES2=-2.5 VTHRES3=2.5 
XViVoComp    VBAT 58 Vout_Vin GND   COMPGNDREFS
+ PARAMS: VTHRES=-25M VHYST=25M VOUTH=5 VOUTL=100M ROUT=0 DELAY=0
GIQ         VBAT GND OscOn GND  6.16U
RTrigger    OSC Vref 1MEG 
XShDComp     UVLO_ GND IEN GND ShDwn_ GND   COMP2INPANDS
+ PARAMS: VTHRES1=2.5 VTHRES2=2.5 VOUTH=5.0 VOUTL=100M RIN=1E12 
R2          VBAT 59 30MEG 
R1          59 GND 60MEG 
XENComp      EN 59 IEN GND   COMPGNDREFS
+ PARAMS: VTHRES=10.0M VHYST=1.0M VOUTH=5 VOUTL=100M ROUT=0 DELAY=0
.MODEL S_VSWITCH_1 VSWITCH (RON=100K ROFF=1G VON=3 VOFF=2)
.MODEL S_VSWITCH_2 VSWITCH (RON=200 ROFF=1G VON=3 VOFF=2)
.MODEL S_VSWITCH_3 VSWITCH (RON=10M ROFF=100MEG VON=2 VOFF=3)
.MODEL S_VSWITCH_4 VSWITCH (RON=10M ROFF=100MEG VON=3 VOFF=2)
.ENDS
*$
.SUBCKT Power_Save_Mode FBf Reset Vref ShDwn_ VBAT VOUT DM GND AROn PS Ref5 PSMOn PSMEn
XPSMRComp    Vref FBf Reset GND GND PSMEn  PSMR GND   COMP3INPOR
+ PARAMS: RIN=1E12 VOUTH=5 VOUTL=0 VTHRES1=7M VTHRES2=2.5 VTHRES3=-2.5 DELAY=50N
+ VHYST1=1.5M VHYST2=0 VHYST3=0
*XPSMRComp    Vref FBf Reset GND GND PSMEn DM GND PSMR GND   COMP4INPORS
*+ PARAMS: RIN=1E12 VOUTH=5 VOUTL=0 VTHRES1=7M VTHRES2=2.5 VTHRES3=-2.5 VTHRES4=2.5 DELAY=50N
*+ VHYST1=1.5M VHYST2=0 VHYST3=0 VHYST4=0
XPFMLatch    AROn  PSMR PSMOn 60 Ref5 GND   RSFF
+ PARAMS: VOUTH=5.0 VOUTL=0 RIN=1E12 DELAY=1N ROUT=10 VTHRESS=2.5 VTHRESR=2.5
XPSMEnCmp    GND PSIN GND GND PSMEn GND   COMP2INPORS
+ PARAMS: VTHRES1=-0.5 VTHRES2=2.5 VOUTH=5 RIN=1E12 VOUTL=0 
R1 PS PS1 1k
R2 PS1 PS2 1k
R3 PS2 PSIN 1k
C1 PS1 GND 1n
C2 PS2 GND 1n
C3 PSIN GND 1n
.ENDS
*$
.SUBCKT MOSDrv DRVP VDD NMOSOn GND PMOSOn VOUT DM Startup DRVN VBAT SW
XScVgs       VBAT VDD VOUT SW DM Startup VG GND   SCVGS
SWPL        VG DRVP PMOSOn GND  S_VSWITCH_1
SWPH        GND DRVP PMOSOn GND  S_VSWITCH_2
SWNL        GND DRVN NMOSOn GND  S_VSWITCH_3
SWNH        VDD DRVN NMOSOn GND  S_VSWITCH_4
.MODEL S_VSWITCH_1 VSWITCH (RON=10 ROFF=100MEG VON=2 VOFF=3)
.MODEL S_VSWITCH_2 VSWITCH (RON=10 ROFF=100MEG VON=3 VOFF=2)
.MODEL S_VSWITCH_3 VSWITCH (RON=10 ROFF=100MEG VON=2 VOFF=3)
.MODEL S_VSWITCH_4 VSWITCH (RON=10 ROFF=100MEG VON=3 VOFF=2)
.ENDS
*$
.Param VTOn = 0.5
.Param VTOp = 0.75
*
.SUBCKT SCVGS  VBAT VDD VOUT SW DM STARTUP VG GND
EVgs VGS GND Value = { If ((V(Startup,GND) > 2.5 | V(DM,GND) > 2.5),
+ Limit( (Sqrt( (947.3684m*V(VBAT,GND)-VTOp)*15.0M + If (V(VOUT,GND) > VTOn, 
+ (V(VOUT,GND)-VTOn)*6.0m, 0) ) + VTOp), V(VBAT,GND), 0), 0 ) }
EVG VG GND Value = { If ( (V(Startup,GND) > 2.5) | (V(DM,GND) > 2.5),
+ V(VBAT,GND) - V(VGS,GND),  V(VDD,GND) ) }
R2 VG GND 1M
.ENDS
*$
.SUBCKT Current_Sense__Current_Limit CurrLimN VOUT PMOSOn GND SWOn Steady Ref5 NMOSOn SW
+ PARAMS: Isw=4000m
.Param RswN = 55m
.Param Vlim = {Isw*RswN}
.Param Vlimsu = {0.4*Vlim}
XU4          Steady 10 Ref5 GND   INV
+ PARAMS: DLH=50U DHL=50N
ECLNV       11 GND VALUE = {IF((V(10,GND))<2.5, Vlimsu, Vlim)}
XNLimComp    SWOn GND NMOSOn GND SW 11 CurrLimN GND   COMP3INPAND
+ PARAMS: RIN=1E12 DELAY=50N VOUTH=5 VOUTL=100M VTHRES1=2.5 VHYST1=0 VTHRES2=2.5
+ VHYST2=0 VTHRES3=0 VHYST3=0
.ENDS
*$
.SUBCKT Backgate_Control Startup D BG S DM VOUT ShDwn_ GND
*XBGComp      GND Startup GND DM ShDwn_ GND BGSW2On GND   COMP3INPANDS
XBGComp      GND Startup GND GND ShDwn_ GND BGSW2On GND   COMP3INPANDS
+ PARAMS: RIN=1E12 VOUTH=5 VOUTL=100M VTHRES1=-2.5 VTHRES2=-2.5 VTHRES3=2.5 
RPMS        S VOUT 35m
XDPMOS       D BG   D_D
CDPM       D BG  10P
CBGSW      BG S 10P 
SBGSW2      BG S BGSW2On GND  S_VSWITCH_1
.MODEL S_VSWITCH_1 VSWITCH (RON=100M ROFF=100MEG VON=1.2 VOFF=800M)
.ENDS
*$
.SUBCKT UVLO GND VBAT UVLO_
XUVLOComp    VBAT GND UVLO_ GND   COMPGNDREFS
+ PARAMS: VTHRES=1500.0M VHYST=10.0M VOUTH=5 VOUTL=100M ROUT=0 DELAY=0 
.ENDS
*$
.SUBCKT TRANSCOND_AMP   INP INM  OUT GND IT
+ PARAMS: K=10 RIN=250MEG ROUT=250MEG IOUTMAXSOURCE=20.0E-6 IOUTMAXSINK=20.0E-6 
*
G1  OUT  GND  VALUE = { LIMIT(V(INP,INM)*V(IT,GND), IOUTMAXSOURCE, -IOUTMAXSINK) }
RIT IT GND {K}
R2   OUT GND  {ROUT}
.ENDS
*$
.SUBCKT DRVNMOS  D G S
*                D G S
M1 D G S S DRVNMOS
C1 G S 400P   
C2 D S 100P   
.MODEL DRVNMOS NMOS LEVEL=1 L=1.5U W=3900U KP=5.75M VTO={VTOn} RD=40m LAMBDA=10E-3 IS=1E-12
.ENDS DRVNMOS 
*$
.SUBCKT DRVPMOS  D G S B
*                D G S B
M1 D G S S DRVPMOS
C1 G S 400P   
C2 D S 100P  
.MODEL DRVPMOS PMOS LEVEL=1 L=1.5U W=6000U KP=5.75M VTO={-VTOp} LAMBDA=10E-3 IS=0 
.ENDS DRVPMOS 
*$
.SUBCKT CCML  TP TM DC DA DUP DUM PARAMS: RON=0 VD=0 RD=0 DMIN = 1M DMAX = 850M
*
E1 N1MD 0 VALUE = {(1-V(DUP,DUM))}
XDIV1 DUP DUM N1DD 0 1DIVX PARAMS: DIVMIN = {DMIN/10}
XDIV2 N1MD 0 DUP DUM N1MDDD 0 DIV PARAMS: DIVMIN = {DMIN/10}
ERON TP TPX VALUE = {I(ETR)*V(N1DD)*(RON*V(DUP,DUM))}
ETR TPX TM VALUE = {V(N1MDDD)*(V(DCX,DA)+VD)}
GD DA DCX VALUE = {V(N1MDDD)*I(ETR)}
ERD DCX DC VALUE = {V(N1DD)*I(ETR)*RD*V(N1MD)}
.ENDS
*$
*
.SUBCKT DIV VNP VNM VDP VDM VOP VOM
+ PARAMS: DIVMIN = 1U
*VX:= VN/(VD + 1/RX) 
RN VNP VNM 1000G
RD VDP VDM 1000G
RX 7 0 {1/DIVMIN} 
G1 0 7 VNP VNM 1
G2 7 0 VALUE = { V(VDP,VDM)*V(7,0) }
E1 VOP VOM 7 0 1
.ENDS
*$
*
.SUBCKT 1DIVX VDP VDM VOP VOM
+ PARAMS: DIVMIN = 1U
*VX:= 1/(VD + 1/RX) 
*RN VNP VNM 1000G
RD VDP VDM 1000G
RX 7 0 {1/DIVMIN} 
*G1 0 7 VNP VNM 1
I1 0 7 1
G2 7 0 VALUE = { V(VDP,VDM)*V(7,0) }
E1 VOP VOM 7 0 1
.ENDS
*$
**********************************************************
* PARAMETERS:
*     R = RESISTANCE IN THE CURRENT GENERATOR
**********************************************************
.SUBCKT DUTY  VIN VOUT IERR D GND IL+  IL- 
+PARAMS: R=166.667K DMIN = 10M DMAX = 900M RON=260M
*
.PARAM VINMAX =  {6.5}
.PARAM VINMIN =  {0.9}
.PARAM VMIN =  {10M}
.PARAM VMAX =  {VINMAX}
*
VIL IL+  IL-  
EVINRED VINR GND VALUE = { LIMIT( V(VIN,GND) - I(VIL) *RON, V(VIN,GND)-2*RON, V(VIN,GND)) } 
RIERR GND IERR {R}
* COMPUTE DUTY CYCLE D,
EDUTY D GND VALUE = { LIMIT( 1 - ( V(VINR,GND) )/(LIMIT(V(VOUT,GND) - V(IERR,GND), VMIN, VMAX ) ), DMIN, DMAX ) }
.ENDS ; 
*$
.Subckt Ampli INp INm OUT  Vp Vm
+ Params: GAIN=1000 RIN=1E12 ROUT=10 ROUTSOURCE=0 ROUTSINK=0 IOUTMAX=10M IOUTMAXSOURCE=0 
+    IOUTMAXSINK=0 SLEWRATE=500K SLEWRATERISE=0 SLEWRATEFALL=0 FPOLE1=5 FPOLE2=1MEG VDROPOH=0 VDROPOL=0 
+    VOFFSNOM=0 TCOVOFFS=0 IBIASNOM=0 IOFFSNOM=0 CURRDOUB=0 VOUTOFFS=0
X1 INp INm Vp Vm Out  OPAMP Params: 
+    GAIN={GAIN} RIN={RIN} ROUT={ROUT} ROUTSOURCE={ROUTSOURCE} ROUTSINK={ROUTSINK} IOUTMAX={IOUTMAX} 
+    IOUTMAXSOURCE={IOUTMAXSOURCE} IOUTMAXSINK={IOUTMAXSINK} SLEWRATE={SLEWRATE} SLEWRATERISE={SLEWRATERISE} 
+    SLEWRATEFALL={SLEWRATEFALL} FPOLE1={FPOLE1} FPOLE2={FPOLE2} VDROPOH={VDROPOH} VDROPOL={VDROPOL} VOFFS={VOFFSNOM} 
+    TCOVOFFS={TCOVOFFS} IBIAS={IBIASNOM} IOFFS={IOFFSNOM} CURRDOUB={CURRDOUB} VOUTOFFS={VOUTOFFS}  );
.Ends Ampli 
*$
.Subckt AmpliGndRefS INp INm OUT GND
+ Params: GAIN=1000 RIN=1E12 ROUT=100 FPOLE1=1MEG VOUTH=10 VOUTL=0 
+    VOFFSNOM=0 IBIASNOM=0 IOFFSNOM=0 VOUTOFFS=0
X1 INp INm GND Out  OPAMPGndRefS  Params: 
+    GAIN={GAIN} RIN={RIN} ROUT={ROUT} FPOLE1={FPOLE1} VOUTH={VOUTH} VOUTL={VOUTL} VOFFS={VOFFSNOM} 
+    IBIAS={IBIASNOM} IOFFS={IOFFSNOM} VOUTOFFS={VOUTOFFS}  );
.Ends AmpliGndRefS 
*$
.Subckt AmpliGndRef INp INm OUT GND
+ Params: GAIN=1000 RIN=1E12 ROUT=100 ROUTSOURCE=0 ROUTSINK=0 IOUTMAX=10M IOUTMAXSOURCE=0 
+    IOUTMAXSINK=0 SLEWRATE=500K SLEWRATERISE=0 SLEWRATEFALL=0 FPOLE1=5 FPOLE2=1MEG VOUTH=10 VOUTL=0 
+    VOFFSNOM=0 TCOVOFFS=0 IBIASNOM=0 IOFFSNOM=0 CURRDOUB=0 VOUTOFFS=0
X1 INp INm GND Out  OPAMPGndRef Params: 
+    GAIN={GAIN} RIN={RIN} ROUT={ROUT}  SLEWRATE={SLEWRATE}  FPOLE1={FPOLE1} FPOLE2={FPOLE2} VOUTH={VOUTH} VOUTL={VOUTL} 
+    VOFFS={VOFFSNOM}  IBIAS={IBIASNOM} IOFFS={IOFFSNOM} 
.Ends AmpliGndRef 
*$
.Subckt Comp INp INm OUT  Vp Vm
+ Params: GAIN=1000 RIN=1E12 ROUT=0 ROUTSOURCE=0 ROUTSINK=0 IOUTMAX=0 IOUTMAXSOURCE=0 
+    IOUTMAXSINK=0 SLEWRATE=0 SLEWRATERISE=0 SLEWRATEFALL=0 DELAY=0 DELAYHL=0 DELAYLH=0 
+    VHYST=0 VDROPOH=0 VDROPOL=0  VOFFSNOM=0 TCOVOFFS=0 IBIASNOM=0 IOFFSNOM=0 
+    CURRDOUB=0 VOUTOFFS=0 VTHRES=0 
X1 INp INm Vp Vm Out  COMPARATOR Params: 
+    GAIN={GAIN} RIN={RIN} ROUT={ROUT} ROUTSOURCE={ROUTSOURCE} ROUTSINK={ROUTSINK} IOUTMAX={IOUTMAX} 
+    IOUTMAXSOURCE={IOUTMAXSOURCE} IOUTMAXSINK={IOUTMAXSINK} SLEWRATE={SLEWRATE} SLEWRATERISE={SLEWRATERISE} 
+    SLEWRATEFALL={SLEWRATEFALL} DELAY={DELAY} DELAYHL={DELAYHL} DELAYLH={DELAYLH} VHYST={VHYST} VDROPOH={VDROPOH}
+    VDROPOL={VDROPOL}  VOFFS={VOFFSNOM} TCOVOFFS={TCOVOFFS} IBIAS={IBIASNOM} IOFFS={IOFFSNOM} 
+    CURRDOUB={CURRDOUB} VOUTOFFS={VOUTOFFS}  VTHRES={VTHRES}
.Ends Comp
*$
.Subckt CompGndRefS  INp INm OUT GND
+ Params: Vthres=1 Vhyst=0 VoutH=5 VoutL=0 Rout=0 DELAY=1N
X1 INp INm Out Gnd   HYSTCOMP  Params: 
+    ROUT={ROUT} Tdel={DELAY} VHYST={VHYST} VOUTH={VOUTH} VOUTL={VOUTL} VTHRES={VTHRES}
.Ends CompGndRefS 
*$
.Subckt CompGndRef  INp INm OUT GND
+ Params: GAIN=1000 RIN=1E12 ROUT=0 ROUTSOURCE=0 ROUTSINK=0 IOUTMAX=0 IOUTMAXSOURCE=0 
+    IOUTMAXSINK=0 SLEWRATE=0 SLEWRATERISE=0 SLEWRATEFALL=0 DELAY=0 DELAYHL=0 DELAYLH=0 
+    VHYST=0 VOUTH=0 VOUTL=0 VOFFSNOM=0 TCOVOFFS=0 IBIASNOM=0 IOFFSNOM=0 
+    CURRDOUB=0 VOUTOFFS=0 VTHRES=0
X1 INp INm Out Gnd  ComparGndRef  Params: 
+    GAIN={GAIN} RIN={RIN} ROUT={ROUT} SLEWRATE={SLEWRATE} 
+    DELAY={DELAY} VHYST={VHYST} VOUTH={VOUTH} 
+    VOUTL={VOUTL} VOFFS={VOFFSNOM} IBIAS={IBIASNOM} IOFFS={IOFFSNOM} 
+     VTHRES={VTHRES}
.Ends CompGndRef 
*$
.Subckt Comp2INpAndS IN1p IN1m IN2p IN2m OUT GND
+ Params: RIN=1E12 VOUTH=5 VOUTL=0 VTHRES1=2.5 VTHRES2=2.5
*
RINp1 IN1p GND {RIN}
RINm1 IN1m GND {RIN}
RINp2 IN2p GND {RIN}
RINm2 IN2m GND {RIN}
Eo OUT gnd Value= { IF ( (V(IN1p,IN1m) > {VTHRES1}) & (V(IN2p,IN2m) > {VTHRES2}), VOUTH, VOUTL ) }
.Ends Comp2INpAndS
*$
.Subckt CompSD IN1p IN1m OUT GND
+ PARAMS: RIN=1E12 ROUT=10 DELAY=10N VOUTH=5 VOUTL=0 VTHRES=2.5
*
.Param Tdel = {IF ( (DELAY < 1e-9) , 1e-9, DELAY ) } 
.Param Rdel = {IF ( (Tdel > 1E-15) & (Rout < 1), 1, Rout ) } 
.Param Cout={Tdel/(0.693*(Rdel+1u))}
RINp1 IN1p GND {RIN}
RINm1 IN1m GND {RIN}
EOUT OUT GND VALUE= { IF ( (V(IN1p,IN1m) > {VTHRES}), 
+ VOUTH + RDEL*I(EOUT), VOUTL + RDEL*I(EOUT) ) }
COUT OUT GND {COUT}
.Ends CompSD
*$
.Subckt CompInvS IN1p IN1m OUT GND
+ PARAMS: RIN=1E12 VOUTH=5 VOUTL=0 VTHRES=2.5
*
RINp1 IN1p GND {RIN}
RINm1 IN1m GND {RIN}
EOUT OUT GND VALUE= { IF ( (V(IN1p,IN1m) > {VTHRES}), VOUTL, VOUTH ) }
.Ends CompInvS
*$
.Subckt CompInvSD IN1p IN1m OUT GND
+ PARAMS: RIN=1E12 ROUT=10 DELAY=10N VOUTH=5 VOUTL=0 VTHRES=2.5
*
.PARAM RDEL = {IF ( (DELAY > 1E-15) & (ROUT < 1), 1, ROUT ) } 
.PARAM COUT={DELAY/(0.693*(RDEL+1U))}
RINp1 IN1p GND {RIN}
RINm1 IN1m GND {RIN}
EOUT OUT GND VALUE= { IF ( (V(IN1p,IN1m) > {VTHRES}), 
+ VOUTL + RDEL*I(EOUT), VOUTH + RDEL*I(EOUT) ) }
COUT OUT GND {COUT}
.Ends CompInvSD
*$
.Subckt Comp2INpAndSD IN1p IN1m IN2p IN2m OUT GND
+ PARAMS: RIN=1E12 ROUT=10 DELAY=10N VOUTH=5 VOUTL=0 VTHRES1=2.5 VTHRES2=2.5
*
.Param Tdel = {IF ( (DELAY < 1e-9) , 1e-9, DELAY ) } 
.Param Rdel = {IF ( (Tdel > 1E-15) & (Rout < 1), 1, Rout ) } 
.Param Cout={Tdel/(0.693*(Rdel+1u))}
RINp1 IN1p GND {RIN}
RINm1 IN1m GND {RIN}
RINp2 IN2p GND {RIN}
RINm2 IN2m GND {RIN}
EOUT OUT GND VALUE= { IF ( (V(IN1p,IN1m) > {VTHRES1}) & (V(IN2p,IN2m) > {VTHRES2}), 
+ VOUTH + RDEL*I(EOUT), VOUTL + RDEL*I(EOUT) ) }
COUT OUT GND {COUT}
.Ends Comp2INpAndSD
*$
.Subckt Comp2INpAnd IN1p IN1m IN2p IN2m OUT GND
+ Params: RIN=1E12 DELAY=10N VOUTH=5 VOUTL=0 VTHRES1=2.5 VHYST1=0 VTHRES2=2.5 VHYST2=0 
.Param VoutM={(VoutH+VoutL)/2}
X1 IN1p IN1m IN2p IN2m ST1 ST2 GND Hyst2INp Params: 
+    RIN={RIN} DELAY={DELAY} VOUTH={VOUTH} VOUTL={VOUTL} 
+    VTHRES1={VTHRES1} VHYST1={VHYST1} VTHRES2={VTHRES2} VHYST2={VHYST2}
Eo OUT gnd Value= { IF ( (V(st1,GND) > {VoutM}) & (V(st2,GND) > {VoutM}), VOUTH, VOUTL ) }
.Ends Comp2INpAnd
*$
.Subckt Comp2INpOrS IN1p IN1m IN2p IN2m OUT GND
+ Params: RIN=1E12 VOUTH=5 VOUTL=0 VTHRES1=2.5 VTHRES2=2.5
*
RINp1 IN1p GND {RIN}
RINm1 IN1m GND {RIN}
RINp2 IN2p GND {RIN}
RINm2 IN2m GND {RIN}
Eo OUT gnd Value= { IF ( (V(IN1p,IN1m) > {VTHRES1}) | (V(IN2p,IN2m) > {VTHRES2}), VOUTH, VOUTL ) }
.Ends Comp2INpOrS
*$
.Subckt Comp2INpNOrS IN1p IN1m IN2p IN2m OUT GND
+ Params: RIN=1E12 VOUTH=5 VOUTL=0 VTHRES1=2.5 VTHRES2=2.5
*
RINp1 IN1p GND {RIN}
RINm1 IN1m GND {RIN}
RINp2 IN2p GND {RIN}
RINm2 IN2m GND {RIN}
Eo OUT gnd Value= { IF ( (V(IN1p,IN1m) > {VTHRES1}) | (V(IN2p,IN2m) > {VTHRES2}), VOUTL, VOUTH ) }
.Ends Comp2INpNOrS
*$
.Subckt Comp2INpOr IN1p IN1m IN2p IN2m OUT GND
+ Params: RIN=1E12 DELAY=10N VOUTH=5 VOUTL=0 VTHRES1=0 VHYST1=0 VTHRES2=0 VHYST2=0 
.Param VoutM={(VoutH+VoutL)/2}
X1 IN1p IN1m IN2p IN2m ST1 ST2 GND Hyst2INp Params: 
+    RIN={RIN} DELAY={DELAY} VOUTH={VOUTH} VOUTL={VOUTL} 
+    VTHRES1={VTHRES1} VHYST1={VHYST1} VTHRES2={VTHRES2} VHYST2={VHYST2}
Eo OUT gnd Value= { IF ( (V(st1,GND) > {VoutM}) | (V(st2,GND) > {VoutM}), VOUTH, VOUTL ) }
.Ends Comp2INpOr 
*$
.Subckt Comp2INpNAndSD IN1p IN1m IN2p IN2m OUT GND
+ PARAMS: RIN=1E12 ROUT=0 DELAY=0 VOUTH=0 VOUTL=0 VTHRES1=0 VTHRES2=0
*
.PARAM RDEL = {IF ( (DELAY > 1E-15) & (ROUT < 1), 1, ROUT ) } 
.PARAM COUT={DELAY/(0.693*(RDEL+1U))}
RINp1 IN1p GND {RIN}
RINm1 IN1m GND {RIN}
RINp2 IN2p GND {RIN}
RINm2 IN2m GND {RIN}
EOUT OUT GND VALUE= { IF ( (V(IN1p,IN1m) > {VTHRES1}) & (V(IN2p,IN2m) > {VTHRES2}), 
+ VOUTL + RDEL*I(EOUT), VOUTH + RDEL*I(EOUT) ) }
COUT OUT GND {COUT}
.Ends Comp2INpNAndSD
*$
.Subckt Comp2INpNOrSD IN1p IN1m IN2p IN2m OUT GND
+ Params: RIN=1E12 ROUT=0 DELAY=0 VOUTH=0 VOUTL=0 VTHRES1=0 VTHRES2=0
*
.Param Tdel = {IF ( (DELAY < 1e-9) , 1e-9, DELAY ) } 
.Param Rdel = {IF ( (Tdel > 1E-15) & (Rout < 1), 1, Rout ) } 
.Param Cout={Tdel/(0.693*(Rdel+1u))}
RINp1 IN1p GND {RIN}
RINm1 IN1m GND {RIN}
RINp2 IN2p GND {RIN}
RINm2 IN2m GND {RIN}
Eout OUT gnd Value= { IF ( (V(IN1p,IN1m) > {VTHRES1}) | (V(IN2p,IN2m) > {VTHRES2}), 
+ VOUTL + Rdel*I(Eout), VOUTH + Rdel*I(Eout) ) }
Cout OUT gnd {Cout}
.Ends Comp2INpNOrSD
*$
.Subckt Hyst2INp IN1p IN1m IN2p IN2m ST1 ST2 GND
+ Params: RIN=1E12 DELAY=0 VOUTH=0 VOUTL=0 VTHRES1=0 VHYST1=0 VTHRES2=0 VHYST2=0 
.Param VoutM={(VoutH+VoutL)/2}
.Param VthH1={Vthres1+Vhyst1}
.Param VthL1={Vthres1-Vhyst1}
.Param VthH2={Vthres2+Vhyst2}
.Param VthL2={Vthres2-Vhyst2}
.Param Rdel=10
.Param Tdel = {IF ( (DELAY < 1e-9) , 1e-9, DELAY ) } 
.Param Cdel={Tdel/(0.693*Rdel)}
*
RINp1 IN1p GND {RIN}
RINm1 IN1m GND {RIN}
RINp2 IN2p GND {RIN}
RINm2 IN2m GND {RIN}
Ethr1 thr1 gnd Value= { IF ( V(st1,gnd) < {VoutM}, {VthH1}, {VthL1}) }
Est1 st1 gnd Value= { IF ( V(IN1p,IN1m) > V(thr1,gnd), VoutH + Rdel*I(Est1), VoutL + Rdel*I(Est1) ) }
Cst1 st1 gnd {Cdel} 
Rp1 st1 gnd 1Meg
Ethr2 thr2 gnd Value= { IF ( V(st2,gnd) < {VoutM}, {VthH2}, {VthL2}) }
Est2 st2 gnd Value= { IF ( V(IN2p,IN2m) > V(thr2,gnd), VoutH + Rdel*I(Est2), VoutL + Rdel*I(Est2) ) }
Cst2 st2 gnd {Cdel} 
Rp2 st2 gnd 1Meg
.Ends Hyst2INp
*$
.Subckt Comp2INp IN1p IN1m IN2p IN2m OUT GND
+ Params: GAIN=1000 RIN=1E12 ROUT=0 ROUTSOURCE=0 ROUTSINK=0 IOUTMAX=0 IOUTMAXSOURCE=0 
+    IOUTMAXSINK=0 SLEWRATE=0 SLEWRATERISE=0 SLEWRATEFALL=0 DELAY=0 
+    DELAYHL=0 DELAYLH=0 VOUTH=0 VOUTL=0 VOFFSNOM=0 TCOVOFFS=0 IBIASNOM=0 IOFFSNOM=0 
+    CURRDOUB=0 VOUTOFFS=0 DCTRANSFER=0 LOGICFUNC=0 VTHRES1=0 VHYST1=0 VTHRES2=0 VHYST2=0
X1 IN1p IN1m IN2p IN2m Out Gnd  Comp2INpAnd  Params: 
+    GAIN={GAIN} RIN={RIN} ROUT={ROUT} ROUTSOURCE={ROUTSOURCE} ROUTSINK={ROUTSINK} IOUTMAX={IOUTMAX} 
+    IOUTMAXSOURCE={IOUTMAXSOURCE} IOUTMAXSINK={IOUTMAXSINK} SLEWRATE={SLEWRATE} SLEWRATERISE={SLEWRATERISE} 
+    SLEWRATEFALL={SLEWRATEFALL} DELAY={DELAY} DELAYHL={DELAYHL} DELAYLH={DELAYLH} VOUTH={VOUTH} 
+    VOUTL={VOUTL} VOFFSNOM={VOFFSNOM} TCOVOFFS={TCOVOFFS} IBIASNOM={IBIASNOM} IOFFSNOM={IOFFSNOM} CURRDOUB={CURRDOUB}
+    VOUTOFFS={VOUTOFFS} DCTRANSFER={DCTRANSFER} LOGICFUNC={LOGICFUNC} 
+    VTHRES1={VTHRES1} VHYST1={VHYST1} VTHRES2={VTHRES2} VHYST2={VHYST2}
.Ends Comp2INpAnd
*$
.Subckt Comp3INpAndS IN1p IN1m IN2p IN2m IN3p IN3m OUT GND
+ Params: RIN=1E12 DELAY=0 VOUTH=0 VOUTL=0 VTHRES1=0 VHYST1=0
+ VTHRES2=0 VHYST2=0 VTHRES3=0 VHYST3=0  
*
RINp1 IN1p GND {RIN}
RINm1 IN1m GND {RIN}
RINp2 IN2p GND {RIN}
RINm2 IN2m GND {RIN}
RINp3 IN3p GND {RIN}
RINm3 IN3m GND {RIN}
Eo OUT gnd Value= { IF ( (V(IN1p,IN1m) > {VTHRES1}) & (V(IN2p,IN2m) >
+ {VTHRES2}) & (V(IN3p,IN3m) > {VTHRES3}), VOUTH, VOUTL ) }
.Ends Comp3INpAndS
*$
.Subckt Comp3INpAnd IN1p IN1m IN2p IN2m IN3p IN3m OUT GND
+ Params: RIN=1E12 DELAY=0 VOUTH=0 VOUTL=0 VTHRES1=0 VHYST1=0 VTHRES2=0 VHYST2=0 VTHRES3=0 VHYST3=0  
.Param VoutM={(VoutH+VoutL)/2}
X1 IN1p IN1m IN2p IN2m IN3p IN3m ST1 ST2  ST3 GND Hyst3INp Params: 
+    RIN={RIN} DELAY={DELAY} VOUTH={VOUTH} VOUTL={VOUTL} 
+    VTHRES1={VTHRES1} VHYST1={VHYST1} VTHRES2={VTHRES2} VHYST2={VHYST2} VTHRES3={VTHRES3} VHYST3={VHYST3}
Eo OUT gnd Value= { IF ( (V(st1,GND) > {VoutM}) & (V(st2,GND) > {VoutM}) & (V(st3,GND) > {VoutM}), VOUTH, VOUTL ) }
.Ends Comp3INpAnd
*$
.Subckt Comp3INpOr IN1p IN1m IN2p IN2m IN3p IN3m OUT GND
+ Params: RIN=1E12 DELAY=0 VOUTH=0 VOUTL=0 VTHRES1=0 VHYST1=0 VTHRES2=0 VHYST2=0 VTHRES3=0 VHYST3=0  
.Param VoutM={(VoutH+VoutL)/2}
X1 IN1p IN1m IN2p IN2m IN3p IN3m ST1 ST2  ST3 GND Hyst3INp Params: 
+    RIN={RIN} DELAY={DELAY} VOUTH={VOUTH} VOUTL={VOUTL} 
+    VTHRES1={VTHRES1} VHYST1={VHYST1} VTHRES2={VTHRES2} VHYST2={VHYST2} VTHRES3={VTHRES3} VHYST3={VHYST3}
Eo OUT gnd Value= { IF ( (V(st1,GND) > {VoutM}) | (V(st2,GND) > {VoutM}) | (V(st3,GND) > {VoutM}), VOUTH, VOUTL ) }
.Ends Comp3INpOr
*$
.Subckt Comp3InpOrS IN1p IN1m IN2p IN2m IN3p IN3m OUT GND
+ Params: RIN=1E12 DELAY=0 VOUTH=0 VOUTL=0 VTHRES1=0 VHYST1=0 VTHRES2=0 VHYST2=0 VTHRES3=0 VHYST3=0  
*
Rinp1 IN1p GND {RIN}
Rinm1 IN1m GND {RIN}
Rinp2 IN2p GND {RIN}
Rinm2 IN2m GND {RIN}
Rinp3 IN3p GND {RIN}
Rinm3 IN3m GND {RIN}
Eo OUT gnd Value= { IF ( (V(IN1p,IN1m) > {VTHRES1}) | (V(IN2p,IN2m) > {VTHRES2}) | (V(IN3p,IN3m) > {VTHRES3}), VOUTH, VOUTL  ) }
.Ends Comp3InpOrS
*$
.Subckt Comp3InpNOrS IN1p IN1m IN2p IN2m IN3p IN3m OUT GND
+ Params: RIN=1E12 DELAY=0 VOUTH=0 VOUTL=0 VTHRES1=0 VHYST1=0 VTHRES2=0 VHYST2=0 VTHRES3=0 VHYST3=0  
*
Rinp1 IN1p GND {RIN}
Rinm1 IN1m GND {RIN}
Rinp2 IN2p GND {RIN}
Rinm2 IN2m GND {RIN}
Rinp3 IN3p GND {RIN}
Rinm3 IN3m GND {RIN}
Eo OUT gnd Value= { IF ( (V(IN1p,IN1m) > {VTHRES1}) | (V(IN2p,IN2m) > {VTHRES2}) | (V(IN3p,IN3m) > {VTHRES3}), VOUTL, VOUTH  ) }
.Ends Comp3InpNOrS
*$
.Subckt Comp3INpNOrSD IN1p IN1m IN2p IN2m IN3p IN3m OUT GND
+ Params: RIN=1E12 ROUT=0 DELAY=0 VOUTH=0 VOUTL=0 VTHRES1=0 VTHRES2=0 VTHRES3=0
*
.Param Tdel = {IF ( (DELAY < 1e-9) , 1e-9, DELAY ) } 
.Param Rdel = {IF ( (Tdel > 1E-15) & (Rout < 1), 1, Rout ) } 
.Param Cout={Tdel/(0.693*(Rdel+1u))}
RINp1 IN1p GND {RIN}
RINm1 IN1m GND {RIN}
RINp2 IN2p GND {RIN}
RINm2 IN2m GND {RIN}
RINp3 IN3p GND {RIN}
RINm3 IN3m GND {RIN}
Eout OUT gnd Value= { IF ( (V(IN1p,IN1m) > {VTHRES1}) | (V(IN2p,IN2m) > {VTHRES2}) | (V(IN3p,IN3m) > {VTHRES3}), 
+ VOUTL + Rdel*I(Eout), VOUTH + Rdel*I(Eout) ) }
Cout OUT gnd {Cout}
.Ends Comp3INpNOrSD
*$
.Subckt Hyst3INp IN1p IN1m IN2p IN2m IN3p IN3m ST1 ST2 ST3 GND
+ Params: RIN=1E12 DELAY=0 VOUTH=0 VOUTL=0 VTHRES1=0 VHYST1=0 VTHRES2=0 VHYST2=0 VTHRES3=0 VHYST3=0 
.Param VoutM={(VoutH+VoutL)/2}
.Param VthH1={Vthres1+Vhyst1}
.Param VthL1={Vthres1-Vhyst1}
.Param VthH2={Vthres2+Vhyst2}
.Param VthL2={Vthres2-Vhyst2}
.Param VthH3={Vthres3+Vhyst3}
.Param VthL3={Vthres3-Vhyst3}
.Param Rdel=10
.Param Tdel = {IF ( (DELAY < 1e-9) , 1e-9, DELAY ) } 
.Param Cdel={Tdel/(0.693*Rdel)}
*
RINp1 IN1p GND {RIN}
RINm1 IN1m GND {RIN}
RINp2 IN2p GND {RIN}
RINm2 IN2m GND {RIN}
RINp3 IN3p GND {RIN}
RINm3 IN3m GND {RIN}
Ethr1 thr1 gnd Value= { IF ( V(st1,gnd) < {VoutM}, {VthH1}, {VthL1}) }
Est1 st1 gnd Value= { IF ( V(IN1p,IN1m) > V(thr1,gnd), VoutH + Rdel*I(Est1), VoutL + Rdel*I(Est1) ) }
Cst1 st1 gnd {Cdel} 
Rp1 st1 gnd 1Meg
Ethr2 thr2 gnd Value= { IF ( V(st2,gnd) < {VoutM}, {VthH2}, {VthL2}) }
Est2 st2 gnd Value= { IF ( V(IN2p,IN2m) > V(thr2,gnd), VoutH + Rdel*I(Est2), VoutL + Rdel*I(Est2) ) }
Cst2 st2 gnd {Cdel} 
Rp2 st2 gnd 1Meg
Ethr3 thr3 gnd Value= { IF ( V(st3,gnd) < {VoutM}, {VthH3}, {VthL3}) }
Est3 st3 gnd Value= { IF ( V(IN3p,IN3m) > V(thr3,gnd), VoutH + Rdel*I(Est3), VoutL + Rdel*I(Est3) ) }
Cst3 st3 gnd {Cdel} 
Rp3 st3 gnd 1Meg
.Ends Hyst3INp
*$
.Subckt Comp3INp IN1p IN1m IN2p IN2m IN3p IN3m OUT GND
+ Params: GAIN=1000 RIN=1E12 ROUT=0 ROUTSOURCE=0 ROUTSINK=0 IOUTMAX=0 IOUTMAXSOURCE=0 
+    IOUTMAXSINK=0 SLEWRATE=0 SLEWRATERISE=0 SLEWRATEFALL=0 DELAY=0 
+    DELAYHL=0 DELAYLH=0 VOUTH=0 VOUTL=0 VOFFSNOM=0 TCOVOFFS=0 IBIASNOM=0 IOFFSNOM=0 
+    CURRDOUB=0 VOUTOFFS=0 DCTRANSFER=0 LOGICFUNC=0 VTHRES1=0 VHYST1=0 VTHRES2=0 VHYST2=0 VTHRES3=0 VHYST3=0
X1 IN1p IN1m IN2p IN2m IN3p IN3m Out Gnd  Comp3INpAnd
.Model _Comp COMP_GR_3INp (GAIN={GAIN} RIN={RIN} ROUT={ROUT} ROUTSOURCE={ROUTSOURCE} ROUTSINK={ROUTSINK} IOUTMAX={IOUTMAX} 
+    IOUTMAXSOURCE={IOUTMAXSOURCE} IOUTMAXSINK={IOUTMAXSINK} SLEWRATE={SLEWRATE} SLEWRATERISE={SLEWRATERISE} 
+    SLEWRATEFALL={SLEWRATEFALL} DELAY={DELAY} DELAYHL={DELAYHL} DELAYLH={DELAYLH} VOUTH={VOUTH} 
+    VOUTL={VOUTL} VOFFSNOM={VOFFSNOM} TCOVOFFS={TCOVOFFS} IBIASNOM={IBIASNOM} IOFFSNOM={IOFFSNOM} CURRDOUB={CURRDOUB}
+    VOUTOFFS={VOUTOFFS} DCTRANSFER={DCTRANSFER} LOGICFUNC={LOGICFUNC}
+    VTHRES1={VTHRES1} VHYST1={VHYST1} VTHRES2={VTHRES2} VHYST2={VHYST2} VTHRES3={VTHRES3} VHYST3={VHYST3} );
.Ends Comp3INp
*$
.Subckt Comp4INpAndS IN1p IN1m IN2p IN2m IN3p IN3m IN4p IN4m OUT GND
+ Params: RIN=1E12 DELAY=0 VOUTH=0 VOUTL=0 VTHRES1=0 VHYST1=0 VTHRES2=0 VHYST2=0 VTHRES3=0 VHYST3=0 VTHRES4=0 VHYST4=0 
*
RINp1 IN1p GND {RIN}
RINm1 IN1m GND {RIN}
RINp2 IN2p GND {RIN}
RINm2 IN2m GND {RIN}
RINp3 IN3p GND {RIN}
RINm3 IN3m GND {RIN}
RINp4 IN4p GND {RIN}
RINm4 IN4m GND {RIN}
Eo OUT gnd Value= { IF ( (V(IN1p,IN1m) > {VTHRES1}) & (V(IN2p,IN2m) > {VTHRES2}) & (V(IN3p,IN3m) > {VTHRES3}) & 
+ (V(IN4p,IN4m) > {VTHRES4}), VOUTH, VOUTL ) }
.Ends Comp4INpAndS
*$
.Subckt Comp4INpAnd IN1p IN1m IN2p IN2m IN3p IN3m IN4p IN4m OUT GND
+ Params: RIN=1E12 DELAY=0 VOUTH=0 VOUTL=0 VTHRES1=0 VHYST1=0 VTHRES2=0 VHYST2=0 VTHRES3=0 VHYST3=0 VTHRES4=0 VHYST4=0  
.Param VoutM={(VoutH+VoutL)/2}
X1 IN1p IN1m IN2p IN2m IN3p IN3m IN4p IN4m ST1 ST2 ST3 ST4 GND Hyst4INp Params: 
+    RIN={RIN} DELAY={DELAY} VOUTH={VOUTH} VOUTL={VOUTL} 
+    VTHRES1={VTHRES1} VHYST1={VHYST1} VTHRES2={VTHRES2} VHYST2={VHYST2}
+    VTHRES3={VTHRES3} VHYST3={VHYST3} VTHRES4={VTHRES4} VHYST4={VHYST4}
Eo OUT gnd Value= { IF ( (V(st1,GND) > {VoutM}) & (V(st2,GND) > {VoutM}) & (V(st3,GND) > {VoutM}) & 
+ (V(st4,GND) > {VoutM}), VOUTH, VOUTL ) }
.Ends Comp4INpAnd
*$
.Subckt Comp4INpOrS IN1p IN1m IN2p IN2m IN3p IN3m IN4p IN4m OUT GND
+ Params: RIN=1E12 DELAY=0 VOUTH=0 VOUTL=0 VTHRES1=0 VHYST1=0 VTHRES2=0 VHYST2=0 VTHRES3=0 VHYST3=0 VTHRES4=0 VHYST4=0 
*
RINp1 IN1p GND {RIN}
RINm1 IN1m GND {RIN}
RINp2 IN2p GND {RIN}
RINm2 IN2m GND {RIN}
RINp3 IN3p GND {RIN}
RINm3 IN3m GND {RIN}
RINp4 IN4p GND {RIN}
RINm4 IN4m GND {RIN}
Eo OUT gnd Value= { IF ( (V(IN1p,IN1m) > {VTHRES1}) | (V(IN2p,IN2m) > {VTHRES2}) | (V(IN3p,IN3m) > {VTHRES3}) | 
+ (V(IN4p,IN4m) > {VTHRES4}), VOUTH, VOUTL ) }
.Ends Comp4INpOrS
*$
.Subckt Hyst4INp IN1p IN1m IN2p IN2m IN3p IN3m IN4p IN4m ST1 ST2 ST3 ST4 GND
+ Params: RIN=1E12 DELAY=0 VOUTH=0 VOUTL=0 VTHRES1=0 VHYST1=0 VTHRES2=0 VHYST2=0 VTHRES3=0 VHYST3=0 VTHRES4=0 VHYST4=0
.Param VoutM={(VoutH+VoutL)/2}
.Param VthH1={Vthres1+Vhyst1}
.Param VthL1={Vthres1-Vhyst1}
.Param VthH2={Vthres2+Vhyst2}
.Param VthL2={Vthres2-Vhyst2}
.Param VthH3={Vthres3+Vhyst3}
.Param VthL3={Vthres3-Vhyst3}
.Param VthH4={Vthres4+Vhyst4}
.Param VthL4={Vthres4-Vhyst4}
.Param Rdel=10
.Param Tdel = {IF ( (DELAY < 1e-9) , 1e-9, DELAY ) } 
.Param Cdel={Tdel/(0.693*Rdel)}
*
RINp1 IN1p GND {RIN}
RINm1 IN1m GND {RIN}
RINp2 IN2p GND {RIN}
RINm2 IN2m GND {RIN}
RINp3 IN3p GND {RIN}
RINm3 IN3m GND {RIN}
RINp4 IN4p GND {RIN}
RINm4 IN4m GND {RIN}
Ethr1 thr1 gnd Value= { IF ( V(st1,gnd) < {VoutM}, {VthH1}, {VthL1}) }
Est1 st1 gnd Value= { IF ( V(IN1p,IN1m) > V(thr1,gnd), VoutH + Rdel*I(Est1), VoutL + Rdel*I(Est1) ) }
Cst1 st1 gnd {Cdel} 
Rp1 st1 gnd 1Meg
Ethr2 thr2 gnd Value= { IF ( V(st2,gnd) < {VoutM}, {VthH2}, {VthL2}) }
Est2 st2 gnd Value= { IF ( V(IN2p,IN2m) > V(thr2,gnd), VoutH + Rdel*I(Est2), VoutL + Rdel*I(Est2) ) }
Cst2 st2 gnd {Cdel} 
Rp2 st2 gnd 1Meg
Ethr3 thr3 gnd Value= { IF ( V(st3,gnd) < {VoutM}, {VthH3}, {VthL3}) }
Est3 st3 gnd Value= { IF ( V(IN3p,IN3m) > V(thr3,gnd), VoutH + Rdel*I(Est3), VoutL + Rdel*I(Est3) ) }
Cst3 st3 gnd {Cdel} 
Rp3 st3 gnd 1Meg
Ethr4 thr4 gnd Value= { IF ( V(st4,gnd) < {VoutM}, {VthH4}, {VthL4}) }
Est4 st4 gnd Value= { IF ( V(IN4p,IN4m) > V(thr4,gnd), VoutH + Rdel*I(Est4), VoutL + Rdel*I(Est4) ) }
Cst4 st4 gnd {Cdel} 
Rp4 st4 gnd 1Meg
.Ends Hyst4INp
*$
.Subckt Comp4INpAndF IN1p IN1m IN2p IN2m IN3p IN3m IN4p IN4m OUT GND
+ Params: GAIN=1000 RIN=1E12 ROUT=0 ROUTSOURCE=0 ROUTSINK=0 IOUTMAX=0 IOUTMAXSOURCE=0 
+    IOUTMAXSINK=0 SLEWRATE=0 SLEWRATERISE=0 SLEWRATEFALL=0 DELAY=0 
+    DELAYHL=0 DELAYLH=0 VOUTH=0 VOUTL=0 VOFFSNOM=0 TCOVOFFS=0 IBIASNOM=0 IOFFSNOM=0 
+    CURRDOUB=0 VOUTOFFS=0 DCTRANSFER=0 LOGICFUNC=0 
+    VTHRES1=0 VHYST1=0 VTHRES2=0 VHYST2=0 VTHRES3=0 VHYST3=0 VTHRES4=0 VHYST4=0 
Y1 IN1p IN1m IN2p IN2m IN3p IN3m IN4p IN4m Out Gnd  _Comp
.Model _Comp COMP_GR_4INp (GAIN={GAIN} RIN={RIN} ROUT={ROUT} ROUTSOURCE={ROUTSOURCE} ROUTSINK={ROUTSINK} IOUTMAX={IOUTMAX} 
+    IOUTMAXSOURCE={IOUTMAXSOURCE} IOUTMAXSINK={IOUTMAXSINK} SLEWRATE={SLEWRATE} SLEWRATERISE={SLEWRATERISE} 
+    SLEWRATEFALL={SLEWRATEFALL} DELAY={DELAY} DELAYHL={DELAYHL} DELAYLH={DELAYLH} VOUTH={VOUTH} 
+    VOUTL={VOUTL} VOFFSNOM={VOFFSNOM} TCOVOFFS={TCOVOFFS} IBIASNOM={IBIASNOM} IOFFSNOM={IOFFSNOM} CURRDOUB={CURRDOUB}
+    VOUTOFFS={VOUTOFFS} DCTRANSFER={DCTRANSFER} LOGICFUNC={LOGICFUNC} 
+    VTHRES1={VTHRES1} VHYST1={VHYST1} VTHRES2={VTHRES2} VHYST2={VHYST2} VTHRES3={VTHRES3} VHYST3={VHYST3} VTHRES4={VTHRES4}
+    VHYST4={VHYST4} );
.Ends Comp4INpAndF
*$
.Subckt Comp4INpNOrS IN1p IN1m IN2p IN2m IN3p IN3m IN4p IN4m OUT GND
+ Params: RIN=1E12 DELAY=0 VOUTH=0 VOUTL=0 VTHRES1=0 VHYST1=0 VTHRES2=0 VHYST2=0 VTHRES3=0 VHYST3=0 VTHRES4=0 VHYST4=0 
*
RINp1 IN1p GND {RIN}
RINm1 IN1m GND {RIN}
RINp2 IN2p GND {RIN}
RINm2 IN2m GND {RIN}
RINp3 IN3p GND {RIN}
RINm3 IN3m GND {RIN}
RINp4 IN4p GND {RIN}
RINm4 IN4m GND {RIN}
Eo OUT gnd Value= { IF ( (V(IN1p,IN1m) > {VTHRES1}) | (V(IN2p,IN2m) > {VTHRES2}) | (V(IN3p,IN3m) > {VTHRES3}) | 
+ (V(IN4p,IN4m) > {VTHRES4}), VOUTL, VOUTH ) }
.Ends Comp4INpNOrS
*$
.Subckt VCO INp INm Gnd OutSin OutTri OutSqr PARAMS: CENTFREQ=1E3 CONVGAIN=1E3 PHI0=0 OUTAMPLI=1 OUTOFFS=0 INLLIM=-1E12 INULIM=1E12
+ LIMRNG=1E-6 DUTYCYC=0.5 RISETIME=1E-9 FALLTIME=1E-9 MODE=1
Y1 INp INm Gnd OutSin OutTri OutSq VCO_
.Model VCO_ VCO (CENTFREQ ={ CENTFREQ} CONVGAIN ={CONVGAIN} PHI0 ={PHI0} OUTAMPLI ={OUTAMPLI} OUTOFFS ={OUTOFFS} 
+ INLLIM ={INLLIM} INULIM ={INULIM} LIMRNG ={LIMRNG} DUTYCYC ={DUTYCYC} RISETIME ={RISETIME} FALLTIME ={FALLTIME} MODE ={MODE})
.Ends
*$
.Subckt SINEVCO INp INm Gnd Out PARAMS: CENTFREQ=1E3 CONVGAIN=1E3 PHI0=0 OUTAMPLI=1 OUTOFFS=0 INLLIM=-1E12 INULIM=1E12
+ LIMRNG=1E-6  MODE=1
Y1 INp INm Gnd Out sinVCO
.Model sinVCO SINE_VCO (CENTFREQ ={ CENTFREQ} CONVGAIN ={CONVGAIN} PHI0 ={PHI0} OUTAMPLI ={OUTAMPLI} OUTOFFS ={OUTOFFS} 
+ INLLIM ={INLLIM} INULIM ={INULIM} LIMRNG ={LIMRNG} MODE ={MODE})
.Ends
*$
.Subckt TRIVCO INp INm Gnd Out PARAMS: CENTFREQ=1E3 CONVGAIN=1E3 PHI0=0 OUTAMPLI=1 OUTOFFS=0 INLLIM=-1E12 INULIM=1E12
+ LIMRNG=1E-6 DUTYCYC=0.5 MODE=1
Y1 INp INm Gnd Out triVCO
.Model triVCO TRI_VCO (CENTFREQ ={ CENTFREQ} CONVGAIN ={CONVGAIN} PHI0 ={PHI0} OUTAMPLI ={OUTAMPLI} OUTOFFS ={OUTOFFS} 
+ INLLIM ={INLLIM} INULIM ={INULIM} LIMRNG ={LIMRNG} DUTYCYC ={DUTYCYC} MODE ={MODE})
.Ends
*$
.Subckt SQRVCO INp INm Gnd Out PARAMS: CENTFREQ=1E3 CONVGAIN=1E3 PHI0=0 OUTAMPLI=1 OUTOFFS=0 INLLIM=-1E12 INULIM=1E12
+ LIMRNG=1E-6 DUTYCYC=0.5 RISETIME=1E-9 FALLTIME=1E-9 MODE=1
Y1 INp INm Gnd Out sqrVCO
.Model sqrVCO SQUARE_VCO (CENTFREQ ={ CENTFREQ} CONVGAIN ={CONVGAIN} PHI0 ={PHI0} OUTAMPLI ={OUTAMPLI} OUTOFFS ={OUTOFFS} 
+ INLLIM ={INLLIM} INULIM ={INULIM} LIMRNG ={LIMRNG} DUTYCYC ={DUTYCYC} RISETIME ={RISETIME} FALLTIME ={FALLTIME} MODE ={MODE})
.Ends
*$
.Subckt NCOUNTER_UDSR   CLK UD CE S R MIN MAX  GND VCC PARAMS: INTYP=0 OUTTYP=0 DEL=0 IOMODEL=0 
+  DELL2H=10E-9 DELH2L=10E-9 LATCH=1 MAXCOUNT=15
Y1  UD CLK CE R MIN MAX S GND VCC  _CNTN
.Model _CNTN  CNTN_UDSR (INTYP ={INTYP} OUTTYP ={OUTTYP} DEL ={DEL} IOMODEL ={IOMODEL} DELL2H ={DELL2H} 
+ DELH2L ={DELH2L} LATCH ={LATCH} MAXCOUNT ={MAXCOUNT} 
.Ends
*$
* Standard operational amplifier macromodel subcircuit
* Created using 08/05/06
* (REV 1.1 29/07/06 )
.SUBCKT StdOpamp INp INm Vp Vm Out
+ Params: GAIN=200K RIN=2MEG RINC=1E9 ROUT=75 SLEWRATE=500K FPOLE1=5 FPOLE2=1MEG 
+         VDROPOH=1.9 VDROPOL=1.9 VOFFS=1m IBIAS=80n IOFFS=20n 
*
.PARAM VOSW = 15
.PARAM pi = 3.141592
.PARAM IS = 1.0E-12
.PARAM VT = 0.02585
.PARAM C1 = {VOSW/ROUT/SLEWRATE}
.PARAM IMAX = {C1*SLEWRATE}
.PARAM R1 = {1/(2*pi*C1*FPOLE1)}
.PARAM GM1 = {GAIN/R1}
.PARAM R2 = 100
.PARAM G2 = {1/R2}
.PARAM GOUT = {1/ROUT}
.PARAM C2 = {1/(2*pi*R2*FPOLE2)}
.PARAM VDF = {VT*Log(1 + IMAX/IS)}
*
Ibiasm      INm 0  {IBIAS - IOFFS}
RINm      INm  8  {2*RINC}
RINp      INp  8  {2*RINC}
Ibias       10 0   {IBIAS}
Voffs       10 INp  {VOFFS}
Vc          Vp 11  {VDROPOH + VDF}
Ve          12 Vm  {VDROPOL + VDF}
D1          Vm Vp  D_1
Rp          Vp Vm  15E3
ROut        OUT 8  {ROUT}
GmO         8 OUT Value =  {V(9,8)*GOUT}
C2          9 8  {C2}
R2          9 8  {R2}
Gm2         8 9  Value =  {V(7,8)*G2}
Rin         INm 10  {RIN}
EGND        8  0  POLY(2) (Vp,0) (Vm,0) 0 .5 .5
D3          12 7  D_1
D2          7 11  D_1
C1          7 8  {C1}
R1          7 8  {R1}
Gm1         8 7 Value = { Limit( GM1*V(10,INm), -IMAX, IMAX) }
.MODEL D_1 D( IS={IS} )
.ENDS
*$
.SUBCKT DFFLOP  PREB CLRB CLK D Q QB
+ OPTIONAL: DPWR=$G_DPWR DGND=$G_DGND
+ PARAMS:    DLH = 0 DHL = 0
U1 DFF(1) DPWR DGND PREB CLRB CLK
+ D Q QB
+ T_DFF IO_HC
.MODEL T_DFF UEFF( TPPCQLHTY = {DLH} TPCLKQLHTY = {DLH}
+                   TPPCQHLTY = {DHL} TPCLKQHLTY = {DHL} )
.ENDS
*$
.SUBCKT DFFLOP_P PREB CLRB CLK D Q QB DPWR DGND 
+ PARAMS:    DLH = 0 DHL = 0
U1 DFF(1) DPWR DGND PREB CLRB CLK
+ D Q QB
+ T_DFF IO_HC
.MODEL T_DFF UEFF( TPPCQLHTY = {DLH} TPCLKQLHTY = {DLH}
+                   TPPCQHLTY = {DHL} TPCLKQHLTY = {DHL} )
.ENDS
*$
.SUBCKT RSFFLOP  S  R  Q  QB
+ OPTIONAL: DPWR=$G_DPWR DGND=$G_DGND
+ PARAMS:    DLH = 0 DHL = 0
U1 INV DPWR DGND R CLRBAR 
+ T_INV IO_HC
.MODEL T_INV UGATE()
U2 INV DPWR DGND S PREBAR 
+ T_INV2 IO_HC
.MODEL T_INV2 UGATE()
U3 DFF(1) DPWR DGND PREBAR CLRBAR DGND DGND Q QB
+ T_DFF IO_HC
.MODEL T_DFF UEFF( TPPCQLHTY = {DLH} TPCLKQLHTY = {DLH}
+                   TPPCQHLTY = {DHL} TPCLKQHLTY = {DHL} )
.ENDS
*$
.SUBCKT RSFFLOP_P   S  R  Q  QB  DPWR  DGND
+ PARAMS:    DLH = 0 DHL = 0
U1 INV DPWR DGND R CLRBAR 
+ T_INV IO_HC
.MODEL T_INV UGATE()
U2 INV DPWR DGND S PREBAR 
+ T_INV2 IO_HC
.MODEL T_INV2 UGATE()
U3 DFF(1) DPWR DGND PREBAR CLRBAR DGND DGND Q QB
+ T_DFF IO_HC
.MODEL T_DFF UEFF( TPPCQLHTY = {DLH} TPCLKQLHTY = {DLH}
+                   TPPCQHLTY = {DHL} TPCLKQHLTY = {DHL} )
.ENDS
*$
.SUBCKT RSFFLOP_P_RES   S  R  Q  QB  DPWR  DGND
+ PARAMS:    DLH = 0 DHL = 0
U1 INV DPWR DGND R CLRBAR 
+ T_INV IO_HC
.MODEL T_INV UGATE()
U2 INV DPWR DGND S PREBAR 
+ T_INV2 IO_HC
.MODEL T_INV2 UGATE()
U3 DFF(1) DPWR DGND PREBAR CLRBAR DGND DGND Q QB
+ T_DFF IO_HC
.MODEL T_DFF UEFF( TPPCQLHTY = {DLH} TPCLKQLHTY = {DLH}
+                   TPPCQHLTY = {DHL} TPCLKQHLTY = {DHL} )
*PULSE (<i1> <i2> <td> <tr> <tf> <pw> <per>)
VRES RES DGND PULSE(0 {VOUTH} 0 10P 10P {4*DELAY} 1000)
.ENDS
*$
.SUBCKT R_S_FFLOP_P   S_  R_  Q  QB  DPWR  DGND
+ PARAMS:    DLH = 0 DHL = 0
U3 DFF(1) DPWR DGND S_ R_ DGND DGND Q QB
+ T_DFF IO_HC
.MODEL T_DFF UEFF( TPPCQLHTY = {DLH} TPCLKQLHTY = {DLH}
+                   TPPCQHLTY = {DHL} TPCLKQHLTY = {DHL} )
.ENDS
*$
.SUBCKT R_S_FF S_ R_ Q  Q_ DPWR DGND
+ PARAMS: VTHRESS=2.5 VTHRESR=2.5 VOUTH=5.0 VOUTL=0 RIN=1E12 DELAY=10N ROUT=10
*
XU1     Q DGND R_ DGND Q_ DGND   COMP2INpNANDSD
+ PARAMS: RIN={RIN} DELAY={1.1*DELAY} VOUTH={VOUTH} VOUTL={VOUTL} VTHRES1={(VOUTH+VOUTL)/2} VTHRES2={VTHRESR}
XU2     S_ DGND Q_ DGND Q DGND   COMP2INpNANDSD
+ PARAMS: RIN={RIN} DELAY={DELAY} VOUTH={VOUTH} VOUTL={VOUTL} VTHRES1={VTHRESS} VTHRES2={(VOUTH+VOUTL)/2}
.ENDS R_S_FF
*$
.SUBCKT RSFF S  R  Q  Q_ DPWR DGND
+ PARAMS:VTHRESS=2.5 VTHRESR=2.5 VOUTH=5.0 VOUTL=0 RIN=1E12 DELAY=10N ROUT=10
*
XU1     Q DGND S DGND Q_ DGND   Comp2INpNOrSD
+ PARAMS: RIN={RIN} DELAY={DELAY} VOUTH={VOUTH} VOUTL={VOUTL} VTHRES1={(VOUTH+VOUTL)/2} VTHRES2={VTHRESS}
XU2     R DGND Q_ DGND Q DGND   Comp2INpNOrSD
+ PARAMS: RIN={RIN} DELAY={1.1*DELAY} VOUTH={VOUTH} VOUTL={VOUTL} VTHRES1={VTHRESR} VTHRES2={(VOUTH+VOUTL)/2} 
*XU2     RES DGND R DGND Q_ DGND Q DGND   Comp3INpNOrSD
*+ PARAMS: RIN={RIN} DELAY={DELAY} VOUTH={VOUTH} VOUTL={VOUTL} VTHRES1={VTHRESR} VTHRES2={(VOUTH+VOUTL)/2} 
*+ VTHRES3={(VOUTH+VOUTL)/2}
*PULSE (<i1> <i2> <td> <tr> <tf> <pw> <per>)
*VRES RES DGND PULSE(0 {VOUTH} 0 100P 100P {4*DELAY} 1000)
.ENDS  RSFF
*$
.SUBCKT TFFLOP  PREB CLRB CLKB T Q QB 
+ OPTIONAL: DPWR=$G_DPWR DGND=$G_DGND
+ PARAMS:    DLH = 0 DHL = 0
U1 JKFF(1) DPWR DGND PREB CLRB CLKB T T Q QB 
+ T_DFF IO_HC
.MODEL T_DFF UEFF( TPPCQLHTY = {DLH} TPCLKQLHTY = {DLH}
+                   TPPCQHLTY = {DHL} TPCLKQHLTY = {DHL} )
.ENDS
*$
.SUBCKT TFFLOP_P  PREB CLRB CLKB T Q QB DPWR DGND 
+ PARAMS:    DLH = 0 DHL = 0
U1 JKFF(1) DPWR DGND PREB CLRB CLKB T T Q QB 
+ T_DFF IO_HC
.MODEL T_DFF UEFF( TPPCQLHTY = {DLH} TPCLKQLHTY = {DLH}
+                   TPPCQHLTY = {DHL} TPCLKQHLTY = {DHL} )
.ENDS
*$
.SUBCKT JKFFLOP  PREB CLRB CLKB J K Q QB 
+ OPTIONAL: DPWR=$G_DPWR DGND=$G_DGND
+ PARAMS:    DLH = 0 DHL = 0
U1 JKFF(1) DPWR DGND PREB CLRB CLKB J K Q QB 
+ T_DFF IO_HC
.MODEL T_DFF UEFF( TPPCQLHTY = {DLH} TPCLKQLHTY = {DLH}
+                   TPPCQHLTY = {DHL} TPCLKQHLTY = {DHL} )
.ENDS
*$
.SUBCKT JKFFLOP_P  PREB CLRB CLKB J K Q QB DPWR DGND 
+ PARAMS:    DLH = 0 DHL = 0
U1 JKFF(1) DPWR DGND PREB CLRB CLKB J K Q QB 
+ T_DFF IO_HC
.MODEL T_DFF UEFF( TPPCQLHTY = {DLH} TPCLKQLHTY = {DLH}
+                   TPPCQHLTY = {DHL} TPCLKQHLTY = {DHL} )
.ENDS
*$
.SUBCKT INV_P  IN OUT DPWR DGND 
+ PARAMS:    DLH = 0 DHL = 0
*
U1 INV DPWR DGND IN OUT 
+ T_GATE IO_HC
.MODEL T_GATE UGATE( TPLHTY={DLH} TPHLTY={DHL} )
.ENDS
*$
.SUBCKT AND2_P  IN1 IN2 OUT DPWR DGND 
+ PARAMS:    DLH = 0 DHL = 0
*
U1 AND(2) DPWR DGND IN1 IN2 OUT 
+ T_GATE IO_HC
.MODEL T_GATE UGATE( TPLHTY={DLH} TPHLTY={DHL} )
.ENDS
*$
.SUBCKT INVS  IN OUT DPWR DGND  
*
XU1     IN DGND OUT DGND   CompInvS 
+ PARAMS: RIN=1E12 VOUTH=5 VOUTL=0 VTHRES=2.5
.ENDS
*$
.SUBCKT AND2  IN1 IN2 OUT DPWR DGND 
+ PARAMS:    DLH = 0 DHL = 0
*
XU1     IN1 DGND IN2 DGND OUT DGND   Comp2INpAndSD  
+ PARAMS: RIN=1E12 ROUT=10 DELAY={(DLH + DHL)/2} VOUTH=5 VOUTL=0 VTHRES1=2.5 VTHRES2=2.5
.ENDS
*$
.SUBCKT ANDS2  IN1 IN2 OUT DPWR DGND 
*
XU1     IN1 DGND IN2 DGND OUT DGND   Comp2INpAndS
+ PARAMS: RIN=1E12 VOUTH=5 VOUTL=0 VTHRES1=2.5 VTHRES2=2.5
.ENDS
*$
.SUBCKT NOR2  IN1 IN2 OUT DPWR DGND 
+ PARAMS:    DLH = 0 DHL = 0
*
XU1     IN1 DGND IN2 DGND OUT DGND   Comp2INpNORSD 
+ PARAMS: RIN=1E12 ROUT=10 DELAY={(DLH + DHL)/2} VOUTH=5 VOUTL=0 VTHRES1=2.5 VTHRES2=2.5
.ENDS
*$
.SUBCKT NORS2  IN1 IN2 OUT DPWR DGND 
*
XU1     IN1 DGND IN2 DGND OUT DGND   Comp2INpNORS 
+ PARAMS: RIN=1E12 VOUTH=5 VOUTL=0 VTHRES1=2.5 VTHRES2=2.5
.ENDS
*$
.SUBCKT INV  IN OUT DPWR DGND  
+ PARAMS:    DLH = 0 DHL = 0
*
XU1     IN DGND OUT DGND   CompInvD 
+ PARAMS: RIN=1E12 ROUT=10 DELAYLH={DLH} DELAYHL={DHL} VOUTH=5 VOUTL=0 VTHRES=2.5
.ENDS
*$
.Subckt CompInvD IN1p IN1m OUT GND
+ PARAMS: RIN=1E12 ROUT=10 DELAYLH=10N DELAYHL=10N VOUTH=5 VOUTL=0 VTHRES=2.5
*
.PARAM RDEL = {IF ( (ROUT < 1), 1, ROUT ) } 
.PARAM COUT={(DELAYLH + DELAYHL)/2/(0.693*RDEL)}
.PARAM RDELHL = {DELAYHL/(0.693*COUT)}
.PARAM RDELLH = {DELAYLH/(0.693*COUT)}
RINp1 IN1p GND {RIN}
RINm1 IN1m GND {RIN}
EOUT OUT GND VALUE= { IF ( (V(IN1p,IN1m) > {VTHRES}), 
+ VOUTL + RDELHL*I(EOUT), VOUTH + RDELLH*I(EOUT) ) }
ROP OUT GND 1MEG
COUT OUT GND {COUT}
.Ends CompInvD
*$
* OPERATIONAL AMPLIFIER MACROMODEL SUBCIRCUIT
* CREATED 08/05/06
* (REV 1.3 26/02/08 )
.SUBCKT OPAMP  INp INm Vp Vm OUT
+ PARAMS: GAIN=200K RIN=2MEG RINC=1E9 ROUT=75 SLEWRATE=500K FPOLE1=5 FPOLE2=1MEG 
+         VDROPOH=1.9 VDROPOL=1.9 VOFFS=1M IBIAS=80N IOFFS=20N 
*
.PARAM PI = 3.141592
.PARAM IS = 1.0E-12
.PARAM N = 0.1
.PARAM VT = 0.02585
.PARAM IMAX = 100.0E-2
.PARAM C1 = {IMAX/SLEWRATE}
.PARAM R1 = {1/(2*PI*C1*FPOLE1)}
.PARAM GM1 = {GAIN/R1}
.PARAM R2 = 100
.PARAM G2 = {1/R2}
.PARAM GOUT = {1/ROUT}
.PARAM C2 = {1/(2*PI*R2*FPOLE2)}
.PARAM VDF = {N*VT*LOG(1 + IMAX/IS)}
*
IBIASM      INm 8  {IBIAS - IOFFS}
RINm      INm  8  {2*RINC}
RINp      INp  8  {2*RINC}
IBIAS       10 8   {IBIAS}
VOFFS       10 INp  {VOFFS}
EVp VpI 0 Vp 0 1
EVm VmI 0 Vm 0 1
VC          VpI 11  {VDROPOH + VDF}
VE          12 VmI  {VDROPOL + VDF}
RP          Vp Vm  15E3
ROUT        OUT 8  {ROUT}
GMO         8 OUT 9 8 {GOUT}
C2          9 8  {C2}
R2          9 8  {R2}
GM2         8 9 7 8 {G2}
RIN         INm 10  {RIN}
EGND        8  0  POLY(2) (Vp,0) (Vm,0) 0 .5 .5
D3         12 7  D_1
D2          7 11  D_1
C1          7 8  {C1}
R1          7 8  {R1}
GM1         8 7 VALUE = { LIMIT( GM1*V(10,INm), -IMAX, IMAX) }
.MODEL D_1 D( IS={IS} N = {N})
.ENDS
*$
* OPERATIONAL AMPLIFIER MACROMODEL SUBCIRCUIT
* CREATED 18/12/08
* (REV 1.0 18/12/08 )
.SUBCKT OPAMPGndRefS  INP INM GND OUT
+ PARAMS: GAIN=200K RIN=2MEG RINC=1E9 ROUT=75 SLEWRATE=500K FPOLE1=5 FPOLE2=1MEG 
+         VOUTH=10 VOUTL=0 VOFFS=1M IBIAS=80N IOFFS=20N VOUTOFFS=0
*
.PARAM PI = 3.141592
.PARAM COUT = {1/(2*PI*ROUT*FPOLE1)}
*
IBIASM      INM GND  {IBIAS - IOFFS}
RINM      INm  GND  {2*RINC}
RINP      INP  GND  {2*RINC}
IBIAS       10 GND   {IBIAS}
VOFFS       10 INP  {VOFFS}
RIN         INM 10  {RIN}
E1         OUT0 GND  VALUE = { LIMIT( GAIN*V(10,INM) + VOUTOFFS, VOUTH, VOUTL) }
ROUT       OUT0  OUT   {ROUT}
COUT        OUT GND  {COUT}
.ENDS
*$
* OPERATIONAL AMPLIFIER MACROMODEL SUBCIRCUIT
* CREATED 15/09/08
* (REV 1.0 15/09/08 )
.SUBCKT OPAMPGndRef  INp INm GND OUT
+ PARAMS: GAIN=200K RIN=2MEG RINC=1E9 ROUT=75 SLEWRATE=500K FPOLE1=5 FPOLE2=1MEG 
+         VOUTH=10 VOUTL=0 VOFFS=1M IBIAS=80N IOFFS=20N 
*
.PARAM Pi = 3.141592
.PARAM IS = 1.0E-12
.PARAM N = 0.1
.PARAM VT = 0.02585
.PARAM IMAX = 100.0E-2
.PARAM C1 = {IMAX/SLEWRATE}
.PARAM R1 = {1/(2*Pi*C1*FPOLE1)}
.PARAM GM1 = {GAIN/R1}
.PARAM R2 = 100
.PARAM G2 = {1/R2}
.PARAM GOUT = {1/ROUT}
.PARAM C2 = {1/(2*Pi*R2*FPOLE2)}
.PARAM VDF = {N*VT*LOG(1 + IMAX/IS)}
*
IBIASM      INm GND  {IBIAS - IOFFS}
RINm      INm  GND  {2*RINC}
RINp      INp  GND  {2*RINC}
IBIAS       10 GND   {IBIAS}
VOFFS       10 INp  {VOFFS}
VC          11 GND {VOUTH - VDF}
VE          GND 12  {VOUTL - VDF}
ROUT        OUT GND  {ROUT}
GMO         GND OUT  VALUE = {GOUT*V( 9,GND)}
C2          9 GND  {C2}
R2          9 GND  {R2}
GM2         GND 9 VALUE = {G2*V( 7,GND)}
RIN         INm 10  {RIN}
D3         12 7  D_1
D2          7 11  D_1
C1          7 GND  {C1}
R1          7 GND  {R1}
GM1         GND 7 VALUE = { LIMIT( GM1*V(10,INm), -IMAX, IMAX) }
.MODEL D_1 D( IS={IS} N = {N})
.ENDS
*$
.SUBCKT HYSTCOMP INp INm out gnd 
+ Params: Vthres=0 Vhyst=1 VoutH=5 VoutL=0 Rout=1 Tdel=1N
*
.Param Tdelay = {IF ( (Tdel < 1e-9) , 1e-9, Tdel ) } 
.Param Rdel = {IF ( (Tdelay > 1E-15) & (Rout < 1), 1, Rout ) } 
.Param VoutM={(VoutH+VoutL)/2}
.Param VthH={Vthres+Vhyst}
.Param VthL={Vthres-Vhyst}
.Param Cout={Tdelay/(0.693*(Rdel+1u))}
*
Ethr thr gnd Value= { IF ( V(out,gnd) < {VoutM}, {VthH}, {VthL}) }
Eout out gnd Value= { IF ( V(INp,INm) > V(thr,gnd), VoutH + Rdel*I(Eout) , VoutL + Rdel*I(Eout)  ) }
Cout out gnd {Cout}
Rpo out gnd 1Meg
.ENDS
*$
.SUBCKT HYSTCOMP_ INp INm out gnd 
+ Params: Vthres=0 Vhyst=1 VoutH=5 VoutL=0 Rout=1 Tdel=1N
*
.Param VoutM={(VoutH+VoutL)/2}
.Param VthH={Vthres+Vhyst}
.Param VthL={Vthres-Vhyst}
.Param Cout={Tdel/(0.693*Rout)}
*
Ethr thr gnd Value= { IF ( V(out,gnd) < {VoutM}, {VthH}, {VthL}) }
Eout 2 gnd Value= { IF ( V(INp,INm) > V(thr,gnd), VoutH, VoutL ) }
Rout 2 out {Rout}
Cout out gnd {Cout}
.ENDS
*$
* COMPARATOR MACROMODEL SUBCIRCUIT
* CREATED 15/09/08
* (REV 1.0 15/09/08 )
.SUBCKT COMPARATOR  INp INm Vp Vm OUT
+ PARAMS: GAIN=200K RIN=2MEG RINC=1E9 ROUT=75 SLEWRATE=500K DELAY=0
+         VDROPOH=1.9 VDROPOL=1.9 VOFFS=1M IBIAS=80N IOFFS=20N VTHRES=0 VHYST=0
*
.PARAM PI = 3.141592
.PARAM IS = 1.0E-12
.PARAM N = 0.1
.PARAM VT = 0.02585
.PARAM IMAX = 100.0E-2
.PARAM G_SRMULT = 10
.PARAM DEL = {IF ( DELAY < 1N, 1N, DELAY ) } 
.PARAM SR = { IF ( SLEWRATE < 1e-6, G_SRMULT*5/DEL, SLEWRATE ) }
.PARAM C1 = {IMAX/SR}
.PARAM G_PMULT = 100
.PARAM FPOLE1:= G_PMULT*SR/(2*pi*5)/Gain;
.PARAM R1 = {1/(2*PI*C1*FPOLE1)}
.PARAM GM1 = {GAIN/R1}
.PARAM GOUT = {1/ROUT}
.PARAM VDF = {N*VT*LOG(1 + IMAX/IS)}
.PARAM VDH = {VDROPOH - IMAX*DEL/C1}
.PARAM VDL = {VDROPOL + IMAX*DEL/C1}
.Param VthH={Vthres+Vhyst}
.Param VthL={Vthres-Vhyst}
*
IBIASM      INm 8  {IBIAS - IOFFS}
RINm      INm  8  {2*RINC}
RINp      INp  8  {2*RINC}
IBIAS       10 8   {IBIAS}
VOFFS       10 INp  {VOFFS}
EVp VpI 0 Vp 0 1
EVm VmI 0 Vm 0 1
VC          VpI 11  {VDH + VDF}
VE          12 VmI  {VDL + VDF}
RP          Vp Vm  15E3
ROUT        OUT 8  {ROUT}
GMO         8 OUT VALUE = {GOUT*LIMIT( V(7,8), V(VmI,0)+VDL, V(VpI,0)-VDH)}
RIN         INm 10  {RIN}
EGND        8  0  POLY(2) (Vp,0) (Vm,0) 0 .5 .5
D3         12 7  D_1
D2          7 11  D_1
C1          7 8  {C1}
R1          7 8  {R1}
Ethr thr 0 Value= { IF ( V(out,8) < V(8,0), {VthH}, {VthL}) }
GM1         8 7 VALUE = { LIMIT( GM1*(V(10,INm)-V(thr,0)), -IMAX, IMAX) }
.MODEL D_1 D( IS={IS} N = {N})
.ENDS
*$
* COMPARATOR MACROMODEL SUBCIRCUIT
* CREATED 15/09/08
* (REV 1.0 15/09/08 )
.SUBCKT COMPARGndRef  INp INm OUT GND
+ PARAMS: GAIN=200K RIN=2MEG RINC=1E9 ROUT=75 SLEWRATE=500K DELAY=0
+         VOUTH=5 VOUTL=0 VOFFS=1M IBIAS=80N IOFFS=20N VTHRES=0 VHYST=0
*
.PARAM PI = 3.141592
.PARAM IS = 1.0E-12
.PARAM N = 0.1
*.PARAM VT = 0.02585
.PARAM IMAX = 100.0E-2
.PARAM G_SRMULT = 10
.PARAM DEL = {IF ( DELAY < 1N, 1N, DELAY ) } 
.PARAM SR = { IF ( SLEWRATE < 1e-6, G_SRMULT*5/DEL, SLEWRATE ) }
.PARAM C1 = {IMAX/SR}
.PARAM G_PMULT = 100
.PARAM FPOLE1= {G_PMULT*SR/(2*PI*5)/GAIN};
.PARAM R1 = {1/(2*PI*C1*FPOLE1)}
.PARAM GM1 = {GAIN/R1}
.PARAM GOUT = {1/ROUT}
.PARAM VDF = {N*VT*LOG(1 + IMAX/IS)}
.PARAM VOH = {VOUTH + IMAX*DEL/C1}
.PARAM VOL = {VOUTL - IMAX*DEL/C1}
.Param VthH={Vthres+Vhyst}
.Param VthL={Vthres-Vhyst}
.Param VoutM={(VoutH+VoutL)/2}
*
IBIASM      INm GND  {IBIAS - IOFFS}
RINm      INm  GND  {2*RINC}
RINp      INp  GND  {2*RINC}
IBIAS       10 GND   {IBIAS}
VOFFS       10 INp  {VOFFS}
VC          11 GND {VOH - VDF}
VE          12 GND  {VOL + VDF}
ROUT        OUT GND  {ROUT}
GMO         GND OUT VALUE = {GOUT*LIMIT( V(7,GND), VOUTL, VOUTH)}
RIN         INm 10  {RIN}
D3         12 7  D_1
D2          7 11  D_1
C1          7 GND  {C1}
R1          7 GND  {R1}
Ethr        thr 0 Value= { IF ( V(out,GND) < {VoutM}, {VthH}, {VthL}) }
GM1         GND 7 VALUE = { LIMIT( GM1*(V(10,INm)-V(thr,0)), -IMAX, IMAX) }
.MODEL D_1 D( IS={IS} N = {N})
.ENDS
*$
********************************************
**        Oscillator SW                   **
**  This file was created by TINA         **
**    (c) 2008 DesignSoft, Inc.           **
********************************************
.SUBCKT Oscillator EN Gnd SYNC CLK TCmul
R_R2         SYNCSW N11144571  1k TC=0,0 
E_ABM4         N11145109 0 VALUE { if( V(EN)>2.5,  
+ V(SYNC),0)   }
C_C9         HIGH_PASS N11145109  1n IC=0 
X_U241         SYNCSW N111894051 INVERTER
G_ABMII1         V5 RAMP_2US VALUE { if(V(EN)>2.5,1u,0)    }
E_ABM8         SW2EXT 0 VALUE { if( V(RAMP13US)>1 &  
+ V(EN)>1, 5,0)   }
C_C7         GND N11144571  8p IC=0 
R_R1         N111445371 N11144627  1k TC=0,0 
R_R22         GND RAMP13US  10Meg  
R_R6         HIGH_PASS GND  1k TC=0,0 
X_U188         SW2EXT SW2EXTCLK BUFFER_PS PARAMS: VHI=5 VLO=0 VTHRESH=2.5
+  TPLH=2E-9 TPHL=6u TR=1E-9 TF=1E-9
X_U242         SYNCSW N11189487 N11200951 AND_o
E_ABS1         HI_PAS_ABS 0 VALUE {ABS(V(HIGH_PASS))}
V_V5         V5 GND 5
R_R24         GND EN  10e6  
C_C4         GND N11144627  2p IC=0 
R_R3         N111894051 N11189487  1k TC=0,0 
V_V4         N11146724 GND  
+PULSE 0 5 0 1n 1n 40n 1.666u
R_R23         GND RAMP_2US  10e6  
X_U240         N11145109 HI_PAS_ABS SYNCSW AND_o
X_U211         TCRAMP V5 DIDEAL_PS PARAMS: RON=1E-3 ROFF=1E9
X_S2    EXTCLK_FOUNDZ GND RAMP13US GND OSCILLATOR_new_S2 
E_ABM10         EXTCLK_FOUNDZ 0 VALUE { if(V(RAMP_2US)>1,1,0)    }
C_C8         GND N11189487  7p IC=0 
E_E1         N11190968 GND TCRAMP GND 1.0
X_U210         RAMP_2US V5 DIDEAL_PS PARAMS: RON=1E-3 ROFF=1E9
E_ABM9         TCMUL 0 VALUE { if( V(SW2EXT)>1,  
+ LIMIT(V(TCRATIO),0.86,1.25), 1)   }
G_ABMII2         V5 TCRAMP VALUE { if(V(SW2EXTCLK)>2.5,1.07u,0)    }
C_C3         GND RAMP_2US  2p IC=0 
G_ABM2I2         V5 RAMP13US VALUE { if(V(EN) >2.5 & V(EXTCLK_FOUNDZ)<0.5,1u,0)
+     }
R_R25         GND TCRAMP  10e6  
E_ABM5         INTCLK1 0 VALUE { if( V(EN)>0.5,  
+ V(N11146724),0)   }
C_C5         GND RAMP13US  13p IC=0 
X_U209         RAMP13US V5 DIDEAL_PS PARAMS: RON=1E-3 ROFF=1E9
X_S1    RES GND RAMP_2US GND OSCILLATOR_new_S1 
R_R26         GND RES  10e6  
C_C6         GND TCRAMP  1.666p IC=0 
C_C10         GND TCRATIO  1m IC=0 
X_S4    SAMPLE GND N11190968 TCRATIO OSCILLATOR_new_S4 
X_U4         RES SYNCCLK one_shot PARAMS: T=40
X_U239         N11144571 N111445371 INVERTER
X_S3    RES GND TCRAMP GND OSCILLATOR_new_S3 
X_U238         N11144571 N11144627 RES AND_o
X_U243         N11200951 SW2EXTCLK SAMPLE AND_o
E_ABM3         CLK 0 VALUE { if( V(SW2EXTCLK)>2.5,  
+ V(SYNCCLK),  
+ V(INTCLK1) )   }
.ends
*$
.subckt OSCILLATOR_new_S2 1 2 3 4  
S_S2         3 4 1 2 _S2
RS_S2         1 2 1G
.MODEL         _S2 VSWITCH Roff=1e11 Ron=1.0m Voff=0 Von=1
.ends OSCILLATOR_new_S2
*$
.subckt OSCILLATOR_new_S1 1 2 3 4  
S_S1         3 4 1 2 _S1
RS_S1         1 2 1G
.MODEL         _S1 VSWITCH Roff=1e11 Ron=1.0m Voff=2 Von=3
.ends OSCILLATOR_new_S1
*$
.subckt OSCILLATOR_new_S4 1 2 3 4  
S_S4         3 4 1 2 _S4
RS_S4         1 2 1G
.MODEL         _S4 VSWITCH Roff=1e11 Ron=1.0n Voff=2 Von=3
.ends OSCILLATOR_new_S4
*$
.subckt OSCILLATOR_new_S3 1 2 3 4  
S_S3         3 4 1 2 _S3
RS_S3         1 2 1G
.MODEL         _S3 VSWITCH Roff=1e11 Ron=1.0m Voff=2 Von=3
.ends OSCILLATOR_new_S3
*$
.subckt one_shot in out
+ params:  t=100
s_s1         meas 0 reset2 0 s1
e_abm1         ch 0 value { if( v(in)>0.5 | v(out)>2.5,1,0)    }
r_r2         reset2 reset  0.1  
e_abm3         out 0 value { if( v(meas)<0.5 & v(ch)>0.5,5,0)    }
r_r1         meas ch  {t} 
c_c2         0 reset2  1.4427n  
c_c1         0 meas  1.4427n  
e_abm2         reset 0 value { if(v(ch)<0.5,1,0)    }
.model s1 vswitch
+ roff=1e+009
+ ron=1
+ voff=0.25
+ von=0.75
.ends one_shot
*$
.SUBCKT BUFFER_PS A Y PARAMS: vhi=1 vlo=0 vthresh=500e-3
+ tplh=1e-9 tphl=1e-9 tr=1e-9 tf=1e-9
RA A 0 1e11
CA A 0 0.01pF
VS VSUP 0 DC 1
EBUF1 Ypp 0 VALUE={IF(V(A) > ({vthresh}), 1, 0)}
ROUTpp Ypp 0 1e11
XNSW1 OUTp Ypp 0 NSW_PS PARAMS: RONval={(tplh+1e-15)
+/(1e-12*0.693)} VTHval=0.5
XPSW1 OUTp Ypp VSUP PSW_PS PARAMS: RONval={(tphl+1e-15)/
+(1e-12*0.693)} VTHval=0.5
CDEL1 OUTp 0 1pF
ETHRESH Yp 0 VALUE={IF(V(OUTp) > 0.5, 1, 0)}
ROUTp Yp 0 1e11
XNSW2 OUTr Yp 0 NSW_PS PARAMS: RONval={(tf+1e-15)
+/(1e-12*2.3)} VTHval=0.5
XPSW2 OUTr Yp VSUP PSW_PS PARAMS: RONval={(tr+1e-15)
+/(1e-12*2.3)} VTHval=0.5
CDEL2 OUTr 0 1pF
EOUT OUTf 0 VALUE={V(OUTr)*({vhi} - {vlo})+{vlo}}
RDR OUTf Y  1000
RO Y 0 1e11
.ENDS BUFFER_PS
*$
.SUBCKT NSW_PS D G S PARAMS: RONval=10k VTHval=0.7
+ VCHARval=0.01 CGval=0.01pF CDval=0.01pF 
RDDUM D 0 1e11
RSDUM S 0 1e11
RGDUM G 0 1e11
CG G D {CGval}
CD D S {CDval}
EEXP F1 0 VALUE={LIMIT(((V(G,S)-VTHval)/VCHARval),-80,80)} 
+/2+(-V(F1)**3)/6+(-V(F1)**4)/24))}
GOUT D S VALUE={V(D,S)/(RONval*(1+EXP(-LIMIT(((V(G,S)
+-VTHval)/VCHARval),-80,80))))}
.ENDS NSW_PS
*$
.SUBCKT PSW_PS D G S PARAMS: RONval=10k VTHval=0.7
+ VCHARval=0.01 CGval=0.01pF CDval=0.01pF 
RDDUM D 0 1e11
RSDUM S 0 1e11
RGDUM G 0 1e11
CG G D {CGval}
CD D S {CDval}
EEXP F1 0 VALUE={LIMIT(((V(S,G)-VTHval)/VCHARval),-80,80)} 
GOUT S D VALUE={V(S,D)/(RONval*(1+EXP(-LIMIT(((V(S,G)
+ -VTHval)/VCHARval),-80,80))))}
.ENDS PSW_PS
*$
.SUBCKT INVERTER IN OUT  
EOUT OUT1 0 VALUE { IF( V(IN)<2.5,5,0) }
R1 OUT1 OUT 1
C1 OUT 0 10p
.ends INVERTER
*$
.SUBCKT AND_o A B OUT  
EOUT OUT1 0 VALUE { IF( V(A)>2.5 & V(B)>2.5,5,0) }
R1 OUT1 OUT 1
C1 OUT 0 10p
.ends AND_o
*$
.SUBCKT DIDEAL_PS A C PARAMS: ron=1e-3 roff=1e9 
RA A 0 1e11
RC C 0 1e11
RAC A C 1e11
CAC A C 0.01pF ic=0
GD A B VALUE={LIMIT((0.01/{roff})*EXP((V(A,B)- 0.01)*{5}/0.026),40,-10u)}
RB B 0 1e9
RD B C {ron}
CC C 0 0.1pF 
.ENDS DIDEAL_PS
*$
*
* CONNECTIONS:   A
*                             |    C
*                             |    |
.SUBCKT D_D       1    2
D1 1 2  DD
.MODEL DD D( IS=1P N=1.0)
.ENDS D_D
*$
.Subckt INDM1 1 2 Params: L=10u ESR=1M 
* L = Inductance ESR = Equivalent Series Resistance
L  1 11 {L}   
RS 11 2 {ESR}
.Ends
*$
