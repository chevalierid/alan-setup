* OPA653
*****************************************************************************
* (C) Copyright 2009 Texas Instruments Incorporated. All rights reserved.
** This model is designed as an aid for customers of Texas Instruments.    
** TI and its licensors and suppliers make no warranties, either expressed  
** or implied, with respect to this model, including the warranties of     
** merchantability or fitness for a particular purpose. The model is   
** provided solely on an "as is" basis. The entire risk as to its quality          
** and performance is with the customer 
*
*****************************************************************************
*
*  Header modified on 2009-05-13 
*
*****************************************************************************
*
* Released by: Analog eLab Design Center, Texas Instruments Inc.
* Part:       OPA653
* Date:       2009-04-03
* Model Type: PSpice
* Simulator:  PSpice 16.0.0.p001
* Datasheet:  DECEMBER 2008
*
*****************************************************************************
*  
* OPA653 SUBCIRCUIT 
* Wideband, Fixed Gain, JFET-Input AMPLIFIER
* THIS MODEL SIMULATES TYPICAL VALUES FOR THE FOLLOWING:
* SETTLING TIME, OUTPUT VOLTAGE LIMIT, INPUT VOLTAGE NOISE, INPUT BIAS CURRENT,
* TYPICAL VALUES FOR INPUT OFFSET VOLTAGE AND OFFSET BIAS CURRENT,
* CURRENT DRAW FROM POWER SUPPLY, OUTPUT IMPEDANCE AND LOADING EFFECTS
* BANDWIDTH IS RIGHT IN GAINS OF +2V/V 
* SLEW RATE IS CLOSE TO WHAT WAS REPORTED ON THE DATA SHEET
* THE FLAT BAND IS ALSO RIGHT
* THIS MODEL WILL NOT PROVIDE ACCURATE SIMULATION OF: 
* CMRR AND PSRR, INPUT CURRENT NOISE, DISTORTION, INPUT OFFSET, OPEN LOOP GAIN
*$
.SUBCKT OPA653 IN+ IN- Vcc+ Vcc- OUT 
Rf   IN1-    OUT     160
Rg   IN1-    IN-     160
*
*INPUT*
J1   VPLUS   IN+     NET0190  NFET 12
J0   VPLUS   IN1-    NET078   NFET 12
Rpz  NET0190 NETM    90
Cpz  NETM    NET078  0.55p
Q9   VMINUS  NET0190 NET0141  PNP 2
Q10  VMINUS  NET078  NET114   PNP 2
Q11  VPLUS   NET078  NET150   NPN 2
Q12  VPLUS   NET0190 NET0116  NPN 2
Q22  NET76   NET0116 NET0117  PNP 2
Q19  NET79   NET0141 NET0117  NPN 2
Q20  NET077  NET114  NET0120  NPN 2
Q21  NET091  NET150  NET0120  PNP 2
R77  NET0117 NET0120 90
C77  NET0117 NET0120 0.2p
F0   NET072  NET114  VCTRL  1
F1   NET150  NET090  VCTRL  1
F2   NET0116 NET061  VCTRL  1
F3   NET085  NET0141 VCTRL  1
F5   NET078  NET0114 VCTRL  1.25
F6   NET0190 NET0112 VCTRL  1.25
RCS1 VPLUS   NET072 150
RCS2 NET090  VMINUS 150
RCS3 VPLUS   NET085 150
RCS4 NET061  VMINUS 150
RCS5 NET0114 VMINUS 150
RCS6 NET0112 VMINUS 150
*
* HIGH Z NODE -Wilson CM
Q23   NET074  NET140  NET79   PNP 2
Q24   NET140  NET140  NET077  PNP 2
Q24W  HIZA    NET074  NET140  PNP 2
Q15   NET0183 NET137  NET76   NPN 2
Q14   NET137  NET137  NET091  NPN 2
Q14W  HIZA    NET0183 NET137  NPN 2
F4    NET074  NET082  VCTRL  1
F7    NET068  NET0183 VCTRL  1
R24   VPLUS   NET068 150
R21   NET082  VMINUS 150
RTOP1 NET79   VPLUS  60
RTOP2 NET077  VPLUS  80
RBOT1 VMINUS  NET76  60
RBOT2 VMINUS  NET091 80
Cc    HIZA    0 1.8e-12
*
E0    HIZB 0 HIZA 0 1
*LFREQS   HIZA  77 1.5e-9
*RFREQS   71   77 25
*CFREQS   71   0  3.5e-12
*
Q14B  NET125 NET137 NET091 NPN 2
Q24B  NET100 NET140 NET077 PNP 2
RTOP3 VPLUS  NET077 80
RBOT3 NET091 VMINUS 80
* 
QEF1  VMINUS HIZB   NET100 PNP 2.5
QEF2  VPLUS  HIZB   NET125 NPN 2.5
QOUT1 VPLUS  NET100 NET098 NPN 10
QOUT2 VMINUS NET125 NET095 PNP 10
RBAL2 NETC   NET098 1
RBAL1 NETC   NET095 1
*
* COMPLEX OUTPUT IMPEDANCE *
RO2   NETC 0139 5
RO1   NETC 0143 5
CO1   0143 OUT 3p
LO1   0139 OUT 1n
*
VA    Vcc+    VPLUS DC 150e-3
VB    VMINUS  Vcc-  DC 150e-3
RBB   VPLUS   NET0204 2e3
Ibias NET0204 NET0205 DC 1.75e-3
VCTRL NET0205 VMINUS  DC 0
*
* MODELS *
.MODEL NFET NJF(VTO=-2 BETA=100E-6 IS=100E-16 RS=25 RD=25 AF=1 KF=4.0000E-17)

.MODEL NPN NPN AF=1 BF=100 BR=1 CJC=0 CJE=0 CJS=0 EG=1.11 FC=0.5 GAMMA=1E-11
+IKF=1E9 IKR=1E9 IRB=1E9 IS=1E-16 ISC=0 ISE=0 ISS=0 ITF=0  MJC=0.33
+MJE=0.33 NC= 2 NE=1.5 NF=1 NK=0.5 NR=1 PTF=0 QCO=0 
+RB=35 RBM=35 RC=50 RCO=0 RE=0 KF=1.35e-14
+TF=0 TR=0 VAF=1E9 VAR=1E9 VJC=0.75 VJE=0.75 VO=10 VTF=1E9 
+XCJC=1 XCJC2=1 XTB=0 XTF=0 XTI=3

.MODEL PNP PNP AF=1 BF=100 BR=1 CJC=0 CJE=0 CJS=0 EG=1.11 FC=0.5 GAMMA=1E-11
+IKF=1E9 IKR=1E9 IRB=1E9 IS=1E-16 ISC=0 ISE=0 ISS=0 ITF=0  MJC=0.33
+MJE=0.33 NC= 2 NE=1.5 NF=1 NK=0.5 NR=1 PTF=0 QCO=0 
+RB=35 RBM=35 RC=50 RCO=0 RE=0 KF=1.35e-14
+TF=0 TR=0 VAF=1E9 VAR=1E9 VJC=0.75 VJE=0.75 VO=10 VTF=1E9 
+XCJC=1 XCJC2=1 XTB=0 XTF=0 XTI=3


.ENDS OPA653
*$
