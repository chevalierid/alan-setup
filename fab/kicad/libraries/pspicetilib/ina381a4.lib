* INA381A4
*****************************************************************************
* (C) Copyright 2018 Texas Instruments Incorporated. All rights reserved.                                            
*****************************************************************************
** This model is designed as an aid for customers of Texas Instruments.
** TI and its licensors and suppliers make no warranties, either expressed
** or implied, with respect to this model, including the warranties of 
** merchantability or fitness for a particular purpose.  The model is
** provided solely on an "as is" basis.  The entire risk as to its quality
** and performance is with the customer
*****************************************************************************
*
* This model is subject to change without notice. Texas Instruments
* Incorporated is not responsible for updating this model.
*
*****************************************************************************
*
** Released by: Texas Instruments Inc.
* Part: INA381A4
* Date: 20MAR2018
* Model Type: All in one
* Simulator: Cadence PSpice
* Simulator Version: 16.6
* EVM Order Number: NA
* EVM Users Guide: NA
* Datasheet: SBOS848B - October 2019
* Topologies Supported: NA
*
* Model Version: Final 1.0
*
*****************************************************************************
*
* Updates:
*
* Final 1.00
* Release to Web.
*
*****************************************************************************
*
* Model Usage Notes:
* 1. The following features have been modeled with a 5V power supply:
*    a. Gain, BW, IIB, VOS, IQ, comparator threshold & hysterisis, noise   
*    b. CMRR, PSRR, VOUT versus IOUT
*
* 2. Temperature effects are not modeled.
* 3. Device behavior over the full power supply range has not been modelled 
*
*****************************************************************************
*
*
*
.SUBCKT INA381A4 INP INN CMPIN OUT ALERT RESET CMPREF VS GND
C_C3         GND S3_IN  1n  TC=0,0 
E_U1_E2         U1_N609752 0 GND 0 -1
X_U1_U40         VS GND U1_INPUT_VCLAMP U1_INPUT_ZOUT U1_VIMON U1_EN U1_GNDF
+  VOUTVSIOUT_NROS_SHDN_INA381A4 
E_U1_E11         U1_INP_BUF_OUT U1_GNDF INP U1_GNDF 1
X_U1_U12         U1_INPUT_TF U1_INPUT_VCLAMP VS GND U1_EN U1_GNDF TF_INA381A4
X_U1_U39         U1_OUT_CNTRL U1_VICM VS GND U1_GNDF CONTROL_INA381A4 
C_U1_C3         U1_N337909 0  1  TC=0,0 
E_U1_E12         U1_INN_BUF_OUT U1_GNDF U1_N409527 U1_GNDF 1
X_U1_U38         VS GND U1_INP_PSRR_IN U1_INP_PSRR_OUT U1_GNDF PSRR_INA381A4 
R_U1_R23         U1_OUT_CNTRL U1_N698091  1m TC=0,0 
X_U1_U31         U1_INPUT_VIMON OUT U1_VIMON U1_GNDF AMETER_INA381A4 
X_U1_U18         INP U1_GNDF VS GND U1_VICM U1_GNDF IIBP_INA381A4
D_U1_D1         U1_IN_COMP U1_N698091 Dideal
V_U1_V9         U1_COMP_REF U1_GNDF 0.7Vdc
R_U1_R22         U1_OUT_CNTRL U1_IN_COMP  1000 TC=0,0 
X_U1_U5         U1_VICM INP INN U1_GNDF VICM_INA381A4
R_U1_U41_R13         U1_U41_N176193 0  40 TC=0,0 
R_U1_U41_R7         0 U1_U41_Z1  1k TC=0,0  
R_U1_U41_R9         U1_U41_N167762 0  40 TC=0,0 
E_U1_U41_E6         U1_U41_N168106 0 U1_U41_AOL_ZO_IN 0 4e5
E_U1_U41_E8         U1_U41_N167838 0 U1_U41_AOL_ZO_IN 0 4e5
R_U1_U41_R10         0 U1_INPUT_VIMON  2k TC=0,0 
R_U1_U41_R5         0 U1_U41_N_SUM  1 TC=0,0 
G_U1_U41_G5         U1_U41_N_SUM 0 U1_U41_P1 0 -1
E_U1_U41_E7         U1_U41_N167666 0 U1_U41_N_SUM 0 1
E_U1_U41_E9         U1_U41_N176109 0 U1_U41_P2 0 1
E_U1_U41_E3         U1_U41_AOL_ZO_IN 0 U1_INPUT_ZOUT U1_INPUT_VIMON 1
C_U1_U41_C6         0 U1_U41_P1  80.0  TC=0,0 
R_U1_U41_R6         U1_U41_OUT_E4_ZO U1_INPUT_VIMON  20k TC=0,0 
R_U1_U41_R11         U1_U41_P1 U1_U41_N167838  100k TC=0,0 
E_U1_U41_E4         U1_U41_OUT_E4_ZO 0 U1_U41_P3 0 1
R_U1_U41_R8         U1_U41_P2 U1_U41_N167666  5k TC=0,0 
G_U1_U41_G3         U1_U41_N_SUM 0 U1_U41_Z1 0 -1
E_U1_U41_E5         U1_U41_N168528 0 U1_U41_AOL_ZO_IN 0 100
R_U1_U41_R12         U1_U41_P3 U1_U41_N176109  5k TC=0,0  
C_U1_U41_C5         U1_U41_N167762 U1_U41_P2  30n  TC=0,0 
C_U1_U41_C7         U1_U41_N176193 U1_U41_P3  30n  TC=0,0 
G_U1_U41_G4         U1_U41_N_SUM 0 U1_U41_N168528 0 -1
C_U1_U41_C4         U1_U41_N168106 U1_U41_Z1  2u  TC=0,0 
E_U1_E15         U1_D_EN1 0 U1_INPUTP_GBW U1_INPUTN_GBW 1
E_U1_E13         U1_INP_BUF_OUT U1_INP_CMRR U1_VICM U1_GNDF 0
R_U1_R16         U1_GNDF INP  1e9 TC=0,0 
R_U1_R25         U1_INPUTN_GBW OUT  500k TC=0,0 
R_U1_R26         U1_INPUTP_GBW GND  500k TC=0,0 
E_U1_E14         U1_INN_BUF_OUT U1_INN_CMRR U1_VICM U1_GNDF 0
V_U1_V8         INN U1_N409527 37.5uVdc
C_U1_C2         U1_N337676 0  1  TC=0,0 
X_U1_U17         INN U1_GNDF VS GND U1_VICM U1_GNDF IIBN_INA381A4
X_U1_U45         U1_INPUT_OUTP U1_INPUTP_GBW VNSE_INA381A4
R_U1_R4         U1_INN_CMRR U1_INPUTN_GBW  2.5k TC=0,0 
E_U1_E16         U1_D_EN2 0 U1_INP_PSRR_OUT U1_INN_CMRR 1
X_U1_U44         U1_EN U1_IN_COMP U1_COMP_REF U1_GNDF COMPARATOR_CONTROL_INA381A4 
X_U1_U43         VS GND U1_VICM U1_EN U1_VIMON U1_GNDF IQ_VICM_INA381A4 
R_U1_R21         U1_N609752 U1_N337909  1 TC=0,0 
R_U1_R3         U1_INP_PSRR_OUT U1_INPUT_OUTP  2.5k TC=0,0 
X_U1_U36         U1_VICM U1_INP_CMRR U1_INP_PSRR_IN U1_GNDF CMRR_INA381A4 
C_U1_C1         U1_N337909 U1_N337676  1  TC=0,0 
E_U1_E3         U1_GNDF 0 U1_N337676 U1_N337909 0.5
R_U1_R17         INN U1_GNDF  1e9 TC=0,0 
R_U1_R20         U1_N609492 U1_N337676  1 TC=0,0 
C_U1_C4         U1_GNDF U1_IN_COMP  3n  TC=0,0 
E_U1_E1         U1_N609492 0 VS 0 1
R_U1_R24         U1_GNDF U1_EN  1k TC=0,0 
X_U1_U41         U1_INPUTP_GBW U1_INPUTN_GBW U1_INPUT_TF U1_EN VS GND U1_GNDF
+  INA381A4_gbw_slew_outlimits
E_E1         CMP_BUFFERED_IN GND CMPREF GND 1 
R_U2_R2         CMPIN U2_N03457  12.5e3 TC=0,0 
G_U2_1          GND U2_N03457 COMP_OUT GND 0.8u
X_U2_U2         COMP_OUT U2_N03457 U2_N03609 VS GND COMPARATOR_ALERT_INA381A4 
V_U2_V1         CMP_BUFFERED_IN U2_N03609 1.0e-3
X_U7         RESET VS ALERT CNTRL1_OUT VS GND LATCH_CONTROL_INA381A4 
R_R2         COMP_OUT S1_IN  10k TC=0,0 
X_S1    S1_IN GND ALERT GND DEV_A1_SCH_S1_INA381A4 
C_C1         0 S1_IN  160p  TC=0,0 
X_S3    S3_IN GND VS S1_IN DEV_A1_SCH_S3_INA381A4 
R_R7         S3_IN CNTRL1_OUT  20k TC=0,0 
.MODEL Dideal D 
.ENDS
*$
*
.SUBCKT DEV_A1_SCH_S1_INA381A4 1 2 3 4  
S_S1         3 4 1 2 _S1
RS_S1         1 2 1G
.MODEL         _S1 VSWITCH Roff=1e6 Ron=1.0 Voff=1.75V Von=2.0V
.ENDS
*$
*
.SUBCKT DEV_A1_SCH_S3_INA381A4 1 2 3 4  
S_S3         3 4 1 2 _S3
RS_S3         1 2 1G
.MODEL         _S3 VSWITCH Roff=100e6 Ron=1.0 Voff=1V Von=2V
.ENDS
*$
*
.SUBCKT VOUTvsIOUT_NROS_SHDN_INA381A4  VCC  VEE  VI  VO VIMON SHDN GNDF
.PARAM ISC_POS = 0.02
.PARAM ISC_NEG = -0.052
.PARAM IMAXLIN_POS = 0.018
.PARAM IMAXLIN_NEG = -0.048
.PARAM VOUTMAXLIN = 3.8
.PARAM VOUTMINLIN = 1.5
.PARAM VHR_VCC = -0.02
.PARAM VHR_VEE = 0.0005
.PARAM VCC_DATASHEET = 5
.PARAM VEE_DATASHEET = 0
.PARAM VMID_DATASHEET = {(VCC_DATASHEET + VEE_DATASHEET)/2}
.PARAM VHR_MAX_LINEAR = {VOUTMAXLIN - VCC_DATASHEET}
.PARAM VHR_MIN_LINEAR = {VOUTMINLIN  - VEE_DATASHEET}
ER1 NR1 GNDF VALUE = {(VOUTMAXLIN - (VCC_DATASHEET + VHR_VCC))/IMAXLIN_POS}
ER2 NR2 GNDF VALUE = {(VOUTMINLIN - (VEE_DATASHEET + VHR_VEE))/IMAXLIN_NEG}
EVOUT1pos NVOUT1_MAX GNDF VALUE = {(V(VCC,GNDF) + VHR_VCC) + V(NR1,GNDF)*V(VIMON,GNDF)}
EVOUT1neg NVOUT1_MIN GNDF VALUE = {(V(VEE,GNDF) + VHR_VEE) + V(NR2,GNDF)*V(VIMON,GNDF)}
EM1 NM1 GNDF VALUE = {(VOUTMAXLIN - VMID_DATASHEET)/(IMAXLIN_POS - ISC_POS)}
EM2 NM2 GNDF VALUE = {(VOUTMINLIN - VMID_DATASHEET)/(IMAXLIN_NEG - ISC_NEG)}
EVMID NVMID GNDF VALUE = {(V(VCC,GNDF) + V(VEE,GNDF))/2}
E1 N1 GNDF VALUE = {V(VCC,GNDF) + VHR_MAX_LINEAR}
E2 N2 GNDF VALUE = {V(VEE,GNDF) + VHR_MIN_LINEAR}
EV1 NV1 GNDF VALUE = {V(NVMID,GNDF) + (V(N1,GNDF) - V(NVMID,GNDF))/(1 - IMAXLIN_POS/ISC_POS)}
EV2 NV2 GNDF VALUE = {V(NVMID,GNDF) + (V(N2,GNDF) - V(NVMID,GNDF))/(1 - IMAXLIN_NEG/ISC_NEG)}
EVOUT2pos NVOUT2_MAX GNDF VALUE = {V(NV1,GNDF) + V(VIMON,GNDF)*V(NM1,GNDF)}
EVOUT2neg NVOUT2_MIN GNDF VALUE = {V(NV2,GNDF) + V(VIMON,GNDF)*V(NM2,GNDF)}
EHRPOS HRPOS GNDF VALUE = {MIN(V(NVOUT1_MAX,GNDF),V(NVOUT2_MAX,GNDF))}
EHRNEG HRNEG GNDF VALUE = {MAX(V(NVOUT1_MIN,GNDF),V(NVOUT2_MIN,GNDF))}
ECLAMP  VO GNDF VALUE = {V(SHDN,GNDF)*MAX(MIN(V(VI,GNDF), V(HRPOS,GNDF)), V(HRNEG,GNDF))}
.ENDS
*$
*
.SUBCKT TF_INA381A4  VI  VO  VCC VEE SHDN GNDF
.PARAM fz1 = 10G 
.PARAM fz2 = 10G 
.PARAM fz3 = 10G 
.PARAM fz4 = 10G 
.PARAM fz5 = 10G
.PARAM fp1 = 10e6 
.PARAM fp2 = 10G 
.PARAM fp3 = 10G 
.PARAM fp4 = 10G
.PARAM Gm = 1M
.PARAM Ro = {1/Gm}
.PARAM PI = 3.141592
.PARAM gL = 1M
Gp1  GNDF Vp1 VI GNDF {Gm}
Rp1  Vp1 GNDF {Ro}
Cp1  Vp1 GNDF {1/(2*PI*Ro*fp1)} IC = 0
Gp2  GNDF Vp2 Vp1 GNDF {Gm}
Rp2  Vp2 GNDF {Ro}
Cp2  Vp2 GNDF {1/(2*PI*Ro*fp2)} IC = 0
Gp3  GNDF Vp3 Vp2 GNDF {Gm}
Rp3  Vp3 GNDF {Ro}
Cp3  Vp3 GNDF {1/(2*PI*Ro*fp3)} IC = 0
Gp4  GNDF VO Vp3 GNDF {Gm}
Rp4  VO GNDF {Ro}
Cp4  VO GNDF {1/(2*PI*Ro*fp4)} IC = 0
.ENDS
*$
*
.SUBCKT CONTROL_INA381A4 OUT_CNTRL IN VCC VEE GNDF
.PARAM VCCMAX = 6.0
.PARAM VCCMIN = 2.69
.PARAM VICMMAX = 26.01
.PARAM VICMMIN = -0.199
EN1 N1 0 VALUE = {SGN(VCCMAX - V(VCC,VEE)) - 1 + ABS(SGN(VCCMAX - V(VCC,VEE)))}
EN2 N2 0 VALUE = {SGN(V(VCC,VEE)-VCCMIN) - 1 + ABS(SGN(V(VCC,VEE)-VCCMIN))}
EVCCMAX NVCCMAX 0 VALUE = {0.5*(1 + V(N1))}
EVCCMIN NVCCMIN 0 VALUE = {0.5*(1 + V(N2))}
EN3 N3 0 VALUE = {SGN(VICMMAX - V(IN)) - 1 + ABS(SGN(VICMMAX - V(IN)))}
EN4 N4 0 VALUE = {SGN(V(IN)-VICMMIN) - 1 + ABS(SGN(V(IN)-VICMMIN))}
EVICMMAX NVICMMAX 0 VALUE = {0.5*(1 + V(N3))}
EVICMMIN NVICMMIN 0 VALUE = {0.5*(1 + V(N4))}
EOUT OUT_CNTRL GNDF VALUE = {V(NVCCMAX)*V(NVCCMIN)*V(NVICMMAX)*V(NVICMMIN)}
.ENDS
*$
*
.SUBCKT PSRR_INA381A4  VDD  VSS  VI  VO  GNDF 
.PARAM PSRR = 105
.PARAM fpsrr = 1300
.PARAM PI = 3.141592
.PARAM RPSRR = 1
.PARAM GPSRR = {PWR(10,-PSRR/20)/RPSRR}
.PARAM LPSRR = {RPSRR/(2*PI*fpsrr)}
G1  GNDF 1 VDD VSS {GPSRR}
R1  1 2 {RPSRR}
L1  2 GNDF {LPSRR} 
G2  GNDF 3 1 GNDF 1
R2  3 4 {RPSRR}
L2  4 GNDF {0.09*LPSRR}
E1  VO VI 3 GNDF 1
.ENDS
*$
*
.SUBCKT AMETER_INA381A4  VI  VO VIMON GNDF
.PARAM GAIN = 1
VSENSE VI VO DC = 0
EMETER VIMON GNDF VALUE = {I(VSENSE)*GAIN}
.ENDS
*$
*
.SUBCKT IIBP_INA381A4 OUT IN VCC VEE INP GNDF
.PARAM VCMBRK = 5
.PARAM REQ_VCC_NEZERO = 450k
.PARAM REQ_VCC_EQZERO = 530k
EVCCTEST NVCCTEST 0 VALUE = {SGN(V(VCC,VEE)) - 1 + ABS(SGN(V(VCC,VEE)))}
E1ACT N1SGN 0 VALUE = {SGN(V(INP)-VCMBRK) - 1 + ABS(SGN(V(INP)-VCMBRK))}
GOUT2 OUT IN VALUE = {V(INP,GNDF)*(1/REQ_VCC_NEZERO 
+ + 0.5*(1-V(NVCCTEST ))/REQ_VCC_EQZERO)
+ + 0.5*(1-V(NVCCTEST ))*(-5u)}
GOUT1 OUT IN VALUE = {0.5*(1+V(N1SGN))*0.5*(1+V(NVCCTEST))*60u}
.ENDS
*$
*
.SUBCKT VICM_INA381A4 OUT INP INN GNDF
EOUT OUT GNDF VALUE = {0.5*(V(INP,GNDF) + V(INN,GNDF))}
.ENDS
*$
*
.SUBCKT IIBN_INA381A4 OUT IN VCC VEE INN GNDF
.PARAM VCMBRK = 5
.PARAM REQ_VCC_NEZERO = 450k
.PARAM REQ_VCC_EQZERO = 530k
EVCCTEST NVCCTEST 0 VALUE = {SGN(V(VCC,VEE)) - 1 + ABS(SGN(V(VCC,VEE)))}
E1ACT N1SGN 0 VALUE = {SGN(V(INN)-VCMBRK) - 1 + ABS(SGN(V(INN)-VCMBRK))}
GOUT2 OUT IN VALUE = {V(INN,GNDF)*(1/REQ_VCC_NEZERO 
+ + 0.5*(1-V(NVCCTEST ))/REQ_VCC_EQZERO)
+ + 0.5*(1-V(NVCCTEST ))*(-5u)}
GOUT1 OUT IN VALUE = {0.5*(1+V(N1SGN))*0.5*(1+V(NVCCTEST))*60u}
.ENDS
*$
*
.SUBCKT VNSE_INA381A4 1 2 
.PARAM NLF = 32 
.PARAM FLW = 1u  
.PARAM NVR = 32
.PARAM GLF={PWR(FLW,0.25)*NLF/1164}
.PARAM RNV={1.184*PWR(NVR,2)}
.MODEL DVN D KF={PWR(FLW,0.5)/1E11} IS=1.0E-16
I1 0 7 10E-3
I2 0 8 10E-3
D1 7 0 DVN
D2 8 0 DVN
E1 3 6 7 8 {GLF}
R1 3 0 1E9
R2 3 0 1E9
R3 3 6 1E9
E2 6 4 5 0 10
R4 5 0 {RNV}
R5 5 0 {RNV}
R6 3 4 1E9
R7 4 0 1E9
E3 1 2 3 4 1
C1 1 0 1E-15
C2 2 0 1E-15
C3 1 2 1E-15
.ENDS
*$
*
.SUBCKT COMPARATOR_CONTROL_INA381A4 OUT IN REF GNDF
.PARAM VOUT_MAX = 1
.PARAM VOUT_MIN = 0
.PARAM GAIN = 1e4
EOUT OUT GNDF VALUE = {MAX(MIN(GAIN*V(IN,REF),VOUT_MAX),VOUT_MIN)}
.ENDS
*$
*
.SUBCKT IQ_VICM_INA381A4 VCC VEE VICM SHDN VIMON GNDF
.PARAM IQ_3 = 209u
.PARAM IQ_2 = 325u
.PARAM IQ_1 = 375u
.PARAM VICM_1 = -0.2
.PARAM VICM_2 = 2
.PARAM VICM_3 = 5.1
.PARAM m_1 = {-2.25*12.4u}
.PARAM b_1 = 370u
.PARAM m_2 = 0
.PARAM b_2 = 313.3u
.PARAM m_3 = -1.04u
.PARAM b_3 = 208.8u
.PARAM IQ_SHDN = 0.01u
.PARAM Geq = 18.75u
EZ1 NSGNVICM1 0 VALUE = {SGN(V(VICM) - VICM_1) + ABS(SGN(V(VICM) - VICM_1)) - 1}
EZ2 NSGNVICM2 0 VALUE = {SGN(V(VICM) - VICM_2) + ABS(SGN(V(VICM) - VICM_2)) - 1}
EZ3 NSGNVICM3 0 VALUE = {SGN(V(VICM) - VICM_3) + ABS(SGN(V(VICM) - VICM_3)) - 1}
EIQ1 NIQ1 0 VALUE = {m_1*V(VICM)+b_1}
EIQ2 NIQ2 0 VALUE = {m_2*V(VICM)+b_2}
EIQ3 NIQ3 0 VALUE = {m_3*V(VICM)+b_3}
EIQ1_ACT NIQ1_ACT 0 VALUE = {0.5*(1 - V(NSGNVICM2))*V(NIQ1)}
EIQ2_ACT NIQ2_ACT 0 VALUE = {0.5*(1 + V(NSGNVICM2))*0.5*(1 - V(NSGNVICM3))*V(NIQ2)}
EIQ3_ACT NIQ3_ACT 0 VALUE = {0.5*(1 + V(NSGNVICM3))*V(NIQ3)}
GIQ VCC VEE VALUE = {(V(NIQ1_ACT) + V(NIQ2_ACT) + V(NIQ3_ACT))*V(SHDN,GNDF)}
ESGNIOUT NSGNIOUT 0 VALUE = {SGN(V(VIMON,GNDF))}
GOUTP VCC GNDF VALUE = {0.5*(1 + V(NSGNIOUT))*V(VIMON,GNDF)*V(SHDN,GNDF)}
GOUTN GNDF VEE VALUE = {0.5*(1 - V(NSGNIOUT))*V(VIMON,GNDF)*V(SHDN,GNDF)}
.ENDS
*$
*
.SUBCKT CMRR_INA381A4  VICM VI  VO  GNDF 
.PARAM CMRR = 110
.PARAM fcmrr = 8k
.PARAM PI = 3.141592
.PARAM RCMRR = 1
.PARAM GCMRR = {PWR(10,-CMRR/20)/RCMRR}
.PARAM LCMRR = {RCMRR/(2*PI*fcmrr)}
.PARAM CCMRR = {RCMRR/(2*PI*fcmrr)}
G1  GNDF 1 VICM GNDF {GCMRR}
R1  1 2 {RCMRR}
L1  2 GNDF {LCMRR}
E1  VI VO 1 GNDF 1
.ENDS
*$
*
.SUBCKT INA381A4_gbw_slew_outlimits  VIP  VIM  VO SHDN VCC VEE GNDF 
.PARAM Aol = 160 
.PARAM GBW = 17.4e6  
.PARAM SRP = 3e6  
.PARAM SRN = 3e6 
.PARAM IT = 0.001
.PARAM PI = 3.141592
.PARAM IP = {IT*MAX(1,SRP/SRN)}
.PARAM IN = {IT*MIN(-1,-SRN/SRP)}
.PARAM CC = {IT*MAX(1/SRP,1/SRN)}
.PARAM FP = {GBW/PWR(10,AOL/20)}
.PARAM RC = {1/(2*PI*CC*FP)}
.PARAM GC = {PWR(10,AOL/20)/RC}
G1          GNDF VOz VALUE = {MAX(MIN(GC*V(SHDN,GNDF)*V(VIP,VIM),IP),IN)}
C1          VOz GNDF {Cc}
R1          VOz GNDF {Rc}
S1  VOz N1 VOz N1 SW1
E1 N1 0 VALUE = {V(VCC,0) - 0.1u}
S2  VOz N2 VOz N2 SW2
E2 N2 0 VALUE = {V(VEE,0) + 0.1u}
EOUT VO GNDF VOz GNDF 1
.MODEL SW1 VSWITCH RON=100 ROFF=1e9 VON=0.1 VOFF=0.0
.MODEL SW2 VSWITCH RON=100 ROFF=1e9 VON=-0.1 VOFF=0.0
.ENDS
*$
*
.SUBCKT COMPARATOR_ALERT_INA381A4 OUT INP INN VCC VEE 
emid nmid 0 VALUE = {(V(VCC) + V(VEE))/2}
eout out vee VALUE = {IF(V(INP) >= V(INN),V(VCC,VEE),V(VEE,VEE))}
.ENDS
*$
*
.SUBCKT LATCH_CONTROL_INA381A4 A B C OUT VCC VEE
EGNDF GNDF 0 VALUE = {0.5*(V(VCC) + V(VEE))}
EA NA GNDF VALUE = {0.5*(1 + SGN(V(A,GNDF)))}
EB NB GNDF VALUE = {0.5*(1 + SGN(V(B,GNDF)))}
EC NC GNDF VALUE = {0.5*(1 - SGN(V(C,GNDF)))}
EABnotC NABnotC GNDF VALUE = {V(NA,GNDF)*V(NB,GNDF)*V(NC,GNDF)}
EOUT OUT VEE VALUE = {V(VCC,VEE)*V(NABnotC,GNDF) + V(VEE,VEE)*(1 - V(NABnotC,GNDF))}
.ENDS