***********************************************************************
**  v1.7.5 PSpice Models Library - TI NexFET Power N/P Channel FET's
*  (C) Copyright 2011 Texas Instruments Incorporated. All rights reserved.
***********************************************************************
***********************************************************************
* Notice:
* The data and models provided herein are intended to be used by 
* customers as an aid in designing electrical circuits using 
* semiconductor devices manufactured &/or sold by Texas Instruments.
* The models and associated information contained herein are not to
* be construed as a guarantee of electrical performance and/or device 
* specification.  Therefore, Texas Instruments does not assume any 
* liability arising out of the use of said data or models, nor from
* the devices or products manufactured therefrom.  Texas Instruments
* does not convey any license under its patent rights nor the rights
* of others.  Texas Instruments reserves the right to change models 
* without prior notice.  
*
* TI is a registered trademark of Texas Instruments Inc.
*************************************************************************
********************************************************************************
*
* Released by: Analog e-Lab Design Center, Texas Instruments Inc.
* Part: CSD87353Q5D
* Date: 11/30/2011
* Model Type: TRANSIENT
* Simulator: PSPICE
* Datasheet: SLPS285C - AUGUST 2011 - REVISED OCTOBER 2011
*
*****************************************************************************
*$
**********************************************************************
**********************************************************************
*                                                                    *
* CSD87353Q5D -  PSpice Model for use with OrCAD                  *
*                                                                    *
**********************************************************************
**********************************************************************
* nFET SUBCKT Definition:  1=D  2=G  3=S
* SUBCKT Definition:  1=2=Vin 3=TG 4=TGR 5=BG 6=7=8=VSW 9=PGND
**********************************************************************
.SUBCKT csd87353q5d  1 2 3 4 5 6 7 8 9
*
XnfetHS  18  17  19  n37318  
XnfetLS  19  16  15  n37317  
RGls      5  10      10e-3
RGhs      3  11      14e-3
RDhs1     1  12      0.8e-3
RDhs2     2  12      0.8e-3
RGlr      4  13      10e-3
Rpsn      8  14      0.6e-3
Rpsn2     7  14      0.6e-3
Rpsn3     6  14      0.6e-3
RSls      9  15      0.001e-3
LGls     10  16      0.61e-9
LGhs     11  17      1.95e-9
LDhs     12  18      1.03e-9
RLDhs    12  18      1e0
LGlr     13  19      0.61e-9
Lpsn     14  19      0.35e-9
.ENDS csd87353q5d  
**********************************************************************
**********************************************************************
*                                                                    *
*   n37317 is the LS silicon die model for use with 87353q5d         *
*                                                                    *
**********************************************************************
**********************************************************************
* SUBCKT Definition:  1=D  2=G  3=S
**********************************************************************
.SUBCKT n37317  1 2 3
* PARAMETER (local) DEFINITIONS SECTION
.PARAM  pWidth  3.668223
.PARAM  pLen    0.55u
.PARAM  perim   {2.1*pWidth}
.PARAM  ptrc1   2.5e-3		; temp co of ext Rd / Rs
.PARAM  ptrc2   6.25e-6
****** MODEL SUBCIRCUIT BEGINS HERE     ******************************
M1    10 11 12 12  NMOS	W={pWidth} L={pLen} PS={perim} PD={perim}
DBD   3  1	 DBD
CBD0  3  1   10e-12
RBD0  3  1   9e9
CGD0  2  13	 50E-12
RGD   13 1   2e6
CGS0  2  3	 6e-12
M2    12 11 12 20  PMOSd W={pWidth} L=0.04u PS={perim} PD={perim}
CDx   20 12  2p
DDx   20 10  DDx
DDG   10 14	 DD
DGD   11 14	 DD
RGG   2  11	 0.682
RSB   12 9	 RTEMP 0.12275e-3
RSS   9  3 	 RTEMP 0.12275e-3
RDD   1  10  RTEMP 1.02750e-3
******************************************************************* 
.MODEL  NMOS   NMOS (    LEVEL  = 7             Version= 3.2  
+ TNOM   = 27            LINT   = 0.055e-6      CAPMOD = 2
+ TOX    = 1.75e-8       NCH    = 2.03e17       NSUB   = 1.0e16
+ AGS    = 0.0           PVAG   = 0.0           U0     = 670
+ A0     = 1.0           A1     = 0.0           A2     = 1.0
+ UA     = 2.25e-9       UB     = 5.87e-19      VBM    = -10
+ UA1    = 3.00e-9       UB1    = -7.61e-18     UTE    = -1.50
+ KT1    = -0.65         KT2    = 0.022         KT1L   = 1.0e-15
+ DVT0   = 2.2           DVT1   = 0.53	      DVT2   = -0.033
+ ETA0   = 0.080         ETAB   = -0.07         XJ     = 4e-7
+ PDIBLC1= 0.40          PDIBLC2= 0.0086        NLX    = 1.75e-7
+ PSCBE1 = 4.24e8        PSCBE2 = 1e-5          PCLM   = 1.75
+ VOFF   = -0.15         NFACTOR= 1.4           JS     = 0
+ DROUT  = 0.9           DSUB   = 1.3           DELTA  = 0.01
+ CJ     = 1e-18         CJSW   = 1e-18         CF     = 0
+ CGSO   = 218.50e-12    CGDO   = 0.1e-12       CGBO   = 1e-18  )
******************************************************************* 
.MODEL  PMOSd   PMOS (   LEVEL  = 1
+ TNOM   = 27            VTO    = -0.30         IS     = 5e-16
+ TOX    = 1.75e-8       gamma  = 25.0
+ CJ     = 1e-18         CJSW   = 1e-18
+ CGSO   = 1e-18         CGDO   = 1e-18         CGBO   = 1e-18  )
******************************************************************* 
.MODEL DBD D (CJO=3.74e-9 VJ=0.70 M=0.35 TNOM=27 FC=0.5 TT=1e-09 
+ XTI=3.0 BV=31 IS=4.5e-12 N=1.010 RS=0.80e-3 TRS1=5.2e-3
+ ISR=1e-11 NR=2.0)
.param kkk 8.1e-10
.MODEL DD  D (CJO={kkk} VJ=0.60 M=0.90 IS=1e-12 RS=1 TNOM=27)
.MODEL DDx D (CJO={kkk/10} VJ=1.0 M=0.9 IS=1e-13 RS=1.0 TNOM=27)
*******************************************************************
.MODEL RTEMP RES (TC1={ptrc1} TC2={ptrc2})
*******************************************************************
.ENDS n37317
*$
**********************************************************************
**********************************************************************
*                                                                    *
*   n36318 is the HS die silicon model for use with 86353q5d         *
*                                                                    *
**********************************************************************
**********************************************************************
* SUBCKT Definition:  1=D  2=G  3=S
**********************************************************************
.SUBCKT n37318  1 2 3
* PARAMETER (local) DEFINITIONS SECTION
.PARAM  pWidthP  3.1298322
.PARAM  pWidth   {pWidthP*1e6}
.PARAM  pLen     0.56u
.PARAM  perimP   {2.1*pWidthP}
.PARAM  perim    {perimP*1e6}
.PARAM  ptrc1    2.25e-3		; temp co of ext Rd / Rs
.PARAM  ptrc2    4.0e-6
****** MODEL SUBCIRCUIT BEGINS HERE     ******************************
M1   10 11 12 12  NMOS	W={pWidthP} L={pLen}  PS={perimP} PD={perimP}
DBD   3  1	 DBD
CBD0  3  1   94e-12
RBD0  3  1   9e9
CGD0  2 13	 5E-12
RGD  13  1   2e6
CGS0  2  3	 6e-12
M2   12 11 12 20  PMOSd W={pWidthP} L=0.015u PS={perimP} PD={perimP}
*RDx  20 12   1e14
*CDx  20 12   2p
CGS   2 99 5e-12
RGS   99 3 10
DDx  20  10  DDx
DDG  14  11	 DD
DGD  14 10	 DD
RGG   2 11	 0.695
RSB  12  9	 rtemp 0.0543333e-3
RSS   9  3 	 rtemp 0.0543333e-3
RDD   1 10 	 rtemp 1.1433334e-3
******************************************************************* 
.MODEL  NMOS   NMOS (    LEVEL  = 7             Version = 3.2  
+ TNOM   = 27            LINT   = 0.055e-6      CAPMOD = 2
+ TOX    = 1.75e-8       NCH    = 2.375e17      NSUB   = 2.5e16
+ AGS    = 0.0           PVAG   = 0.00          U0     = 670
+ A0     = 1.0           A1     = 0.0           A2     = 1.0
+ UA     = 2.25e-9       UB     = 5.87e-19      VBM    = -10
+ UA1    = 7.0e-9        UB1    = -7.61e-18     UTE    = -1.50
+ KT1    = -0.95         KT2    = 0.022         KT1L   = 1.0e-13
+ DVT0   = 2.2           DVT1   = 0.53	      DVT2   = -0.033
+ ETA0   = 0.080         ETAB   = -0.07         XJ     = 4e-7
+ PDIBLC1= 0.01          PDIBLC2= 0.0086        NLX    = 1.75e-7
+ PSCBE1 = 4.24e8        PSCBE2 = 1e-5          PCLM   = 1e-3
+ VOFF   = -0.1          NFACTOR= 2.0           JS     = 0
+ DROUT  = 0.9           DSUB   = 1.3           DELTA  = 0.01
+ CJ     = 1e-18         CJSW   = 1e-18         CF     = 0
+ CGSO   = 199.75e-12    CGDO   = 0.1e-12       CGBO   = 1e-18  )
******************************************************************* 
.MODEL  PMOSd   PMOS (   LEVEL  = 1
+ TNOM   = 27            VTO    = -0.65         IS     = 4e-14
+ TOX    = 1.75e-8       NSUB   = 5e17          GAMMA  = 10.0
+ CJ     = 1e-18         CJSW   = 1e-18 
+ CGSO   = 1e-18         CGDO   = 1e-18         CGBO   = 1e-18  )
******************************************************************* 
.MODEL DBD D (CJO=28.05e-10 VJ=0.38 M=0.2885 TNOM=27 FC=0.5 TT=1e-09 
+ XTI=3.4 BV=31 IS=2.2e-12 N=1.02 RS=0.40e-3 TRS1=2.5e-3)
.MODEL DD  D (CJO=6.75e-10 VJ=0.73750 M=0.90 IS=1e-13 TNOM=27)
.MODEL DDx D (CJO=0.02e-09 VJ=1.0 M=0.9 IS=1e-13 RS=1.0 TNOM=25)
*******************************************************************
.model rtemp res (tc1={ptrc1} tc2={ptrc2})
.ENDS n37318
*