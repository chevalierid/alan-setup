* OPA2614
*****************************************************************************
* (C) Copyright 2011 Texas Instruments Incorporated. All rights reserved.                                            
*****************************************************************************
** This model is designed as an aid for customers of Texas Instruments.
** TI and its licensors and suppliers make no warranties, either expressed
** or implied, with respect to this model, including the warranties of
** merchantability or fitness for a particular purpose. The model is
** provided solely on an "as is" basis. The entire risk as to its quality
** and performance is with the customer.
*****************************************************************************
*
* This model is subject to change without notice. Texas Instruments
* Incorporated is not responsible for updating this model.
*
*****************************************************************************
*
** Released by: Analog eLab Design Center, Texas Instruments Inc.
* Part: OPA2614
* Date: 15JUL2011
* Model Type: ALL IN ONE
* Simulator: PSPICE
* Simulator Version: 16.0.0.p001
* EVM Order Number: N/A
* EVM Users Guide: N/A
* Datasheet: SBOS305D - JUNE 2004 - REVISED AUGUST 2008
*
* Model Version: 1.0
*
*****************************************************************************
* 
* Updates:
*
* Version 1.0 : 
* Release to Web
*
*****************************************************************************
* CONNECTIONS:
*               Non-Inverting Input
*               | Inverting Input
*               | |  Positive Supply
*               | |  |   Negative Supply
*               | |  |   |  OutNegative Supply
*               | |  |   |  |  
*               | |  |   |  |  
*               | |  |   |  |  
*               | |  |   |  |  
.SUBCKT OPA2614 + - V+ V- OUT
*   changed the pin order by JHB
V_V2 V+ 10 DC .5
R_R1 11 10 400
R_R2 12 10 400
R_R5 21 V+ 15
D_D1 14 13 Da_OPA2614 1
D_D5 20 19 Da_OPA2614 1
D_D6 22 OUT Da_OPA2614 .5
D_D7 OUT 23 Da_OPA2614 .5
E_E3 21 22 POLY(2) 0 19 V+ V- -885m 1 0.5
E_E4 23 24 POLY(2) 19 0 V+ V- -885M 1 0.5
R_R6 V- 24 15
Q_Q3 11 + 13 NPN8_OPA2614 10.5
Q_Q4 12 - 13 NPN8_OPA2614 10.5
E_E1 18 0 POLY(1) V+ V- -1 0.5 0
E_E2 20 0 POLY(1) V- V+ 1 0.5 0
V_V1 14 V- DC 1.10
C_C1 0 + 1p
R_R4 19 0 1K
G_G2 0 19 POLY(1) 15 0 0 1m 0 0
G_G1 0 15 POLY(1) 12 11 0 27.5m 0 0
D_D4 19 18 DX_OPA2614 1
R_R3 15 0 442.1K
C_C5 15 0 142.5p
C_C4 11 12 850F
I_I1 13 V- DC 1.256mA
C_C3 0 19 795f
*
*
*
.MODEL Da_OPA2614 D IS=1.0000E-15
*
.MODEL DX_OPA2614 D(IS=800.0E-18)
*
.MODEL NPN8_OPA2614 NPN
+ IS = 7.604E-18 BF = 1.570E+02 NF = 1.000E+00 VAF= 7.871E+01
+ IKF= 3.975E-02 ISE= 3.219E-14 NE = 2.000E+00 BR = 7.614E-01
+ NR = 1.000E+00 VAR= 1.452E+00 IKR= 8.172E-02 ISC= 7.618E-21
+ NC = 1.847E+00 RB = 1.060E+02 IRB= 0.000E+00 RBM= 2.400E+00
+ RE = 2.520E+00 RC = 1.270E+02 CJE= 1.120E-15 VJE= 7.591E-01
+ MJE= 5.406E-01 TF = 1.213E-11 XTF= 2.049E+00 VTF= 1.813E+00
+ ITF= 4.293E-02 PTF= 0.000E+00 CJC= 8.208E-14 VJC= 6.666E-01
+ MJC= 4.509E-01 XCJC=8.450E-02 TR = 4.000E-11 CJS= 1.160E-15
+ VJS= 5.286E-01 MJS= 4.389E-01 XTB= 1.022E+00 EG = 1.120E+00
+ XTI= 1.780E+00 KF = 45.00E-16 AF = 1.000E+00 FC = 8.273E-01
*
.ENDS
*
