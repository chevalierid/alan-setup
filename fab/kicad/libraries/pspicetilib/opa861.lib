*OPA861  Wide Bandwidth Operational Transconductance Amplifier 
* REV. A - Created 12/06/04 RS
* REV. B - Revised 4/25/06 X-ramus2@ti.com
*
*
* NOTES:
*   1- This macromodel predicts well: DC, small-signal AC, noise,
*      , and transient performance under a wide range
*      of conditions.
*   2- This macromodel does not predict well: distortion
*      (harmonic, intermod, diff. gain & phase, ...),
*      temperature effects, board parasitics, differences
*      between package styles, and process changes
*   3- For Spice3F4 users they might need to un-comment the lines for the F 
*      function and comment out the Lines for PSpice F functions
*      and subckts. First try the present netlist then comment out lines if 
*      errors appear.
*      General form:
*      FXXXXXXX N+ N- <POLY(ND)> VN1 <VN2 ...> P0 <P1 ...> <IC=...>
*      Examples:
*      F1 12 10 VCC 1MA 1.3M
*   4- For some simulators the subckt for the F statement need to be placed 
*      inside the ends statement followed by carriage return  
*   5- Known Problems: - Slew Rate too high on single +5V supply
*
* |-------------------------------------------------------------|
* |  This macro model is being supplied as an aid to            |
* |  circuit designs.  While it reflects reasonably close       |
* |  similarity to the actual device in terms of performance,   |
* |  it is not suggested as a replacement for breadboarding.    |
* |  Simulation should be used as a forerunner or a supplement  |
* |  to traditional lab testing.                                |
* |                                                             |
* |  Neither this library nor any part may be copied without    |
* |  the express written consent of Texas Instruments Corp.     |
* |-------------------------------------------------------------|
*
* CONNECTIONS:
*                                  B Input
*                                  |  C Output
*                                  |  | E Input/Output
*                                  |  | |  Quiescent Current Adjust 
*                                  |  | |  |         Positive Supply
*                                  |  | |  |         |     Negative Supply
*                                  |  | |  |         |      |
*                                  |  | |  |         |      |
*                                  |  | |  |         |      |
*                                  |  | |  |         |      | 
.SUBCKT OPA861 B C E IQADJ VCC VEE 

.PARAM A_Q35 = 1.25
.PARAM A_Q46 = {A_Q35*1.6}
.PARAM A_Q78 = {A_Q35*3.2}
.PARAM A_Q1011 = {A_Q35*0.8}

V_V1         b1 VEE DC 0.10813096  
R_R11         B 23a  50  
R_R10         B 23  50  
R_R6         b4 b3  329  
R_R17         VEE 29  250  
R_R12         VEE 25  150  
R_R4         IQADJ b4  220  
R_R5         VEE b4  30k  
R_R21         VEE 39  50  
C_C2         C 37  1.85pF  
R_R35         C 37  100K  
C_C3         39 C  1.85pF  
R_R36         39 C  100K  
R_R13         26 VCC  150  
R_R8         21 VCC  250  
R_R20         37 VCC  50  
R_R18         CM1 VCC  50  
R_R19         VEE CM3  50  
R_R14         22 30  125  
R_R15         28 31  110  
X_F1    b1 b2 +Io VEE OPA861_F1 
X_F2    b2 b3 VCC -Io OPA861_F2 
Q_Q3         22 +Io 21 PNP6 {A_Q35}
Q_Q4         +Io +Io 26 PNP6 {A_Q46}
Q_Q1         -Io 23 22 PNP6 0.25
Q_Q2         +Io 23a 28 NPN6 0.25
Q_Q5         -Io -Io 25 NPN6 {A_Q35}
Q_Q6         28 -Io 29 NPN6 {A_Q46}
G_G4         C 39 CM3 VEE 0.02
G_G3         37 C VCC CM1 0.0201190476
V_V5         CM1 33 DC .792  
V_V6         35 CM3 DC .792  
Q_Q10         30 30 99 NPN6 {A_Q1011}
Q_Q11         31 31 99 PNP6 {A_Q1011}
Q_Q8         35 31 E PNP6 {A_Q78}
Q_Q7         33 30 E NPN6 {A_Q78}


.MODEL NPN6 NPN
+ IS=70.40E-18 BF=157 NF=1.0 VAF=70 IKF=39.750E-3 ISE=32.190E-15 NE=2
+ BR=.7614 NR=1.0E+00 VAR=1.4520 IKR=81.720E-3 ISC=7.6180E-21 NC=1.8470
+ RE=5.537E+00 RB=3.346E+01 IRB= 0.000E+00 RBM= 2.400E+00 RC=2.156E+01
+ CJE=120.20E-15 VJE=.7591 MJE=.5406 CJC=133.80E-15 VJC=.6666
+ MJC=.4509 XCJC=8.450E-02 TR = 4.000E-11 CJS= 3.180E-14 FC=.827
+ TF=12.130E-12 XTF=2.0490 VTF=1.8130 ITF=42.930E-3 TR=40.000E-12 
+ CJS= 3.180E-14 EG=1.1840 XTB=1.0220 XTI=1.7800 KF = 17.500E-15 
+ AF = 1.000E+00 FC = 8.273E-01


.MODEL PNP6 PNP
+ IS = 2.015E-16 BF = 1.418E+02 NF = 1.000E+00 VAF= 5.058E+01
+ IKF= 1.085E-01 ISE= 2.233E-15 NE = 1.505E+00 BR = 3.252E+01
+ NR = 1.050E+00 VAR= 1.093E+00 IKR= 5.000E-05 ISC= 6.621E-16
+ NC = 1.150E+00 RB = 3.346E+01 IRB= 0.000E+00 RBM= 2.40e+00
+ RE = 5.537E+00 RC = 2.156E+01 CJE= 1.202E-13 VJE= 7.320E-01
+ MJE= 4.930E-01 TF = 1.303E-11 XTF= 3.500E+01 VTF= 3.259E+00
+ ITF= 2.639E-01 PTF= 0.000E+00 CJC= 1.595E-13 VJC= 7.743E-01
+ MJC= 5.000E-01 XCJC=8.504E-02 TR = 1.500E-10 CJS= 7.620E-13
+ VJS= 9.058E-01 MJS= 4.931E-01 XTB= 1.732E+00 EG = 1.184E+00
+ XTI= 2.000E+00 KF = 10.890E-16 AF = 1.000E+00 FC = 8.500E-01


.ENDS    OPA861

.subckt OPA861_F1 1 2 3 4  
F_F1         3 4 VF_F1 5.230536
VF_F1         1 2 0V
.ends OPA861_F1

.subckt OPA861_F2 1 2 3 4  
F_F2         3 4 VF_F2 5.15646
VF_F2         1 2 0V
.ends OPA861_F2
