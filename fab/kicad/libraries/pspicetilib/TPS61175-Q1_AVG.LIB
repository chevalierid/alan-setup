*$
* TPS61175-Q1
*****************************************************************************
* (C) Copyright 2016 Texas Instruments Incorporated. All rights reserved.      
*****************************************************************************
** This model is designed as an aid for customers of Texas Instruments.
** TI and its licensors and suppliers make no warranties, either expressed
** or implied, with respect to this model, including the warranties of 
** merchantability or fitness for a particular purpose.  The model is
** provided solely on an "as is" basis.  The entire risk as to its quality
** and performance is with the customer
*****************************************************************************
*
* This model is subject to change without notice. Texas Instruments
* Incorporated is not responsible for updating this model.
*
*****************************************************************************
*
** Released by: WEBENCH Design Center, Texas Instruments Inc.
* Part: TPS61175-Q1
* Date: 27SEP2016
* Model Type: AVERAGE
* Simulator: PSPICE
* Simulator Version: 16.2.0.p001
* EVM Order Number: TPS61175EVM-326
* EVM Users Guide: SLVU260–December 2008
* Datasheet: SLVSCN9A –DECEMBER 2014–REVISED APRIL 2016
*
* Model Version: Final 1.00
*
*****************************************************************************
*
* Updates:
*
* Final 1.00
* Release to Web.
*
*****************************************************************************
* Model Usage Notes
*
* 1. Since this is an average model used for AC analysis, only the pins affecting AC analysis 
*    are modeled.
* 
* 2. Make sure the value of inductor(L),frequency(FS), VIN , RT and VOUT used in 
*    the testbench is passed to the macromodel appropriately. The macromodel uses these values internally.
*
******************************************************************************
.SUBCKT TPS61175_Q1_AVG VOUT VIN SW COMP FB PGND PARAMS: L=16u FS=1148314.5
+ vinx=8.5 voutx=24 rtx=84500.00
R_REA   PGND 	COMP  	6E6
C_CEA   PGND 	COMP  	10E-18
V_VREF   Vref 	PGND 	1.229
G_GEA    PGND 	COMP 	Vref 	FB	340u
X_U2    PGND SW VOUT COMP N1165537 CCM PARAMS: L={L} FS={FS} 
+ Ri=-40m Acs=1 vinx={vinx} voutx={voutx} rtx={rtx}
.ENDS TPS61175_Q1_AVG
*$
.SUBCKT CCM a c p vc dc params: L={L} FS={FS}  
+ Ri=-40m Acs=1 vinx={vinx} voutx={voutx} rtx={rtx}
ESe Sa 0 value = {0.32/(16*rtx*(1-(1-(vinx/voutx)))*0.000000000006)+(0.0000005/0.000000000006)}
Edc dcx 0 value = { v(cx,p)/(v(a,p)+1u) }
Xdc dcx dc limit params: clampH={1-{58.33n*{Fs}}} clampL={60n*{Fs}}
Ed2 d2X 0 value = {(1 - V(dc))} 
Xd2 d2X dc d2 limit2
GIap a p value =  {V(dc)*I(VM)} 
GIpc p cx value = { V(vc)/({Acs}*{Ri}) }
GImju cx p value ={V(Sa)*V(dc)/({Acs}*{Ri}*{Fs}) + 
+ (v(cx,p))/({L}*{Fs})*((1-V(dc))/2)}
VM cx c
XC1 cx p mode varicap 
Emode mode 0 Value={ IF ((2*{L}*{Fs}*I(VM)/(V(dc)*V(a,cx)+1u))
+ < 1, {4/({L}*(6.28*{Fs})**2)}, {4/({L}*(6.28*{Fs})**2)}) } 
.ENDS
*$
.subckt limit d dc params:  clampH=0.76 clampL=0.15
Gd 0 dcx VALUE = { V(d)*100u }
Rdc dcx 0 10k
V1 clpn 0 {clampL}
V2 clpp 0 {clampH}
D1 clpn dcx dclamp
D2 dcx clpp dclamp
Edc dc 0 value={ V(dcx) }
.model dclamp d n=0.01 rs=100m
.ENDS
*$
.subckt limit2 d2nc d d2c
Gd 0 d2cx d2nc 0 100u
Rdc d2cx 0 10k
V1 clpn 0 48m
E2 clpp 0 Value = { 1-V(d) }
D1 clpn d2cx dclamp
D2 d2cx clpp dclamp
Edc d2c 0 value={ V(d2cx) }
.model dclamp d n=0.01 rs=100m
.ENDS
*$
.SUBCKT VARICAP 1 2 CTRL
R1 1 3 1u
VC 3 4
EBC 4 2 Value = { (1/v(ctrl))*v(int) }
GINT 0 INT Value = { I(VC) }
CINT INT 0 1
Rdum INT 0 10E10
.ENDS
*$
.SUBCKT LDCR IN OUT
+ PARAMs:  L=1u DCR=0.01 
L             IN 1  {L} 
RDCR	1 OUT {DCR}
.ENDS LDCR
*$
.SUBCKT CESR IN OUT
+ PARAMs:  C=100u ESR=0.01 X=2 
C	IN 1  {C*X} 
RESR	1 OUT {ESR/X}
.ENDS CESR
*$
.SUBCKT D_D1 1 2
D1 1 2 DD1
.MODEL DD1 D BV=500 CJO=470E-12 EG=0.69 IBV=1E-6
+ IS=9E-6 M=0.8 N=1.442 RS=60E-3 VJ=1 XTI=2
.ENDS D_D1
*$
