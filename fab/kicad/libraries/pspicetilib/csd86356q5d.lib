***********************************************************************

*  (C) Copyright 2018 Texas Instruments Incorporated. All rights reserved.

***********************************************************************

***********************************************************************

** This model is designed as an aid for customers of Texas Instruments.

** TI and its licensors and suppliers make no warranties, either expressed

** or implied, with respect to this model, including the warranties of

** merchantability or fitness for a particular purpose. The model is

** provided solely on an "as is" basis. The entire risk as to its quality

** and performance is with the customer

********************************************************************************

*

* Released by: WEBENCH(R) Design Center, Texas Instruments Inc.

* Part: CSD86356Q5D

* Date: 20/11/2018

* Model Type: 

* Simulator: PSPICE

* Simulator Version: 16.2

* EVM Order Number: 

* EVM Users Guide: 

* Datasheet: SLPS664 - MARCH 2018

* Model Version: Final 1.00

*

*****************************************************************************

*

* Updates:

*

* Final 1.00

* Release to Web

*

*****************************************************************************

*

* Model Usage Notes:

*

*****************************************************************************
* PSpice Model Editor - Version 16.2.0
*
*$
************************************************************************
* PSpice model for TI NexFET n-channel power block CSD86356Q5D
* PSpice Model Editor - Version 16.2.0
************************************************************************
* Notice:
* The data and models provided herein are intended to be used by 
* customers as an aid in designing electrical circuits using 
* semiconductor devices manufactured &/or sold by Texas Instruments.
* The models and associated information contained herein are not to
* be construed as a guarantee of electrical performance and/or device 
* specification.  Therefore, Texas Instruments does not assume any 
* liability arising out of the use of said data or models, nor from
* the devices or products manufactured therefrom.  Texas Instruments
* does not convey any license under its patent rights nor the rights
* of others.  Texas Instruments reserves the right to change models 
* without prior notice.  
*
* TI is a registered trademark of Texas Instruments Inc.
*************************************************************************
*$
**********************************************************************
**********************************************************************
*                                                                    *
* CSD86356Q5D  -  P-Spice Model                                     *
*   model verified in Cadence OrCAD / Allegro Simulation Environment *
*                                                                    *
* Model Status:   model based on measured results                    *
*                                                                    *
**********************************************************************
* Subcircuit Instantiation Notes:
*  nFET SUBCKT Definition:  1=D  2=G  3=S
*  SUBCKT Definition:  1=2=Vin 3=TG 4=TGR 5=BG 6=7=8=VSW 9=PGND
*  - see csd86356q5d data sheet for further details
**********************************************************************
.SUBCKT csd86356q5d  1 2 3 4 5 6 7 8 9
XnfetHS  18  17  19  n36335
XnfetLS  19  16  15  n36334
RGls      5  10      22e-3
RGhs      3  11      26e-3
RDhs      1  12      0.9e-3
RDhs2     2  12      0.9e-3
RGlr      4  13      22e-3
Rpsn      8  14      0.60e-3
Rpsn2     7  14      0.60e-3
Rpsn3     6  14      0.60e-3
RSls      9  15      0.001e-3
LGls     10  16      1.54e-9
LGhs     11  17      1.94e-9
LDhs     12  18      0.5e-9
RLDhs    12  18      1
LGlr     13  19      1.54e-9
Lpsn     14  19      0.45e-9
.ENDS csd86356q5d
*$
**********************************************************************
**********************************************************************
*                                                                    *
*   n36334   -  PSpice Model -  STATUS:  PRELIMINARY                *
*                                                                    *
*   n36334 is the LS silicon die model for use with 86356q5d       *
*                                                                    *
**********************************************************************
**********************************************************************
* SUBCKT Definition:  1=D  2=G  3=S
**********************************************************************
.SUBCKT n36334  1 2 3
* PARAMETER (local) DEFINITIONS SECTION
.PARAM  pWidth  3.4348		; m, total gate width
.PARAM  pLen    0.35u
.PARAM  perim   {2*pWidth}	; m, gate/drain perim (rough est)
.PARAM  ptrc1   9.0e-4		; temp co of ext Rd / Rs
.PARAM  ptrc2   2.2e-6
****** MODEL SUBCIRCUIT BEGINS HERE     ******************************
M1   10 11 12 12  NMOS	W={pWidth} L={pLen}  PS={perim} PD={perim}
DBD   3  1	 DBD
CBD0  3  1   10e-12
RBD0  3  1   9e9
CGD0  2 13	 5E-12
RGD  13  1   2e6
CGS0  2  3	 6e-12
M2   12 11 12 20  PMOSd W={pWidth} L=0.099u PS={perim} PD={perim}
RDx  20 12   1e14
CDx  20 12   2p
DDx  20 10   DD
RGG   2 11	 0.72
RSB  12  9	 RTEMP 0.15e-3
RSS   9  3 	 RTEMP 0.30e-3
RDD   1 10 	 RTEMP 0.38e-3
*******************************************************************
.MODEL  NMOS   NMOS (    LEVEL  = 7             Version = 3.2  
+ TNOM   = 27            LINT   = 0.045e-6      CAPMOD = 2
+ TOX    = 1.75e-8       NCH    = 2.618e17      NSUB   = 5.6e16
+ AGS    = 0.0           PVAG   = 0.0           U0     = 400
+ A0     = 1.0           A1     = 0.0           A2     = 1.0
+ UA     = 2.70e-9       UB     = 1.0e-18       VBM    = -10
+ UA1    = 1.4e-9        UB1    = -0.9e-18      UTE    = -1.50
+ KT1    = -0.82         KT2    =-0.022         KT1L   = 1.0e-15
+ DVT0   = 2.1           DVT1   = 0.45	      DVT2   = -0.033
+ ETA0   = 0.020         ETAB   = -0.07         XJ     = 3e-7
+ PDIBLC1= 0.08          PDIBLC2= 0.006         NLX    = 1.75e-7
+ PSCBE1 = 2.5e8         PSCBE2 = 1e-7          PCLM   = 1.5
+ VOFF   = -0.25         NFACTOR= 1.75          JS     = 0
+ DROUT  = 1.2           DSUB   = 1.4           DELTA  = 0.022
+ CJ     = 1e-18         CJSW   = 1e-18         CF     = 0
+ CGSO   = 12e-12        CGDO   = 0.2e-12       CGBO   = 0  
+ VOFFCV = -0.45 )
******************************************************************* 
.MODEL  PMOSd   PMOS (   LEVEL  = 7		     Version = 3.2
+ TNOM   = 27            CAPMOD = 0
+ VTH0    = -1.0         IS     = 1.0e-16		U0	 = 250
+ TOX    = 1.80e-8       K1	  = 7.0		K2     = -0.1
+ CJ     = 1e-18         CJSW   = 1e-18
+ CGSO   = 1e-18         CGDO   = 1e-18         CGBO   = 0  )
******************************************************************* 
.MODEL DBD D (CJO=3.58e-9 VJ=0.88 M=0.38 TNOM=27 FC=0.5 TT=0.5e-09 
+ XTI=2.5 BV=25.825 TBV1=7.35e-4 IS=1.8e-11 N=1.060 
+ RS=8.8e-4 TRS1=3e-4  ISR=6.6e-9 NR=2.0)
.MODEL DD  D (CJO=25.5e-10 VJ=0.55 M=1.28 IS=1e-13 RS=1 FC=0.9 
+ TNOM=27)
*******************************************************************
.MODEL RTEMP RES (TC1={ptrc1} TC2={ptrc2})
*******************************************************************
.ENDS n36334
*$
**********************************************************************
**********************************************************************
*                                                                    *
*   n36335   -  PSpice Model -  STATUS:  PRELIMINARY                 *
*                                                                    *
*   n36335 is the HS die silicon model for use with 86356q5d         *
*                                                                    *
**********************************************************************
**********************************************************************
* SUBCKT Definition:  1=D  2=G  3=S
**********************************************************************
.SUBCKT n36335  1 2 3
* PARAMETER (local) DEFINITIONS SECTION
.PARAM  pWidth  1.4018		; m, total gate width
.PARAM  pLen    0.35u
.PARAM  perim   {2*pWidth}	; m, gate/drain perim (rough est)
.PARAM  ptrc1   9.5e-4		; temp co of ext Rd / Rs
.PARAM  ptrc2   3.0e-6
****** MODEL SUBCIRCUIT BEGINS HERE     ******************************
M1   10 11 12 12  NMOS	W={pWidth} L={pLen}  PS={perim} PD={perim}
DBD   3  1	 DBD
CBD0  3  1   10e-12
RBD0  3  1   9e9
CGD0  2 13	 5E-12
RGD  13  1   2e6
CGS0  2  3	 94e-12
M2   12 11 12 10  PMOSd W={pWidth*0.51} L=0.001u PS={perim} PD={perim}
DDG  25 11   DDG
DGD  25 10   DGD
* Note:  gate oxide capacitance included in NMOS below
RGG   2 11	 2.08
RSB  12  9	 RTEMP 0.38e-3
RSS   9  3 	 RTEMP 0.54e-3
RDD   1 10 	 RTEMP 0.64e-3
******************************************************************* 
.MODEL  NMOS   NMOS (    LEVEL  = 7             Version = 3.2  
+ TNOM   = 27            LINT   = 0.045e-6      CAPMOD = 2
+ TOX    = 1.75e-8       NCH    = 2.718e17      NSUB   = 5.5e16
+ AGS    = 0.0           PVAG   = 0.10          U0     = 380
+ A0     = 1.00          A1     = 0.0           A2     = 1.0
+ UA     = 2.50e-9       UB     = 1.0e-18       VBM    = -10
+ UA1    = 1.20e-9       UB1    = -1.5e-18      UTE    = -1.50
+ KT1    = -0.85         KT2    =-0.022         KT1L   = 1.0e-15
+ DVT0   = 2.1           DVT1   = 0.48	      DVT2   = -0.033
+ ETA0   = 0.020         ETAB   = -0.07         XJ     = 3e-7
+ PDIBLC1= 0.05          PDIBLC2= 0.005         NLX    = 1.75e-7
+ PSCBE1 = 2.5e8         PSCBE2 = 1e-7          PCLM   = 1.35
+ VOFF   = -0.29         NFACTOR= 1.75          JS     = 0
+ DROUT  = 1.2           DSUB   = 1.4           DELTA  = 0.052
+ CJ     = 1e-18         CJSW   = 1e-18         CF     = 0
+ CGSO   = 12e-12        CGDO   = 0.1e-12       CGBO   = 0  
+ VOFFCV = -0.05)
******************************************************************* 
.MODEL  PMOSd   PMOS (   LEVEL  = 7			Version = 3.2
+ TNOM   = 27            CAPMOD = 0
+ VTH0    = -1.0         IS     = 1.0e-16		U0	 = 250
+ TOX    = 1.75e-8       K1	  = 0.05		K2     = -0.2
+ CJ     = 1e-18         CJSW   = 1e-18
+ CGSO   = 1e-18         CGDO   = 1e-18         CGBO   = 0  )
******************************************************************* 
.MODEL DBD D (CJO=1.40e-9 VJ=0.87 M=0.37 TNOM=27 FC=0.5 TT=1e-09 
+ XTI=3.6 BV=25.85 TBV1=7.45e-4 IS=4.9e-12 N=1.060 
+ RS=8.7e-4 TRS1=2.2e-3  ISR=4.2e-09 NR=2.0)
.MODEL DDG  D (CJO=1.0e-09 VJ=0.65 M=0.4 IS=1e-13 FC=0.9 RS=0.1 TNOM=27)
.MODEL DGD  D (CJO=2.9e-10 VJ=0.87 M=0.85 IS=1e-13 FC=0.9 RS=1 TNOM=27)
*******************************************************************
.MODEL RTEMP RES (TC1={ptrc1} TC2={ptrc2})
*******************************************************************
.ENDS n36335
*
*$
