* PSpice Model Editor - Version 16.2.0

*$
* TPS65252
*****************************************************************************
* (C) Copyright 2011 Texas Instruments Incorporated. All rights reserved.
*****************************************************************************
** This model is designed as an aid for customers of Texas Instruments.
** TI and its licensors and suppliers make no warranties, either expressed
** or implied, with respect to this model, including the warranties of
** merchantability or fitness for a particular purpose. The model is
** provided solely on an "as is" basis. The entire risk as to its quality
** and performance is with the customer
*****************************************************************************
*
** Released by: Analog eLab Design Center, Texas Instruments Inc.
* Part: TPS65252
* Date: 24MAY2011
* Model Type: Average
* Simulator: PSpice
* Simulator Version: 16.2.0.p001
* EVM Order Number: TPS65252EVM 
* EVM Users Guide: SLVU438- January 2011
* Datasheet: SLVSAM1B - DECEMBER 2010 - REVISED FEBRUARY 2011
* Model Version: Final 1.00
*
*****************************************************************************
*
* Updates:
*
* Final 1.00
* Release to Web.
*
***************************************************************************
.SUBCKT TPS65252_AVG VIN1 VIN2 COMP1 COMP2 LX1 LX2 
+ FB1 FB2 GND PARAMS: FS=500k L1=4.7U L2=4.7U 
X_U5         VIN1 LX1 GND COMP1 N1165537 CCM PARAMS:  L={L1} FS={FS}
+  RI=83.3333M SE=0 ACS=1
V_V29         N1177017 GND 0.8
R_R19         GND COMP1  10e6 TC=0,0 
V_V28         N1165059 GND 0.8
X_U3         VIN2 LX2 GND COMP2 N1177187 CCM PARAMS:  L={L2} FS={FS}
+  RI=83.3333M SE=0 ACS=1
G_G3         GND COMP2 N1177017 FB2 130u
G_G2         GND COMP1 N1165059 FB1 130u
R_R20         GND COMP2  10e6 TC=0,0 
.ENDS TPS65252_AVG
*$
.SUBCKT CCM a c p vc dc params: L=4.7u Fs=500k Ri=83.3333m Acs=1
ESe Sa 0 value = { (3.3*{Ri})/(4.7u) }
Edc dcx 0 value = { v(cx,p)/(v(a,p)+1u) }
Xdc dcx dc limit params: clampH=0.99 clampL=48m
Ed2 d2X 0 value = {(1 - V(dc))} 
Xd2 d2X dc d2 limit2
GIap a p value =  {V(dc)*I(VM)}
GIpc p cx value = { V(vc)/({Acs}*{Ri}) }
GImju cx p value = {V(Sa)*V(dc)/({Acs}*{Ri}*{Fs})+(v(cx,p))/({L}*{Fs})*((1-(V(dc))/2)) }
XC1 cx p mode varicap ; 
Emode mode 0 Value = { IF ((2*{L}*{Fs}*I(VM)/(V(dc)*V(a,cx)+1u))
+ < 1, {4/({L}*(6.28*{Fs})^2)}, {4/({L}*(6.28*{Fs})^2)}) } ;
VM cx c
.ENDS
*$
.SUBCKT VARICAP 1 2 CTRL
R1 1 3 1u
VC 3 4
EBC 4 2 Value = { (1/v(ctrl))*v(int) }
GINT 0 INT Value = { I(VC) }
CINT INT 0 1
Rdum INT 0 10E10
.ENDS
*$
.subckt limit d dc params: clampH=0.99 clampL=48m
Gd 0 dcx VALUE = { V(d)*100u }
Rdc dcx 0 10k
V1 clpn 0 {clampL}
V2 clpp 0 {clampH}
D1 clpn dcx dclamp
D2 dcx clpp dclamp
Edc dc 0 value={ V(dcx) }
.model dclamp d n=0.01 rs=100m
.ENDS
*$
*
.subckt limit2 d2nc d d2c
Gd 0 d2cx d2nc 0 100u
Rdc d2cx 0 10k
V1 clpn 0 7m
E2 clpp 0 Value = { 1-V(d) }
D1 clpn d2cx dclamp
D2 d2cx clpp dclamp
Edc d2c 0 value={ V(d2cx) }
.model dclamp d n=0.01 rs=100m
.ENDS
*
*$
.SUBCKT LDCR IN OUT
+ PARAMs:  L=1u DCR=0.01 
L             IN 1  {L} 
RDCR	1 OUT {DCR}
.ENDS LDCR
*$
*
.SUBCKT CESR IN OUT
+ PARAMs:  C=100u ESR=0.01 X=2 
C	IN 1  {C*X} 
RESR	1 OUT {ESR/X}
.ENDS CESR
*$
