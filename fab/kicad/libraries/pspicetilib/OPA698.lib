* OPA698  Unit Gain Wide Bandwidth Voltage Limiting Amp
* REV. A - Created 7/02/04 Rea.Schmid@ti.com
*
*
* NOTES:
*   1- This macromodel predicts well: DC, small-signal AC,
*      noise, and transient performance under a wide range
*      of conditions
*   2- This macromodel does not predict well: distortion
*      (harmonic, intermod, diff. gain & phase, ...),
*      temperature effects, board parasitics, differences
*      between package styles, and process changes
*   3- Limit numeric command is a statement supported by most spice 
*      simulators. Not all simulators recognize this statement,
*      therefore contact TI and provide what spice program you are
*      working with for help.
*
* |-------------------------------------------------------------|
* |  This macro model is being supplied as an aid to            |
* |  circuit designs.  While it reflects reasonably close       |
* |  similarity to the actual device in terms of performance,   |
* |  it is not suggested as a replacement for breadboarding.    |
* |  Simulation should be used as a forerunner or a supplement  |
* |  to traditional lab testing.                                |
* |                                                             |
* |  Neither this library nor any part may be copied without    |
* |  the express written consent of Texas Instruments Corp.     |
* |-------------------------------------------------------------|
*
* CONNECTIONS:
*              Non-Inverting Input
*              | Inverting Input
*              | |  Positive Supply
*              | |  |   Negative Supply
*              | |  |   |  Out
*              | |  |   |  |  Limit Hi
*              | |  |   |  |  |  Limit Lo
*              | |  |   |  |  |  |
*              | |  |   |  |  |  |
.SUBCKT OPA698 + -  V+ V- Out VH VL 
D_D1         17 14 DI 1
Q_Q3         13 10 14 NPN8 .667
R_R3         17 19  500  
R_R4         18 21  500  
Q_Q4         15 11 14 PNP8 .667
Q_Q1         V- - 10 PNP8 .667
R_R6         V- 23  420  
R_R2         V- 16  420  
D_D2         14 18 DI 1
R_R1         12 V+  420  
R_R5         22 V+  420  
Q_Q2         V+ - 11 NPN8 .667
R_R7         24 V+  820  
E_U5         34 0 VALUE {LIMIT(V(32),V(VL)-10m,V(VH)+10m)}
G_G12         0 32 NCMP 0 1m
D_D4         20 21 DI 1
D_D3         19 20 DI 1
R_R8         V- 27  890  
I_I3         V+ 17 DC 480u  
I_I5         V+ 19 DC 480u  
I_I6         21 V- DC 480u  
I_I4         18 V- DC 480u  
I_I2         11 V- DC 290u  
I_I7         V+ 28 DC 290u  
I_I8         29 V- DC 290u  
I_I1         V+ 10 DC 290u  
I_I9         V+ 36 DC 2.5m  
I_I10         37 V- DC 4.5m  
V_V1         V+ 33 DC 1.85  
V_V2         35 V- DC 1.85  
Q_Q5         13 13 12 PNP8 .667
Q_Q6         15 15 16 NPN8 .667
Q_Q7         20 13 22 PNP8 .667
Q_Q8         20 15 23 NPN8 .667
Q_Q9         25 25 24 PNP8 1
Q_Q10         25 28 20 NPN8 .667
Q_Q11         26 29 20 PNP8 .667
Q_Q12         26 26 27 NPN8 1
Q_Q13         V- + 28 PNP8 .667
Q_Q14         V+ + 29 NPN8 .667
Q_Q15         NCMP 25 30 PNP8 1
Q_Q16         NCMP 26 31 NPN8 1
Q_Q17         V- 34 36 PNP8 29
Q_Q18         V+ 34 37 NPN8 29
Q_Q19         V+ 36 Out NPN8 29
Q_Q20         V- 37 Out PNP8 29
D_D5         34 33 Da 1
D_D6         35 34 Da 1
R_R10         30 V+  420  
R_R11         V- 31  420  
R_R12         0 32  1K  
C_C1         NCMP V+  3.75p  
C_C2         32 0  500f  
C_C3         0 +  1p  
.ENDS    OPA698
*
*Models for circuit OPA698
*
.MODEL Da D IS=1.0000E-15
.MODEL DI D IS=1.0000E-15
*
.MODEL NPN8 NPN
+ IS = 7.604E-18 BF = 1.570E+02 NF = 1.000E+00 VAF= 7.871E+01
+ IKF= 3.975E-02 ISE= 3.219E-14 NE = 2.000E+00 BR = 7.614E-01
+ NR = 1.000E+00 VAR= 1.452E+00 IKR= 8.172E-02 ISC= 7.618E-21
+ NC = 1.847E+00 RB = 1.060E+02 IRB= 0.000E+00 RBM= 2.400E+00
+ RE = 2.520E+00 RC = 1.270E+02 CJE= 1.120E-15 VJE= 7.591E-01
+ MJE= 5.406E-01 TF = 1.213E-11 XTF= 2.049E+00 VTF= 1.813E+00
+ ITF= 4.293E-02 PTF= 0.000E+00 CJC= 8.208E-14 VJC= 6.666E-01
+ MJC= 4.509E-01 XCJC=8.450E-02 TR = 4.000E-11 CJS= 1.160E-15
+ VJS= 5.286E-01 MJS= 4.389E-01 XTB= 1.022E+00 EG = 1.120E+00
+ XTI= 1.780E+00 KF = 3.500E-16 AF = 1.000E+00 FC = 8.273E-01
*
.MODEL PNP8 PNP
+ IS = 7.999E-18 BF = 1.418E+02 NF = 1.000E+00 VAF= 4.158E+01
+ IKF= 1.085E-01 ISE= 2.233E-15 NE = 1.505E+00 BR = 3.252E+01
+ NR = 1.050E+00 VAR= 1.093E+00 IKR= 5.000E-05 ISC= 6.621E-16
+ NC = 1.150E+00 RB = 6.246E+01 IRB= 0.000E+00 RBM= 2.240E+00
+ RE = 2.537E+00 RC = 1.260E+02 CJE= 9.502E-14 VJE= 7.320E-01
+ MJE= 4.930E-01 TF = 1.303E-11 XTF= 3.500E+01 VTF= 3.259E+00
+ ITF= 2.639E-01 PTF= 0.000E+00 CJC= 1.080E-13 VJC= 7.743E-01
+ MJC= 5.000E-01 XCJC=8.504E-02 TR = 1.500E-10 CJS= 1.290E-13
+ VJS= 9.058E-01 MJS= 4.931E-01 XTB= 1.732E+00 EG = 1.120E+00
+ XTI= 2.000E+00 KF = 3.500E-16 AF = 1.000E+00 FC = 8.500E-01

$