* PSpice Model Editor - Version 16.2.0

*$
* TPS65320-Q1
*****************************************************************************
* (C) Copyright 2013 Texas Instruments Incorporated. All rights reserved.
*****************************************************************************
** This model is designed as an aid for customers of Texas Instruments.
** TI and its licensors and suppliers make no warranties, either expressed
** or implied, with respect to this model, including the warranties of
** merchantability or fitness for a particular purpose. The model is
** provided solely on an "as is" basis. The entire risk as to its quality
** and performance is with the customer
*****************************************************************************
*
** Released by: WEBENCH Design Center, Texas Instruments Inc.
* Part: TPS65320-Q1
* Date: 14JUN2013
* Model Type: Average
* Simulator: PSpice
* Simulator Version: 16.2.0.p001
* EVM Order Number: TPS65320EVM 
* EVM Users Guide: SLVU691 - February 2013 
* Datasheet: SLVSAY9A - DECEMBER 2012 - REVISED APRIL 2013 
* Model Version: Final 1.00
*
*****************************************************************************
*
* Updates:
*
* Final 1.00
* Release to Web
*
*****************************************************************************
*
* Model Usage Notes:
*
* 1. Use the average model to see the loop response.
* 2. Temperature effects are not modeled
* 3. Only those pins affecting the loop response have been shown. 
*    Other pins have been left out of this model. 
*
*****************************************************************************
.SUBCKT TPS65320-Q1_AVERAGE SW COMP VIN FB1 GND PARAMS: L=10U FS = 400K
X_U2         VIN SW GND COMP N1165537 CCMDCMCM PARAMS:  L={L} FS={FS}
R_R19         GND COMP  322E6 TC=0,0 
C_C13         GND COMP  8.24p  TC=0,0 
V_V28         N1165059 GND 0.8
G_G2         GND COMP N1165059 FB1 310u
.ENDS TPS65320-Q1_AVERAGE
*$
.SUBCKT ccmdcmcm a c p vc dc params: L=35u Fs=200k 
Edc dcx 0 value = { v(d2)*v(cx,p)/(v(a,p)-v(cx,p)+1u) }
Xdc dcx dc l1 params: clampH=0.99 clampL=7m
Ed2 d2X 0 value = { (2*{L}*{Fs}*I(VM)/(V(dc)*V(a,cx)+1u)) - V(dc) }
Xd2 d2X dc d2 l2
ESe Se 0 value = {1.31*{Fs}*V(Ri)}
Eri Ri 0 value = {107m}
GIap a p value = { (V(dc)/(V(dc)+V(d2)+1u))*I(VM) }
GIpc p cx value = { V(vc)/V(Ri)}
GImju cx p value = {V(Se)*V(dc)/(V(Ri)*{Fs}) + 
+ (v(cx,p)/{L})*V(d2)*{1/Fs}*(1-(V(dc)+V(d2))/2) }
VM cx c
XC1 cx p mode VAC ;
Emode mode 0 Value = { IF ((2*{L}*{Fs}*I(VM)/(V(dc)*V(a,cx)+1u))
+ < 1, 1f , {1/((L)*(3.14*Fs)^2)}) } ; 
.ENDS ccmdcmcm
*$
.subckt l1 d dc params: clampH=0.99 clampL=16m
Gd 0 dcx VALUE = { V(d)*100u }
Rdc dcx 0 10k
V1 clpn 0 {clampL}
V2 clpp 0 {clampH}
D1 clpn dcx dclamp
D2 dcx clpp dclamp
Edc dc 0 value={ V(dcx) }
.model dclamp d n=0.01 rs=100m
.ENDS l1
*$
.subckt l2 d2nc d d2c
Gd 0 d2cx d2nc 0 100u
Rdc d2cx 0 10k
V1 clpn 0 7m
E2 clpp 0 Value = { 1-V(d) }
D1 clpn d2cx dclamp
D2 d2cx clpp dclamp
Edc d2c 0 value={ V(d2cx) }
.model dclamp d n=0.01 rs=100m
.ENDS l2
*$
.SUBCKT VAC 1 2 CTRL
R1 1 3 1u
VC 3 4
EBC 4 2 Value = { (1/v(ctrl))*v(int) }
GINT 0 INT Value = { I(VC) }
CINT INT 0 1
Rdum INT 0 10E10
.ENDS VAC
*$
.SUBCKT LDCR IN OUT
+ PARAMs:  L=1u DCR=0.01 
L             IN 1  {L} 
RDCR	1 OUT {DCR}
.ENDS LDCR
*$
*
.SUBCKT CESR IN OUT
+ PARAMs:  C=100u ESR=0.01 X=2 
C	IN 1  {C*X} 
RESR	1 OUT {ESR/X}
.ENDS CESR
*$


