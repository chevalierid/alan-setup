***********************************************************************
*  (C) Copyright 2013 Texas Instruments Incorporated. All rights reserved.
* TI Information - Selective Disclosure to Frank Sun
***********************************************************************
***********************************************************************
* Notice:
* The data and models provided herein are intended to be used by 
* customers as an aid in designing electrical circuits using 
* semiconductor devices manufactured &/or sold by Texas Instruments.
* The models and associated information contained herein are not to
* be construed as a guarantee of electrical performance and/or device 
* specification.  Therefore, Texas Instruments does not assume any 
* liability arising out of the use of said data or models, nor from
* the devices or products manufactured therefrom.  Texas Instruments
* does not convey any license under its patent rights nor the rights
* of others.  Texas Instruments reserves the right to change models 
* without prior notice.  
*
* TI is a registered trademark of Texas Instruments Inc.
*************************************************************************
********************************************************************************
*
* Released by: WEBENCH(R) Design Center, Texas Instruments Inc.
* Part: CSD87588N
* Date: 04/18/2013
* Model Type: TRANSIENT
* Simulator: PSPICE
* Datasheet: SLPS384 - MARCH 2013
*
*****************************************************************************
*$
**********************************************************************
**********************************************************************
*                                                                    *
* CSD87588N   -  PSpice Model for use with OrCAD                   *
*                                                                    *
**********************************************************************
* SUBCKT Definition:  1=VIN  2=TG  3=BG 4=PGND 5=VSW
**********************************************************************
.SUBCKT CSD87588N   1 2 3 4 5
XH	1	2	5	N37524  
XL	5	3	4	N17517
.ends CSD87588N
*$
**********************************************************************
**********************************************************************
*                                                                    *
* N17517   -  PSpice Model for use with OrCAD                   *
*                                                                    *
**********************************************************************
**********************************************************************
* SUBCKT Definition:  1=D  2=G  3=S
**********************************************************************
.SUBCKT N17517   1 2 3
* PARAMETER (local) DEFINITIONS SECTION
 .PARAM  ptrc1    10.0e-3  
 .PARAM  ptrc2    19.00e-6
 .PARAM  pwidthP  3.68458333333
* .PARAM  pwidthP  3.9199333333
 .PARAM  pwidth   {pwidthP*1e6}
 .PARAM  perimP   {2.1*pwidthP}
 .PARAM  perim    {perimP*1e6}
****** MODEL SUBCIRCUIT BEGINS HERE     ******************************
M1   10 11 12 12  NMOS	W={pwidthP} L=0.5u  PS={perimP} PD={perimP}
DBD   3  1	 DBD
CBD0  8  7   3e-12
RBD0  8  7   1e10
*CGD0  5  7	 100E-12
CGS0  5  8	 0.5e-12
*M2   12 11 12 20  PMOSd W={pwidthP} L=0.1u PS={perimP} PD={perimP}
*DDx  20  10  DDx
DDG1  10	101	DD1
DDG2  5	101	DD1
* Note:  gate oxide capacitance included in NMOS below
CGS   5 13	 377E-12
RGS  13  8	 35
LGG   2  5	 2.1e-9
RGG   5 11	 0.8
RSB  12  9	 RTEMP 0.05e-3
RSS   9  8 	 RTEMP 0.15e-3
RSP   8  6 	 0.45e-3
LSS   6  3	 0.47e-9
RSL   6  3   0.5
RDD   7 10 	 RTEMP 0.90e-3
RDP   4  7	 {0.90e-3-0.075e-3+0.2e-3}
LDD   1  4	 0.05e-9
******************************************************************* 
.MODEL  NMOS   NMOS (
* CONTROL PARAMETERS
+ LEVEL  = 7             VERSION = 3.2         CAPMOD = 2
* DC PARAMETERS
+ NLX    = 1.74E-7       VBM    = -5           DVT0   = 2.2
+ DVT1   = 0.53          DVT2   = -0.032       U0     = 650
+ UA     = 2.5e-9        UB     = 2.4e-18      A0     = 1.10
+ AGS    = 0.0           A1     = 0.0          A2     = 1.0
+ LINT   = 0.055e-6      VOFF   = -0.24        NFACTOR= 1.7
+ ETA0   = 0.005         ETAB   = -0.07        DSUB   = 1.4
+ PCLM   = 1.30          PDIBLC1= 0.02         PDIBLC2= 0.004
+ DROUT  = 0.90          PSCBE1 = 3.0e8        PSCBE2 = 1e-6
+ PVAG   = 0.1           DELTA  = 0.0220       JS     = 0
* AC AND CAPACITANCE PARAMETERS
+ CGSO   = 150.00e-12		CGDO   = 1e-18        CGBO   = 1e-18
+ CJ     = 1e-18         CJSW   = 1e-18        CF     = 0
* TEMPERATURE PARAMETERS
+ TNOM   = 25            UTE    = -1.5         KT1    = -0.8
+ KT1L   = 1.0e-15       KT2    = 0.022        UA1    = 1.75e-9
+ UB1    = -0.8e-18      
* PROCESS PARAMETERS
+ TOX    = 3.00e-8       XJ     = 3e-7          NCH    = 1.285e17
+ NSUB   = 1.0e16)
******************************************************************* 
.MODEL  PMOSd   PMOS (   LEVEL  = 1
+ TNOM   = 25            VTO    = -1.0          IS     = 6.0e-14
+ TOX    = 3.0e-8        NSUB   = 2.0e17		GAMMA  = 15
+ CJ     = 1e-18         CJSW   = 1e-18
+ CGSO   = 1e-18         CGDO   = 1e-18         CGBO   = 0  )
*******************************************************************
.MODEL DBD D (CJO=3150e-12 VJ=1.0 M=0.625 TNOM=25
+ FC=0.5 TT=2e-09 XTI=3.3 BV=31
+ IS=8.0e-12 N=1.050 RS=0.50e-3 TRS1=4.0e-3)
.MODEL DD1  D (CJO=0.85e-09 VJ=0.9 M=0.7 IS=1e-13 RS=0.1 TNOM=25)
.MODEL DD2  D (CJO=4.00e-10 VJ=0.90 M=0.9 IS=1e-13 RS=0.1 TNOM=25)
.MODEL DDx D (CJO=0.0001e-09 VJ=0.9 M=0.9 IS=1e-13 RS=1.0 TNOM=25)
*******************************************************************
.MODEL RTEMP RES (TC1={ptrc1} TC2={ptrc2})
*******************************************************************
.ENDS N17517
*$
**********************************************************************
*                    	                                                *
*   n37524   -  PSpice Model                                         *
*  		                                                  *
**********************************************************************
**********************************************************************
* SUBCKT Definition:  1=D  2=G  3=S
**********************************************************************
.SUBCKT n37524  1 2 3
* PARAMETER (local) DEFINITIONS SECTION
.PARAM  pWidth  0.8243781095
.PARAM  pLen    0.55u
.PARAM  perim   {2.1*pWidth}
.PARAM  ptrc1   2.25e-3		; temp co of ext Rd / Rs
.PARAM  ptrc2   7.50e-6
****** MODEL SUBCIRCUIT BEGINS HERE     ******************************
M1    10 11 12 12  NMOS	W={pWidth} L={pLen} PS={perim} PD={perim}
DBD   3  1	 DBD
CBD0  3  1   10e-12
RBD0  3  1   9e9
CGD0  2  13	 50E-12
RGD   13 1   2e6
CGS0  2  3	 6e-12
M2    12 11 12 20  PMOSd W={pWidth} L=0.04u PS={perim} PD={perim}
CDx   20 12  2p
DDx   20 10  DDx
DDG   10 14	 DD
DGD   11 14	 DD
RGG   2  11	 1.2
.param	a	4.8e-3
RSB   12 9	 RTEMP {a/12}
RSS   9  3 	 RTEMP {a/12}
RDP	7	10	0.7e-3
RDD   1  7  RTEMP {a*5/6+1.575e-3+0.27e-3}
******************************************************************* 
.MODEL  NMOS   NMOS (    LEVEL  = 7             Version= 3.2  
+ TNOM   = 27            LINT   = 0.055e-6      CAPMOD = 2
+ TOX    = 3.00e-8       NCH    = 1.825e17       NSUB   = 1.0e16
+ AGS    = 0.0           PVAG   = 0.0           U0     = 650
+ A0     = 1.0           A1     = 0.0           A2     = 1.0
+ UA     = 0.25e-9       UB     = 5.87e-19      VBM    = -10
+ UA1    = 3.00e-9       UB1    = -7.61e-18     UTE    = -1.50
+ KT1    = -0.87         KT2    = 0.022         KT1L   = 1.0e-15
+ DVT0   = 2.2           DVT1   = 0.53	      DVT2   = -0.033
+ ETA0   = 0.080         ETAB   = -0.07         XJ     = 4e-7
+ PDIBLC1= 0.40          PDIBLC2= 0.0086        NLX    = 1.75e-7
+ PSCBE1 = 4.24e8        PSCBE2 = 1e-5          PCLM   = 1.75
+ VOFF   = -0.28         NFACTOR= 1.4           JS     = 0
+ DROUT  = 0.9           DSUB   = 1.3           DELTA  = 0.01
+ CJ     = 1e-18         CJSW   = 1e-18         CF     = 0
+ CGSO   = 270.00e-12    CGDO   = 0.1e-12       CGBO   = 1e-18  )
******************************************************************* 
.MODEL  PMOSd   PMOS (   LEVEL  = 1
+ TNOM   = 27            VTO    = -0.30         IS     = 5e-16
+ TOX    = 3.00e-8       gamma  = 10.0
+ CJ     = 1e-18         CJSW   = 1e-18
+ CGSO   = 1e-18         CGDO   = 1e-18         CGBO   = 1e-18  )
******************************************************************* 
.MODEL DBD D (CJO=8.00e-10 VJ=0.90 M=0.35 TNOM=27 FC=0.5 TT=1e-09 
+ XTI=2.0 BV=31 IS=4e-12 N=1.050 RS=3.0e-3 TRS1=0.0e-3
+ ISR=1e-11 NR=2.0)
.param kkk 1.40e-10
.MODEL DD  D (CJO={kkk} VJ=0.90 M=0.90 IS=1e-12 RS=1 TNOM=27)
.MODEL DDx D (CJO={kkk/10} VJ=1.0 M=0.9 IS=1e-13 RS=1.0 TNOM=27)
*******************************************************************
.MODEL RTEMP RES (TC1={ptrc1} TC2={ptrc2})
*******************************************************************
.ENDS n37524
*