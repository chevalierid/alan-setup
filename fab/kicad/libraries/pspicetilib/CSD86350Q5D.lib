***********************************************************************
**  v1.7.5 PSpice Models Library - TI NexFET Power N/P Channel FET's
*  (C) Copyright 2010 Texas Instruments Incorporated. All rights reserved.
***********************************************************************
***********************************************************************
* Notice:
* The data and models provided herein are intended to be used by 
* customers as an aid in designing electrical circuits using 
* semiconductor devices manufactured &/or sold by Texas Instruments.
* The models and associated information contained herein are not to
* be construed as a guarantee of electrical performance and/or device 
* specification.  Therefore, Texas Instruments does not assume any 
* liability arising out of the use of said data or models, nor from
* the devices or products manufactured therefrom.  Texas Instruments
* does not convey any license under its patent rights nor the rights
* of others.  Texas Instruments reserves the right to change models 
* without prior notice.  
*
* TI is a registered trademark of Texas Instruments Inc.
*************************************************************************
********************************************************************************
*
* Released by: Analog e-Lab Design Center, Texas Instruments Inc.
* Part: CSD86350Q5D
* Date: 12/08/2010
* Model Type: TRANSIENT
* Simulator: PSPICE
* Datasheet: SLPS223C - May 2010 - Revised November 2010
*
*****************************************************************************
*
* Updates:
*
* Final 1.00 - 12/08/2010
* Release to Web
*
* Final 1.10 - 04/08/2011
* Updated temp performance
*
*****************************************************************************
*$
**********************************************************************
**********************************************************************
*                                                                    *
* CSD86350Q5D  -  P-Spice Model, for use with OrCAD Spice Simulator  *
*                                                                    *
*   Stacked die Q5 assembly for power block                          *
*                                                                    *
**********************************************************************
**********************************************************************
* Subcircuit Instantiation Notes:
*  nFET SUBCKT Definition:  1=D  2=G  3=S
*  SUBCKT Definition:  1=2=Vin 3=TG 4=TGR 5=BG 6=7=8=VSW 9=PGND
*  - see csd86350q5d data sheet for further details
**********************************************************************
.SUBCKT csd86350q5d  1 2 3 4 5 6 7 8 9
XnfetHS  18  17  19  n36303
XnfetLS  19  16  15  n36301
RGls      5  10      22e-3
RGhs      3  11      26e-3
RDhs      1  12      0.9e-3
RDhs2     2  12      0.9e-3
RGlr      4  13      22e-3
Rpsn      8  14      0.60e-3
Rpsn2     7  14      0.60e-3
Rpsn3     6  14      0.60e-3
RSls      9  15      0.001e-3
LGls     10  16      1.54e-9
LGhs     11  17      1.94e-9
LDhs     12  18      0.5e-9
RLDhs    12  18      4
LGlr     13  19      1.54e-9
Lpsn     14  19      0.45e-9
.ENDS csd86350q5d
*$
**********************************************************************
**********************************************************************
*                                                                    *
*   n36301   -  PSpice Model -  STATUS:  PRELIMINARY                 *
*                                                                    *
*   n36301 is the LS silicon die model for use with 86350q5d         *
*                                                                    *
**********************************************************************
**********************************************************************
* SUBCKT Definition:  1=D  2=G  3=S
**********************************************************************
.SUBCKT n36301  1 2 3
* PARAMETER (local) DEFINITIONS SECTION
.PARAM  pWidth  3.501		; m, total gate width
.PARAM  pLen    0.5u
.PARAM  perim   {2*pWidth}	; m, gate/drain perim (rough est)
.PARAM  ptrc1   1.2e-3		; temp co of ext Rd / Rs
.PARAM  ptrc2   4.1e-6
****** MODEL SUBCIRCUIT BEGINS HERE     ******************************
M1   10 11 12 12  NMOS	W={pWidth} L={pLen}  PS={perim} PD={perim}
DBD   3  1	 DBD
CBD0  3  1   10e-12
RBD0  3  1   9e9
CGD0  2 13	 5E-12
RGD  13  1   2e6
CGS0  2  3	 6e-12
M2   12 11 12 20  PMOSd W={pWidth} L=0.093u PS={perim} PD={perim}
RDx  20 12   1e14
CDx  20 12   2p
DDx  20 10   DD
RGG   2 11	 1.33
RSB  12  9	 RTEMP 0.42e-3
RSS   9  3 	 RTEMP 0.52e-3
RDD   1 10 	 RTEMP 0.27e-3
******************************************************************* 
.MODEL  NMOS   NMOS (    LEVEL  = 7             Version = 3.2  
+ TNOM   = 27            LINT   = 0.058e-6      CAPMOD = 2
+ TOX    = 1.75e-8       NCH    = 2.190e17      NSUB   = 1.0e16
+ AGS    = 0.0           PVAG   = 0.0           U0     = 600
+ A0     = 1.0           A1     = 0.0           A2     = 0.75
+ UA     = 2.00e-9       UB     = 0.9e-18       VBM    = -10
+ UA1    = 2.0e-9        UB1    = -0.5e-18      UTE    = -1.50
+ KT1    = -0.59         KT2    =-0.022         KT1L   = 1.0e-15
+ DVT0   = 2.1           DVT1   = 0.45	      DVT2   = -0.033
+ ETA0   = 0.020         ETAB   = -0.07         XJ     = 3e-7
+ PDIBLC1= 0.08          PDIBLC2= 0.006         NLX    = 1.75e-7
+ PSCBE1 = 2.5e8         PSCBE2 = 1e-7          PCLM   = 1.5
+ VOFF   = -0.21         NFACTOR= 1.41          JS     = 0
+ DROUT  = 0.9           DSUB   = 1.4           DELTA  = 0.022
+ CJ     = 1e-18         CJSW   = 1e-18         CF     = 0
+ CGSO   = 341e-12       CGDO   = 0.1e-12       CGBO   = 0  )
******************************************************************* 
.MODEL  PMOSd   PMOS (   LEVEL  = 1
+ TNOM   = 27            VTO    = -0.6
+ TOX    = 1.75e-8       NSUB   = 2e18	      JS     = 0
+ CJ     = 1e-18         CJSW   = 1e-18         CF     = 0
+ CGSO   = 0             CGDO   = 0             CGBO   = 0  )
******************************************************************* 
.MODEL DBD D (CJO=3.58e-9 VJ=0.90 M=0.33 TNOM=27 FC=0.5 TT=1e-09 
+ XTI=2.3 BV=26 IS=6.5e-12 N=1.010 RS=6.8e-4 TRS1=4.2e-3
+ ISR=1e-11 NR=2.0)
.MODEL DD  D (CJO=5.4e-10 VJ=0.70 M=0.90 IS=1e-13 RS=1 TNOM=27)
*******************************************************************
.MODEL RTEMP RES (TC1={ptrc1} TC2={ptrc2})
*******************************************************************
.ENDS n36301
*$
**********************************************************************
**********************************************************************
*                                                                    *
*   n36303   -  PSpice Model -  STATUS:  PRELIMINARY                 *
*                                                                    *
*   n36303 is the HS die silicon model for use with 86350q5d         *
*                                                                    *
**********************************************************************
**********************************************************************
* SUBCKT Definition:  1=D  2=G  3=S
**********************************************************************
.SUBCKT n36303  1 2 3
* PARAMETER (local) DEFINITIONS SECTION
.PARAM  pWidth  1.58091		; m, total gate width
.PARAM  pLen    0.5u
.PARAM  perim   {2*pWidth}	; m, gate/drain perim (rough est)
.PARAM  ptrc1   1.3e-3		; temp co of ext Rd / Rs
.PARAM  ptrc2   7.5e-6
****** MODEL SUBCIRCUIT BEGINS HERE     ******************************
M1   10 11 12 12  NMOS	W={pWidth} L={pLen}  PS={perim} PD={perim}
DBD   3  1	 DBD
CBD0  3  1   10e-12
RBD0  3  1   9e9
CGD0  2 13	 5E-12
RGD  13  1   2e6
CGS0  2  3	 6e-12
M2   12 11 12 20  PMOSd W={pWidth} L=0.080u PS={perim} PD={perim}
CDx  20 12   2p
DDx  20 10   DD
* Note:  gate oxide capacitance included in NMOS below
RGG   2 11	 1.42
RSB  12  9	 RTEMP 1.01e-3
RSS   9  3 	 RTEMP 1.27e-3
RDD   1 10 	 RTEMP 0.70e-3
******************************************************************* 
.MODEL  NMOS   NMOS (    LEVEL  = 7             Version = 3.2  
+ TNOM   = 27            LINT   = 0.058e-6      CAPMOD = 2
+ TOX    = 1.75e-8       NCH    = 2.630e17      NSUB   = 2.5e16
+ AGS    = 0.0           PVAG   = 0.30          U0     = 600
+ A0     = 1.10          A1     = 0.0           A2     = 1.0
+ UA     = 1.10e-9       UB     = 2.5e-18       VBM    = -10
+ UA1    = 2.45e-9       UB1    = -1.5e-18      UTE    = -1.50
+ KT1    = -0.77         KT2    =-0.022         KT1L   = 1.0e-15
+ DVT0   = 2.1           DVT1   = 0.48	      DVT2   = -0.033
+ ETA0   = 0.020         ETAB   = -0.07         XJ     = 3e-7
+ PDIBLC1= 0.05          PDIBLC2= 0.005         NLX    = 1.75e-7
+ PSCBE1 = 2.5e8         PSCBE2 = 1e-7          PCLM   = 1.35
+ VOFF   = -0.22         NFACTOR= 1.65          JS     = 0
+ DROUT  = 0.9           DSUB   = 1.4           DELTA  = 0.042
+ CJ     = 1e-18         CJSW   = 1e-18         CF     = 0
+ CGSO   = 308e-12       CGDO   = 0.1e-12       CGBO   = 0  )
******************************************************************* 
.MODEL  PMOSd   PMOS (   LEVEL  = 1
+ TNOM   = 27            VTO    = -0.6
+ TOX    = 1.75e-8       NSUB   = 2e18	      JS     = 0
+ CJ     = 1e-18         CJSW   = 1e-18         CF     = 0
+ CGSO   = 0             CGDO   = 0             CGBO   = 0  )
******************************************************************* 
.MODEL DBD D (CJO=1.46e-9 VJ=0.88 M=0.32 TNOM=27 FC=0.5 TT=1e-09 
+ XTI=3.7 BV=26 IS=5.9e-13 N=1.010 RS=9.2e-4 TRS1=4.3e-3
+ ISR=1e-11 NR=2.0)
.MODEL DD  D (CJO=2.3e-10 VJ=0.75 M=0.90 IS=1e-13 RS=1 TNOM=27)
*******************************************************************
.MODEL RTEMP RES (TC1={ptrc1} TC2={ptrc2})
*******************************************************************
.ENDS n36303