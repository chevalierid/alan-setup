***********************************************************************
**  v1.7.5 PSpice Models Library - TI NexFET Power N/P Channel FET's
*  (C) Copyright 2010 Texas Instruments Incorporated. All rights reserved.
***********************************************************************
***********************************************************************
* Notice:
* The data and models provided herein are intended to be used by 
* customers as an aid in designing electrical circuits using 
* semiconductor devices manufactured &/or sold by Texas Instruments.
* The models and associated information contained herein are not to
* be construed as a guarantee of electrical performance and/or device 
* specification.  Therefore, Texas Instruments does not assume any 
* liability arising out of the use of said data or models, nor from
* the devices or products manufactured therefrom.  Texas Instruments
* does not convey any license under its patent rights nor the rights
* of others.  Texas Instruments reserves the right to change models 
* without prior notice.  
*
* TI is a registered trademark of Texas Instruments Inc.
*************************************************************************
********************************************************************************
*
* Released by: Analog e-Lab Design Center, Texas Instruments Inc.
* Part: CSD16412Q5A
* Date: 12/08/2010
* Model Type: TRANSIENT
* Simulator: PSPICE
* Datasheet: SLPS207A - August 2009 - Revised September 2010
*
*****************************************************************************
*$
**********************************************************************
**********************************************************************
*                                                                    *
* CSD16412Q5A  -  PSpice Model for use with OrCAD                    *
*                                                                    *
**********************************************************************
**********************************************************************
*
* SUBCKT Definition:  1=D  2=G  3=S
*
**********************************************************************
.SUBCKT CSD16412Q5A  1 2 3
**********************************************************************
* PARAMETER (local) DEFINITIONS SECTION
****** Package Contributions *****************************************
.PARAM  pRclip    0.81e-3	 ; Ohms (source clip / bond wire res)
.PARAM  pRGwire   0.12		 ; Ohms  (gate wire resistance)
* RG_wire = 0.1 for q5;  RG_wire = 0.25 for q3  (estimate)
****** Device Geometry       *****************************************
.PARAM  pWidth  0.6178256     ; m, scale width 
.PARAM  perim  {2*pWidth}
.PARAM  ptrc1  3.1e-3		 ; temp coef of extr Rd / Rs
.PARAM  ptrc2  7.2e-6
**********************************************************************
M1   10 11 12 12  NMOS	W={pWidth} L=0.5u  PS={perim} PD={perim}
DBD   8  7	 DBD
CGD0  5  7	 1E-12
CGS0  5  8	 6e-12
M2   12 11 12 10  PMOSd W={pWidth} L=0.173u PS={perim} PD={perim}
DDG  14  5	 DD
DGD  14 10	 DD
* Note:  gate oxide capacitance included in NMOS below
CGS   5 13	 53e-12
RGS  13  8	 1450
LGG   2  5	 1.45e-9
RGG   5 11	 {0.550 + pRGwire}
RSB  12  9	 RTEMP 1.92e-3
RSS   9  8 	 0.47e-3
RSP   8  6 	 {1e-6 + pRclip}
LSS   6  3	 0.38e-9
RDP   4  7	 0.55e-3
RDD   7 10 	 RTEMP 3.05e-3
LDD   1  4	 0.05E-9
******************************************************************* 
.MODEL  NMOS   NMOS (    LEVEL = 7             Version = 3.2  
+ TNOM   = 27            LINT   = 0.070e-6      CAPMOD = 2
+ TOX    = 3.0E-8        NCH    = 1.390e17      NSUB   = 5e15
+ AGS    = 0.0           PVAG   = 1.1
+ A0     = 1.00          A1     = 0.0           A2     = 0.90
+ UA     = 1.10e-9       UB     = 1.0e-18       VBM    = -5
+ UA1    = 2.4e-9        UB1    = -5.5e-18      UTE    = -1.50
+ KT1    = -0.81         KT2    =-0.022         KT1L   = 1.0e-12
+ DVT0   = 2.3           DVT1   = 0.55	      DVT2   = -0.033
+ ETA0   = 0.045         ETAB   = -0.07         XJ     = 3e-7
+ PDIBLC1= 0.08          PDIBLC2= 0.006         NLX    = 1.7e-7
+ PSCBE1 = 0.3e8         PSCBE2 = 2.5e-8        PCLM   = 1.25
+ VOFF   = -0.09         NFACTOR= 2.3           JS     = 0
+ DROUT  = 0.8           DSUB   = 1.6           DELTA  = 0.01
+ CJ     = 1e-18         CJSW   = 1e-18         CF     = 0
+ CGSO   = 185e-12       CGDO   = 7.0e-12       CGBO   = 0  )
******************************************************************* 
.MODEL  PMOSd   PMOS (   LEVEL  = 7            Version = 3.2
+ TNOM = 27              CAPMOD = 2
+ JS     = 0             VBM    = -10
+ TOX    = 3.0e-8        NCH    = 1.20e17       NSUB   = 6e16
+ CJ     = 1e-18         CJSW   = 1e-18         CF     = 0
+ CGSO   = 0             CGDO   = 0             CGBO   = 0 )
******************************************************************* 
.MODEL DBD D (CJO=0.996e-9 VJ=1.0 M=0.440 TNOM=27 FC=0.5 TT=5e-09 
+ XTI=4.0 BV=27 IS=5.35e-13 N=1.02 RS=1.57e-3 TRS1=3.5e-3)
.MODEL DD  D (CJO=9e-12 VJ=0.40 M=0.33 IS=1e-12 RS=0.1 TNOM=27)
*******************************************************************
.MODEL RTEMP RES (TC1={ptrc1} TC2={ptrc2})
*******************************************************************
.ENDS CSD16412Q5A
