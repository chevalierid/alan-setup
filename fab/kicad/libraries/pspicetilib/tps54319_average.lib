*$
* TPS54319
*****************************************************************************
* (C) Copyright 2011 Texas Instruments Incorporated. All rights reserved.
*****************************************************************************
** This model is designed as an aid for customers of Texas Instruments.
** TI and its licensors and suppliers make no warranties, either expressed
** or implied, with respect to this model, including the warranties of
** merchantability or fitness for a particular purpose. The model is
** provided solely on an "as is" basis. The entire risk as to its quality
** and performance is with the customer
*****************************************************************************
*
** Released by: Analog eLab Design Center, Texas Instruments Inc.
* Part: TPS54319
* Date: 06SEP2011
* Model Type: Average
* Simulator: PSpice
* Simulator Version: 16.0.0.p001
* EVM Order Number: TPS54319EVM-626 
* EVM Users Guide: SLVU377–May 2010
* Datasheet: SLVSA83 –JUNE 2010
* Model Version: Final 1.00
*
*****************************************************************************
*
* Updates:
*
* Final 1.00
* Release to Web.
*
***************************************************************************
.SUBCKT TPS54319_AVERAGE VSNS PH COMP VIN GND PARAMS: L=1.5u FS=1000K
X_U1         VIN PH GND COMP N46091 CCM PARAMS:  L={L} FS={FS} RI=0.063
+  SE=100M ACS=1
G_G1         GND COMP N45871 VSNS 245u
R_R7         GND COMP  2.2e6 TC=0,0 
C_C4         GND COMP  5p  TC=0,0 
V_V2         N45871 GND 0.827
.ENDS TPS54319_AVERAGE
*$
.SUBCKT CCM a c p vc dc params: L=1.5u Fs=1000k Ri=55.55m Acs=1
ESe Se 0 value = {11*{Fs}*{Ri}}
Edc dcx 0 value = { v(cx,p)/(v(a,p)+1u) }
Xdc dcx dc limit params: clampH=0.98 clampL=25m
Ed2 d2X 0 value = {(1 - V(dc))} 
Xd2 d2X dc d2 limit2
GIap a p value =  {V(dc)*I(VM)} 
GIpc p cx value = { V(vc)/({Acs}*{Ri}) }
GImju cx p value = {V(Se)*V(dc)/({Acs}*{Ri}*{Fs}) + (v(cx,p))/({L}*{Fs})*((1-(V(dc))/2)) }
VM cx c
XC1 cx p mode varicap ; 
Emode mode 0 Value = { IF ((2*{L}*{Fs}*I(VM)/(V(dc)*V(a,cx)+1u))
+ < 1, {4/({L}*(6.28*{Fs})^2)}, {4/({L}*(6.28*{Fs})^2)}) } ;
.ENDS
*$
.SUBCKT VARICAP 1 2 CTRL
R1 1 3 1u
VC 3 4
EBC 4 2 Value = { (1/v(ctrl))*v(int) }
GINT 0 INT Value = { I(VC) }
CINT INT 0 1
Rdum INT 0 10E10
.ENDS
*$
.subckt limit d dc params: clampH=0.99 clampL=48m
Gd 0 dcx VALUE = { V(d)*100u }
Rdc dcx 0 10k
V1 clpn 0 {clampL}
V2 clpp 0 {clampH}
D1 clpn dcx dclamp
D2 dcx clpp dclamp
Edc dc 0 value={ V(dcx) }
.model dclamp d n=0.01 rs=100m
.ENDS
*$
.subckt limit2 d2nc d d2c
Gd 0 d2cx d2nc 0 100u
Rdc d2cx 0 10k
V1 clpn 0 48m
E2 clpp 0 Value = { 1-V(d) }
D1 clpn d2cx dclamp
D2 d2cx clpp dclamp
Edc d2c 0 value={ V(d2cx) }
.model dclamp d n=0.01 rs=100m
.ENDS
*$
.SUBCKT LDCR IN OUT
+ PARAMs:  L=1u DCR=0.01 IC=0
L	IN 1  {L} 
RDCR	1 OUT {DCR}
.ENDS LDCR
*$
.SUBCKT CESR IN OUT
+ PARAMs:  C=100u ESR=0.01 X=2 IC=0
C	IN 1  {C*X} 
RESR	1 OUT {ESR/X}
.ENDS CESR
*$

