***********************************************************************
**  v1.7.5 PSpice Models Library - TI NexFET Power N/P Channel FET's
*  (C) Copyright 2011 Texas Instruments Incorporated. All rights reserved.
***********************************************************************
***********************************************************************
* Notice:
* The data and models provided herein are intended to be used by 
* customers as an aid in designing electrical circuits using 
* semiconductor devices manufactured &/or sold by Texas Instruments.
* The models and associated information contained herein are not to
* be construed as a guarantee of electrical performance and/or device 
* specification.  Therefore, Texas Instruments does not assume any 
* liability arising out of the use of said data or models, nor from
* the devices or products manufactured therefrom.  Texas Instruments
* does not convey any license under its patent rights nor the rights
* of others.  Texas Instruments reserves the right to change models 
* without prior notice.  
*
* TI is a registered trademark of Texas Instruments Inc.
*************************************************************************
********************************************************************************
*
* Released by: Analog e-Lab Design Center, Texas Instruments Inc.
* Part: CSD17327Q5A
* Date: 11/30/2011
* Model Type: TRANSIENT
* Simulator: PSPICE
* Datasheet: SLPS332 - JUNE 2011
*
*****************************************************************************
*$
**********************************************************************
**********************************************************************
*                                                                    *
* CSD17327Q5A   -  PSpice Model for use OrCAD / Allegro Simulator    *
*                                                                    *
**********************************************************************
**********************************************************************
* SUBCKT Definition:  1=D  2=G  3=S
**********************************************************************

.SUBCKT CSD17327Q5A  1 2 3
.PARAM  ptrc1   3.75e-3  
.PARAM  ptrc2   6.50e-6
.PARAM  pwidthP 0.6120192
.PARAM  pwidth  {pwidthP*1e6}
.PARAM  perimP  {2.1*pwidthP}
.PARAM  perim   {perimP*1e6}
*pkgL = logical value:  0 for Si Die ONLY;  1 for packaged part.
.PARAM  pkgL    1.0
M1   10 11 12 12  NMOS	W={pwidthP} L=0.5u  PD={perimP}  PS={perimP}
DBD   8  7	 DBD
RDB   8  7   1e10
CGD0  5  7	 {pkgL*1E-12 + 1e-14}
CGS0  5  8	 {pkgL*2e-12 + 1e-14}
CDS0 10 12	 {pkgL*5e-12 + 1e-14}
M2   12 11 12 20  PMOSd W={pwidthP} L=0.085u  PD={perimP}  PS={perimP}
DDx  20 10   DDx
DDG  14  5   DD
DGD  14 10   DD
* Note:  gate oxide capacitance included in NMOS below
CGS   5 13	 60E-12
RGS  13  8	 225
LGG   2  5	 {pkgL*1.3e-9 + 0.1e-9}
RGG   5 11	 {pkgL*0.17 + 6.3}
RSB  12  9	 RTEMP 0.90E-3
RSS   9  8 	 0.05e-3
RSP   8  6 	 {pkgL*0.60e-3 + 0.02e-3}
LSS   6  3	 {pkgL*0.53E-9 + 0.02e-9}
RSL   6  3   1.5
RDP   4  7	 {pkgL*0.65e-3 + 0.05e-3}
RDD   7 10 	 RTEMP 4.50e-3
LDD   1  4	 0.05E-9
******************************************************************* 
.MODEL  NMOS   NMOS (    LEVEL  = 7             Version = 3.2
+ TNOM   = 27            LINT   = 0.045e-6      CAPMOD = 2
+ TOX    = 3.3e-8        NCH    = 1.253e17      NSUB   = 1.0e16
+ AGS    = 0.0           PVAG   = 0.08
+ A0     = 1.10          A1     = 0.0           A2     = 0.85
+ UA     = 1.10e-9       UB     = 1.5e-18       VBM    = -5.0
+ UA1    = 0.9e-9        UB1    = -0.5e-18      UTE    = -1.50
+ KT1    = -0.81         KT2    = 0.022         KT1L   = 1.0e-15
+ DVT0   = 2.2           DVT1   = 0.53	      DVT2   = -0.032
+ ETA0   = 0.045         ETAB   = -0.07         XJ     = 3e-7
+ PDIBLC1= 0.001         PDIBLC2= 0.015         NLX    = 1.78e-7
+ PSCBE1 = 6.0e8         PSCBE2 = 1e-8          PCLM   = 1.3
+ VOFF   = -0.16         NFACTOR= 1.9           JS     = 0
+ DROUT  = 0.7           DSUB   = 1.0           DELTA  = 0.025
+ CJ     = 1e-18         CJSW   = 1e-18         CF     = 0
+ CGSO   = 199e-12       CGDO   = 24e-12        CGBO   = 0    )
******************************************************************* 
.MODEL  PMOSd   PMOS (   LEVEL  = 1
+ TNOM   = 27            VTO    = -0.9		IS     = 6.0e-14
+ TOX    = 3.3e-8        NSUB   = 2.5e18        GAMMA  = 4
+ CJ     = 1e-18         CJSW   = 1e-18
+ CGSO   = 1e-18         CGDO   = 1e-18         CGBO   = 1e-18 )
******************************************************************* 
.MODEL DBD D (CJO=1202e-12 VJ=0.90 M=0.54 FC=0.5 TT=2e-09 TNOM=27
+ IS=1.0e-12 N=1.01 RS=2.9e-3 TRS1=4.2e-3 XTI=3.5 BV=33)
.MODEL DDx D (CJO=25e-12 VJ=0.45 M=0.80 IS=1e-12 RS=0.1 TNOM=27)
.MODEL DD  D (CJO=130e-12 VJ=0.55 M=0.82 IS=1e-13 RS=1 TNOM=27 BV=31)
******************************************************************* 
.MODEL RTEMP RES (TC1={ptrc1} TC2={ptrc2})
*******************************************************************
.ENDS CSD17327Q5A
*$