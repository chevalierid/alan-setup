***********************************************************************
**  v1.7.5 PSpice Models Library - TI NexFET Power N/P Channel FET's
*  (C) Copyright 2010 Texas Instruments Incorporated. All rights reserved.
***********************************************************************
***********************************************************************
* Notice:
* The data and models provided herein are intended to be used by 
* customers as an aid in designing electrical circuits using 
* semiconductor devices manufactured &/or sold by Texas Instruments.
* The models and associated information contained herein are not to
* be construed as a guarantee of electrical performance and/or device 
* specification.  Therefore, Texas Instruments does not assume any 
* liability arising out of the use of said data or models, nor from
* the devices or products manufactured therefrom.  Texas Instruments
* does not convey any license under its patent rights nor the rights
* of others.  Texas Instruments reserves the right to change models 
* without prior notice.  
*
* TI is a registered trademark of Texas Instruments Inc.
*************************************************************************
********************************************************************************
*
* Released by: Analog e-Lab Design Center, Texas Instruments Inc.
* Part: CSD16410Q5A
* Date: 12/08/2010
* Model Type: TRANSIENT
* Simulator: PSPICE
* Datasheet: SLPS205A - August 2009 - Revised May 2010
*
*****************************************************************************
*$
**********************************************************************
**********************************************************************
*                                                                    *
* CSD16410Q5A  -  PSpice Model for use with OrCAD                    *
*                                                                    *
**********************************************************************
**********************************************************************
* SUBCKT Definition:  1=D  2=G  3=S
.SUBCKT CSD16410Q5A  1 2 3
* PARAMETER (local) DEFINITIONS SECTION
.PARAM  pWidth  0.843525 	; m, total gate width
.PARAM  perim   {2*pWidth}	; m, gate/drain perim (rough est)
.PARAM  ptrc1   3.50e-3	; temp co of ext Rd / Rs
.PARAM  ptrc2   1.50e-5
****** MODEL SUBCIRCUIT BEGINS HERE     ******************************
M1   10 11 12 12  NMOS	W={pWidth} L=0.5u  PS={perim} PD={perim}
DBD   8  7	 DBD
CBD0  8  7   50e-12
RBD0  8  7   5e11
CGD0  5  7	 1E-12
CGS0  5  8	 4e-12
M2   12 11 12 10  PMOSd W={pWidth} L=0.170u PS={perim} PD={perim}
DDG  14  5	 DD
DGD  14 10	 DD
* Note:  gate oxide capacitance included in NMOS below
CGS   5 13	 75e-12
RGS  13  8	 1400
LGG   2  5	 1.35e-9
RGG   5 11	 0.76
RSB  12  9	 RTEMP 0.20e-3
RSS   9  8 	 RTEMP 0.55e-3
RSP   8  6 	 0.75e-3
LSS   6  3	 0.45e-9
RDD   7 10 	 RTEMP 2.34e-3
RDP   4  7	 1.0e-4
LDD   1  4	 0.05e-9
******************************************************************* 
.MODEL  NMOS   NMOS (    LEVEL  = 7             Version = 3.2  
+ TNOM   = 27            LINT   = 0.055e-6      CAPMOD = 2
+ TOX    = 3.0e-8        NCH    = 1.385e17      NSUB   = 1.0e16
+ AGS    = 0.0           PVAG   = 1.2
+ A0     = 1.20          A1     = 0.0           A2     = 0.95
+ UA     = 0.5e-9        UB     = 5.5e-18       VBM    = -5
+ UA1    = 1.2e-9        UB1    = -1.0e-18      UTE    = -1.50
+ KT1    = -0.81         KT2    =-0.022         KT1L   = 1.0e-12
+ DVT0   = 2.5           DVT1   = 0.70	      DVT2   = -0.033
+ ETA0   = 0.050         ETAB   = -0.07         XJ     = 4e-7
+ PDIBLC1= 0.050         PDIBLC2= 0.006         NLX    = 1.75e-7
+ PSCBE1 = 1.3e8         PSCBE2 = 1e-7          PCLM   = 1.30
+ VOFF   = -0.080        NFACTOR= 2.40          JS     = 0
+ DROUT  = 0.60          DSUB   = 1.0           DELTA  = 0.06
+ CJ     = 1e-18         CJSW   = 1e-18         CF     = 0
+ CGSO   = 182e-12       CGDO   = 4.0e-12       CGBO   = 0  )
******************************************************************* 
.MODEL  PMOSd   PMOS (   LEVEL  = 7            Version = 3.2
+ TNOM   = 27            CAPMOD = 2
+ JS     = 0             VBM    = -10
+ TOX    = 3.0e-8        NCH    = 1.0e17        NSUB   = 6e16
+ CJ     = 1e-18         CJSW   = 1e-18         CF     = 0
+ CGSO   = 0             CGDO   = 0             CGBO   = 0  )
*******************************************************************
.MODEL DBD D (CJO=1.365e-9 VJ=0.92 M=0.49 TNOM=27
+ FC=0.5 TT=5e-09 XTI=4.5 BV=27
+ IS=4.7e-13 N=1.01 RS=1.54e-3 TRS1=3.83e-3)
.MODEL DD  D (CJO=3.50e-11 VJ=0.35 M=0.85 IS=1e-15 RS=0.1 TNOM=27)
*******************************************************************
.MODEL RTEMP RES (TC1={ptrc1} TC2={ptrc2})
*******************************************************************
.ENDS CSD16410Q5A
