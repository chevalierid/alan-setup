***********************************************************************
**  v1.7.5 PSpice Models Library - TI NexFET Power N/P Channel FET's
*  (C) Copyright 2011 Texas Instruments Incorporated. All rights reserved.
***********************************************************************
***********************************************************************
* Notice:
* The data and models provided herein are intended to be used by 
* customers as an aid in designing electrical circuits using 
* semiconductor devices manufactured &/or sold by Texas Instruments.
* The models and associated information contained herein are not to
* be construed as a guarantee of electrical performance and/or device 
* specification.  Therefore, Texas Instruments does not assume any 
* liability arising out of the use of said data or models, nor from
* the devices or products manufactured therefrom.  Texas Instruments
* does not convey any license under its patent rights nor the rights
* of others.  Texas Instruments reserves the right to change models 
* without prior notice.  
*
* TI is a registered trademark of Texas Instruments Inc.
*************************************************************************
********************************************************************************
*
* Released by: Analog e-Lab Design Center, Texas Instruments Inc.
* Part: CSD17306Q5A
* Date: 12/14/2011
* Model Type: TRANSIENT
* Simulator: PSPICE
* Datasheet: SLPS253A - FEBRUARY 2010 - REVISED JULY 2010
*
*****************************************************************************
*$
**********************************************************************
**********************************************************************
*                                                                    *
* CSD17306Q5A  -  PSpice Model for use with OrCAD                    *
*                                                                    *
**********************************************************************
**********************************************************************
* SUBCKT Definition:  1=D  2=G  3=S
**********************************************************************
.SUBCKT csd17306q5a  1 2 3
**********************************************************************
* PARAMETER (local) DEFINITIONS SECTION
****** Package Contributions *****************************************
 .PARAM  pRclip    0.81e-3	 ; Ohms (source clip / bond wire res)
 .PARAM  pRGwire   0.12		 ; Ohms  (gate wire resistance)
* RG_wire = 0.1 for q5;  RG_wire = 0.25 for q3  (estimate)
****** Device Geometry       *****************************************
 .PARAM  pWidth  1.986436      ; m, scale width 
 .PARAM  perim  {2*pWidth}
 .PARAM  ptrc1  7e-3		 ; temp coef of extr Rd / Rs
 .PARAM  ptrc2  12.50e-6
**********************************************************************
M1   10 11 12 12  NMOS	W={pWidth} L=0.5u  PS={perim} PD={perim}
DBD   8  7	 DBD
CGD0  5  7	 1e-12
CGS0  5  8	 6e-12
M2   12 11 12 20  PMOSd W={pWidth} L=0.075u PS={perim} PD={perim}
DDx  20  10  DDx
DDG  14  5	 DD
DGD  14 10	 DD
* Note:  gate oxide capacitance included in NMOS below
CGS   5 13	 53e-12
RGS  13  8	 1450
LGG   2  5	 1.45e-9
RGG   5 11	 {1.25+ pRGwire}
RSB  12  9	 RTEMP 0.15e-3
RSS   9  8 	 0.18e-3
RSP   8  6 	 0.65e-3
LSS   6  3	 0.38e-9
RDP   4  7	 0.55e-3
RDD   7 10 	 RTEMP 0.90e-3
LDD   1  4	 0.05E-9
******************************************************************* 
.MODEL  NMOS   NMOS (    LEVEL = 7              Version = 3.2  
+ TNOM   = 27            LINT   = 0.055e-6      CAPMOD = 2
+ TOX    = 1.75E-8       NCH    = 1.575e17      NSUB   = 5e15
+ AGS    = 0.0           PVAG   = 1.1
+ A0     = 1.00          A1     = 0.0           A2     = 0.90
+ UA     = 1.50e-9       UB     = 1.0e-18       VBM    = -5
+ UA1    = 2.4e-9        UB1    = -5.5e-18      UTE    = -1.50
+ KT1    = -0.6          KT2    =-0.022         KT1L   = 1.0e-12
+ DVT0   = 2.3           DVT1   = 0.55	      DVT2   = -0.033
+ ETA0   = 0.045         ETAB   = -0.07         XJ     = 3e-7
+ PDIBLC1= 0.08          PDIBLC2= 0.006         NLX    = 1.7e-7
+ PSCBE1 = 0.3e8         PSCBE2 = 2.5e-8        PCLM   = 3.00
+ VOFF   = -0.20         NFACTOR= 2.3           JS     = 0
+ DROUT  = 0.8           DSUB   = 1.6           DELTA  = 0.01
+ CJ     = 1e-18         CJSW   = 1e-18         CF     = 0
+ CGSO   = 230.0e-12     CGDO   = 7.0e-12       CGBO   = 0  )
******************************************************************* 
.MODEL  PMOSd   PMOS (   LEVEL  = 1
+ TNOM   = 27            VTO    = -1.0          IS     = 6.0e-14
+ TOX    = 1.75e-8       NSUB   = 2.0e17        GAMMA  = 15
+ CJ     = 1e-18         CJSW   = 1e-18
+ CGSO   = 1e-18         CGDO   = 1e-18         CGBO   = 1e-18  )
*******************************************************************
.MODEL DBD D (CJO=3.675e-9 VJ=0.70 M=0.477 TNOM=27
+ FC=0.5 TT=2e-09 XTI=3.3 BV=31
+ IS=5.8e-12 N=1.01 RS=1.3e-3 TRS1=4.0e-3)
.MODEL DD  D (CJO=0.66e-09 VJ=0.40 M=0.883 IS=1e-12 RS=0.1 TNOM=27)
.MODEL DDx D (CJO=0.094e-09 VJ=0.40 M=0.50 IS=1e-12 RS=1.0 TNOM=27)
*******************************************************************
.MODEL RTEMP RES (TC1={ptrc1} TC2={ptrc2})
*******************************************************************
.ENDS csd17306q5a  
*$
*