*$
* TLC551_2
*****************************************************************************
* (C) Copyright 2023 Texas Instruments Incorporated. All rights reserved.     
*****************************************************************************
** This model is designed as an aid for customers of Texas Instruments.      
** TI and its licensors and suppliers make no warranties, either expressed   
** or implied, with respect to this model, including the warranties of       
** merchantability or fitness for a particular purpose. The model is        
** provided solely on an "as is" basis. The entire risk as to its quality   
** and performance is with the customer
*****************************************************************************
*
* This model is subject to change without notice. Texas Instruments
* Incorporated is not responsible for updating this model.
*
*****************************************************************************
*
** Released by: Online Design Tools, Texas Instruments Inc.
* Part: TLC551_2
* Date: 31JAN2023
* Model Type: ALL IN ONE
* EVM Order Number: N/A 
* EVM Users Guide:  N/A 
* Datasheet: SLFS044B - FEBRUARY 1984 - REVISED SEPTEMBER 1997
*
* Model Version: Final 1.1
*
*****************************************************************************
*
* Updates:
*
* Final 1.1
* 1. Updated PSpice symbol.
* 2. Updated Model name from TLC551 to TLC551_2.
*
* Final 1.0 : 
* Release to Web
*
*****************************************************************************
* Model Usage Notes:
* Model is applicable to TLC551 and TLC552.
*****************************************************************************
.SUBCKT TLC551_2 THRES CONT TRIG RESET OUT DISC VCC GND
X1  THRES CONT TRIG RESET OUT DISC VCC GND TLC55X_TLC551_2
.ENDS TLC551_2
*
.SUBCKT TLC55X_TLC551_2 THRES CONT TRIG RESET OUT DISC VCC GND
RP VCC GND 120K
DP GND VCC DPZ_18V
.PARAM ISP = 20u
.PARAM NP = {0.3/(VT*LOG(1 + 5.0M/ISP))}
.MODEL DPZ_18V D( IS={ISP} N={NP} BV=18.0 IBV=5.0M EG={5.0*NP*VT} XTI={NP*3})
XD8          GND RESI   D_Z18V_TLC551_2
XD7          GND RESET   D_Z18V_TLC551_2
XR2          RESET RESI   TLC55X_RWELL_TLC551_2
+ PARAMS: W=50U L=20U 
XD2          GND TRGI   D_Z18V_TLC551_2
XD1          GND TRIG   D_Z18V_TLC551_2
XR3          TRIG TRGI   TLC55X_RWELL_TLC551_2
+ PARAMS: W=50U L=20U 
XD4          GND THRI   D_Z18V_TLC551_2
XD3          GND THRES   D_Z18V_TLC551_2
XR2_2        THRES THRI   TLC55X_RWELL_TLC551_2
+ PARAMS: W=50U L=20U 
XD6          GND CONTI   D_Z18V_TLC551_2
XD5          GND CONT   D_Z18V_TLC551_2
XR2_3        CONT CONTI   TLC55X_RWELL_TLC551_2
+ PARAMS: W=50U L=20U 
XMN15        GOUT GND QFF GND   MDSWN_TLC551_2
+ PARAMS: W=100U L=10U M=7
XMP15        GOUT VCC QFF GND   MDSWP_TLC551_2
+ PARAMS: W=195U L=10U M=9
XMN3         GND TRGO 23   IIMIRRN_TLC551_2
+ PARAMS: W1=170U L1=18U M1=1 W2=170U L2=18U M2=1 IDIN=1U 
XMN5         GND THRS 25   IIMIRRN_TLC551_2
+ PARAMS: W1=13U L1=26U M1=1 W2=52U L2=13U M2=4 IDIN=100N 
XMP9         VCC RESO 15 GND   IMIRRP_TLC551_2
+ PARAMS: W=112U L=15U M=2 IO=2U 
XMP6         VCC 25 15 GND   IMIRRP_TLC551_2
+ PARAMS: W=18U L=26U M=1 IO=100N 
XMP5         VCC TRGS 15 GND   IMIRRP_TLC551_2
+ PARAMS: W=112U L=15U M=2 IO=2U 
XMP1         VCC THRO 29   IIMIRRP_TLC551_2
+ PARAMS: W1=172U L1=15U M1=1 W2=172U L2=15U M2=1 IDIN=1U 
XIB          VCC GND 15   IBIAS_TLC551_2
XRSFF        TRGO THRO RESO 30 QFF VCC GND   RR1SFF_TLC551_2
+ PARAMS: VOUTH=1 VOUTL=0 RIN=1E12 DELAY=30N ROUT=10
XMN9         TRGO RESO GND   MSWN_TLC551_2
+ PARAMS: W=100U L=10U M=1 
XMN17        DISC GOUT GND GND   TLC55X_NMOS_HV_TLC551_2
+ PARAMS: W=350U L=10U M=20 
XMN16        OUT GOUT GND GND   TLC55X_NMOS_HV_TLC551_2
+ PARAMS: W=175U L=10U M=20 
XMP16        OUT GOUT VCC VCC   TLC55X_PMOS_HV_TLC551_2
+ PARAMS: W=270U L=10U M=7 
XMN10        RESO RESI GND GND   TLC55X_NMOS_HV_TLC551_2_L1
+ PARAMS: W=100U L=10U M=1 
XMN2         THRO THRI THRS GND   TLC55X_NMOS_MV_TLC551_2
+ PARAMS: W=170U L=18U M=2 
XMP4         TRGO TRGI TRGS VCC   TLC55X_PMOS_MV_TLC551_2
+ PARAMS: W=172U L=15U M=2 
XMP3         23 TRGC TRGS VCC   TLC55X_PMOS_MV_TLC551_2
+ PARAMS: W=172U L=15U M=2 
XMPR1F       GND GND 32 TRGC   TLC55X_PMOS_LV_TLC551_2
+ PARAMS: W=20U L=15U M=1
XMPR1E       32 32 TRGC TRGC   TLC55X_PMOS_LV_TLC551_2
+ PARAMS: W=20U L=15U M=1
XMPR1D       TRGC TRGC 33 CONTI   TLC55X_PMOS_LV_TLC551_2
+ PARAMS: W=20U L=15U M=1
XMPR1C       33 33 CONTI CONTI   TLC55X_PMOS_LV_TLC551_2
+ PARAMS: W=20U L=15U M=1
XMPR1B       CONTI CONTI 34 VCC   TLC55X_PMOS_LV_TLC551_2
+ PARAMS: W=20U L=15U M=1 
XMPR1A       34 34 VCC VCC   TLC55X_PMOS_LV_TLC551_2
+ PARAMS: W=20U L=15U M=1 
XMN1         29 CONTI THRS GND   TLC55X_NMOS_MV_TLC551_2
+ PARAMS: W=170U L=18U M=2 
.ENDS TLC55X_TLC551_2
*
.SUBCKT TLC55X_NMOS_HV_TLC551_2  D G S B PARAMS: W = 100U L = 10U M = 1
M1 D G S B TLC55X_NMOSD_HV W = {W} L = {L} M = {M} AD={W*LS} AS={W*LS} PD={W + 2*LS} PS={W + 2*LS} 
+ NRD={LS/W} NRS={LS/W}
.MODEL TLC55X_NMOSD_HV NMOS LEVEL=3 L=10U W=100U KP={KPN} VTO={VTOHN} THETA=1.8E-01
+ CJ={CJN} CJSW={CJSWN} CGSO={CGSON} CGDO={CGDON} RSH= 10 PB=0.65 LD= 70N RDS=50G TOX={TOX} XJ={XJN}
+ GAMMA={GAMMA}
.PARAM CJN = {80U} 
.PARAM CJSWN = {0.5N}
.PARAM XJN = 0.6U
.PARAM CGSON = {0.6*XJN*COX} 
.PARAM CGDON = {CGSON}
.PARAM CGBON = {CGBF*CGDON}
.PARAM TOX = 400E-10
.PARAM COX = {EPSSIO2/TOX}
.PARAM EPSSIO2 = {3.9*8.854214871E-12}
.PARAM LS = 1.0U
.PARAM VTOHN = 0.80
.PARAM GAMMA = 0.075
.PARAM KPN = 5.0E-05
.PARAM CGBF = 0.0
.ENDS TLC55X_NMOS_HV_TLC551_2
*
.SUBCKT TLC55X_NMOS_HV_TLC551_2_L1  D G S B PARAMS: W = 100U L = 10U M = 1
M1 D G S B TLC55X_NMOSD_HV_L1 W = {W} L = {L} M = {M} AD={W*LS} AS={W*LS} PD={W + 2*LS} PS={W + 2*LS} 
+ NRD={LS/W} NRS={LS/W}
.MODEL TLC55X_NMOSD_HV_L1 NMOS LEVEL=1 L=10U W=100U KP={KPN} VTO={VTOHN} LAMBDA=2E-3 
+ CJ={CJN} CJSW={CJSWN} CGSO={CGSON} CGDO={CGDON} RSH= 10 PB=0.65 LD= 70N RDS=50G TOX={TOX}
+ GAMMA={GAMMA}
.PARAM CJN = {80U} 
.PARAM CJSWN = {0.5N}
.PARAM XJN = 0.6U
.PARAM CGSON = {0.6*XJN*COX} 
.PARAM CGDON = {CGSON}
.PARAM CGBON = {CGBF*CGDON}
.PARAM EPSSIO2 = {3.9*8.854214871E-12}
.PARAM TOX = 400E-10
.PARAM COX = {EPSSIO2/TOX}
.PARAM LS = 1.0U
.PARAM VTOHN = 0.80
.PARAM LAMBDA = 2M
.PARAM GAMMA = 0.075
.PARAM KPN = 5.0E-05
.PARAM CGBF = 0.0
.ENDS TLC55X_NMOS_HV_TLC551_2_L1
*
.SUBCKT TLC55X_NMOS_MV_TLC551_2  D G S B PARAMS: W = 100U L = 10U M = 1
M1 D G S B TLC55X_NMOSD_MV W = {W} L = {L} M = {M} AD={W*LS} AS={W*LS} PD={W + 2*LS} PS={W + 2*LS} 
+ NRD={LS/W} NRS={LS/W}
.MODEL TLC55X_NMOSD_MV NMOS LEVEL=1 L=10U W=100U KP={KCG*KPN} VTO={VTOMN} LAMBDA=2E-3 
+ CJ={CJNCG} CJSW={CJSWNCG} CGSO={CGSONCG} CGDO={CGDONCG} CGBO={CGBONCG} RSH=10 PB=0.65 LD= 70N RDS=50G TOX={TOXCG}
+ GAMMA={GAMMA}
.PARAM TOX = 400E-10
.PARAM KCG = 0.15
.PARAM CJNCG = {80U}
.PARAM CJSWNCG = {0.5N}
.PARAM XJNCG = 0.6U
.PARAM CGSONCG = {0.6*XJNCG*COXCG} 
.PARAM CGDONCG = {CGSONCG}
.PARAM CGBONCG = {CGBF*CGDONCG}
.PARAM TOXCG = 1000E-10
.PARAM COXCG = {EPSSIO2/TOXCG}
.PARAM EPSSIO2 = {3.9*8.854214871E-12}
.PARAM LS = 1.0U
.PARAM VTOMN = 0.6
.PARAM LAMBDA = 2M
.PARAM GAMMA = 0.075
.PARAM KPN = 5.0E-05
.PARAM CGBF = 0.0
.ENDS TLC55X_NMOS_MV_TLC551_2
*
.SUBCKT TLC55X_NMOS_LV  D G S B PARAMS: W = 100U L = 10U M = 1
M1 D G S B TLC55X_NMOSD_LV W = {W} L = {L} M = {M} AD={W*LS} AS={W*LS} PD={W + 2*LS} PS={W + 2*LS} 
+ NRD={LS/W} NRS={LS/W}
.MODEL TLC55X_NMOSD_LV NMOS LEVEL=1 L=10U W=100U KP={KPN} VTO={VTON} LAMBDA=2E-3 
+ CJ={CJN} CJSW={CJSWN} CGSO={CGSON} CGDO={CGDON} RSH=10 PB=0.65 LD= 300N RDS=50G TOX={TOX}
+ GAMMA={GAMMA}
.PARAM CJN = {80U}
.PARAM CJSWN = {0.5N}
.PARAM XJN = 0.6U
.PARAM CGSON = {0.6*XJN*COX} 
.PARAM CGDON = {CGSON}
.PARAM CGBON = {CGBF*CGDON}
.PARAM EPSSIO2 = {3.9*8.854214871E-12}
.PARAM TOX = 400E-10
.PARAM COX = {EPSSIO2/TOX}
.PARAM LS = 1.0U
.PARAM VTON = 0.14
.PARAM LAMBDA = 2M
.PARAM GAMMA = 0.075
.PARAM KPN = 5.0E-05
.PARAM CGBF = 0.0
.ENDS TLC55X_NMOS_LV
*
.SUBCKT TLC55X_PMOS_HV_TLC551_2  D G S B PARAMS: W = 100U L = 10U M = 1
M1 D G S B TLC55X_PMOSD_HV W = {W} L = {L} M = {M} AD={W*LS} AS={W*LS} PD={W + 2*LS} PS={W + 2*LS} 
+ NRD={LS/W} NRS={LS/W}
.MODEL TLC55X_PMOSD_HV PMOS LEVEL=3 L=10U W=100U KP={KPP} VTO={-VTOHP} THETA=2.2E-01
+ CJ={CJP} CJSW={CJSWP} CGSO={CGSOP} CGDO={CGDOP} RSH=10 PB=0.65 LD=70N RDS=50G TOX={TOX} XJ={XJP}
+ GAMMA={GAMMA}
.PARAM CJP = {120U} 
.PARAM CJSWP = {1.0N}
.PARAM XJP = 0.6U
.PARAM CGSOP = {0.6*XJP*COX} 
.PARAM CGDOP = {CGSOP}
.PARAM CGBOP = {CGBF*CGDOP}
.PARAM EPSSIO2 = {3.9*8.854214871E-12}
.PARAM TOX = 400E-10
.PARAM COX = {EPSSIO2/TOX}
.PARAM LS = 1.0U
.PARAM VTOHP = 0.85
.PARAM GAMMA = 0.075
.PARAM KPP = 2.0E-05
.PARAM CGBF = 0.0
.ENDS TLC55X_PMOS_HV_TLC551_2
*
.SUBCKT TLC55X_PMOS_MV_TLC551_2  D G S B PARAMS: W = 100U L = 10U M = 1
M1 D G S B TLC55X_PMOSD_MV W = {W} L = {L} M = {M} AD={W*LS} AS={W*LS} PD={W + 2*LS} PS={W + 2*LS} 
+ NRD={LS/W} NRS={LS/W}
.MODEL TLC55X_PMOSD_MV PMOS LEVEL=1 L=10U W=100U KP={KCG*KPP} VTO={-VTOMP} LAMBDA=2E-3 
+ CJ={CJNCG} CJSW={CJSWNCG} CGSO={CGSONCG} CGDO={CGDONCG} CGBO={CGBONCG} RSH=10 PB=0.65 LD= 70N RDS=50G TOX={TOXCG}
+ GAMMA={GAMMA}
.PARAM TOX = 400E-10
.PARAM KCG = 0.15
.PARAM CJNCG = {80U}
.PARAM CJSWNCG = {0.5N}
.PARAM XJNCG = 0.6U
.PARAM CGSONCG = {0.6*XJNCG*COXCG} 
.PARAM CGDONCG = {CGSONCG}
.PARAM CGBONCG = {CGBF*CGDONCG}
.PARAM XJPCG = 0.6U
.PARAM CGSOPCG = {0.6*XJPCG*COXCG} 
.PARAM CGDOPCG = {CGSOPCG}
.PARAM CGBOPCG = {CGBF*CGDOPCG}
.PARAM TOXCG = 1000E-10
.PARAM COXCG = {EPSSIO2/TOXCG}
.PARAM EPSSIO2 = {3.9*8.854214871E-12}
.PARAM LS = 1.0U
.PARAM VTOMP = 0.65
.PARAM LAMBDA = 2M
.PARAM GAMMA = 0.075
.PARAM KPP = 2.0E-05
.PARAM CGBF = 0.0
.ENDS TLC55X_PMOS_MV_TLC551_2
*
.SUBCKT TLC55X_PMOS_LV_TLC551_2  D G S B PARAMS: W = 100U L = 10U M = 1
M1 D G S B TLC55X_PMOSD_LV W = {W} L = {L} M = {M} AD={W*LS} AS={W*LS} PD={W + 2*LS} PS={W + 2*LS} 
+ NRD={LS/W} NRS={LS/W}
.MODEL TLC55X_PMOSD_LV PMOS LEVEL=1 L=10U W=100U KP={KPP} VTO={-VTOP} LAMBDA=2E-3 
+ CJ={CJP} CJSW={CJSWP} CGSO={CGSOP} CGDO={CGDOP} RSH=10 PB=0.65 LD=300N RDS=50G TOX={TOX}
+ GAMMA={GAMMA}
.PARAM CJP = {120U} 
.PARAM CJSWP = {1.0N}
.PARAM XJP = 0.6U
.PARAM CGSOP = {0.6*XJP*COX} 
.PARAM CGDOP = {CGSOP}
.PARAM CGBOP = {CGBF*CGDOP}
.PARAM EPSSIO2 = {3.9*8.854214871E-12}
.PARAM TOX = 400E-10
.PARAM COX = {EPSSIO2/TOX}
.PARAM LS = 1.0U
.PARAM VTOP = 0.14
.PARAM LAMBDA = 2M
.PARAM GAMMA = 0.075
.PARAM KPP = 2.0E-05
.PARAM CGBF = 0.0
.ENDS TLC55X_PMOS_LV_TLC551_2
*
.SUBCKT TLC55X_RWELL_TLC551_2  1 2 PARAMS: W = 10U L = 100U
XR1 1 2 TLC55X_RWELL_TLC551_2D_TLC551_2 PARAMS: W = {W} L = {L} 
.ENDS TLC55X_RWELL_TLC551_2
*
.SUBCKT TLC55X_RWELL_TLC551_2D_TLC551_2  1 2 PARAMS: W = 10U L = 100U
R1 1 2 {RSW*L/W}
.PARAM RSW = 1810
.ENDS TLC55X_RWELL_TLC551_2D_TLC551_2
*
.SUBCKT TLC55X_RNSD_TLC551_2  1 2 PARAMS: W = 10U L = 100U
XR1 1 2 TLC55X_RNSD_D_TLC551_2 PARAMS: W = {W} L = {L} 
.ENDS TLC55X_RNSD_TLC551_2
*
.SUBCKT TLC55X_RNSD_D_TLC551_2  1 2 PARAMS: W = 10U L = 100U
R1 1 2 {RSN*L/W}
.PARAM RSN = 1.41
.ENDS TLC55X_RNSD_D_TLC551_2
*
.SUBCKT TLC55X_RC_TLC551_2   1 2 PARAMS: WW = 10U LW = 100U WNSD = 10U LNSD = 100U
XR1 1 2 TLC55X_RC_D_TLC551_2 PARAMS: WW = {WW} LW = {LW}  WNSD = {WNSD}  LNSD = {LNSD} 
.ENDS TLC55X_RC_TLC551_2
*
.SUBCKT TLC55X_RC_D_TLC551_2  1 2 PARAMS:  WW = 10U LW = 100U WNSD = 10U LNSD = 100U
R1 1 2 {RSW*LW/WW + RSN*LNSD/WNSD}
.PARAM RSW = 1810
.PARAM RSN = 1.41
.ENDS TLC55X_RC_D_TLC551_2
*
.SUBCKT IBIAS_TLC551_2 VCC GND VIB
.PARAM M1 = 8
.PARAM M2 = 5
.PARAM MP = 1
.PARAM WP = 13U
.PARAM WN = 130U
.PARAM LPE = {36U - LDP}
.PARAM LNE = {13U - LDN}
.PARAM BP = {MP*(WP/LPE)*(KPP/2)}
.PARAM WW = 13U
*.PARAM LW = 213U
.PARAM LW = 113U
.PARAM WNN = 25U
.PARAM LNN = 87U
.PARAM R1 = {(RSW*LW/WW + RSN*LNN/WNN)}
.PARAM K2 = {M2*(WN/LNE)*(KPN/2)}
.PARAM MR = {M2/M1}
R1 VIB GND {VBMUL}
GB  VCC VIB  VALUE = {LIMIT( IF ( V(VCC,GND) > VTOHP, BP*PWR(V(VCC,GND)-VTOHP, 2), 0), 
+ (1 + 1*LAMBDA*(V(VCC,GND) - VTOHN))*PWR(( 1 - SQRT(MR/(1+2*LAMBDA*(V(VCC,GND) - VTOHP))) )/R1, 2)/K2, 0)}
R2 VIB VCC {RPAR}
.PARAM VTOHP = 0.85
.PARAM VTOHN = 0.80
.PARAM LAMBDA = 2M
.PARAM KPN = 5.0E-05
.PARAM KPP = 2.0E-05
.PARAM LDN = 0.07U
.PARAM LDP = 0.07U
.PARAM RSW = 1810
.PARAM RSN = 1.41
.PARAM VBMUL = 1E6
.PARAM RPAR = 1T
.ENDS IBIAS_TLC551_2
*
.SUBCKT IMIRRP_TLC551_2 VCC IO VIB GND PARAMS: W = 100U L = 10U M = 1 IO = 1U
.PARAM MP = 1
.PARAM WP = 13U
.PARAM LPE = {36U - LDP}
.PARAM LE = {L - LDP}
.PARAM MR = { M*W/LE/(MP*WP/LPE)/VBMUL }
.PARAM B1 = { (KPP/2*MP*WP/LPE)*VBMUL }
.PARAM IS = 1E-12
.PARAM N = {VTOHP/(VT*LOG(1 + IO/IS))}
GB VCC IO VIB GND {MR}
R1 VCC IO {RPAR}
C1 VCC IO {M*(CBDJ*CJP*LS*W + CBDS*CJSWP*(2*LS + W))}
V1 VCC 10 {VTOHP}
D1 IO  10  DMOD1
.MODEL DMOD1 D (IS={IS} N={N} EG={1*NZ*VT} XTI={NZ*0})
.PARAM ISZ = 5p
.PARAM CJP = {120U} 
.PARAM NZ = {0.3/(VT*LOG(1 + 5.0M/ISZ))}
.PARAM CJSWP = {1.0N}
.PARAM XJNCG = 0.6U
.PARAM LS = 1.0U
.PARAM VTOHP = 0.85
.PARAM KPP = 2.0E-05
.PARAM LDP = 0.07U
.PARAM VBMUL = 1E6
.PARAM RPAR = 1T
.PARAM CBDJ = 1.0 
.PARAM CBDS = 1.0
.ENDS IMIRRP_TLC551_2
*
.SUBCKT IIMIRRP_TLC551_2 VCC IO II PARAMS: W1 = 100U L1 = 10U M1 = 1 W2 = 100U L2= 10U M2 = 2 IDIN = 1U
.PARAM L1E = {L1 - LDP}
.PARAM L2E = {L2 - LDP}
.PARAM B1 = {M1*(W1/L1)*(KPP/2)}
.PARAM MR = {M2*W2/L2E/(M1*W1/L1E)}
.PARAM RDS = {1/(2*SQRT(M2*(W2/L2E)*(KPP/2)*IDIN))}
.PARAM IS = 1E-12
.PARAM NP = {VTOP/(VT*LOG(1 + IDIN/IS))}
FB VCC IO V1 {MR}
R1 VCC IO {RPAR}
C1 VCC IO {M2*(CBDJ*CJP*LS*W2 + CBDS*CJSWP*(2*LS + W2))}
D1 IO  10  DMODP
V1 VCC 10 {VTOP}
R2 II 10  {RDS}
C2 VCC II {M1*(CBDJ*CJP*LS*W1 + CBDS*CJSWP*(2*LS + W1)) + 2/3*COX*(M1*W1*L1E + M2*W2*L2E) + CGSOP*(M1*W1 + M2*W2)}
*C3 II IO {CGDOP*W2}
.MODEL DMODP D (IS={IS} N={NP} EG={1*NZ*VT} XTI={NZ*0})
.PARAM ISZ = 5p
.PARAM NZ = {0.3/(VT*LOG(1 + 5.0M/ISZ))}
.PARAM CJP = {120U} 
.PARAM CJSWP = {1.0N}
.PARAM XJP = 0.6U
.PARAM CGSOP = {0.6*XJP*COX} 
.PARAM CGDOP = {CGSOP}
.PARAM CGBOP = {CGBF*CGDOP}
.PARAM EPSSIO2 = {3.9*8.854214871E-12}
.PARAM TOX = 400E-10
.PARAM COX = {EPSSIO2/TOX}
.PARAM LS = 1.0U
.PARAM VTOP = 0.14
.PARAM KPP = 2.0E-05
.PARAM LDP = 0.07U
.PARAM RPAR = 1T
.PARAM CBDJ = 1.0 
.PARAM CBDS = 1.0
.PARAM CGBF = 0.0
.ENDS IIMIRRP_TLC551_2
*
.SUBCKT IIMIRRN_TLC551_2 GND IO II PARAMS: W1 = 100U L1 = 10U M1 = 1 W2 = 100U L2= 10U M2 = 2 IDIN = 1U
.PARAM L1E = {L1 - LDN}
.PARAM L2E = {L2 - LDN}
.PARAM B1 = {M1*(W1/L1)*(KPN/2)}
.PARAM MR = { M2*W2/L2E/(M1*W1/L1E) }
.PARAM RDS = {1/(2*SQRT(M2*(W2/L2E)*(KPN/2)*IDIN))}
.PARAM IS = 1E-12
.PARAM NN = {VTON/(VT*LOG(1 + IDIN/IS))}
FB IO GND V1 {MR}
R1 IO GND {RPAR}
C1 IO GND {M2*(CBDJ*CJN*LS*W2 + CBDS*CJSWN*(2*LS + W2))}
D1 10 IO  DMODN
V1 10 GND {VTON}
R2 II 10  {RDS}
C2 II GND {M1*(CBDJ*CJN*LS*W1 + CBDS*CJSWN*(2*LS + W1)) + 2/3*COX*(M1*W1*L1E + M2*W2*L2E) + CGSON*(M1*W1 + M2*W2)}
*C3 II IO {M2*CGDON*W2}
.MODEL DMODN D (IS={IS} N={NN} EG={1*NZ*VT} XTI={NZ*0}) 
.PARAM CJN = {80U}
.PARAM ISZ = 5p
.PARAM NZ = {0.3/(VT*LOG(1 + 5.0M/ISZ))}
.PARAM CJSWN = {0.5N}
.PARAM XJN = 0.6U
.PARAM CGSON = {0.6*XJN*COX} 
.PARAM CGDON = {CGSON}
.PARAM CGBON = {CGBF*CGDON}
.PARAM EPSSIO2 = {3.9*8.854214871E-12}
.PARAM TOX = 400E-10
.PARAM COX = {EPSSIO2/TOX}
.PARAM LS = 1.0U
.PARAM VTON = 0.14
.PARAM KPN = 5.0E-05
.PARAM LDN = 0.07U
.PARAM RPAR = 1T
.PARAM CBDJ = 1.0 
.PARAM CBDS = 1.0
.PARAM CGBF = 0.0
.ENDS IIMIRRN_TLC551_2
*
.SUBCKT MDSWP_TLC551_2 D S DG GND PARAMS: W = 100U L = 10U M = 1
.PARAM LE = {L - LDP}
S1 D S DG GND SWN
C1 D S {M*(CBDJ*CJP*LS*W + CBDS*CJSWP*(2*LS + W))} 
*D B
.MODEL SWN VSWITCH ( VON = {0.55} VOFF = {0.49} RON={1/(2*M*(W/LE)*(KPP/2)*5)} ROFF={1G} )
.PARAM CJP = {120U} 
.PARAM CJSWP = {1.0N}
.PARAM LS = 1.0U
.PARAM KPP = 2.0E-05
.PARAM LDP = 0.07U
.PARAM CBDJ = 1.0 
.PARAM CBDS = 1.0
.ENDS MDSWP_TLC551_2
*
.SUBCKT MDSWN_TLC551_2 D S DG GND PARAMS: W = 100U L = 10U M = 1
.PARAM LE = {L - LDN}
S1 D S DG GND SWN
C1 D S {M*(CBDJ*CJN*LS*W + CBDS*CJSWN*(2*LS + W))} 
*D B
.MODEL SWN VSWITCH ( VON = {0.49} VOFF = {0.55} RON={1/(2*M*(W/LE)*(KPN/2)*5)} ROFF={1G} )
.PARAM CJN = {80U}
.PARAM LS = 1.0U
.PARAM CJSWN = {0.5N}
.PARAM KPN = 5.0E-05
.PARAM LDN = 0.07U
.PARAM CBDJ = 1.0 
.PARAM CBDS = 1.0
.ENDS MDSWN_TLC551_2
*
.SUBCKT MSWN_TLC551_2 D G S PARAMS: W = 100U L = 10U M = 1
.PARAM LE = {L - LDN}
C1 D S {M*(CBDJ*CJN*LS*W + CBDS*CJSWN*(2*LS + W))} 
*D B
C2 G S {M*2/3*COX*(W*LE) + CGSON*W}
C3 G D {CGDON*W}
S1 D S G S SWN
.MODEL SWN VSWITCH ( VON = {VTON+1} VOFF = {VTON} RON={1/(2*M*(W/L)*(KPN/2)*5)} ROFF={1G} )
.PARAM CJN = {80U}
.PARAM CJSWN = {0.5N}
.PARAM XJN = 0.6U
.PARAM CGSON = {0.6*XJN*COX} 
.PARAM CGDON = {CGSON}
.PARAM CGBON = {CGBF*CGDON}
.PARAM EPSSIO2 = {3.9*8.854214871E-12}
.PARAM TOX = 400E-10
.PARAM COX = {EPSSIO2/TOX}
.PARAM LS = 1.0U
.PARAM VTON = 0.14
.PARAM KPN = 5.0E-05
.PARAM LDN = 0.07U
.PARAM CBDJ = 1.0 
.PARAM CBDS = 1.0
.PARAM CGBF = 0.0
.ENDS MSWN_TLC551_2
*
* CONNECTIONS:            A
*                         |    C
*                         |    |
.SUBCKT D_Z18V_TLC551_2   1    2
D1 1 2  DZ_18V
.PARAM ISZ = 5p
.PARAM NZ = {0.3/(VT*LOG(1 + 5.0M/ISZ))}
.MODEL DZ_18V D( IS={ISZ} N={NZ} BV=18.0 IBV=5.0M EG={11.25*NZ*VT} XTI={NZ*3})
.ENDS D_Z18V_TLC551_2
*
.SUBCKT RR1SFF_TLC551_2 S R R1 Q  Q_ VCC GND
+ PARAMS: VOUTH=5.0 VOUTL=0 RIN=1E12 DELAY=10N ROUT=10
.PARAM W1 = 100U 
.PARAM L1 = 10U 
.PARAM W2 = 100U 
.PARAM L2= 10U 
.PARAM W3 = 10U 
.PARAM L3 = 25U 
.PARAM W4 = 10U 
.PARAM L4= 100U 
XU1     Q GND S GND Q_ VCC GND   COMP2INPNORSD_TLC551_2
+ PARAMS: ROUT={ROUT} DELAYLH={3N} DELAYHL={25N} VOUTH={VOUTH} VOUTL={VOUTL} 
+ VTHRES1={0.5*(VOUTH-VOUTL)} VTHRES2={VTOCN}
XU2     VCC R R1 GND Q_ GND Q VCC GND   COMP3INPNORSD_TLC551_2
+ PARAMS: ROUT={ROUT} DELAYLH={3N} DELAYHL={3N} VOUTH={VOUTH} VOUTL={VOUTL} 
+ VTHRES1={VTOCP} VTHRES2={VTOCN} VTHRES3={0.49*(VOUTH-VOUTL)}
C1 S GND {0.67*COX*(W1*L1) + CGSON*W1}
C2 R VCC {0.67*COX*(W2*L2) + CGSOP*W2}
C3 R1 GND {0.67*COX*(W3*L3) + CGSON*W3}
C4 R1 VCC {0.67*COX*(W4*L4) + CGSOP*W4}
.PARAM XJN = 0.6U
.PARAM CGSON = {0.6*XJN*COX} 
.PARAM CGDON = {CGSON}
.PARAM CGBON = {CGBF*CGDON}
.PARAM XJP = 0.6U
.PARAM CGSOP = {0.6*XJP*COX} 
.PARAM CGDOP = {CGSOP}
.PARAM CGBOP = {CGBF*CGDOP}
.PARAM EPSSIO2 = {3.9*8.854214871E-12}
.PARAM TOX = 400E-10
.PARAM COX = {EPSSIO2/TOX}
.PARAM VTOCP = {0.8}
.PARAM VTOCN = {0.75}
.PARAM LS = 1.0U
.PARAM CGBF = 0.0
.ENDS RR1SFF_TLC551_2
*
.SUBCKT COMP2INPNORSD_TLC551_2 IN1+ IN1- IN2+ IN2- OUT VCC GND
+ PARAMS: ROUT=0 DELAYLH=0 DELAYHL=0 VOUTH=0 VOUTL=0 VTHRES1=0 VTHRES2=0
.PARAM TDELLH = {IF ( (DELAYLH < 1E-9) , 1E-9, DELAYLH ) } 
.PARAM TDELHL = {IF ( (DELAYHL < 1E-9) , 1E-9, DELAYHL ) } 
.PARAM RO = {IF ( (TDEL > 1E-15) & (ROUT < 1), 1, ROUT ) } 
.PARAM TDEL = {(TDELLH+TDELHL)/2}
.PARAM COUT={TDEL/(0.693*(RO+1U))}
*.PARAM RDELLH = {TDELLH/(0.693*(COUT+1F))}
*.PARAM RDELHL = {TDELHL/(0.693*(COUT+1F))}
.PARAM VREF = {(15-VTOHP)}
.PARAM RDELLH = {TDELLH/(0.693*(COUT+1F))*VREF}
.PARAM RDELHL = {TDELHL/(0.693*(COUT+1F))*VREF}
EOUT OUT GND VALUE= { IF ( (V(IN1+,IN1-) > {VTHRES1}) | (V(IN2+,IN2-) > {VTHRES2}), 
*+ VOUTL + RDELLH*I(EOUT), VOUTH + RDELHL*I(EOUT) ) }
+ VOUTL + RDELLH*I(EOUT)*V(1,GND), VOUTH + RDELHL*I(EOUT)*V(1,GND) ) }
E1 1 GND VALUE= { IF ( (V(VCC,GND) > {VTOHP+0.01}), 1/(V(VCC,GND)-VTOHP), 100 ) }
COUT OUT GND {COUT}
.PARAM VTOHP = 0.85
.ENDS COMP2INPNORSD_TLC551_2
*
.SUBCKT COMP3INPNORSD_TLC551_2 IN1+ IN1- IN2+ IN2- IN3+ IN3- OUT VCC GND
+ PARAMS: ROUT=0 DELAYLH=0 DELAYHL=0 VOUTH=0 VOUTL=0 VTHRES1=0 VHYST1=0 VTHRES2=0 VHYST2=0 VTHRES3=0 VHYST3=0  
.PARAM TDELLH = {IF ( (DELAYLH < 1E-9) , 1E-9, DELAYLH ) } 
.PARAM TDELHL = {IF ( (DELAYHL < 1E-9) , 1E-9, DELAYHL ) } 
.PARAM RO = {IF ( (TDEL > 1E-15) & (ROUT < 1), 1, ROUT ) } 
.PARAM TDEL = {(TDELLH+TDELHL)/2}
.PARAM COUT={TDEL/(0.693*(RO+1U))}
.PARAM VREFN = {(15-VTOHN)}
.PARAM VREFP = {(15-VTOHP)}
.PARAM VREF = {5*(15-VTOHP)}
*.PARAM RDELLH = {TDELLH/(0.693*(COUT+1F))*VREF}
*.PARAM RDELHL = {TDELHL/(0.693*(COUT+1F))*VREF}
.PARAM RDELLH = {TDELLH/(0.693*(COUT+1F))}
.PARAM RDELHL = {TDELHL/(0.693*(COUT+1F))}
EOUT OUT GND VALUE= { IF ( (V(IN1+,IN1-) > {VTHRES1}) | (V(IN2+,IN2-) > {VTHRES2}) | (V(IN3+,IN3-) > {VTHRES3}), 
+ VOUTL + RDELLH*I(EOUT), VOUTH + RDELHL*I(EOUT) ) }
*+ VOUTL + RDELLH*I(EOUT)*V(1,GND), VOUTH + RDELHL*I(EOUT)*V(1,GND) ) }
*E1 1 GND VALUE= { IF ( (V(VCC,GND) > {VTOHP+0.01}), 1/(5*(V(VCC,GND)-VTOHP)), 100 ) }
COUT OUT GND {COUT}
.PARAM VTOHP = 0.85
.PARAM VTOHN = 0.80
.ENDS COMP3INPNORSD_TLC551_2
*
