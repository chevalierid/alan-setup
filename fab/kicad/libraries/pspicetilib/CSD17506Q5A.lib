***********************************************************************
**  v1.7.5 PSpice Models Library - TI NexFET Power N/P Channel FET's
*  (C) Copyright 2011 Texas Instruments Incorporated. All rights reserved.
***********************************************************************
***********************************************************************
* Notice:
* The data and models provided herein are intended to be used by 
* customers as an aid in designing electrical circuits using 
* semiconductor devices manufactured &/or sold by Texas Instruments.
* The models and associated information contained herein are not to
* be construed as a guarantee of electrical performance and/or device 
* specification.  Therefore, Texas Instruments does not assume any 
* liability arising out of the use of said data or models, nor from
* the devices or products manufactured therefrom.  Texas Instruments
* does not convey any license under its patent rights nor the rights
* of others.  Texas Instruments reserves the right to change models 
* without prior notice.  
*
* TI is a registered trademark of Texas Instruments Inc.
*************************************************************************
********************************************************************************
*
* Released by: Analog e-Lab Design Center, Texas Instruments Inc.
* Part: CSD17506Q5A
* Date: 11/30/2011
* Model Type: TRANSIENT
* Simulator: PSPICE
* Datasheet: SLPS304A - DECEMBER 2010 - REVISED JULY 2011
*
*****************************************************************************
*$
**********************************************************************
**********************************************************************
*                                                                    *
* CSD17506Q5A   -  PSpice Model for use with OrCAD                   *
*                                                                    *
**********************************************************************
**********************************************************************
* SUBCKT Definition:  1=D  2=G  3=S
**********************************************************************
.SUBCKT csd17506q5a  1 2 3
* PARAMETER (local) DEFINITIONS SECTION
 .PARAM  ptrc1    5.5e-3  
 .PARAM  ptrc2    10.0e-6
 .PARAM  pwidthP  1.972
 .PARAM  pwidth   {pwidthP*1e6}
 .PARAM  perimP   {2.1*pwidthP}
 .PARAM  perim    {perimP*1e6}
****** MODEL SUBCIRCUIT BEGINS HERE     ******************************
M1   	10 11 12 12  NMOS	W={pwidthP} L=0.55u  PS={perimP} PD={perimP}
DBD   8  7	 DBD
CBD0  8  7   3e-12
RBD0  8  7   1e10
CGD0  5  7	 1E-12
CGS0  5  8	 6e-12
M2   	12 11 12 20  PMOSd W={pwidthP} L=0.1u PS={perimP} PD={perimP}
DDx  	20 10  DDx
DDG  	14 5	 DD
DGD  	14 10	 DD
* Note:  gate oxide capacitance included in NMOS below
CGS   5  13	 200E-12
RGS  	13 8	 80
LGG   2  5	 2.1e-9
RGG   5  11	 1.35
RSB  	12 9	 RTEMP 0.15e-3
RSS   9  8 	 RTEMP 0.104e-3
RSP   8  6 	 0.65e-3
LSS   6  3	 0.47e-9
RSL   6  3   1.5
RDD   7  10	 RTEMP 1.474e-3
RDP   4  7	 0.15e-3
LDD   1  4	 0.05e-9
******************************************************************* 
.MODEL  NMOS   NMOS (
* CONTROL PARAMETERS
+ LEVEL  = 7             VERSION = 3.2         CAPMOD = 2
* DC PARAMETERS
+ VTH0   = 1.80
+ NLX    = 1.74E-7	VBM    = -5			DVT0   = 2.2
+ DVT1   = 0.53         DVT2   = -0.032   	U0     = 670
+ UA     = 0.50e-9      UB     = 1.00e-19 	A0     = 1.0
+ AGS    = 0.0          A1     = 0.0          	A2     = 1.0
+ LINT   = 0.055e-6     VOFF   = -0.10        	NFACTOR= 2.0
+ ETA0   = 0.08         ETAB   = -0.07        	DSUB   = 0.56
+ PCLM   = 0.001		PDIBLC1= 0.07         	PDIBLC2= 0.0086
+ DROUT  = 0.56         PSCBE1 = 4.24e8        	PSCBE2 = 1e-5
+ PVAG   = 0.0          DELTA  = 0.01       	JS     = 1e-4
* AC AND CAPACITANCE PARAMETERS
+ CGSO   = 163.625e-12  CGDO   = 8e-12        	CGBO   = 1e-12
+ CJ     = 1e-18        CJSW   = 1e-18        	CF     = 0
* TEMPERATURE PARAMETERS
+ TNOM   = 27           UTE    = -1.5         	KT1    = -0.75
+ KT1L   = 0.0          KT2    = 0.022        	UA1    = 1.00e-9
+ UB1    = -7.61e-18      
* PROCESS PARAMETERS
+ TOX    = 3.30e-8      XJ     = 3e-7          	NCH    = 1.19e17
+ NSUB   = 1.0e16)
******************************************************************* 
.MODEL  PMOSd   PMOS (  LEVEL  = 1
+ TNOM   = 27           VTO    = -0.3          	IS     = 6.0e-14
+ TOX    = 3.3e-8       NSUB   = 1.0e18		gamma  = 5.0
+ CJ     = 1e-18        CJSW   = 1e-18
+ CGSO   = 1e-18        CGDO   = 1e-18		CGBO   = 1e-18  )
*******************************************************************
.MODEL DBD D (	CJO=4.51e-9 	VJ=1.0
+ M=0.62 		TNOM=25		FC=0.5 		TT=2e-09
+ XTI=3.5		BV=31			IS=50.0e-13 	N=1.045
+ RS=0.50e-3	TRS1=4.0e-3)
.MODEL DD  D (	CJO=0.39e-09 	VJ=0.75 		M=.9
+ IS=1e-13		RS=0.1 TNOM=25)
.MODEL DDx D (	CJO=0.085e-09 	VJ=0.9 		M=0.9
+ IS=1e-13 		RS=1.0 		TNOM=25)
*******************************************************************
.MODEL RTEMP RES (TC1={ptrc1} TC2={ptrc2})
*******************************************************************
.ENDS csd17506q5a
*$