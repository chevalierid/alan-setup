***********************************************************************
**  v1.7.5 PSpice Models Library - TI NexFET Power N/P Channel FET's
*  (C) Copyright 2010 Texas Instruments Incorporated. All rights reserved.
***********************************************************************
***********************************************************************
* Notice:
* The data and models provided herein are intended to be used by 
* customers as an aid in designing electrical circuits using 
* semiconductor devices manufactured &/or sold by Texas Instruments.
* The models and associated information contained herein are not to
* be construed as a guarantee of electrical performance and/or device 
* specification.  Therefore, Texas Instruments does not assume any 
* liability arising out of the use of said data or models, nor from
* the devices or products manufactured therefrom.  Texas Instruments
* does not convey any license under its patent rights nor the rights
* of others.  Texas Instruments reserves the right to change models 
* without prior notice.  
*
* TI is a registered trademark of Texas Instruments Inc.
*************************************************************************
********************************************************************************
*
* Released by: Analog e-Lab Design Center, Texas Instruments Inc.
* Part: CSD17311Q5
* Date: 12/08/2010
* Model Type: TRANSIENT
* Simulator: PSPICE
* Datasheet: SLPS257A - March 2010 - Revised September 2010
*
*****************************************************************************
*$
**********************************************************************
**********************************************************************
*                                                                    *
* CSD17311Q5   -  PSpice Model for use with OrCAD                    *
*                                                                    *
**********************************************************************
**********************************************************************
* SUBCKT Definition:  1=D  2=G  3=S
.SUBCKT CSD17311Q5  1 2 3
* PARAMETER (local) DEFINITIONS SECTION
.PARAM  pWidth  3.844883
.PARAM  perim   {2*pWidth}
.PARAM  ptrc1   3.90e-3	; temp co of ext Rd / Rs
.PARAM  ptrc2   8.90e-6
****** MODEL SUBCIRCUIT BEGINS HERE     ******************************
M1   10 11 12 12  NMOS	W={pWidth} L=0.5u  PS={perim} PD={perim}
DBD   8  7	 DBD
CBD0  8  7   10e-12
RBD0  8  7   1e10
CGD0  5  7	 1E-12
CGS0  5  8	 6e-12
M2   12 11 12 20  PMOSd W={pWidth} L=0.115u PS={perim} PD={perim}
DDx  20  10  DDx
DDG  14  5	 DD
DGD  14 10	 DD
* Note:  gate oxide capacitance included in NMOS below
CGS   5 13	 530e-12
RGS  13  8	 130
LGG   2  5	 1.6e-9
RGG   5 11	 1.7
RSB  12  9	 RTEMP 0.06e-3
RSS   9  8 	 RTEMP 0.11e-3
RSP   8  6 	 0.31e-3
LSS   6  3	 0.45e-9
RDD   7 10 	 RTEMP 0.58e-3
RDP   4  7	 1.0e-4
LDD   1  4	 0.05e-9
******************************************************************* 
.MODEL  NMOS   NMOS (    LEVEL  = 7             Version = 3.2  
+ TNOM   = 27            LINT   = 0.055e-6      CAPMOD = 2
+ TOX    = 1.75e-8       NCH    = 1.602e17      NSUB   = 1.0e16
+ AGS    = 0.0           PVAG   = 0.0
+ A0     = 1.00          A1     = 0.0           A2     = 1.0
+ UA     = 1.4e-9        UB     = 4.5e-18       VBM    = -5
+ UA1    = 2.0e-9        UB1    = -0.5e-18      UTE    = -1.50
+ KT1    = -0.62         KT2    =-0.022         KT1L   = 1.0e-12
+ DVT0   = 2.3           DVT1   = 0.65	      DVT2   = -0.033
+ ETA0   = 0.050         ETAB   = -0.07         XJ     = 4e-7
+ PDIBLC1= 0.010         PDIBLC2= 0.004         NLX    = 1.75e-7
+ PSCBE1 = 2.5e8         PSCBE2 = 1e-7          PCLM   = 1.30
+ VOFF   = -0.21         NFACTOR= 1.8           JS     = 0
+ DROUT  = 0.70          DSUB   = 1.4           DELTA  = 0.025
+ CJ     = 1e-18         CJSW   = 1e-18         CF     = 0
+ CGSO   = 153e-12       CGDO   = 2e-12         CGBO   = 0  )
******************************************************************* 
.MODEL  PMOSd   PMOS (   LEVEL  = 1
+ TNOM   = 27            VTO    = -2.1          JS     = 0
+ TOX    = 1.75e-8       NSUB   = 6e17
+ CJ     = 1e-18         CJSW   = 1e-18         CF     = 0
+ CGSO   = 0             CGDO   = 0             CGBO   = 0  )
*******************************************************************
.MODEL DBD D (CJO=7.99e-9 VJ=0.90 M=0.55 TNOM=27
+ FC=0.5 TT=5e-09 XTI=3.2 BV=31
+ IS=6.35e-12 N=1.01 RS=1.05e-3 TRS1=3.6e-3)
.MODEL DD  D (CJO=11.5e-10 VJ=0.40 M=0.83 IS=1e-12 RS=0.1 TNOM=27)
.MODEL DDx D (CJO=10e-11 VJ=0.50 M=0.50 IS=1e-12 RS=0.1 TNOM=27)
*******************************************************************
.MODEL RTEMP RES (TC1={ptrc1} TC2={ptrc2})
*******************************************************************
.ENDS CSD17311Q5
