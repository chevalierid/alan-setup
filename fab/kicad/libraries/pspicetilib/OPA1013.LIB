* OPA1013
*$
*****************************************************************************
* (C) Copyright 2011 Texas Instruments Incorporated. All rights reserved.                                            
*****************************************************************************
** This model is designed as an aid for customers of Texas Instruments.
** TI and its licensors and suppliers make no warranties, either expressed
** or implied, with respect to this model, including the warranties of 
** merchantability or fitness for a particular purpose.  The model is
** provided solely on an "as is" basis.  The entire risk as to its quality
** and performance is with the customer.
*****************************************************************************
*
* This model is subject to change without notice. Texas Instruments
* Incorporated is not responsible for updating this model.
*
*****************************************************************************
*
** Released by: Analog eLab Design Center, Texas Instruments Inc.
* Part: OPA1013
* Date: 20MAY2011
* Model Type: ALL IN ONE
* Simulator: PSPICE
* Simulator Version: 16.0.0.p001
* EVM Order Number: N/A 
* EVM Users Guide: N/A
* Datasheet: SBOS005
*
* Model Version: 1.0
*
*****************************************************************************
*
* Updates:
*
* Version 1.0 : 
* Release to Web
*
*****************************************************************************
* CONNECTIONS:		  NON-INVERTING INPUT
*	                  | INVERTING INPUT
*       	          | | POSITIVE POWER SUPPLY
*               	  | | | NEGATIVE POWER SUPPLY
*	        	  | | | | OUTPUT
*       	          | | | | |
* PIN CONFIG FOR OPA1013: 1 2 3 4 5
*****************************************************************************
.SUBCKT OPA1013 1 2 3 4 5
C1   11 12 6.062E-12
C2    6  7 21.00E-12
DC    5 53 DX
DE   54  5 DX
DLP  90 91 DX
DLN  92 90 DX
DP    4  3 DX
EGND 99  0 POLY(2) (3,0) (4,0) 0 .5 .5
FB    7 99 POLY(5) VB VC VE VLP VLN 0 1.624E9 -2E9 2E9 2E9 -2E9
GA    6  0 11 12 92.36E-6
GCM   0  6 10 99 130.5E-12
IEE   3 10 DC 8.418E-6
HLIM 90  0 VLIM 1K
Q1   11  2 13 QX
Q2   12  1 14 QX
R2    6  9 100.0E3
RC1   4 11 10.83E3
RC2   4 12 10.83E3
RE1  13 10 4.659E3
RE2  14 10 4.659E3
REE  10 99 23.76E6
RO1   8  5 120
RO2   7 99 30
*  RP    3  4 87.82E3
VB    9  0 DC 0
VC    3 53 DC 1
VE   54  4 DC 1
VLIM  7  8 DC 0
VLP  91  0 DC 30
VLN   0 92 DC 30

* OUTPUT SUPPLY MIRROR

FQ3   0 20 POLY(1) VLIM 0  1
DQ1  20 21 DX
DQ2  22 20 DX
VQ1  21  0 0
VQ2  22  0 0
FQ1   3  0 POLY(1) VQ1  0.2814E-3  1
FQ2   0  4 POLY(1) VQ2  0.2814E-3 -1

* QUIESCIENT CURRENT

RQ    3  4  5.0E5

* DIFF INPUT CAPACITANCE

CDIF  1  2  1.0E-12

* COMMON MODE INPUT CAPACITANCE

C1CM  1  99 1.5E-12
C2CM  2  99 1.5E-12

***************************
.MODEL DX D(IS=800.0E-18)
.MODEL QX PNP(IS=800.0E-18 BF=466.7)

.ENDS OPA1013
*$

