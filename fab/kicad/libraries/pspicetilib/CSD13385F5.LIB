* PSpice Model Editor - Version 16.6.0
*$
* CSD13385F5
*****************************************************************************
* (C) Copyright 2016 Texas Instruments Incorporated. All rights reserved.
*****************************************************************************
** This model is designed as an aid for customers of Texas Instruments.
** TI and its licensors and suppliers make no warranties, either expressed
** or implied, with respect to this model, including the warranties of
** merchantability or fitness for a particular purpose. The model is
** provided solely on an "as is" basis. The entire risk as to its quality
** and performance is with the customer.
*****************************************************************************
*
* Released by: WEBENCH Design Center, Texas Instruments Inc.
* Part: CSD13385F5
* Date: 08NOV2016
* Model Type: Transient
* Simulator: PSPICE
* Simulator Version: 16.2.0.p001
* EVM Order Number: 
* EVM Users Guide:   
* Datasheet: SLPS612 â€“OCTOBER 2016
*
* Model Version: Final 1.00
*
*****************************************************************************
*
* Updates:
*
* Final 1.00
* Release to Web
*
*****************************************************************************
* Model Usage Notes:
*
*****************************************************************************
**********************************************************************
**********************************************************************
*                                                                    *
* CSD13385F5   -  PSpice Model for use with OrCAD/Allegro            *
*                                                                    *
**********************************************************************
**********************************************************************
* Characteristics:
*   Technology:  n-channel 12/08
*   Assembly:    LGA femtofet F5
*
**********************************************************************
* Model History
*   Based on silicon data.     gd 10/28/2016
*   
* SUBCKT Definition:  1=D  2=G  3=S
**********************************************************************
.SUBCKT CSD13385F5   1 2 3
.PARAM  ptrc1   3.3e-3  
.PARAM  ptrc2   5.5e-6
.PARAM  pAA	    0.80680
.PARAM  pwidthP {pAA/1.41}
.PARAM  pwidth  {pwidthP*1e6}
.PARAM  perimP  {2.1*pwidthP}
.PARAM  perim   {perimP*1e6}
.PARAM  pRgate  27.5
M1   10 11 12 12  NMOS	W={pwidthP} L=0.42u  PD={perimP} PS={perimP}
*M2   12 11 12 10  PMOSd W={pWidthP} L=0.002u PS={perimP} PD={perimP}
DBD   8  7	 DBD	{pwidthP}
RDB   8  7   1e10
CGD0  5  7	 0.5e-12
CGS0  5  8	 2e-12
CDS0 10 12	 27e-12
DDG  14  5   DD	{pwidthP}
DGD  14 10   DD 	{pwidthP}
DZC   8  5   DCLAMP
LGG   2  5	 0.1e-9
RGG   5 11	 {8 + pRgate}
RSB  12  9	 RTEMP 0.1e-3
RSS   9  8 	 RTEMP 3.7e-3
RSP   8  6 	 0.5e-3
LSS   6  3	 0.05E-9
RDP   4  7	 1.3e-3
RDD   7 10 	 RTEMP 5.9e-3
LDD   1  4	 0.05E-9
******************************************************************* 
.MODEL  NMOS   NMOS (    LEVEL  = 7             Version = 3.2
+ TNOM   = 27            LINT   = 0.025e-6      CAPMOD = 2
+ TOX    = 1.20e-8       NCH    = 2.225e17      NSUB   = 1.5e16
+ AGS    = 0.0           PVAG   = 0.1           U0     = 450
+ A0     = 0.7           A1     = 0.2           A2     = 1.0
+ UA     = 1.7e-9        UB     = 9e-19         VBM    = -10.0
+ UA1    = 2.0e-9        UB1    = -5e-18        UTE    = -1.50
+ KT1    = -0.53         KT2    = 0.022         KT1L   = 1.0e-12
+ DVT0   = 2.3           DVT1   = 0.55	      DVT2   = -0.033
+ ETA0   = 0.035         ETAB   = -0.07         XJ     = 4e-7
+ PDIBLC1= 0.15          PDIBLC2= 0.010         NLX    = 1.78e-7
+ PSCBE1 = 7.0e8         PSCBE2 = 1e-7          PCLM   = 1.0
+ VOFF   = -0.070        NFACTOR= 1.7           JS     = 0
+ DROUT  = 0.85          DSUB   = 1.3           DELTA  = 0.085
+ CJ     = 1e-18         CJSW   = 1e-18         CF     = 0
+ CGSO   = 273e-12       CGDO   = 2e-12         CGBO   = 0    )
******************************************************************* 
.MODEL  PMOSd   PMOS (   LEVEL  = 1
+ TNOM   = 27            VTO    = -0.30		IS     = 1.0e-14
+ TOX    = 1.20e-8       NSUB   = 1.0e16
+ CJ     = 1e-18         CJSW   = 1e-18
+ CGSO   = 1e-18         CGDO   = 1e-18         CGBO   = 1e-18 )
******************************************************************* 
.MODEL DBD D (CJO=957e-12 VJ=0.55 M=0.33 FC=0.5 TT=3e-09 TNOM=27
+  IS=24e-11 N=1.1  RS=13.25e-3 TRS1=1.4e-3 XTI=-4.0 BV=14)
.MODEL DDx D (CJO=49e-12 VJ=0.45 M=0.50 IS=2e-12 RS=1 TNOM=27)
.MODEL DD  D (CJO=451e-12 VJ=0.45 M=0.825 IS=2e-12 RS=1 TNOM=27 
+  BV=13)
.MODEL DCLAMP  D (CJO=10e-12 VJ=0.60 M=0.40 IS=6E-16 RS=1.0
+  TRS1=5e-4 XTI=3.5 TNOM=27 BV=10.12 TBV1=5.25e-4)
******************************************************************* 
.MODEL RTEMP RES (TC1={ptrc1} TC2={ptrc2})
*******************************************************************
.ENDS CSD13385F5   
*$