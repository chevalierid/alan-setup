***
*$
*TPS54620
*****************************************************************************
* (C) Copyright 2011 Texas Instruments Incorporated. All rights reserved.                                            
*****************************************************************************
** This model is designed as an aid for customers of Texas Instruments.                                             
** TI and its licensors and suppliers make no warranties, either expressed                                           
** or implied, with respect to this model, including the warranties of                                                    
** merchantability or fitness for a particular purpose. The model is                                                      
** provided solely on an "as is" basis. The entire risk as to its quality                                                    
** and performance is with the customer                                                                                          
*****************************************************************************
*
** Released by: Analog eLab(TM) Design Center, Texas Instruments Inc.
* Part: TPS54620
* Date: 08/04/2011
* Model Type: Average 
* Simulator: PSpice 
* Simulator Version: 16.0.0.p001
* EVM Order Number: TPS54620EVM-374
* EVM Users Guide: SLVU281 - May 2009
* Datasheet: SLVS949C - May 2009 - Revised May 2011 
* Model Version: Final 1.00
*
*****************************************************************************
*
* Updates:
*
* Final 1.00
* Release to Web.
*
***************************************************************************
.SUBCKT TPS54620_AVG VIN COMP VSENSE PH PARAMS: L=3.3U FS=480K 
X_U2         VIN PH 0 COMP N1154830 CCM PARAMS:  L={L} FS={FS} RI=0.063
+  ACS=1
V_V28         N1148122 0 0.8
R_R19         0 COMP  2.38meg 
G_G2         0 COMP N1148122 VSENSE 1300u
C_C13         0 COMP  20.7p   
.ENDS TPS54620_AVG
*$
*
.SUBCKT CCM a c p vc dc params: L=3.3u Fs=480k Ri=0.063 Acs=1
Ese Se 0 value = {9*{Fs}*{Ri}}
Edc dcx 0 value = { v(cx,p)/(v(a,p)+1u) }
Xdc dcx dc limit params: clampH=0.98 clampL=48m
Ed2 d2X 0 value = {(1 - V(dc))} 
Xd2 d2X dc d2 limit2
GIap a p value =  {V(dc)*I(VM)} 
GIpc p cx value = { V(vc)/({Acs}*{Ri}) }
GImju cx p value = {V(Se)*V(dc)/({Acs}*{Ri}*{Fs}) + (v(cx,p))/({L}*{Fs})*((1-(V(dc))/2)) }
VM cx c
.ENDS
*$
*
.subckt limit d dc params: clampH=0.99 clampL=48m
Gd 0 dcx VALUE = { V(d)*100u }
Rdc dcx 0 10k
V1 clpn 0 {clampL}
V2 clpp 0 {clampH}
D1 clpn dcx dclamp
D2 dcx clpp dclamp
Edc dc 0 value={ V(dcx) }
.model dclamp d n=0.01 rs=100m
.ENDS
*$
********
.subckt limit2 d2nc d d2c
*
Gd 0 d2cx d2nc 0 100u
Rdc d2cx 0 10k
V1 clpn 0 48m
E2 clpp 0 Value = { 1-V(d) }
D1 clpn d2cx dclamp
D2 d2cx clpp dclamp
Edc d2c 0 value={ V(d2cx) }
.model dclamp d n=0.01 rs=100m
.ENDS
*$
********
.SUBCKT VARICAP 1 2 CTRL
R1 1 3 1u
VC 3 4
EBC 4 2 Value = { (1/v(ctrl))*v(int) }
GINT 0 INT Value = { I(VC) }
CINT INT 0 1
Rdum INT 0 10E10
.ENDS
*$