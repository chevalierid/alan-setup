*$
*CSD85301Q2
***********************************************************************
* (C) Copyright 2015 Texas Instruments Incorporated. All rights reserved.
***********************************************************************
***********************************************************************
** This model is designed as an aid for customers of Texas Instruments.
** TI and its licensors and suppliers make no warranties, either expressed
** or implied, with respect to this model, including the warranties of
** merchantability or fitness for a particular purpose. The model is
** provided solely on an "as is" basis. The entire risk as to its quality
** and performance is with the customer
********************************************************************************
**Released by: WEBENCH(R) Design Center, Texas Instruments Inc.
* Part: CSD85301Q2
* Date: 02/26/2015
* Model Type: TRANSIENT
* Simulator: PSPICE
* Simulator Version: 16.2.0.p001
* EVM Order Number:
* EVM Users Guide:
* Datasheet: SLPS521–DECEMBER 2014
* Model Version: Final 1.00
**
*****************************************************************************
*
* Updates:
*
* Final 1.00
* Release to Web
*
*****************************************************************************
**********************************************************************
**********************************************************************
*                                                                    *
* CSD85301Q2   -  PSpice Model for use with OrCAD                    *
*                                                                    *
**********************************************************************
**********************************************************************
* Characteristics:
*   Technology:   NCH 30/20
*
**********************************************************************
* Model History: theoritical
*   tr, 07/01/2015
*    
**********************************************************************
* SUBCKT Definition:  1=S1  2=G1  3=D2	4=S2  5=G2  6=D1  7=D1  8=D2
.SUBCKT	CSD85301Q2	1	2	3	4   5   6   7   8
R_D1 6 7 1e-6
R_D2 3 8 1e-6

X1	6	2	1	N85301
X2	3	5	4	N85301
.ends	CSD85301Q2
* SUBCKT Definition:  1=D  2=G  3=S
.SUBCKT N85301  1 2 3
* PARAMETER (local) DEFINITIONS SECTION
 .param	 AAN85301	0.558
 .PARAM  ptrc1    	2.25e-3  
 .PARAM  ptrc2    	03.00e-6
 .PARAM  pwidthP  	{AAN85301/0.8*1}
 .PARAM  pwidth   	{pwidthP*1e6}
 .PARAM  perimP   	{2.1*pwidthP}
 .PARAM  perim    	{perimP*1e6}
****** MODEL SUBCIRCUIT BEGINS HERE     ******************************
M1		5	6	7	7	NMOS	W={pwidthP} L=0.5u  PS={perimP} PD={perimP}

DBD		8	4	DBD

DDG  	9	4	DD
DGD  	9	2	DD

DGS		10	2	DDZ
DSG		10	11	DDZ

CBD0  	8	4	3e-12
RBD0  	8	4	1e10

RESD	11	8	10

RDP		1	4	0.3e-3
RDD		4	5	RTEMP	11.0e-3

RGG		2	6	20
CGS		6	7	0.66e-10
RSS		7	8	0.1e-3
RSP   	8	3	4.8e-3
******************************************************************* 
.MODEL  NMOS   NMOS (
* CONTROL PARAMETERS
+ LEVEL  = 7             VERSION = 3.2         CAPMOD = 2
* DC PARAMETERS
+ NLX    = 1.74E-7       VBM    = -5           DVT0   = 2.2
+ DVT1   = 0.53          DVT2   = -0.032       U0     = 587.5
+ UA     = 8.0e-9        UB     = 0.0e-19      A0     = 0.9
+ AGS    = 0.0           A1     = 0.0          A2     = 0.9
+ LINT   = 0.055e-6      VOFF   = -0.27        NFACTOR= 1.7
+ ETA0   = 0.005         ETAB   = -0.07        DSUB   = 1.4
+ PCLM   = 3.00          PDIBLC1= 0.02         PDIBLC2= 0.004
+ DROUT  = 0.90          PSCBE1 = 3.0e8        PSCBE2 = 1e-6
+ PVAG   = 0.1           DELTA  = 0.0220       JS     = 0
* AC AND CAPACITANCE PARAMETERS
+ CGSO   = 1.00e-18		 CGDO   = 1e-18        CGBO   = 1e-18
+ CJ     = 1e-18         CJSW   = 1e-18        CF     = 0
* TEMPERATURE PARAMETERS
+ TNOM   = 25            UTE    = -1.5         KT1    = -0.60
+ KT1L   = 1.0e-15       KT2    = 0.022        UA1    = -01.00e-9
+ UB1    = 5.0e-17      
* PROCESS PARAMETERS
+ TOX    = 2.50e-8       XJ     = 3e-7         NCH    = 1.185e17
+ NSUB   = 1.0e16)
******************************************************************* 
.MODEL DBD D (CJO=0.4e-10 VJ=1.0 M=0.3 TNOM=25
+ FC=0.5 TT=2e-09 XTI=0.0 BV=22
+ IS=3.0e-11 N=1.07 RS=2.0e-2 TRS1=4.0e-3)
*******************************************************************
.MODEL DD  D (CJO=2.25e-010 VJ=0.9 M=0.50 IS=1e-13 RS=0.1 TNOM=25)
*******************************************************************
.MODEL DDZ D (CJO=1e-018 VJ=0.9 M=0.9 IS=1e-13 RS=1.0e-3 TNOM=25 bv=11.00)
*******************************************************************
.MODEL RTEMP RES (TC1={ptrc1} TC2={ptrc2})
*******************************************************************
.ENDS N85301  
*$