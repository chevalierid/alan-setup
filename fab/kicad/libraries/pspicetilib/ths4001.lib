* PSpice Model Editor - Version 9.2

*$
* [Disclaimer] (C) Copyright Texas Instruments Incorporated 1999 All rights reserved 
* Texas Instruments Incorporated hereby grants the user of this SPICE Macro-model a 
* non-exclusive, nontransferable license to use this SPICE Macro-model under the following 
* terms. Before using this SPICE Macro-model, the user should read this license. If the 
* user does not accept these terms, the SPICE Macro-model should be returned to Texas 
* Instruments within 30 days. The user is granted this license only to use the SPICE 
* Macro-model and is not granted rights to sell, load, rent, lease or license the SPICE 
* Macro-model in whole or in part, or in modified form to anyone other than user. User may 
* modify the SPICE Macro-model to suit its specific applications but rights to derivative 
* works and such modifications shall belong to Texas Instruments. This SPICE Macro-model is 
* provided on an "AS IS" basis and Texas Instruments makes absolutely no warranty with 
* respect to the information contained herein. TEXAS INSTRUMENTS DISCLAIMS AND CUSTOMER 
* WAIVES ALL WARRANTIES, EXPRESS OR IMPLIED, INCLUDING WARRANTIES OF MERCHANTABILITY OR 
* FITNESS FOR A PARTICULAR PURPOSE. The entire risk as to quality and performance is with 
* the Customer. ACCORDINGLY, IN NO EVENT SHALL THE COMPANY BE LIABLE FOR ANY DAMAGES, 
* WHETHER IN CONTRACT OR TORT,INCLUDING ANY LOST PROFITS OR OTHER INCIDENTAL, CONSEQUENTIAL,
* EXEMPLARY, OR PUNITIVE DAMAGES ARISING OUT OF THE USE OR APPLICATION OF THE SPICE 
* Macro-model provided in this package. Further, Texas Instruments reserves the right to 
* discontinue or make changes without notice to any product herein to improve reliability, 
* function, or design. Texas Instruments does not convey any license under patent rights or 
* any other intellectual property rights, including those of third parties.
*  
* THS4001 SUBCIRCUIT - Rev A
* HIGH SPEED MONLITHIC OPERATIONAL AMPLIFIER 
* WRITTEN 10/6/99
* NULL PINS ARE NOT MODELED
* TEMPLATE=X^@REFDES %IN+ %IN- %VCC+ %VCC- %OUT @MODEL
* CONNECTIONS:      NON-INVERTING INPUT
*                   | INVERTING INPUT
*                   | | POSITIVE POWER SUPPLY
*                   | | | NEGATIVE POWER SUPPLY
*                   | | | | OUTPUT
*                   | | | | | 
*                   | | | | | 
*                   | | | | | 
.SUBCKT THS4001     1 2 3 4 5 
*
* INPUT *
Q1 35 34 36 NPN_IN 1
Q2 12 2 32 NPN_IN2 1
R1 33 36 400
R2 33 32 400

* OFFSET *
Vio 34 14 1.1m

* NOISE *
RN 0 31 16.45m
HN 14 1 VHN 8
VHN 31 0 0V

* PROTECTION DIODES *
D1 4 1 D1N_BE
D2 4 2 D1N_BE
D3 13 1 D1N_BC
D4 13 2 D1N_BC
D5 7 4 D1N_BC
D6 4 7 D1N_BC
D7 6 4 D1N_BC
D8 4 6 D1N_BC

* SECOND STAGE *
Q3 11 41 35 PNP .5
Q4 11 11 10 NPN .5
Q5 10 38 37 NPN .5
Q6 40 41 12 PNP .5
Q7 40 11 38 NPN .5
Q8 38 38 09 NPN .5
C1 3 40 1.4p
C2 4 40 1.4p
R3 37 7 100
R4 4 37 333
R5 6 09 100
R6 4 09 333

* HIGH FREQUENCY SHAPING *
Lhf         118 119  13n  
Rhf         39 119  25  
Chf         0  39  21p  
Ehf         118 0 40 0 1

* OUTPUT *
Q9 39 39 15 PNP 1
Q10 39 39 16 NPN 1
Q11 3 15 17 NPN 12
Q12 4 16 18 PNP 14
R7 19 17 Rt 10
R8 18 19 Rt 10
RO1 19 20 10
RO2 19 21 10
CO 20 5 25p
L_LO 21 5 10n

* BIAS SOURCES *
G1         3 35 3 4 9e-6 
G2         3 12 3 4 9e-6
G3         33 4 3 4 9e-6
G4         0 15 3 4 9e-6
G5         16 0 3 4 9e-6
I1         3 35  DC 1.1e-3 
I2         3 12  DC 1.1e-3 
I3         33 4  DC 1.1e-3 
I4         0 15  DC 1.4e-3 
I5         16 0  DC 1.6e-3 
V1         3 41 DC 2 

* MODELS *
.MODEL NPN_IN NPN
+ IS=170E-18 BF=400 NF=1 VAF=100 IKF=0.0389 ISE=7.6E-18
+ NE=1.13489 BR=1.11868 NR=1 VAR=4.46837 IKR=8 ISC=8E-15
+ NC=1.8 RB=251.6 RE=0.1220 RC=197 CJE=120.2E-15 VJE=1.0888 MJE=0.381406
+ VJC=0.589703 MJC=0.265838 FC=0.1 CJC=133.8E-15 XTF=272.204 TF=12.13E-12
+ VTF=10 ITF=0.294 TR=3E-09 XTB=1 XTI=5 KF=20E-15

.MODEL NPN_IN2 NPN
+ IS=170E-18 BF=390 NF=1 VAF=100 IKF=0.0389 ISE=7.6E-18
+ NE=1.13489 BR=1.11868 NR=1 VAR=4.46837 IKR=8 ISC=8E-15
+ NC=1.8 RB=251.6 RE=0.1220 RC=197 CJE=120.2E-15 VJE=1.0888 MJE=0.381406
+ VJC=0.589703 MJC=0.265838 FC=0.1 CJC=133.8E-15 XTF=272.204 TF=12.13E-12
+ VTF=10 ITF=0.294 TR=3E-09 XTB=1 XTI=5 KF=20E-15

.MODEL NPN NPN
+ IS=170E-18 BF=100 NF=1 VAF=100 IKF=0.0389 ISE=7.6E-18
+ NE=1.13489 BR=1.11868 NR=1 VAR=4.46837 IKR=8 ISC=8E-15
+ NC=1.8 RB=251.6 RE=0.1220 RC=197 CJE=120.2E-15 VJE=1.0888 MJE=0.381406
+ VJC=0.589703 MJC=0.265838 FC=0.1 CJC=133.8E-15 XTF=272.204 TF=12.13E-12
+ VTF=10 ITF=0.147 TR=3E-09 XTB=1 XTI=5

.MODEL PNP PNP
+ IS=296E-18 BF=100 NF=1 VAF=100 IKF=0.021 ISE=494E-18
+ NE=1.49168 BR=0.491925 NR=1 VAR=2.35634 IKR=8 ISC=8E-15
+ NC=1.8 RB=251.6 RE=0.1220 RC=197 CJE=120.2E-15 VJE=0.940007 MJE=0.55
+ VJC=0.588526 MJC=0.55 FC=0.1 CJC=133.8E-15 XTF=141.135 TF=12.13E-12
+ VTF=6.82756 ITF=0.267 TR=3E-09 XTB=1 XTI=5

.MODEL Rt RES TC1=-0.006

.MODEL D1N_BE D IS=10E-15 N=1.836 ISR=1.565e-9 IKF=.04417 BV=30 IBV=10E-6 RS=45
+ TT=11.54E-9 CJO=3E-12 VJ=.5 M=.3333

.MODEL D1N_BC D IS=10E-15 N=1.836 ISR=1.565e-9 IKF=.04417 BV=30 IBV=10E-6 RS=15
+ TT=11.54E-9 CJO=3E-12 VJ=.5 M=.3333

.ENDS
*$
