*$
* LM5085
*****************************************************************************
* (C) Copyright 2019 Texas Instruments Incorporated. All rights reserved.                                            
*****************************************************************************
** This model is designed as an aid for customers of Texas Instruments.
** TI and its licensors and suppliers make no warranties, either expressed
** or implied, with respect to this model, including the warranties of 
** merchantability or fitness for a particular purpose.  The model is
** provided solely on an "as is" basis.  The entire risk as to its quality
** and performance is with the customer
*****************************************************************************
*
* This model is subject to change without notice. Texas Instruments
* Incorporated is not responsible for updating this model.
*
*****************************************************************************
*
** Released by: WEBENCH Design Center, Texas Instruments Inc.
* Part: LM5085
* Date: 17DEC2019
* Model Type: TRANSIENT
* Simulator: PSPICE
* Simulator Version: 16.2.0.p001
* EVM Order Number: LM5085EVAL
* EVM Users Guide: SNVA357B–October 2008–Revised April 2013
* Datasheet: SNVS565G –NOVEMBER 2008–REVISED MARCH 2013
*
* Model Version: Final 1.20
*
*****************************************************************************
*
* Updates:
*
*
* Final 1.20
* Modified the GATE DRIVE implementation and added a blanking time of 10ns to the DRIVER node. 
* Implemented the Gate Driver Output Source/Sink currents
* and connected additional 20nF capacitor at Gate node to Ground for proper functioning.  
*
*
* Final 1.10
* Current limit has been implemented by using both
* Rs resistor and High side MOS RdsOn resistance.
*
*
* Final 1.00
* Release to Web.
*
*****************************************************************************
.SUBCKT LM5085_TRANS IADJ RON FB GND ISEN PGATE VCC VIN 
VL 1 GND
X2 COMP INVCOMP INVB_0
EBREF1 REF1 GND VALUE={IF(V(ENON)>1,1.25,0)}
X5 INVCOMP ONT ONCOMP AND2B_0
D_D57         PGATE_INT N27024 D_D57 
I_I1         N27024 PGATE DC 1.75  
D_D58         PGATE N27024 D_D57 
I_I95         N27024 PGATE_INT DC 1.5
EB4 PGATE_INT GND VALUE={IF(V(DRIVE)>1,V(VIN),V(VCC))}
XOFF CAPOFF REF1 OFFT QLATCH COMPWDELEN_0 
X22 INVCOMP 49 DRVNOT DRIVEI LATCHNAND_0 
SSD V5V ENON RON GND _S12_MOD
.MODEL _S12_MOD vswitch VON=0.775 VOFF=0.685
EBENVCC 24 GND VALUE={IF(V(ENON)<1,5,0)}
R58 ENON GND 1K
X8 QLATCH OFFRESET INVB_0 
X6 INVCL 54 QLATCH QN LATCHNAND_0 
EB7 VIN VCC VALUE={IF(V(VIN)<8.5,V(VIN),7.7)}
SEV VCC GND 24 GND _S1_MOD
.MODEL _S1_MOD vswitch VT=1 RON=1K
XA1 QLATCH CL 50 NAND2A1
S1 V5V ENINT RON GND _S1_MOD
R20 OFFT 28 10
C11 28 GND 10P
S2 ENINT VCCUVLO VIN VCC _S2_MOD
.MODEL _S2_MOD vswitch VT=5 VH=200M
R1X GND VCCUVLO 1K
I_ILIM IADJ GND DC= 42u
XA2  HI 50 IFAULTCLEAR NAND2A2
EBFB 15 GND VALUE={IF(V(SS)<V(REF1), V(SS),V(REF1))}
R17 GND ENINT 1K
V_V5V V5V GND DC=5
CON 7 GND 20P
GBTON GND 7 VALUE = {((20p * 1.25 * (V(Vin) - 1.56 + ((V(Vin) - V(Ron))/(3167*1000*I(V17))))) / 
+ (1.45*1E-7*(((V(Vin) - V(Ron))/(1000*I(V17)))+1.4)))}
V17 RON 22
X7 7 GND DRIVE GND SSWITCH_0 
X69 7 REF1 ONT OFFRESET COMPWDELEN_1
DON1 22 4 _D3_MODX
.MODEL _D3_MODX D IS=1.0E-13 N=1 EG=1.11 M=0.5
DON2 4 46 _D
.MODEL _D D IS=1.0E-13 N=1
RONIN 46 GND 500
COFF CAPOFF GND 5.004P
XFB 15 FB COMP COMP_BASIC_GEN 
E_ABM100         ISEN_1 0 VALUE { if((V(DRIVE)<2.5),V(ISEN),V(VIN)+0.5)}
XCL IADJ ISEN_1 CL1 COMP_BASIC_GEN
RDRIVE DRIVEI DRIVE 1
CDRIVE DRIVE GND 10n
RCL CL1 CL 1
CCL CL GND 50n
RFB FB FB1 1
CFB FB1 GND 1n
EBOVER OV GND VALUE={IF(V(FB1)>1.6,5,0)}
G_ABMI1  VIN CAPOFF VALUE {(1.25*5.004p*((((V(FB)*0.93)+0.28))
+  /(4.1e-6*((V(VIN)/31)+0.15))))}
XSOFF CAPOFF GND OFFRESET GND SSWITCH_0 
CSS SS GND 30N IC=0
MSS SS VSDN GND GND _M
.MODEL _M NMOS RD=20K VTO=1
EBSSEN VSDN GND  VALUE={IF (V(ENON)<1,5,0)}
QSS1 45 45 SSV QP
.MODEL QP PNP
QSS2 SS 45 SSV QP
R63 45 1 200K
X66 LO OV ONCOMP QLATCH 49 NOR4B_0 
X67 HI LO INVB_0 
VNOFAULTS HI 0 DC=5
X68 CL INVCL INVB_0 
X71 LO 28 54 NOR2B_0 
EBSSV SSV GND VALUE={IF(V(VIN)-V(VCC) -0.26>3.8,3.8,V(VIN)-V(VCC))}
.ENDS LM5085_TRANS
*$
.SUBCKT INVB_0 1  2
EB1 3 0 VALUE={IF(V(1)>3,0.1,IF(V(1)<0.6,5,0.1))}
RD 3 2 1
CD 2 0 0.087NF
.ENDS
*$
.SUBCKT AND2B_0 1 2 3 
EB1 4 0 VALUE={IF((V(1)>3 & V(2)>3),5,0.1)}
RD 4 3 1
CD 3 0 0.087NF
.ENDS
*$
.SUBCKT COMPWDELEN_0 1 2 3 EN
EB1 4 0 VALUE={IF(V(EN)<1,0,IF(V(1)>V(2),5,0))}
T1 4 0 5 0 Z0=1 TD=100.000N
R1 5 0 1
EB2 3 0 VALUE={IF(V(5)<0,0,V(5))}
.ENDS
*$
.SUBCKT COMPWDELEN_1 1 2 3 EN
EB1 4 0 VALUE={IF(V(EN)<1,0,IF(V(1)>V(2),5,0))}
T1 4 0 5 0 Z0=1 TD=75.000N
R1 5 0 1
*B2 3 0 V=V(5) < 0 ? 0 : V(5)
EB2 3 0 VALUE={IF(V(5)<0,0,V(5))}
.ENDS
*$
.SUBCKT LATCHNAND_0  3 4 1 2
X1 3 2 1 NAND2B_0
X2 4 1 2 NAND2B_0
.ENDS
*$
.SUBCKT NAND2A1 1 2 3
EB1 4 0 VALUE ={IF((V(1)<0.6 & V(2)<0.6),5,0)}
RD 4 3 1
CD 3 0 0.087NF
.ENDS
*$
.SUBCKT NAND2A2 1 2 3
EB1 4 0 VALUE ={IF((V(1)<0.6 & V(2)<0.6),5,0)}
RD 4 3 1
CD 3 0 0.087NF
.ENDS
*$
.SUBCKT NAND2B_0 1 2 3
EB1 4 0 VALUE ={IF((V(1)<0.6 | V(2)<0.6),5,0)}
RD 4 3 1
CD 3 0 0.087NF
.ENDS
*$
.SUBCKT SSWITCH_0 1 2 3   4
SA1 1 2 3 4 SMOOTH
.MODEL SMOOTH VSWITCH RON=0.00001M ROFF=10000.0000MEG VON=2.0000  VOFF=0
.ENDS
*$
.SUBCKT COMPWDEL_0 1 2 3 PARAMS: TD=100N
EB1 4 0 VALUE={IF(V(1)>V(2),5,0)}
T1 4 0 5 0 Z0=1 TD={TD}
R1 5 0 1
EB2 3 0 VALUE={IF(V(5)<0,0,V(5))}
.ENDS
*$
.SUBCKT NOR4B_0 1 2 3 4 5 
EB1 6 0 VALUE = {IF((V(1)<0.6 & V(2) <0.6 & V(3)<0.6 & V(4)
+<0.6),5,0.1)}
RD 6 5 1
CD 5 0 0.087NF
.ENDS
*$
.SUBCKT NOR2B_0 1 2 3 PARAMS: VHIGH = 5 VLOW = 0 VTHLOW = 0.6 VTHHIGH = 3
EB1 4 0 VALUE = {IF((V(1)<VTHLOW & V(2)<VTHLOW),VHIGH,VLOW)}
RD 4 3 1
CD 3 0 0.087NF
.ENDS
*$
.subckt MBRB2060CT AN CAT 
+ params:
+ AREA=1.0
+ IS=9e-6
+ RS=60e-3
+ N=1.442
+ CJO=470e-12
+ M=0.8
+ EG=0.69
+ XTI=2
+ BV=200
+ IBV=1e-6
D_MBRB2060CT AN CAT model22 {area} 
.model model22 d
+ is={IS}
+ rs={RS}
+ n={N}
+ cjo={CJO}
+ m={M}
+ eg={EG}
+ xti={XTI}
+ bv={BV}
+ ibv={IBV}
.ends MBRB2060CT
*$
.SUBCKT P_MOSFET 20 10 30
V50 50 0 27
RG 10 11X 1
RDU 12X 1 1U
M1 2 1 4X 4X DMOS L=1U W=1U
.MODEL DMOS PMOS(VTO=-2 KP=13
+THETA=0.1 VMAX=3E5 LEVEL=3)
CGS 1 5X 750p
RD 20 4 3.2E-2
DDS 4 5X DDS
.MODEL DDS D(M=4.22E-1 VJ=7.65E-1 CJO=253P)
DBODY 20 5X DBODY
.MODEL DBODY D(IS=4.7E-12 N=1.116224 RS=0.000695 TT=30.4N)
RA 4 2 3.2E-2
RS 5X 5 0.5M
LS 5 30 0.5N
M2 1 8 6 6 INTER
E2 8 6 4 1 2
.MODEL INTER PMOS(VTO=0 KP=10 LEVEL=1)
CGDMAX 7 4 652P
RCGD 7 4 10MEG
DGD 4 6 DGD
RDGD 4 6 10MEG
.MODEL DGD D(M=3.2E-1 VJ=7.7E-3 CJO=652P)
M3 7 9 1 1 INTER
E3 9 1 4 1 -2
*ZX SECTION
EOUT 4X 6X POLY(2) (1X,0) (3X,0) 0 0 0 0 1
FCOPY 0 3X VSENSE 1
RIN 1X 0 1G
VSENSE 6X 5X 0
RREF 3X 0 10M
E_BED 101 0 Value ={V(50,100)}
VAMB 100 0 25
EKP 1X 0 101 0 .042
*VTO SECTION
EVTO 102 0 101 0 .004
EVT 11X 12X 102 0 1
EBL VB1 VB2 101 0  0.8
VBLK VB2 0 60
D DB1 20 DBLK
.MODEL DBLK D(IS=1E-14 CJO=.1P RS=.1)
EDB 0 DB1 VB1 0 1
.ENDS
*$
.SUBCKT COUT 13 2 PARAMS: IC = 0
C3 13 11 4.2580725000000006E-6 IC= {IC}
C4 13 3 2.1290362500000003E-6 IC={IC}
C5 13 4 1.0645181250000001E-6 IC={IC}
R1 4 5 0.4375
R2 3 6 0.315
R3 11 7 0.6124999999999999
R4 12 8 0.8119999999999999
C6 13 9 1.7032290000000002E-5 IC={IC}
R5 13 0 1000000.0
R6 9 10 2.1875
L1 5 2 8.0E-9
L2 6 2 8.0E-9
L3 7 2 8.0E-9
L4 8 2 8.0E-9
L5 10 2 8.0E-9
R8 2 0 1000000.0
C2 13 12 8.516145000000001E-6 IC={IC}
.ENDS
*$
.subckt LDCR 1 2 PARAMS: L = 10u IC = 0 DCR = 0.06
L1 2 3 {L} IC = {IC}
RDCR 3 1 {DCR}
.ENDS
*$
.SUBCKT CIN 1 2
+ PARAMs: C_cap = 2u ESR = 0.0055 IC = 0
Ccap 1 3 {C_cap} IC = {IC} 
Resr 3 2 {ESR}
.ENDS
*$
.model D_D57 d
+ is=1e-015
+ tt=1e-011
+ rs=0.05
+ n=0.1
*$
.SUBCKT COMP_BASIC_GEN INP INM Y PARAMS: VDD=1 VSS=0 VTHRESH=2.5	
E_ABM Yint 0 VALUE {IF (V(INP) > 
+ V(INM), {VDD},{VSS})}
R1 Yint Y 1
C1 Y 0 1n
.ENDS COMP_BASIC_GEN
*$
