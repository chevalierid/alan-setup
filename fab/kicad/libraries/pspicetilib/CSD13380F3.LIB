* PSpice Model Editor - Version 16.2.0
*$
* CSD13380F3
*****************************************************************************
* (C) Copyright 2016 Texas Instruments Incorporated. All rights reserved.
*****************************************************************************
** This model is designed as an aid for customers of Texas Instruments.
** TI and its licensors and suppliers make no warranties, either expressed
** or implied, with respect to this model, including the warranties of
** merchantability or fitness for a particular purpose. The model is
** provided solely on an "as is" basis. The entire risk as to its quality
** and performance is with the customer.
*****************************************************************************
*
* Released by: WEBENCH Design Center, Texas Instruments Inc.
* Part: CSD13380F3
* Date: 08NOV2016
* Model Type: Transient
* Simulator: PSPICE
* Simulator Version: 16.2.0.p001
* EVM Order Number: 
* EVM Users Guide:   
* Datasheet: SLPS593 - OCTOBER 2016
*
* Model Version: Final 1.00
*
*****************************************************************************
*
* Updates:
*
* Final 1.00
* Release to Web
*
*****************************************************************************
* Model Usage Notes:
*
*****************************************************************************
**********************************************************************
**********************************************************************
*                                                                    *
* CSD13380F3   -  PSpice Model for use with OrCAD/Allegro            *
*                                                                    *
**********************************************************************
**********************************************************************
* Characteristics:
*   Technology:  n-channel 12/08
*   Assembly:    LGA femtofet
*
**********************************************************************
* Model History
*   Based on silicon data.     gd 10/26/2016
*   
* SUBCKT Definition:  1=D  2=G  3=S
**********************************************************************
.SUBCKT CSD13380F3   1 2 3
.PARAM  ptrc1   3.75e-3  
.PARAM  ptrc2   9.3e-6
.PARAM  pAA	    0.156727
.PARAM  pwidthP {pAA/1.3}
.PARAM  pwidth  {pwidthP*1e6}
.PARAM  perimP  {2.1*pwidthP}
.PARAM  perim   {perimP*1e6}
.PARAM  pRgate  31.0
M1   10 11 12 12  NMOS	W={pwidthP} L=0.4u  PD={perimP} PS={perimP}
DBD   8  7	 DBD	{pwidthP}
RDB   8  7   1e10
CGD0  5  7	 0.5e-12
CGS0  5  8	 2e-12
CDS0 10 12	 4e-12
DDG  14  5   DD	{pwidthP}
DGD  14 10   DD	{pwidthP}
DZC   8  5   DCLAMP
LGG   2  5	 0.1e-9
RGG   5 11	 {12 + pRgate}
RSB  12  9	 RTEMP 2.5e-3
RSS   9  8 	 RTEMP 9.5e-3
RSP   8  6 	 2.9e-3
LSS   6  3	 0.05E-9
RDP   4  7	 19.2e-3
RDD   7 10 	 RTEMP 20.2e-3
LDD   1  4	 0.05E-9
******************************************************************* 
.MODEL  NMOS   NMOS (    LEVEL  = 7             Version = 3.2
+ TNOM   = 27            LINT   = 0.065e-6      CAPMOD = 2
+ TOX    = 1.20e-8       NCH    = 1.595e17      NSUB   = 1.0e16
+ AGS    = 0.0           PVAG   = 0.2           U0     = 400
+ A0     = 1.0           A1     = 0.0           A2     = 0.9
+ UA     = 0.55e-9       UB     = 1.5e-18       VBM    = -5.0
+ UA1    = 1.0e-9        UB1    = -1.0e-18      UTE    = -1.50
+ KT1    = -0.49         KT2    = 0.022         KT1L   = 1.0e-12
+ DVT0   = 2.5           DVT1   = 0.64	      DVT2   = -0.033
+ ETA0   = 0.045         ETAB   = -0.07         XJ     = 4e-7
+ PDIBLC1= 0.05          PDIBLC2= 0.020         NLX    = 1.78e-7
+ PSCBE1 = 4.0e8         PSCBE2 = 1e-7          PCLM   = 1.0
+ VOFF   = -0.104        NFACTOR= 1.9           JS     = 0
+ DROUT  = 0.8           DSUB   = 1.2           DELTA  = 0.015
+ CJ     = 1e-18         CJSW   = 1e-18         CF     = 0
+ CGSO   = 204e-12       CGDO   = 5e-12         CGBO   = 0    )
******************************************************************* 
.MODEL  PMOSd   PMOS (   LEVEL  = 1
+ TNOM   = 27            VTO    = -0.3		IS     = 5.0e-14
+ TOX    = 1.20e-8       NSUB   = 1.0e18
+ CJ     = 1e-18         CJSW   = 1e-18
+ CGSO   = 1e-18         CGDO   = 1e-18         CGBO   = 1e-18 )
******************************************************************* 
.MODEL DBD D (CJO=1175e-12 VJ=0.72 M=0.34 FC=0.5 TT=3e-09 TNOM=27
+  IS=18e-11 N=1.1  RS=4.8e-3 TRS1=1.5e-3 XTI=-3.8 BV=14)
.MODEL DDx D (CJO=30e-12 VJ=0.45 M=0.43 IS=1e-12 RS=0.1 TNOM=27)
.MODEL DD  D (CJO=303e-12 VJ=0.40 M=0.45 IS=3e-13 RS=1 TNOM=27 BV=13)
.MODEL DCLAMP  D (CJO=29.2e-12 VJ=0.50 M=0.45 IS=5e-15 RS=2
+  TRS1=5e-4 XTI=2.8 TNOM=27 BV=8.3 TBV1=-2.5e-5)
******************************************************************* 
.MODEL RTEMP RES (TC1={ptrc1} TC2={ptrc2})
*******************************************************************
.ENDS CSD13380F3   
*$