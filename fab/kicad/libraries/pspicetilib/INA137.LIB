* INA137
*$
*****************************************************************************
* (C) Copyright 2011 Texas Instruments Incorporated. All rights reserved.                                            
*****************************************************************************
** This model is designed as an aid for customers of Texas Instruments.
** TI and its licensors and suppliers make no warranties, either expressed
** or implied, with respect to this model, including the warranties of 
** merchantability or fitness for a particular purpose.  The model is
** provided solely on an "as is" basis.  The entire risk as to its quality
** and performance is with the customer.
*****************************************************************************
*
* This model is subject to change without notice. Texas Instruments
* Incorporated is not responsible for updating this model.
*
*****************************************************************************
*
** Released by: Analog eLab Design Center, Texas Instruments Inc.
* Part: INA137
* Date: 21JUL2011
* Model Type: ALL IN ONE
* Simulator: PSPICE
* Simulator Version: 16.0.0.p001
* EVM Order Number: N/A
* EVM Users Guide: N/A
* Datasheet: SBOS072
*
* Model Version: 1.0
*
*****************************************************************************
* 
* Updates:
*
* Version 1.0 : 
* Release to Web
*
*****************************************************************************
* COMMENTS
*
* INA137 = A3_137 OPERATIONAL AMPLIFIER + PRECISION RESISTOR NETWORK 
*
* CONNECTIONS:		NON-INVERTING INPUT
*			|  INVERTING INPUT
*			|  |  POSITIVE POWER SUPPLY
*			|  |  |  NEGATIVE POWER SUPPLY
*			|  |  |  |  OUTPUT
*			|  |  |  |  |  REFERENCE
*			|  |  |  |  |  |  SENSE
*			|  |  |  |  |  |  |
* PIN CONFIGURATION     1  2  3  4  5  8  9
*****************************************************************************
.SUBCKT INA137	1  2  3  4  5  8  9
*
X1       14 13 3 4 5  A3_137
*
R1       2 13   12K
R2       13 9   5.9995K
R3       1 14   12K
R4       14 8   6K
*
CDIF     14 13  1.8PF
C1CM     4 14   0.05PF
*
.ENDS INA137
*
* A3_137 operational amplifier "macromodel"
*
* connections:   non-inverting input
*                | inverting input
*                | | positive power supply
*                | | | negative power supply
*                | | | | output
*                | | | | |
.SUBCKT A3_137  1 2 3 4 5
*
  c1   11 12 11.43E-12
  c2    6  7 16.00E-12
  dc    5 53 dx
  de   54  5 dx
  dlp  90 91 dx
  dln  92 90 dx
  dp    4  3 dx
  egnd 99  0 poly(2) (3,0) (4,0) 0 .5 .5
  fb    7 99 poly(5) vb vc ve vlp vln 0 77.11E6 -77E6 77E6 77E6 -77E6
  ga    6  0 11 12 432.3E-6
  gcm   0  6 10 99 432.3E-12
  iee  10  4 dc 224.5E-6
  hlim 90  0 vlim 1K
  q1   11  2 13 qx
  q2   12  1 14 qx
  r2    6  9 100.0E3
  rc1   3 11 2.313E3
  rc2   3 12 2.313E3
  re1  13 10 2.078E3
  re2  14 10 2.078E3
  ree  10 99 890.9E3
  ro1   8  5 15
  ro2   7 99 15
  vb    9  0 dc 0
  vc    3 53 dc 2.500
  ve   54  4 dc 2.200
  vlim  7  8 dc 0
  vlp  91  0 dc 70
  vln   0 92 dc 70

* OUTPUT SUPPLY MIRROR
FQ3   0 20 POLY(1) VLIM 0  1
DQ1  20 21 DX
DQ2  22 20 DX
VQ1  21  0 0
VQ2  22  0 0
FQ1   3  0 POLY(1) VQ1  2.1E-3  1
FQ2   0  4 POLY(1) VQ2  2.1E-3 -1

* QUIESCIENT CURRENT
RQ    3  4  2.8E5
****************************
.model dx D(Is=800.0E-18)
.model qx NPN(Is=800.0E-18 Bf=448)
.ENDS A3_137
*$