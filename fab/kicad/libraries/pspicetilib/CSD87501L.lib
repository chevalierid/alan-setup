*$
*CSD87501L
***********************************************************************
* (C) Copyright 2015 Texas Instruments Incorporated. All rights reserved.
***********************************************************************
***********************************************************************
** This model is designed as an aid for customers of Texas Instruments.
** TI and its licensors and suppliers make no warranties, either expressed
** or implied, with respect to this model, including the warranties of
** merchantability or fitness for a particular purpose. The model is
** provided solely on an "as is" basis. The entire risk as to its quality
** and performance is with the customer
********************************************************************************
**Released by: WEBENCH(R) Design Center, Texas Instruments Inc.
* Part: CSD87501L
* Date: 02/05/2015
* Model Type: TRANSIENT
* Simulator: PSPICE
* Simulator Version: 16.2.0.p001
* EVM Order Number:
* EVM Users Guide:
* Datasheet: SLPS523 â€“JANUARY 2015
* Model Version: Final 1.00
**
*****************************************************************************
*
* Updates:
*
* Final 1.00
* Release to Web
*
**********************************************************************
*                                                                    *
* CSD87501L   -  PSpice Model for use with OrCAD                     *
*                                                                    *
**********************************************************************
**********************************************************************
* Characteristics:
*   Technology:   NCH 30/20
*
**********************************************************************
* Model History:
*   tr, 01/26/2015
*    
**********************************************************************
* SUBCKT Definition:  1=S1  2=S2  3=G1	4=G2  5=S3  6= S4
.SUBCKT	CSD87501L	1	2	3	4   5   6
X1	D	3	1	N87501
X2	D	4	2	N87501
R_R1    5  1  1u  TC=0,0 
R_R2    6  2  1u  TC=0,0
.ENDS	CSD87501L
*$
* SUBCKT Definition:  1=D  2=G  3=S
.SUBCKT N87501  1 2 3
* PARAMETER (local) DEFINITIONS SECTION
 .param	 AAN87501	{3.449397/2}
 .param	 AA17570	11.16
 .PARAM  ptrc1    	3.75e-3  
 .PARAM  ptrc2    	{10.00e-6*1}
 .PARAM  pwidthP  	{AAN87501/0.8*2}
 .PARAM  pwidth   	{pwidthP*1e6}
 .PARAM  perimP   	{2.1*pwidthP}
 .PARAM  perim    	{perimP*1e6}
****** MODEL SUBCIRCUIT BEGINS HERE     ******************************
M1   10 11 12 12  NMOS	W={pwidthP} L=0.5u  PS={perimP} PD={perimP}

DBD   8  7	 DBD	{AAN87501/AA17570}
DDG1  10	101	DD	{AAN87501/AA17570}
DDG2  5	101	DD	{AAN87501/AA17570}
DESD1	2	30	DDZ
DESD2	3	30	DDZ

* Note:  gate oxide capacitance included in NMOS below

CGS		2	3	1.53e-9	ic=0
CBD0  8  7   {3e-12*AAN87501/AA17570}
RBD0  8  7   1e10

RGG	  5	11	 300
RSB  12  9	 RTEMP 2.675e-4
RSS   9  8 	 RTEMP 2.675e-4
RSP   8  6 	 1.00e-6
RDD   7 10 	 RTEMP 2.675e-3
RDP   4  7	 1.00e-6

LGG   2  5	 1.00e-18

LSS   6  3	 1.00e-18
RSL   6  3   1.00e-6

LDD   1  4	 1.00e-18

******************************************************************* 
.MODEL  NMOS   NMOS (
* CONTROL PARAMETERS
+ LEVEL  = 7             VERSION = 3.2         CAPMOD = 2
* DC PARAMETERS
+ NLX    = 1.74E-7       VBM    = -5           DVT0   = 2.2
+ DVT1   = 0.53          DVT2   = -0.032       U0     = 550
+ UA     = 3.0e-9        UB     = 0.0e-19      A0     = 0.9
+ AGS    = 0.0           A1     = 0.0          A2     = 0.9
+ LINT   = 0.055e-6      VOFF   = -0.555       NFACTOR= 1.7
+ ETA0   = 0.005         ETAB   = -0.07        DSUB   = 1.4
+ PCLM   = 3.00          PDIBLC1= 0.02         PDIBLC2= 0.004
+ DROUT  = 0.90          PSCBE1 = 3.0e8        PSCBE2 = 1e-6
+ PVAG   = 0.1           DELTA  = 0.0220       JS     = 0
* AC AND CAPACITANCE PARAMETERS
+ CGSO   = 1.00e-18		 CGDO   = 1e-18        CGBO   = 1e-18
+ CJ     = 1e-18         CJSW   = 1e-18        CF     = 0
* TEMPERATURE PARAMETERS
+ TNOM   = 25            UTE    = -1.5         KT1    = -0.80
+ KT1L   = 1.0e-15       KT2    = 0.022        UA1    = -01.00e-9
+ UB1    = -0.0e-17      
* PROCESS PARAMETERS
+ TOX    = 3.00e-8       XJ     = 3e-7          NCH    = 1.530e17
+ NSUB   = 1.0e16)
******************************************************************* 
.MODEL  PMOSd   PMOS (   LEVEL  = 1
+ TNOM   = 25            VTO    = -1.0          IS     = 6.0e-14
+ TOX    = 3.0e-8        NSUB   = 2.0e17		GAMMA  = 15
+ CJ     = 1e-18         CJSW   = 1e-18
+ CGSO   = 1e-18         CGDO   = 1e-18         CGBO   = 0  )
*******************************************************************
.MODEL DBD D (CJO=1500e-12 VJ=5.0 M=0.55 TNOM=25
+ FC=0.5 TT=2e-09 XTI=3.3 BV=31
+ IS=5.0e-11 N=1.07 RS=1.00e-3 TRS1=4.0e-3)
.MODEL DD  D (CJO=29.00e-10 VJ=0.7 M=0.525 IS=1e-13 RS=0.1 TNOM=25)
*******************************************************************
.MODEL DDZ D (CJO=1e-12 VJ=0.9 M=0.9 IS=1e-13 RS=1.0e+1 TNOM=25 bv=22.00)
*******************************************************************
.MODEL RTEMP RES (TC1={ptrc1} TC2={ptrc2})
*******************************************************************
.ENDS N87501
*$
