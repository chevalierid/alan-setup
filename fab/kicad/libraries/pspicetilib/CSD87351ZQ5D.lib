***********************************************************************
*  (C) Copyright 2013 Texas Instruments Incorporated. All rights reserved.
***********************************************************************
***********************************************************************
* Notice:
* The data and models provided herein are intended to be used by 
* customers as an aid in designing electrical circuits using 
* semiconductor devices manufactured &/or sold by Texas Instruments.
* The models and associated information contained herein are not to
* be construed as a guarantee of electrical performance and/or device 
* specification.  Therefore, Texas Instruments does not assume any 
* liability arising out of the use of said data or models, nor from
* the devices or products manufactured therefrom.  Texas Instruments
* does not convey any license under its patent rights nor the rights
* of others.  Texas Instruments reserves the right to change models 
* without prior notice.  
*
* TI is a registered trademark of Texas Instruments Inc.
*************************************************************************
********************************************************************************
*
* Released by: WEBENCH(R) Design Center, Texas Instruments Inc.
* Part: CSD87351ZQ5D
* Date: 04/18/2013
* Model Type: TRANSIENT
* Simulator: PSPICE
* Datasheet: SLPS426 - DECEMBER 2012
*
*****************************************************************************
*$
**********************************************************************
**********************************************************************
*                                                                    *
* CSD87351ZQ5D -  PSpice Model                   *
*                                                                    *
*                                                                    *
**********************************************************************
* Characteristics:
* Technology:  TI NexFETLV 30/10 n-channel Source Down 
**********************************************************************
* nFET SUBCKT Definition:  1=D  2=G  3=S
* SUBCKT Definition:  1=2=Vin 3=TG 4=TGR 5=BG 6=7=8=VSW 9=PGND
**********************************************************************
.SUBCKT CSD87351ZQ5D  1 2 3 4 5 6 7 8 9
XnfetHS  18  17  19  n37315z
XnfetLS  19  16  15  n37314
RGls      5  10      10e-3
RGhs      3  11      14e-3
RDhs1     1  12      1.1e-3
RDhs2     2  12      1.1e-3
RGlr      4  13      10e-3
Rpsn      8  14      0.95e-3
Rpsn2     7  14      0.95e-3
Rpsn3     6  14      0.95e-3
RSls      9  15      0.001e-3
LGls     10  16      0.61e-9
LGhs     11  17      0.99e-9
LDhs     12  18      0.45e-9
RLDhs    12  18      1
LGlr     13  19      0.61e-9
Lpsn     14  19      0.35e-9
.ENDS CSD87351ZQ5D
*$
**********************************************************************
**********************************************************************
*                                                                    *
*   n37314  -  PSpice Model                                          *
*                                                                    *
*   n37314  is the LS silicon die model for use with csd87351q5d     *
*                                                                    *
**********************************************************************
**********************************************************************
* SUBCKT Definition:  1=D  2=G  3=S
**********************************************************************
.SUBCKT n37314  1 2 3
* PARAMETER (local) DEFINITIONS SECTION
.PARAM  pWidthP  3.1185458
.PARAM  pWidth   {pWidthP*1e6}
.PARAM  pLen     0.5u
.PARAM  perimP   {2.1*pWidthP}
.PARAM  perim    {perimP*1e6}
.PARAM  ptrc1    2.125e-3		; temp co of ext Rd / Rs
.PARAM  ptrc2    5.0e-6
****** MODEL SUBCIRCUIT BEGINS HERE     ******************************
M1   10 11 12 12  NMOS	W={pWidthP} L={pLen} PS={perimP} PD={perimP}
M2   12 11 12 20  PMOSd W={pWidthP} L=0.07u PS={perimP} PD={perimP}
DBD   3  1	 DBD
DDx  20 10   DDx
DDG  14 11	 DD
DGD  14 10	 DD
CBD0  3  1   148e-12
CGD0  2 13	 2E-12
CGS0  2  3	 1e-18
CGS   2 99   5e-12
RBD0  3  1   9e9
RGD  13  1   2e6
RGS  99  3 	 7.5
RGG   2 11	 1.1
.param a 1e-3
RSB  12  9	 rtemp {0.1602*a}
RSS   9  3 	 rtemp {0.1602*a}
RDD   1 10 	 rtemp {1.2853333333333333333333333333333*a}
******************************************************************* 
.MODEL  NMOS   NMOS (    LEVEL  = 7             Version = 3.2  
+ TNOM   = 27            LINT   = 0.050e-6      CAPMOD = 2
+ TOX    = 1.75e-8       NCH    = 2.02e17      NSUB   = 1.5e16
+ AGS    = 0.0           PVAG   = 0.0           U0     = 567
+ A0     = 1.10          A1     = 0.0           A2     = 1.0
+ UA     = 2.25e-9       UB     = 0.10e-19      VBM    = -10
+ UA1    = 0.01e-9       UB1    = -0.01e-18     UTE    = -1.50
+ KT1    = -0.60         KT2    = 0.022         KT1L   = 1.0e-15
+ DVT0   = 2.2           DVT1   = 0.53	      DVT2   = -0.033
+ ETA0   = 0.020         ETAB   = -0.07         XJ     = 4e-7
+ PDIBLC1= 0.1           PDIBLC2= 0.005         NLX    = 1.75e-7
+ PSCBE1 = 7.0e8         PSCBE2 = 2e-7          PCLM   = 0.5
+ VOFF   = -0.130        NFACTOR= 1.83          JS     = 0
+ DROUT  = 0.9           DSUB   = 1.3           DELTA  = 0.045
+ CJ     = 1e-18         CJSW   = 1e-18         CF     = 0
+ CGSO   = 268.75e-12    CGDO   = 0.1e-12       CGBO   = 1e-18  )
******************************************************************* 
.MODEL  PMOSd   PMOS (   LEVEL  = 1
+ TNOM   = 27            VTO    = -0.90         IS     = 7e-14
+ TOX    = 1.75e-8       NSUB   = 5.0e17	      gamma  = 4.0
+ CJ     = 1e-18         CJSW   = 1e-18
+ CGSO   = 1e-18         CGDO   = 1e-18         CGBO   = 1e-18  )
******************************************************************* 
.MODEL DBD D (		 CJO    = 37.45e-10 	VJ     = 1.00
+ M	   = 0.5		 TNOM   = 27		FC     = 0.5
+ TT	   = 1e-09		 XTI    = 0.0           BV     = 31
+ IS     = 25.0e-12      N      = 0.99          RS     = 4.50e-3
+ TRS1=3.5e-3)
.MODEL DD  D (		CJO     = 6.00e-10 	VJ     = 0.74
+ M      = 0.90		IS      = 1e-13		TNOM   = 27)
.MODEL DDx D (		CJO     = 0.085e-09	VJ     = 0.9
+ M	   = 0.9		IS      = 1e-13		RS     = 1.0
+ TNOM=25)
*******************************************************************
.model rtemp res (tc1={ptrc1} tc2={ptrc2})
.ENDS n37314  
*$
**********************************************************************
**********************************************************************
*                                                                    *
*   n37315z  -  PSpice Model                                          *
*                                                                    *
*   n37315z  is the HS silicon die model for use with csd87351zq5d     *
*                                                                    *
**********************************************************************
**********************************************************************
* SUBCKT Definition:  1=D  2=G  3=S
**********************************************************************
.SUBCKT n37315z  1 2 3
* PARAMETER (local) DEFINITIONS SECTION
.PARAM  pWidthP  1.0767352
.PARAM  pWidth   {pWidthP*1e6}
.PARAM  pLen     0.5u
.PARAM  perimP   {2.1*pWidthP}
.PARAM  perim    {perimP*1e6}
.PARAM  ptrc1    2.00e-3		; temp co of ext Rd / Rs
.PARAM  ptrc2    6.0e-6
****** MODEL SUBCIRCUIT BEGINS HERE     ******************************
M1   10 11 12 12  NMOS	W={pWidthP} L={pLen}  PS={perimP} PD={perimP}
M2   12 11 12 20  PMOSd W={pWidthP} L=0.120u PS={perimP} PD={perimP}
DBD   3  1	 DBD
DDx  20 10   DDx
DDG  14 11	 DD
DGD  14 10	 DD
DESD	999	2	DZ
RESD	999	3	9.6
CBD0  3  1   94e-12
CGD0  2 13	 5E-12
CGS0  2  3	 6e-12
CGS   2 99   5e-12
RBD0  3  1   9e9
RGD  13  1   2e6
RGS  99  3   10
RGG   2 11	 0.85
.param a 1e-3
RSB  12  9	 rtemp {0.29266666666666666666666666666633*a}
RSS   9  3 	 rtemp {0.29266666666666666666666666666633*a}
RDD   1 10 	 rtemp {3.959*a}
******************************************************************* 
.MODEL  NMOS   NMOS (    LEVEL  = 7            Version= 3.2  
+ TNOM   = 27            LINT   = 0.05e-6      CAPMOD = 2
+ TOX    = 1.75e-8       NCH    = 2.60e17      NSUB   = 2.5e16
+ AGS    = 0.0           PVAG   = 0.00         U0     = 670
+ A0     = 1.15          A1     = 0.0          A2     = 1.2
+ UA     = 2.00e-9       UB     = 6.0e-19      VBM    = -10
+ UA1    = 0.10e-9       UB1    = -0.10e-18    UTE    = -1.50
+ KT1    = -0.65         KT2    = 0.022        KT1L   = 1.0e-13
+ DVT0   = 2.2           DVT1   = 0.53	     DVT2   = -0.033
+ ETA0   = 0.020         ETAB   = -0.07        XJ     = 4e-7
+ PDIBLC1= 0.001         PDIBLC2= 0.006        NLX    = 1.75e-7
+ PSCBE1 = 6.5e8         PSCBE2 = 1e-7         PCLM   = 1.3
+ VOFF   = -0.24         NFACTOR= 2.10         JS     = 1e-12
+ DROUT  = 0.9           DSUB   = 1.3          DELTA  = 0.045
+ CJ     = 1e-18         CJSW   = 1e-18        CF     = 0
+ CGSO   = 266.50e-12    CGDO   = 0.1e-12      CGBO   = 1e-18  )
******************************************************************* 
.MODEL  PMOSd   PMOS (   LEVEL  = 1
+ TNOM   = 27            VTO    = -0.65         IS     = 4e-14
+ TOX    = 1.75e-8       NSUB   = 5e17          GAMMA  = 0.1
+ CJ     = 1e-18         CJSW   = 1e-18 
+ CGSO   = 1e-18         CGDO   = 1e-18         CGBO   = 1e-18  )
******************************************************************* 
.MODEL DBD D (		 CJO    = 1.386e-9 	VJ     = 1.0
+ M      = 0.59	 	 TNOM   = 27 		FC     = 0.5
+ TT     = 1e-09 		 XTI    = 1.25 		BV     = 31
+ IS	   = 2.00e-12	 N	  = 1.02		RS     = 4.00e-3
+ TRS1   = 2.5e-3)
.MODEL DD  D (		 CJO    = 2.500e-10	VJ	 = 0.82
+ M	   = 0.90		 IS     = 1e-13 		TNOM   = 27)
.MODEL DDx D (		 CJO    = 0.02e-09 	VJ     = 1.0
+ M      = 0.9		 IS     = 1e-13         RS     = 1.0
+ TNOM   = 27)
.MODEL DZ D (TNOM   = 27	BV     = 10.5)
*******************************************************************
.model rtemp res (tc1={ptrc1} tc2={ptrc2})
.ENDS n37315z
*
*$
