***********************************************************************
**  v1.7.5 PSpice Models Library - TI NexFET Power N/P Channel FET's
*  (C) Copyright 2010 Texas Instruments Incorporated. All rights reserved.
***********************************************************************
***********************************************************************
* Notice:
* The data and models provided herein are intended to be used by 
* customers as an aid in designing electrical circuits using 
* semiconductor devices manufactured &/or sold by Texas Instruments.
* The models and associated information contained herein are not to
* be construed as a guarantee of electrical performance and/or device 
* specification.  Therefore, Texas Instruments does not assume any 
* liability arising out of the use of said data or models, nor from
* the devices or products manufactured therefrom.  Texas Instruments
* does not convey any license under its patent rights nor the rights
* of others.  Texas Instruments reserves the right to change models 
* without prior notice.  
*
* TI is a registered trademark of Texas Instruments Inc.
*************************************************************************
********************************************************************************
*
* Released by: Analog e-Lab Design Center, Texas Instruments Inc.
* Part: CSD16403Q5A
* Date: 12/08/2010
* Model Type: TRANSIENT
* Simulator: PSPICE
* Datasheet: SLPS201A - August 2009 - Revised September 2010
*
*****************************************************************************
*$
**********************************************************************
**********************************************************************
*                                                                    *
* CSD16403Q5A  -  PSpice Model for use with OrCAD                    *
*                                                                    *
**********************************************************************
**********************************************************************
* SUBCKT Definition:  1=D  2=G  3=S
.SUBCKT CSD16403Q5A  1 2 3
M1  4 5 6 6 NMOS W=3.07319 L=0.5u 
DBD   6  4	DBD
RBD   6  4	1E12
CGD0  5  4	5E-12
CGD   5 12	135E-12
RG0  12  4	7.5e3
DGD   5 10	DGD
RGD   5 10	1E13
DDG   4 10	DDG
RDG   4 10	1E13
* Note:  gate oxide capacitance included in NMOS below
CGS   5  6	520E-12
LGG   2 11	1.11e-9
RGG  11  5	1.2
RS0   7  6	RTEMP 0.45E-3
LSS   3  7	0.18E-9
RDD   9  4	0.20e-3
LDD   1  9	0.05E-9
******************************************************************* 
.MODEL  NMOS   NMOS (     LEVEL  = 6            
+ TOX    = 3e-8          NPEAK  = 1.423e17      NSUB   = 1.0e16
+ RD     = 0.84e-3       RS     = 0.20e-3
+ A0     = 1.30          A1     = 0.0           A2     = 1.0
+ UA     = 0.70e-09      UB     = 3.2e-18
+ UA1    = 4.8e-9        UB1    = -3.0e-18      UTE    = -1.50
+ KT1    = -0.14         KT2    = 0.022         KT1L   = 1e-08
+ DVT0   = 2.2           DVT1   = 0.40	      DVT2   = -0.033
+ ETA    = 0.3           ETA0   = 0.05          ETAB   = -0.07  
+ LDD    = 1.0E-6        XJ     = 5.0E-7        NLX    = 1.64e-7
+ PDIBL1 = 0.50          PDIBL2 = 0.035
+ PSCBE1 = 4.4e8         PSCBE2 = 3.0e-6        PCLM   = 1.8
+ VOFF   = -0.08         NFACTOR= 1.80          IS     = 0
+ CGSO   = 0             CGDO   = 0             CGBO   = 0  )
******************************************************************* 
.MODEL DBD D (CJO=5000e-12 VJ=0.90 M=0.44 FC=0.5 IS=2.65e-12 N=1.020
+ RS=6.95e-4 TRS1=-7.0e-4 XTI=4.3  TT=5E-09 BV=27)
*******************************************************************
.MODEL DGD D (CJO=845E-12 VJ=0.60 M=0.65 FC=0.5 IS=1e-25)
*******************************************************************
.MODEL DDG D (CJO=845e-12 VJ=0.60 M=0.65 FC=0.5 IS=1e-12)
******************************************************************* 
.MODEL RTEMP RES (TC1=6.5E-3  TC2=1.75E-5)
******************************************************************* 
.ENDS CSD16403Q5A
