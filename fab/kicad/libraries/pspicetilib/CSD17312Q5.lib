***********************************************************************
**  v1.7.5 PSpice Models Library - TI NexFET Power N/P Channel FET's
*  (C) Copyright 2010 Texas Instruments Incorporated. All rights reserved.
***********************************************************************
***********************************************************************
* Notice:
* The data and models provided herein are intended to be used by 
* customers as an aid in designing electrical circuits using 
* semiconductor devices manufactured &/or sold by Texas Instruments.
* The models and associated information contained herein are not to
* be construed as a guarantee of electrical performance and/or device 
* specification.  Therefore, Texas Instruments does not assume any 
* liability arising out of the use of said data or models, nor from
* the devices or products manufactured therefrom.  Texas Instruments
* does not convey any license under its patent rights nor the rights
* of others.  Texas Instruments reserves the right to change models 
* without prior notice.  
*
* TI is a registered trademark of Texas Instruments Inc.
*************************************************************************
********************************************************************************
*
* Released by: Analog e-Lab Design Center, Texas Instruments Inc.
* Part: CSD17312Q5
* Date: 12/08/2010
* Model Type: TRANSIENT
* Simulator: PSPICE
* Datasheet: SLPS256A - March 2010 - Revised October 2010
*
*****************************************************************************
*$
**********************************************************************
**********************************************************************
*                                                                    *
* CSD17312Q5   -  PSpice Model for use with Cadence OrCAD/Allegro    *
*                                                                    *
**********************************************************************
**********************************************************************
* SUBCKT Definition:  1=D  2=G  3=S
.SUBCKT csd17312q5  1 2 3
* PARAMETER (local) DEFINITIONS SECTION
 .PARAM  pWidth  4.7962 	; m, total gate width
 .PARAM  perim   {2*pWidth}	; m, gate/drain perim (rough est)
 .PARAM  ptrc1   3.9e-3		; temp co of ext Rd / Rs
 .PARAM  ptrc2   9.3e-6
****** MODEL SUBCIRCUIT BEGINS HERE     ******************************
M1   10 11 12 12  NMOS	W={pWidth} L=0.5u  PS={perim} PD={perim}
DBD   8  7	 DBD
CBD0  8  7   98e-12
RBD0  8  7   1e10
CGD0  5  7	 1E-12
CGS0  5  8	 6e-12
M2   12 11 12 20  PMOSd W={pWidth} L=0.095u PS={perim} PD={perim}
DDx  20  10  DDx
DDG  14  5	 DD
DGD  14 10	 DD
* Note:  gate oxide capacitance included in NMOS below
CGS   5 13	 650e-12
RGS  13  8	 105
LGG   2  5	 1.5e-9
RGG   5 11	 1.43
RSB  12  9	 RTEMP 0.04e-3
RSS   9  8 	 RTEMP 0.05e-3
RSP   8  6 	 0.22e-3
LSS   6  3	 0.45e-9
RSA   6  3   2.0
RDD   7 10 	 RTEMP 0.394e-3
RDP   4  7	 0.15e-3
LDD   1  4	 0.05e-9
******************************************************************* 
.MODEL  NMOS   NMOS (    LEVEL  = 7             Version = 3.2  
+ TNOM   = 27            LINT   = 0.056e-6      CAPMOD = 2
+ TOX    = 1.75e-8       NCH    = 1.611e17      NSUB   = 1.0e16
+ AGS    = 0.0           PVAG   = 0.1           VSAT   = 9.5e4
+ A0     = 1.0           A1     = 0.0           A2     = 1.0
+ UA     = 2.30e-9       UB     = 2.5e-18       VBM    = -5
+ UA1    = 1.7e-9        UB1    = -1.0e-18      UTE    = -1.50
+ KT1    = -0.61         KT2    = 0.022         KT1L   = 1.0e-10
+ DVT0   = 2.2           DVT1   = 0.53	      DVT2   = -0.033
+ ETA0   = 0.050         ETAB   = -0.07         XJ     = 3e-7
+ PDIBLC1= 0.020         PDIBLC2= 0.004         NLX    = 1.79e-7
+ PSCBE1 = 5.0e8         PSCBE2 = 1e-7          PCLM   = 1.5
+ VOFF   = -0.24         NFACTOR= 1.6           JS     = 0
+ DROUT  = 0.90          DSUB   = 1.4           DELTA  = 0.018
+ CJ     = 1e-18         CJSW   = 1e-18         CF     = 0
+ CGSO   = 152e-12       CGDO   = 8e-12         CGBO   = 1e-12  )
******************************************************************* 
.MODEL  PMOSd   PMOS (   LEVEL  = 1
+ TNOM   = 27            VTO    = -0.3          IS     = 6e-14
+ TOX    = 1.75e-8       NSUB   = 2.2e18
+ CJ     = 1e-18         CJSW   = 1e-18
+ CGSO   = 1e-18         CGDO   = 1e-18         CGBO   = 0  )
*******************************************************************
.MODEL DBD D (CJO=10.12e-9 VJ=0.90 M=0.56 TNOM=27
+ FC=0.5 TT=7e-09 XTI=3.3 BV=33
+ IS=8.5e-12 N=1.01 RS=0.77e-3 TRS1=5.1e-3)
.MODEL DD  D (CJO=1.12e-09 VJ=0.40 M=0.90 IS=1e-12 RS=0.1 TNOM=27)
.MODEL DDx D (CJO=0.1e-09 VJ=0.40 M=0.60 IS=1e-12 RS=1  TNOM=27)
*******************************************************************
.MODEL RTEMP RES (TC1={ptrc1} TC2={ptrc2})
*******************************************************************
.ENDS csd17312q5
