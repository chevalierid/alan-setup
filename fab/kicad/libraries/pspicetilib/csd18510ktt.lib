*$
* CSD18510KTT  
*****************************************************************************
* (C) Copyright 2018 Texas Instruments Incorporated. All rights reserved.
*****************************************************************************
** This model is designed as an aid for customers of Texas Instruments.
** TI and its licensors and suppliers make no warranties, either expressed
** or implied, with respect to this model, including the warranties of
** merchantability or fitness for a particular purpose. The model is
** provided solely on an "as is" basis. The entire risk as to its quality
** and performance is with the customer.
*****************************************************************************
*
** Released by: WEBENCH Design Center, Texas Instruments Inc.
* Part: CSD18510KTT  
* Date: 29OCT2018
* Model Type: 
* Simulator: PSPICE
* Simulator Version: 16.2.0.s003
* EVM Order Number:
* EVM Users Guide: 
* Datasheet: SLPS638A�NOVEMBER 2016�REVISED JANUARY 2017
* Model Version: Final 1.00
*
*****************************************************************************
*
* Updates:
*
* Final 1.00
* Release to Web
* PSpice Model Editor - Version 16.6.0
*$
**********************************************************************
**********************************************************************
*                                                                    *
* csd18510ktt   -  PSpice Model for use with OrCAD/Allegro           *
*                                                                    *
**********************************************************************
* Characteristics:
*   Technology:  n-channel 40/20
*   Assembly:    D2PAK
*
**********************************************************************
* Model History
*
*   Initial model.     gd 01/28/2017
*   Revised,           gd 09/15/2017
*   
* SUBCKT Definition:  1=D  2=G  3=S
**********************************************************************
.SUBCKT csd18510ktt   1 2 3
.PARAM  ptrc1   5.32e-3  
.PARAM  ptrc2   1.28e-5
.PARAM  pAA	    10.856327
.PARAM  pwidth  {pAA/0.8*2}
.PARAM  perim   {2.1*pwidth}
.PARAM  pRgate  0.70
.PARAM  pCJO    58e-12
M1   10 11 12 12  NMOS	W={pwidth} L=0.50u  PD={perim} PS={perim}
M2   12 11 12 20  PMOSd W={pwidth} L=0.063u PS={perim} PD={perim}
DDx  20  10  DD
DBD   8  7	 DBD	{pwidth}
RDB   8  7   9e10
CGD0  5  7	 0.5e-12
CGS0  5  8	 4e-12
CDS0 10 12	 {5.8e-12 * pwidth}
DGD  14 10	 DD	{pwidth}
DDG  14  5	 DD	{pwidth}
RDC  14  5   1e10
LGG   2  5	 2.5e-9
RGG   5 11	 {0.6 + pRgate}
RSB  12  9	 RTEMP 0.01e-3
RSS   9  8 	 RTEMP 0.145e-3
RSP   8  6 	 RTEMP 0.35e-3
LSS   6  3	 1.7E-9
RSL   6  3   2.5
RDP   4  7	 RTEMP 0.182e-3
RDD   7 10 	 RTEMP 0.596e-3
LDD   1  4	 0.1E-9
*******************************************************************
.MODEL  NMOS   NMOS (    LEVEL  = 7             Version = 3.2
+ TNOM   = 27            LINT   = 0.060e-6      CAPMOD = 2
+ TOX    = 5.50e-8       NCH    = 5.00e16		VTH0	 = 2.664
+ AGS    = 0.0           PVAG   = 0.1		VOFFCV = -0.2
+ U0     = 325		 VSAT   = 5.0e4		AT	 = 3.82e4
+ A0     = 1.0           A1     = 0.0           A2     = 0.9
+ UA     = 1.2e-09       UB     = 2.0e-18       VBM    = -10.0
+ UA1    = 0.1e-09       UB1    =-9e-18         UTE    = -1.0
+ KT1    = -0.95         KT2    = 0.022         KT1L   = 1.0e-09
+ DVT0   = 2.2           DVT1   = 0.53	      DVT2   = -0.033
+ ETA0   = 0.05          ETAB   = -0.07         XJ     = 3e-7
+ PDIBLC1= 0.02          PDIBLC2= 0.001         NLX    = 1.74e-7
+ PSCBE1 = 4.0e8         PSCBE2 = 1e-07         PCLM   = 1.3
+ VOFF   = -0.42         NFACTOR= 1.70          JS     = 0
+ DROUT  = 1.0           DSUB   = 1.04          DELTA  = 0.12
+ K1	   = 3.5		 K2	  =-0.25	      K3	 = 7.0
+ CJ     = 1e-18         CJSW   = 1e-18         CF     = 0
+ CGSO   = 11.8e-12      CGDO   = 14.6e-12      CGBO   = 0  )
*******************************************************************
.MODEL  PMOSd   PMOS (   LEVEL  = 7		      Version = 3.2
+ TNOM   = 27            CAPMOD = 0
+ VTH0    = -1.0         IS     = 1.0e-15		U0	 = 250
+ TOX    = 5.5e-8        K1	  = 5.0		K2     = -0.1
+ CJ     = 1e-18         CJSW   = 1e-18
+ CGSO   = 1e-18         CGDO   = 1e-18         CGBO   = 0  )
*******************************************************************
.MODEL DBD D (CJO=270e-12 VJ=0.98 M=1.1 FC=0.5 TT=3e-09 TNOM=27
+  IS=2.5e-12 N=1.06 RS=1.15e-2 TRS1=5e-4 TRS2=-3e-6 XTI=3.3 
+  BV=43.8 TBV1=9.0e-4 TBV2=-2.3e-6  NR=3.5 ISR=2.0e-10)
*******************************************************************
.MODEL DD  D (CJO={2*pCJO} VJ=0.85 M=1.55 IS=1e-15 RS=10 TNOM=27 
+  FC=0.90 )
*******************************************************************
.MODEL RTEMP RES (TC1={ptrc1} TC2={ptrc2})
*******************************************************************
.ENDS csd18510ktt
*
*$
