* INA103
*****************************************************************************
* (C) Copyright 2022 Texas Instruments Incorporated. All rights reserved.                                            
*****************************************************************************
** This model is designed as an aid for customers of Texas Instruments.
** TI and its licensors and suppliers make no warranties, either expressed
** or implied, with respect to this model, including the warranties of 
** merchantability or fitness for a particular purpose.  The model is
** provided solely on an "as is" basis.  The entire risk as to its quality
** and performance is with the customer.
*****************************************************************************
*
* This model is subject to change without notice. Texas Instruments
* Incorporated is not responsible for updating this model.
*
*****************************************************************************
*
** Released by: Analog eLab Design Center, Texas Instruments Inc.
* Part: INA103
* Date: 08SEP2022
* Model Type: ALL IN ONE
* EVM Order Number: N/A
* EVM Users Guide: N/A
* Datasheet: SBOS003
*
* Model Version: 1.1
*
*****************************************************************************
*
* Updates:
*
* Final 1.1
* 1. Updated PSpice Symbol.
* 2. Added .ENDS name as INA103.
*
* Final 1.0
* Release to Web
*
*****************************************************************************
***** INA103 SUB-CIRCUIT
* CONNECTIONS:   NON-INVERTING INPUT
*                |   INVERTING INPUT
*                |   |   POSITIVE POWER SUPPLY
*                |   |   |   NEGATIVE POWER SUPPLY
*                |   |   |   |   OUTPUT
*                |   |   |   |   |   REFERENCE
*                |   |   |   |   |   |   GAIN SENSE 1
*                |   |   |   |   |   |   |   GAIN SENSE 2
*                |   |   |   |   |   |   |   |
.SUBCKT INA103   1   2   3   4   5   8   9  10
*****************************************************************************
***** A1_103E SUB-CIRCUIT
* CONNECTIONS:          NON-INVERTING INPUT
*                       |   INVERTING INPUT
*                       |   |   POSITIVE POWER SUPPLY
*                       |   |   |   NEGATIVE POWER SUPPLY
*                       |   |   |   |   OUTPUT
*                       |   |   |   |   |
X1                     15  17   3   4  11   A1_103E
*
***** A2_103E SUB-CIRCUIT
* CONNECTIONS:          NON-INVERTING INPUT
*                       |   INVERTING INPUT
*                       |   |   POSITIVE POWER SUPPLY
*                       |   |   |   NEGATIVE POWER SUPPLY
*                       |   |   |   |   OUTPUT
*                       |   |   |   |   |
X2                     15  16   3   4  12   A2_103E
*
***** A3_103E SUB-CIRCUIT
* CONNECTIONS:          NON-INVERTING INPUT
*                       |   INVERTING INPUT
*                       |   |   POSITIVE POWER SUPPLY
*                       |   |   |   NEGATIVE POWER SUPPLY
*                       |   |   |   |   OUTPUT
*                       |   |   |   |   |
X3                     14  13   3   4   5   A3_103E
*
R1    11  13   6.0000K
R2    13   5   5.9994K
C2    13   5   2.0000PF
R3    12  14   6.0000K
R4    14   8   6.0000K
*
R1FB   9  11   3.0000K
CC1   17  11  35.0000PF
R2FB  10  12   3.0000K
CC2   16  12  35.0000PF
*
RCE   17   9   75MEG
*
I1     3  16  DC  1.300E-3
I2     3  17  DC  1.300E-3
I3    10   4  DC  1.300E-3
I4     9   4  DC  1.300E-3
*
D1    17  15      DX
D2    16  15      DX
*
Q1    16   1  10  QX
Q2    17   2   9  QX
*
V1     3  15  DC  2.000
*
C1CM   1   0   2.5PF
C2CM   2   0   2.5PF
CDIF   1   2   2.0PF
*
.MODEL DX D(IS=1.0E-24)
.MODEL QX NPN(IS=800.0E-18 BF=500)
.ENDS INA103
*
* CONNECTIONS:   NON-INVERTING INPUT
*                | INVERTING INPUT
*                | | POSITIVE POWER SUPPLY
*                | | | NEGATIVE POWER SUPPLY
*                | | | | OUTPUT
*                | | | | |
.SUBCKT A1_103E  1 2 3 4 5
*
C1   11 12 2.098E-12
C2    6  7 5.000E-12
DC    5 53 DX
DE   54  5 DX
DLP  90 91 DX
DLN  92 90 DX
DP    4  3 DX
EGND 99  0 POLY(2) (3,0) (4,0) 0 .5 .5
FB    7 99 POLY(5) VB VC VE VLP VLN 0 2.653E6 -3E6 3E6 3E6 -3E6
GA    6  0 11 12 188.5E-6
GCM   0  6 10 99 2.987E-9
ISS   3 10 DC 100.0E-6
HLIM 90  0 VLIM 1K
J1   11  2 10 JX
J2   12  1 10 JX
G11 2 4 POLY(3) (10,2) (11,2) (4,2) 0 1E-12 1E-12 1E-12
G21 1 4 POLY(3) (10,1) (12,1) (4,1) 0 1E-12 1E-12 1E-12
R2    6  9 100.0E3
RD1   4 11 5.305E3
RD2   4 12 5.305E3
RO1   8  5 20
RO2   7 99 20
*  RP    3  4 14.06E3
RSS  10 99 2.000E6
VB    9  0 DC 0
VC    3 53 DC 3
VE   54  4 DC 3
VLIM  7  8 DC 0
VLP  91  0 DC 50
VLN   0 92 DC 50
* OUTPUT SUPPLY MIRROR
FQ3   0 20 POLY(1) VLIM 0  1
DQ1  20 21 DX
DQ2  22 20 DX
VQ1  21  0 0
VQ2  22  0 0
FQ1   3  0 POLY(1) VQ1  1.70E-3  1
FQ2   0  4 POLY(1) VQ2  1.70E-3 -1
* QUIESCIENT CURRENT
RQ    3  4  9.0E4
* DIFF INPUT CAPACITANCE
CDIF  1  2  1.0E-12
* COMMON MODE INPUT CAPACITANCE
C1CM  1  99 1.5E-12
C2CM  2  99 1.5E-12
****************************
.MODEL DX D(IS=800.0E-18)
.MODEL JX PJF(IS=2.500E-12 BETA=177.7E-6 VTO=-1)
.ENDS A1_103E
*
* CONNECTIONS:   NON-INVERTING INPUT
*                | INVERTING INPUT
*                | | POSITIVE POWER SUPPLY
*                | | | NEGATIVE POWER SUPPLY
*                | | | | OUTPUT
*                | | | | |
.SUBCKT A2_103E  1 2 3 4 5
*
C1   11 12 2.098E-12
C2    6  7 5.000E-12
DC    5 53 DX
DE   54  5 DX
DLP  90 91 DX
DLN  92 90 DX
DP    4  3 DX
EGND 99  0 POLY(2) (3,0) (4,0) 0 .5 .5
FB    7 99 POLY(5) VB VC VE VLP VLN 0 2.653E6 -3E6 3E6 3E6 -3E6
GA    6  0 11 12 188.5E-6
GCM   0  6 10 99 2.987E-9
ISS   3 10 DC 100.0E-6
HLIM 90  0 VLIM 1K
J1   11  2 10 JX
J2   12  1 10 JX
G11 2 4 POLY(3) (10,2) (11,2) (4,2) 0 1E-12 1E-12 1E-12
G21 1 4 POLY(3) (10,1) (12,1) (4,1) 0 1E-12 1E-12 1E-12
R2    6  9 100.0E3
RD1   4 11 5.305E3
RD2   4 12 5.305E3
RO1   8  5 20
RO2   7 99 20
*  RP    3  4 14.06E3
RSS  10 99 2.000E6
VB    9  0 DC 0
VC    3 53 DC 3
VE   54  4 DC 3
VLIM  7  8 DC 0
VLP  91  0 DC 50
VLN   0 92 DC 50
* OUTPUT SUPPLY MIRROR
FQ3   0 20 POLY(1) VLIM 0  1
DQ1  20 21 DX
DQ2  22 20 DX
VQ1  21  0 0
VQ2  22  0 0
FQ1   3  0 POLY(1) VQ1  1.70E-3  1
FQ2   0  4 POLY(1) VQ2  1.70E-3 -1
* QUIESCIENT CURRENT
RQ    3  4  9.0E4
* DIFF INPUT CAPACITANCE
CDIF  1  2  1.0E-12
* COMMON MODE INPUT CAPACITANCE
C1CM  1  99 1.5E-12
C2CM  2  99 1.5E-12
****************************
.MODEL DX D(IS=800.0E-18)
.MODEL JX PJF(IS=2.500E-12 BETA=177.7E-6 VTO=-1)
.ENDS A2_103E
*
* CONNECTIONS:   NON-INVERTING INPUT
*                | INVERTING INPUT
*                | | POSITIVE POWER SUPPLY
*                | | | NEGATIVE POWER SUPPLY
*                | | | | OUTPUT
*                | | | | |
.SUBCKT A3_103E  1 2 3 4 5
*
C1   11 12 3.356E-12
C2    6  7 8.000E-12
DC    5 53 DX
DE   54  5 DX
DLP  90 91 DX
DLN  92 90 DX
DP    4  3 DX
EGND 99  0 POLY(2) (3,0) (4,0) 0 .5 .5
FB    7 99 POLY(5) VB VC VE VLP VLN 0 16.62E6 -20E6 20E6 20E6 -20E6
GA    6  0 11 12 301.6E-6
GCM   0  6 10 99 4.780E-9
ISS   3 10 DC 160.0E-6
HLIM 90  0 VLIM 1K
J1   11  2 10 JX
J2   12  1 10 JX
G11 2 4 POLY(3) (10,2) (11,2) (4,2) 0 1E-12 1E-12 1E-12
G21 1 4 POLY(3) (10,1) (12,1) (4,1) 0 1E-12 1E-12 1E-12
R2    6  9 100.0E3
RD1   4 11 3.316E3
RD2   4 12 3.316E3
RO1   8  5 10
RO2   7 99 10
*  RP    3  4 14.06E3
RSS  10 99 1.250E6
VB    9  0 DC 0
VC    3 53 DC 3
VE   54  4 DC 3
VLIM  7  8 DC 0
VLP  91  0 DC 70
VLN   0 92 DC 70
* OUTPUT SUPPLY MIRROR
FQ3   0 20 POLY(1) VLIM 0  1
DQ1  20 21 DX
DQ2  22 20 DX
VQ1  21  0 0
VQ2  22  0 0
FQ1   3  0 POLY(1) VQ1  1.64E-3  1
FQ2   0  4 POLY(1) VQ2  1.64E-3 -1
* QUIESCIENT CURRENT
RQ    3  4  9.0E4
* DIFF INPUT CAPACITANCE
CDIF  1  2  1.0E-12
* COMMON MODE INPUT CAPACITANCE
C1CM  1  99 1.5E-12
C2CM  2  99 1.5E-12
****************************
.MODEL DX D(IS=800.0E-18)
.MODEL JX PJF(IS=2.500E-12 BETA=284.3E-6 VTO=-1)
.ENDS A3_103E
*
