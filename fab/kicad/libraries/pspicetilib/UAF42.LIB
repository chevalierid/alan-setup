*$
* UAF42
*****************************************************************************
* (C) Copyright 2022 Texas Instruments Incorporated. All rights reserved.                                            
*****************************************************************************
** This model is designed as an aid for customers of Texas Instruments.
** TI and its licensors and suppliers make no warranties, either expressed
** or implied, with respect to this model, including the warranties of 
** merchantability or fitness for a particular purpose.  The model is
** provided solely on an "as is" basis.  The entire risk as to its quality
** and performance is with the customer
*****************************************************************************
*
* This model is subject to change without notice. Texas Instruments
* Incorporated is not responsible for updating this model.
*
*****************************************************************************
*
** Released by: Online Design Tools, Texas Instruments Inc.
* Part: UAF42
* Date: 14NOV2022
* Model Type: All IN ONE
* EVM Order Number: N/A 
* EVM Users Guide:  N/A 
* Datasheet: SBFS002B - JULY 1992 - REVISED OCTOBER 2010
*
* Model Version: Final 1.3
*
*****************************************************************************
*
* Final 1.3
* 1.Updated PSpice symbol.
* 2.Updated Unique Subckt name.
*
* Final 1.2
* Date: 1/3/94
* Updates: Changed the entire macro model file to be a UAF42 instead of a
*		   single op amp contained in the UAF42
*
* Final 1.1
* Date: 3/21/92
* Updates: Added input bias current error correction to AMP_UAFE
* 
* Final 1.0
* Date: 10/17/90
* Release to Web.
*
*****************************************************************************
* Model Usage Notes:
* 1. Model file is updated with latest header.   
* 2. UAF42E = 4x AMP_UAFE_UAF42 + PRECISION RESISTORS AND CAPS    
*****************************************************************************
* CONNECTIONS:   LOW-PASS OUTPUT
*                | V IN 3
*                | | V IN 2
*                | | | AUX AMP NON-INVERTING INPUT
*                | | | | AUX AMP INVERTING INPUT
*                | | | | | AUX AMP OUTPUT
*                | | | | | | BAND-PASS OUTPUT
*                | | | | | | | FREQUENCY ADJ1
*                | | | | | | | | NEGATIVE POWER SUPPLY
*                | | | | | | | | | POSITIVE POWER SUPPLY
*                | | | | | | | | | |  GROUND
*                | | | | | | | | | |  |  V IN 1
*                | | | | | | | | | |  |  |  HIGH-PASS OUTPUT
*                | | | | | | | | | |  |  |  |  FREQUENCY ADJ2
*                | | | | | | | | | |  |  |  |  |
.SUBCKT UAF42    1 2 3 4 5 6 7 8 9 10 11 12 13 14
*****************************************************************************
* CONNECTIONS:   NON-INVERTING INPUT
*                | INVERTING INPUT
*                | |  POSITIVE POWER SUPPLY
*                | |  | NEGATIVE POWER SUPPLY
*                | |  |  | OUTPUT
*                | |  |  | |
X1               3 12 10 9 13   AMP_UAFE_UAF42
*
* CONNECTIONS:   NON-INVERTING INPUT
*                |  INVERTING INPUT
*                |  | POSITIVE POWER SUPPLY
*                |  | | NEGATIVE POWER SUPPLY
*                |  | |  | OUTPUT
*                |  | |  | |
X2               11 8 10 9 7   AMP_UAFE_UAF42
*
* CONNECTIONS:   NON-INVERTING INPUT
*                |  INVERTING INPUT
*                |  |  POSITIVE POWER SUPPLY
*                |  |  | NEGATIVE POWER SUPPLY
*                |  |  |  | OUTPUT
*                |  |  |  | |
X3               11 14 10 9 1   AMP_UAFE_UAF42
*
* CONNECTIONS:   NON-INVERTING INPUT
*                | INVERTING INPUT
*                | | POSITIVE POWER SUPPLY
*                | | | NEGATIVE POWER SUPPLY
*                | | |  | OUTPUT
*                | | |  | |
X4               4 5 10 9 6   AMP_UAFE_UAF42
*
R1 12 13 50K
R2 12 1 50K
R3 3 7 50K
R4A 3 2 100K
R4B 3 2 100K
C1 7 8 1000P
C2 1 14 1000P
C3 13 14 1P
*
.ENDS UAF42
*
* AMP_UAFE_UAF42 OPERATIONAL AMPLIFIER "MACROMODEL" SUBCIRCUIT
* "E" IS ENHANCED MODEL
*
* CONNECTIONS:         NON-INVERTING INPUT
*                      | INVERTING INPUT
*                      | | POSITIVE POWER SUPPLY
*                      | | | NEGATIVE POWER SUPPLY
*                      | | | | OUTPUT
*                      | | | | |
.SUBCKT AMP_UAFE_UAF42 1 2 3 4 5
*
C1   11 12 8.938E-12
C2    6  7 15.00E-12
CSS  10 99 8.077E-12
DC    5 53 DX
DE   54  5 DX
DLP  90 91 DX
DLN  92 90 DX
DP    4  3 DX
EGND 99  0 POLY(2) (3,0) (4,0) 0 .5 .5
FB    7 99 POLY(5) VB VC VE VLP VLN 0 99.42E6 -10E6 10E6 10E6 -10E6
GA    6  0 11 12 424.1E-6
GCM   0  6 10 99 6.722E-9
ISS   3 10 DC 300.0E-6
HLIM 90  0 VLIM 1K
J1   11  2 10 JX
J2   12  1 10 JX
G11 2 4 POLY(3) (10,2) (11,2) (4,2) 0 1E-12 1E-12 1E-12
G21 1 4 POLY(3) (10,1) (12,1) (4,1) 0 1E-12 1E-12 1E-12
R2    6  9 100.0E3
RD1   4 11 2.358E3
RD2   4 12 2.358E3
RO1   8  5 75
RO2   7 99 75
*  RP    3  4 20.00E3
RSS  10 99 666.7E3
VB    9  0 DC 0
VC    3 53 DC 3.500
VE   54  4 DC 3.500
VLIM  7  8 DC 0
VLP  91  0 DC 25
VLN   0 92 DC 25
****************************
* UAF42 "E" - ENHANCEMENTS
****************************
* OUTPUT SUPPLY MIRROR
FQ3   0 20 POLY(1) VLIM 0  1
DQ1  20 21 DX
DQ2  22 20 DX
VQ1  21  0 0
VQ2  22  0 0
FQ1   3  0 POLY(1) VQ1  0.950E-3  1
FQ2   0  4 POLY(1) VQ2  0.950E-3 -1
* QUIESCIENT CURRENT
RQ    3  4  1.2E5
* DIFF INPUT CAPACITANCE
CDIF  1  2  2.0E-12
* COMMON MODE INPUT CAPACITANCE
C1CM  1  99 3.0E-12
C2CM  2  99 3.0E-12
****************************
.MODEL DX D(IS=800.0E-18)
.MODEL JX PJF(IS=5.000E-12 BETA=299.8E-6 VTO=-1)
.ENDS AMP_UAFE_UAF42
