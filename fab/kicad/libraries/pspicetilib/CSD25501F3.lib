* CSD25501F3
*****************************************************************************
* (C) Copyright 2020 Texas Instruments Incorporated. All rights reserved.
*****************************************************************************
** This model is designed as an aid for customers of Texas Instruments.
** TI and its licensors and suppliers make no warranties, either expressed
** or implied, with respect to this model, including the warranties of
** merchantability or fitness for a particular purpose. The model is
** provided solely on an "as is" basis. The entire risk as to its quality
** and performance is with the customer.
*****************************************************************************
*
** Released by: Texas Instruments Inc.
* Part: CSD25501F3 
* Date: 01JAN2020
* Model Type:
* Simulator: PSPICE
* Simulator Version: 16.2.0.s003
* EVM Order Number:
* EVM Users Guide: 
* Datasheet: SLPS692A–OCTOBER 2017–REVISED JANUARY2018
* Model Version: Final 1.00
*
*****************************************************************************
*
* Updates:
*
* Final 1.00
* Release to Web
*
*****************************************************************************
*
*													*
*  CSD25501F3  -  P-Channel	20/06V LGA						*
*  Version:	Initial model based on measurements f/datasheet;	*
*								ged	    10/20/2017		*
*  SUBCKT Definition:  1=D  2=G  3=S							*
**********************************************************************
*$
.SUBCKT csd25501f3  1 2 3
.param AA		0.256662
.param width 	{AA/(2*1.3)} 
.param perim	{2.1*width} 
.param pLg		4e-07
.param plint	{0.1*pLg}
.param ptrc1	1.8e-03
.param ptrc2	1.8e-06
.param rred		2e+09
.param rbd		9e9
M1    10 11 12 12   PMOS W={width} L={pLg} PD={perim} PS={perim}
DGD   88 16 	DD	{width} 
DDG   10 16 	DD	{width} 
RGD   11 16 	RTEMPRED {rred}
RSS   12  9 	RTEMP	 2.8e-3 
RSUB   9  6 	RTEMP  0.02E-3
RSP    6  3 	RTEMP	 0.05E-3
CGS    2  9 	5e-12
RDD   10  7 	RTEMP	 39e-3
RDP    7  1 	RTEMP	 1e-3
CGD    2  7 		 0.1e-12
DBD    7  6 	DBD	 {width} 
RBD    7  9 		 {rbd}
CDS    7  9 		 2.0e-11
RGG   11  13 	3.7
RESD   8  13 	33
DDrc1	 2  9		DRCC
DDrc2  8  9		DRCC
RGC1   2  88 	RTEMPRC {12.4k*0.10}
RGC2  88  89 	RTEMPRC {12.5k*0.60}
RGC3  89  8		RTEMPRC {12.4k*0.30} 
DDr1  88  9		DRC	0.5
DDr2	89  9		DRC	0.5
DDz 	 8  9		ZENERD
******************************************************************* 
.MODEL  PMOS   PMOS (   LEVEL  = 7			Version = 3.2
+ TNOM    = 27	       K1	   = 0.95		K2	  = -1E-02            
+ VBM     = -8           NLX     = 1.74E-07	VTH0	  =-0.924
+ DVT0    = 2.7   	 DVT1    = 0.53		DVT2    =-3.2E-02
+ A0      = 1.0		 A1      = 0		A2      = 1
+ LINT    = {0.1*pLg}    TOX     = 1e-8		NCH 	  = 1.2e17
+ U0      = 220		 UA	   = 1.18E-09	UB 	  = 1E-19
+ DROUT   = 1.30		 DELTA   = 0.04		VOFFCV  =-0.45
+ PDIBLC1 = 0.3436	 PDIBLC2 = 8.6E-03	VSAT 	  = 8.0e4
+ PSCBE1  = 4.24E+08	 PSCBE2  = 2E-07		PVAG 	  = 0.0
+ DSUB    = 1.50		 ETA0    = 3E-02		ETAB    =-7E-02 
+ JS      = 1E-20		 VOFF    = -0.16		NFACTOR = 1.85 
+ KT1     = -0.546	 KT2     = 2.2E-03	KT1L    = 0
+ UA1     = 7E-10 	 UB1     =-1E-19     	UTE     =-1.45
+ CJ      = 1E-18		 CJSW    = 1E-18		CF      = 0
+ CGSO    = 10e-12	 CGDO    = 2e-12		CGBO    = 0  )
*******************************************************************
.MODEL DBD D ( TNOM=27
+ CJO=1.24E-09  VJ=0.87  M=0.385  FC=0.5  TT=2e-9
+ IS=1.1e-10 N=1.06  RS=1E-02  TRS1=1.4E-03  TRS2=-8E-06 
+ XTI=-3.5	BV=20.93  TBV1=6E-04  TBV2=-3.1e-7  NR=4.0 ISR=1.6E-08)
*******************************************************************
.MODEL DD D ( TNOM = 27
+ CJO=7.80E-10  VJ=0.65  M=0.96 FC=0.5  IS=1E-16  N=1.1  RS= 50 )
*******************************************************************
.MODEL ZENERD D ( TNOM = 27
+ CJO=2E-11	 VJ=0.65	M=0.5	 RS=0.2	TRS1=3.5e-3
+ FC=0.5	IS=5E-15  N=1.01	  XTI=3.2  ISR = 2e-12 NR=1.7
+ BV=6.289		TBV1=2.95E-04  )
*******************************************************************
.MODEL DRC D ( TNOM = 27
+ CJO=2E-11 	VJ=0.40	M=0.40   RS=1	TRS1=3.3e-3
+ FC=0.5	IS=3.32E-16  N=1.01  XTI=3.2  ISR=1e-12 NR=1.7 )
*******************************************************************
.MODEL DRCC D ( TNOM = 27
+ CJO=2.29E-10 	VJ=0.50	M=0.55   RS=0.01	IS=2e-16 )
* CJO=2.51e-10
*******************************************************************
.MODEL RTEMP RES ( TC1	= {ptrc1}  TC2	= {ptrc2} )
.MODEL RTEMPRC RES ( TC1 = 4e-3 )
.MODEL RTEMPRED RES ( TC1 = -3e-3 )
*******************************************************************
.ENDS csd25501f3
*$
