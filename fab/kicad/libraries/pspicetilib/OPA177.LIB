* OPA177
*$
*****************************************************************************
* (C) Copyright 2011 Texas Instruments Incorporated. All rights reserved.                                            
*****************************************************************************
** This model is designed as an aid for customers of Texas Instruments.
** TI and its licensors and suppliers make no warranties, either expressed
** or implied, with respect to this model, including the warranties of 
** merchantability or fitness for a particular purpose.  The model is
** provided solely on an "as is" basis.  The entire risk as to its quality
** and performance is with the customer.
*****************************************************************************
*
* This model is subject to change without notice. Texas Instruments
* Incorporated is not responsible for updating this model.
*
*****************************************************************************
*
** Released by: Analog eLab Design Center, Texas Instruments Inc.
* Part: OPA177
* Date: 01JUL2011
* Model Type: ALL IN ONE
* Simulator: PSPICE
* Simulator Version: 16.0.0.p001
* EVM Order Number: N/A 
* EVM Users Guide: N/A
* Datasheet: SBOS008
*
* Model Version: 1.0
*
*****************************************************************************
*
* Updates:
*
* Version 1.0 : 
* Release to Web
*
*****************************************************************************
* CONNECTIONS:		 NON-INVERTING INPUT
*	                  |  INVERTING INPUT
*       	          |   |  POSITIVE POWER SUPPLY
*               	  |   |   |  NEGATIVE POWER SUPPLY
*	        	  |   |   |   |  OUTPUT
*       	          |   |   |   |   |
* PIN CONFIG FOR OPA177: INP INM  3   4   5
*****************************************************************************
.SUBCKT OPA177 INP INN 3 4 5
C1   11 12 40.00E-12
C2    6  7 80.00E-12
DC    5 53 DX
DE   54  5 DX
DLP  90 91 DX
DLN  92 90 DX
DP    4  3 DX
EGND 99  0 POLY(2) (3,0) (4,0) 0 .5 .5
FB    7 99 POLY(5) VB VC VE VLP VLN 0 1.326E9 -1E9 1E9 1E9 -1E9
GA    6  0 11 12 301.6E-6
GCM   0  6 10 99 30.16E-12
IEE  10  4 DC 20.00E-6
HLIM 90  0 VLIM 1K
Q1   11  2 13 QX
Q2   12  1 14 QX
R2    6  9 100.0E3
RC1   3 11 3.316E3
RC2   3 12 3.316E3
RE1  13 10 729.2
RE2  14 10 729.2
REE  10 99 9.999E6
RO1   8  5 30
RO2   7 99 30
*  RP    3  4 15.15E3
VB    9  0 DC 0
VC    3 53 DC 1.500
VE   54  4 DC 1.500
VLIM  7  8 DC 0
VLP  91  0 DC 22
VLN   0 92 DC 22

* OUTPUT SUPPLY MIRROR
FQ3   0 20 POLY(1) VLIM 0  1
DQ1  20 21 DX
DQ2  22 20 DX
VQ1  21  0 0
VQ2  22  0 0
FQ1   3  0 POLY(1) VQ1  0.976E-3  1
FQ2   0  4 POLY(1) VQ2  0.976E-3 -1

* QUIESCIENT CURRENT
RQ    3  4  3.0E4

* DIFF INPUT CAPACITANCE
CDIF  1  2  1.0E-12

* COMMON MODE INPUT CAPACITANCE
C1CM  1  99 1.5E-12
C2CM  2  99 1.5E-12

* INPUT PROTECTION
R1IN  INP 1  500
D1A    1 VD1 DX
D1B   VD1 2  DX
R2IN  INN 2  500
D2A    2 VD2 DX
D2B   VD2 1  DX

****************************
.MODEL DX D(IS=800.0E-18)
.MODEL QX NPN(IS=800.0E-18 BF=10.00E3)

.ENDS OPA177
*$