***********************************************************************
**  v1.7.5 PSpice Models Library - TI NexFET Power N/P Channel FET's
*  (C) Copyright 2010 Texas Instruments Incorporated. All rights reserved.
***********************************************************************
***********************************************************************
* Notice:
* The data and models provided herein are intended to be used by 
* customers as an aid in designing electrical circuits using 
* semiconductor devices manufactured &/or sold by Texas Instruments.
* The models and associated information contained herein are not to
* be construed as a guarantee of electrical performance and/or device 
* specification.  Therefore, Texas Instruments does not assume any 
* liability arising out of the use of said data or models, nor from
* the devices or products manufactured therefrom.  Texas Instruments
* does not convey any license under its patent rights nor the rights
* of others.  Texas Instruments reserves the right to change models 
* without prior notice.  
*
* TI is a registered trademark of Texas Instruments Inc.
*************************************************************************
********************************************************************************
*
* Released by: Analog e-Lab Design Center, Texas Instruments Inc.
* Part: CSD16406Q3
* Date: 12/08/2010
* Model Type: TRANSIENT
* Simulator: PSPICE
* Datasheet: SLPS202A - August 2009 - Revised September 2010
*
*****************************************************************************
*$
**********************************************************************
**********************************************************************
*                                                                    *
* CSD16406Q3 -  PSpice Model for use with OrCAD                      *
*                                                                    *
**********************************************************************
**********************************************************************
* SUBCKT Definition:  1=D  2=G  3=S
*
.SUBCKT CSD16406Q3  1 2 3
.PARAM  pwidth 1.2991
.PARAM  perim  2.6
M1  10 11 12 12   NMOS	W={pwidth} L=0.5u  PS={perim} PD={perim}
DBD   8  7	 DBD
RBD   7  8	 1E12
CGD   5 14	 81e-12
RGD  14  7   3.0e3
M2   12 11 12 10  PMOSd W={pwidth} L=0.038u  PS={perim} PD={perim}
DGD  13 11	 DGD
DDG  13 10	 DDG
* Note:  gate oxide capacitance included in NMOS below
CGS   5  8	 10E-12
LGG   2  5	 0.87e-9
RGG   5 11	 1.1
RSB  12  9   RTEMP 0.80e-3
RSS   9  8   0.05e-3
RSP   8  6   0.45e-3
LSS   3  6	 0.55E-9
RDD   7 10	 RTEMP 1.70E-3
RDP   4  7   0.2e-3
LDD   1  4	 0.08E-9
******************************************************************* 
.MODEL  NMOS   NMOS (    LEVEL  = 7              Version = 3.2
+ TNOM   = 27            LINT   = 0.060e-6       CAPMOD  = 2
+ TOX    = 3e-8          NCH    = 1.340e17       NSUB   = 1.0e16
+ AGS    = 0             PVAG   = 2.8
+ A0     = 1.2           A1     = 0.0            A2     = 1.70
+ UA     = 2.0e-9        UB     = 2.4e-18        VBM    = -5
+ UA1    = 3.3e-9        UB1    = -7e-19         UTE    = -1.50
+ KT1    = -0.95         KT2    = 0.022          KT1L   = 1e-12
+ DVT0   = 2.3           DVT1   = 0.52	       DVT2   = -0.033
+ ETA0   = 0.04          ETAB   = -0.07          XJ     = 5e-7
+ PDIBLC1= 0.5           PDIBLC2= 0.01           NLX    = 1.77e-7
+ PSCBE1 = 4.2e8         PSCBE2 = 4.0e-6         PCLM   = 1.3
+ VOFF   = -0.14         NFACTOR= 1.5            JS     = 0
+ DROUT  = 0.7           DSUB   = 1.0
+ CJ     = 1e-18         CJSW   = 1e-18          CF     = 0
+ CGSO   = 242e-12       CGDO   = 1e-12          CGBO   = 0  )
******************************************************************* 
.MODEL  PMOSd   PMOS (  LEVEL  = 7     Version = 3.2
+ TNOM = 27              CAPMOD = 2
+ JS     = 0             VBM    = -10
+ TOX    = 3e-8          NCH    = 1.8e17        NSUB   = 1e16
+ CJ     = 1e-18         CJSW   = 1e-18         CF     = 0
+ CGSO   = 0             CGDO   = 0             CGBO   = 0 )
******************************************************************* 
.MODEL DBD D (CJO=2203e-12 VJ=0.8 M=0.45 FC=0.5 IS=9.0e-13 N=1.02
+ RS=1.0e-3 TRS1=3e-3 XTI=4.0  TNOM=27 TT=5E-09 BV=27)
*******************************************************************
.MODEL DGD D (CJO=195E-12 VJ=0.65 M=0.85 FC=0.5 IS=1e-12 
+ TNOM=27 Rs=0.1)
.MODEL DDG D (CJO=195e-12 VJ=0.65 M=0.85 FC=0.5 IS=1e-25
+ TNOM=27 RS=0.1)
******************************************************************* 
.MODEL RTEMP RES (TC1=1.9E-3  TC2=7.5E-6)
******************************************************************* 
.ENDS CSD16406Q3
