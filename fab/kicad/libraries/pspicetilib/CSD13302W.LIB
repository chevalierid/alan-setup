*$
*CSD13302W
***********************************************************************
* (C) Copyright 2015 Texas Instruments Incorporated. All rights reserved.
***********************************************************************
***********************************************************************
** This model is designed as an aid for customers of Texas Instruments.
** TI and its licensors and suppliers make no warranties, either expressed
** or implied, with respect to this model, including the warranties of
** merchantability or fitness for a particular purpose. The model is
** provided solely on an "as is" basis. The entire risk as to its quality
** and performance is with the customer
********************************************************************************
**Released by: WEBENCH(R) Design Center, Texas Instruments Inc.
* Part: CSD13302W
* Date: 03/20/2015
* Model Type: TRANSIENT
* Simulator: PSPICE
* Simulator Version: 16.2.0.p001
* EVM Order Number:
* EVM Users Guide:
* Datasheet: SLPS535 â€“MARCH 2015
* Model Version: Final 1.00
**
**********************************************************************
*                                                                    *
* CSD13302W   -  PSpice Model for use with OrCAD                     *
*                                                                    *
**********************************************************************
**********************************************************************
* Characteristics:
*   Technology:   NCH 12/10
*
**********************************************************************
* Model History:
*   tr, 8:46 AM 3/17/2015
*    
**********************************************************************
* SUBCKT Definition:  1=D  2=G  3=S
.SUBCKT CSD13302W	1	2	3
* PARAMETER (local) DEFINITIONS SECTION
 .param	 AAN58890	0.455
 .param	 AAN83325	1.58
 .PARAM  ptrc1    	1.00e-3  
 .PARAM  ptrc2    	00.00e-6
 .PARAM  pwidthP  	{AAN58890/0.8*2}
 .PARAM  pwidth   	{pwidthP*1e6}
 .PARAM  perimP   	{2.1*pwidthP}
 .PARAM  perim    	{perimP*1e6}
****** MODEL SUBCIRCUIT BEGINS HERE     ******************************
RDD	1	4	RTEMP 10.50e-3
RSP   	6	3	0.15e-3
RGG	2	5	3.60

M1	4	5	6	6	NMOS	W={pwidthP} L=0.5u  PS={perimP} PD={perimP}

cgs0	3	5	1.00e-10

DDG1  	4	7	DD	{AAN58890/AAN83325}
DDG2  	5	7	DD	{AAN58890/AAN83325}

DBD		6	4	DBD	{AAN58890/AAN83325}

******************************************************************* 
.MODEL  NMOS   NMOS (
* CONTROL PARAMETERS
+ LEVEL  = 7             VERSION = 3.2         CAPMOD = 2
* DC PARAMETERS
+ NLX    = 1.74E-7       VBM    = -5           DVT0   = 2.2
+ DVT1   = 0.53          DVT2   = -0.032       U0     = 650
+ UA     = 8.0e-9        UB     = 0.0e-19      A0     = 0.9
+ AGS    = 0.0           A1     = 0.0          A2     = 0.9
+ LINT   = 0.055e-6      VOFF   = -0.10        NFACTOR= 1.7
+ ETA0   = 0.005         ETAB   = -0.07        DSUB   = 1.4
+ PCLM   = 3.00          PDIBLC1= 0.02         PDIBLC2= 0.004
+ DROUT  = 0.90          PSCBE1 = 3.0e8        PSCBE2 = 1e-6
+ PVAG   = 0.1           DELTA  = 0.0220       JS     = 0
* AC AND CAPACITANCE PARAMETERS
+ CGSO   = 1.00e-18		CGDO   = 1e-18        CGBO   = 1e-18
+ CJ     = 1e-18        CJSW   = 1e-18        CF     = 0
* TEMPERATURE PARAMETERS
+ TNOM   = 25            UTE    = -1.5         KT1    = -0.625
+ KT1L   = 1.0e-15       KT2    = 0.022        UA1    = -01.00e-9
+ UB1    = 5.0e-17      
* PROCESS PARAMETERS
+ TOX    = 2.50e-8       XJ     = 3e-7          NCH    = 1.200e17
+ NSUB   = 1.0e16)
******************************************************************* 
.MODEL DBD D (CJO=3.0e-10 VJ=1.0 M=0.3 TNOM=25
+ FC=0.5 TT=2e-09 XTI=1.0 BV=13.2
+ IS=2.0e-11 N=1.07 RS=1.0e-3 TRS1=4.0e-3)
*******************************************************************
.MODEL DD  D (CJO=2.75e-009 VJ=0.40 M=0.50 IS=1e-13 RS=0.1 TNOM=25)
*******************************************************************
.MODEL RTEMP RES (TC1={ptrc1} TC2={ptrc2})
*******************************************************************
.ENDS CSD13302W
*$