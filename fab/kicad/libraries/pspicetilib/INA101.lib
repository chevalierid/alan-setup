* INA101
*****************************************************************************
* (C) Copyright 2011 Texas Instruments Incorporated. All rights reserved.                                            
*****************************************************************************
** This model is designed as an aid for customers of Texas Instruments.
** TI and its licensors and suppliers make no warranties, either expressed
** or implied, with respect to this model, including the warranties of 
** merchantability or fitness for a particular purpose.  The model is
** provided solely on an "as is" basis.  The entire risk as to its quality
** and performance is with the customer.
*****************************************************************************
*
* This model is subject to change without notice. Texas Instruments
* Incorporated is not responsible for updating this model.
*
*****************************************************************************
*
** Released by: Analog eLab Design Center, Texas Instruments Inc.
* Part: INA101
* Date: 11MAY2011
* Model Type: ALL IN ONE
* Simulator: PSPICE
* Simulator Version: 16.0.0.p001
* EVM Order Number: N/A
* EVM Users Guide: N/A
* Datasheet: SBOS133
*
* Model Version: 1.0
*
*****************************************************************************
*
* Updates:
*
* Version 1.0 : 
* Release to Web
*
*****************************************************************************
***** INA101 SUB-CIRCUIT
* CONNECTIONS:          NON-INVERTING INPUT
*                       |   INVERTING INPUT
*                       |   |   POSITIVE POWER SUPPLY
*                       |   |   |   NEGATIVE POWER SUPPLY
*                       |   |   |   |   OUTPUT
*                       |   |   |   |   |   REFERENCE
*                       |   |   |   |   |   |   GAIN SENSE 1
*                       |   |   |   |   |   |   |   GAIN SENSE 2
*                       |   |   |   |   |   |   |   |
.SUBCKT INA101         1   2   3   4   5   8   9  10
*
***** A1_101E SUB-CIRCUIT
* CONNECTIONS:          NON-INVERTING INPUT
*                       |   INVERTING INPUT
*                       |   |   POSITIVE POWER SUPPLY
*                       |   |   |   NEGATIVE POWER SUPPLY
*                       |   |   |   |   OUTPUT
*                       |   |   |   |   |
X1                      2   9   3   4  11   A1_101E
*
***** A2_101E SUB-CIRCUIT
* CONNECTIONS:          NON-INVERTING INPUT
*                       |   INVERTING INPUT
*                       |   |   POSITIVE POWER SUPPLY
*                       |   |   |   NEGATIVE POWER SUPPLY
*                       |   |   |   |   OUTPUT
*                       |   |   |   |   |
X2                      1  10   3   4  12   A2_101E
*
***** A3_101E SUB-CIRCUIT
* CONNECTIONS:          NON-INVERTING INPUT
*                       |   INVERTING INPUT
*                       |   |   POSITIVE POWER SUPPLY
*                       |   |   |   NEGATIVE POWER SUPPLY
*                       |   |   |   |   OUTPUT
*                       |   |   |   |   |
X3                     14  13   3   4   5   A3_101E
*
R1    11  13  10.0000K
R2    13   5   9.9994K
C2    13   5   0.8000PF
R3    12  14  10.0000K
R4    14   8  10.0000K
*
RFB1   9  11  20.0000K
CC1    9  11  15.0000PF
RFB2  10  12  20.0000K
CC2   10  12  15.0000PF
*
.ENDS


.SUBCKT A1_101E 1 2 3 4 5
*
C1   11 12 8.660E-12
C2    6  7 30.00E-12
DC    5 53 DX
DE   54  5 DX
DLP  90 91 DX
DLN  92 90 DX
DP    4  3 DX
EGND 99  0 POLY(2) (3,0) (4,0) 0 .5 .5
FB    7 99 POLY(5) VB VC VE VLP VLN 0 70.20E6 -70E6 70E6 70E6 -70E6
GA    6  0 11 12 320.4E-6
GCM   0  6 10 99 2.022E-9
IEE  10  4 DC 30.05E-6
HLIM 90  0 VLIM 1K
Q1   11  2 13 QX
Q2   12  1 14 QX
R2    6  9 100.0E3
RC1   3 11 3.121E3
RC2   3 12 3.121E3
RE1  13 10 1.394E3
RE2  14 10 1.394E3
REE  10 99 6.656E6
RO1   8  5 25
RO2   7 99 25
*  RP    3  4 10.70E3
VB    9  0 DC 0
VC    3 53 DC 2.500
VE   54  4 DC 2.500
VLIM  7  8 DC 0
VLP  91  0 DC 10
VLN   0 92 DC 10
****************************
* A1_101 "E" - ENHANCEMENTS
****************************
* OUTPUT SUPPLY MIRROR
FQ3   0 20 POLY(1) VLIM 0  1
DQ1  20 21 DX
DQ2  22 20 DX
VQ1  21  0 0
VQ2  22  0 0
FQ1   3  0 POLY(1) VQ1  2.136E-3  1
FQ2   0  4 POLY(1) VQ2  2.136E-3 -1
* QUIESCIENT CURRENT
RQ    3  4  4.5E4
* DIFF INPUT CAPACITANCE
CDIF  1  2  3.0E-12
* COMMON MODE INPUT CAPACITANCE
C1CM  1  99 1.5E-12
C2CM  2  99 1.5E-12
* INPUT PROTECTION
R1IN  INP 1  1000
D1IN   1  2  DX
R2IN  INN 2  1000
D2IN   2  1  DX
*****************************
.MODEL DX D(IS=800.0E-18)
.MODEL QX NPN(IS=800.0E-18 BF=600)
R100 INP 0 1E9
R101 INN 0 1E9
.ENDS



.SUBCKT A2_101E 1 2 3 4 5
*
C1   11 12 8.660E-12
C2    6  7 30.00E-12
DC    5 53 DX
DE   54  5 DX
DLP  90 91 DX
DLN  92 90 DX
DP    4  3 DX
EGND 99  0 POLY(2) (3,0) (4,0) 0 .5 .5
FB    7 99 POLY(5) VB VC VE VLP VLN 0 59.67E6 -60E6 60E6 60E6 -60E6
GA    6  0 11 12 377.0E-6
GCM   0  6 10 99 1.192E-9
IEE  10  4 DC 30.05E-6
HLIM 90  0 VLIM 1K
Q1   11  2 13 QX
Q2   12  1 14 QX
R2    6  9 100.0E3
RC1   3 11 2.653E3
RC2   3 12 2.653E3
RE1  13 10 926.8
RE2  14 10 926.8
REE  10 99 6.656E6
RO1   8  5 25
RO2   7 99 25
*  RP    3  4 10.70E3
VB    9  0 DC 0
VC    3 53 DC 2.500
VE   54  4 DC 2.500
VLIM  7  8 DC 0
VLP  91  0 DC 10
VLN   0 92 DC 10
****************************
* A2_101 "E" - ENHANCEMENTS
****************************
* OUTPUT SUPPLY MIRROR
FQ3   0 20 POLY(1) VLIM 0  1
DQ1  20 21 DX
DQ2  22 20 DX
VQ1  21  0 0
VQ2  22  0 0
FQ1   3  0 POLY(1) VQ1  2.136E-3  1
FQ2   0  4 POLY(1) VQ2  2.136E-3 -1
* QUIESCIENT CURRENT
RQ    3  4  4.5E4
* DIFF INPUT CAPACITANCE
CDIF  1  2  3.0E-12
* COMMON MODE INPUT CAPACITANCE
C1CM  1  99 1.5E-12
C2CM  2  99 1.5E-12
* INPUT PROTECTION
R1IN  INP 1  1000
D1IN   1  2  DX
R2IN  INN 2  1000
D2IN   2  1  DX
****************************
.MODEL DX D(IS=800.0E-18)
.MODEL QX NPN(IS=800.0E-18 BF=600)
R100 INP 0 1E9
R101 INN 0 1E9
.ENDS




.SUBCKT A3_101E  1 2 3 4 5
*
C1   11 12 8.660E-12
C2    6  7 10.00E-12
DC    5 53 DX
DE   54  5 DX
DLP  90 91 DX
DLN  92 90 DX
DP    4  3 DX
EGND 99  0 POLY(2) (3,0) (4,0) 0 .5 .5
FB    7 99 POLY(5) VB VC VE VLP VLN 0 358.0E6 -40E6 40E6 40E6 -40E6
GA    6  0 11 12 62.83E-6
GCM   0  6 10 99 198.7E-12
IEE  10  4 DC 5.050E-6
HLIM 90  0 VLIM 1K
Q1   11  2 13 QX
Q2   12  1 14 QX
R2    6  9 100.0E3
RC1   3 11 15.92E3
RC2   3 12 15.92E3
RE1  13 10 5.515E3
RE2  14 10 5.515E3
REE  10 99 39.60E6
RO1   8  5 25
RO2   7 99 25
*  RP    3  4 10.61E3
VB    9  0 DC 0
VC    3 53 DC 2.500
VE   54  4 DC 2.500
VLIM  7  8 DC 0
VLP  91  0 DC 10
VLN   0 92 DC 10
****************************
* A3_101 "E" - ENHANCEMENTS
****************************
* OUTPUT SUPPLY MIRROR
FQ3   0 20 POLY(1) VLIM 0  1
DQ1  20 21 DX
DQ2  22 20 DX
VQ1  21  0 0
VQ2  22  0 0
FQ1   3  0 POLY(1) VQ1  2.162E-3  1
FQ2   0  4 POLY(1) VQ2  2.162E-3 -1
* QUIESCIENT CURRENT
RQ    3  4  4.5E4
* DIFF INPUT CAPACITANCE
CDIF  1  2  3.0E-12
* COMMON MODE INPUT CAPACITANCE
C1CM  1  99 1.5E-12
C2CM  2  99 1.5E-12
****************************
.MODEL DX D(IS=800.0E-18)
.MODEL QX NPN(IS=800.0E-18 BF=100)
.ENDS

