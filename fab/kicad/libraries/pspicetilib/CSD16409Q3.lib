***********************************************************************
**  v1.7.5 PSpice Models Library - TI NexFET Power N/P Channel FET's
*  (C) Copyright 2010 Texas Instruments Incorporated. All rights reserved.
***********************************************************************
***********************************************************************
* Notice:
* The data and models provided herein are intended to be used by 
* customers as an aid in designing electrical circuits using 
* semiconductor devices manufactured &/or sold by Texas Instruments.
* The models and associated information contained herein are not to
* be construed as a guarantee of electrical performance and/or device 
* specification.  Therefore, Texas Instruments does not assume any 
* liability arising out of the use of said data or models, nor from
* the devices or products manufactured therefrom.  Texas Instruments
* does not convey any license under its patent rights nor the rights
* of others.  Texas Instruments reserves the right to change models 
* without prior notice.  
*
* TI is a registered trademark of Texas Instruments Inc.
*************************************************************************
********************************************************************************
*
* Released by: Analog e-Lab Design Center, Texas Instruments Inc.
* Part: CSD16409Q3
* Date: 03/11/2013
* Model Type: TRANSIENT
* Simulator: PSPICE
* Datasheet: SLPS204A - August 2009 - Revised May 2010
*
* Model Version: Final 2.00
*
*****************************************************************************
*
* Updates:
*
* Final 1.00
* Release to Web.
*
* Final 2.00
* Updated to BSIM3v3 model subcircuit format
*
*****************************************************************************
*$
**********************************************************************
**********************************************************************
*                                                                    *
* CSD16409Q3  -  PSpice Model for use with OrCAD                    *
*                                                                    *
**********************************************************************
**********************************************************************
* SUBCKT Definition:  1=D  2=G  3=S
.SUBCKT CSD16409Q3  1 2 3
* PARAMETER (local) DEFINITIONS SECTION
 .PARAM  pWidth  0.8623488 	; m, total gate width
 .PARAM  perim   {2*pWidth}	; m, gate/drain perim (rough est)
 .PARAM  ptrc1   2.50e-3	; temp co of ext Rd / Rs
 .PARAM  ptrc2   1.50e-5
****** MODEL SUBCIRCUIT BEGINS HERE     ******************************
M1   10 11 12 12  NMOS	W={pWidth} L=0.5u  PS={perim} PD={perim}
M2   12 11 12 10  PMOSd W={pWidth} L=0.160u PS={perim} PD={perim}

DBD   8  7	 DBD
DDG  14  5	 DD
DGD  14 10	 DD

RBD0  8  7   5e11
RGS  13  8	 400
RGG   5 11	 1.1
RSB  12  9	 RTEMP 0.20e-3
RSS   9  8 	 RTEMP 0.55e-3
RSP   8  6 	 0.50e-3
RDD   7 10 	 RTEMP 3.61e-3
RDP   4  7	 0.40e-3

LGG   2  5	 0.95e-9
LSS   6  3	 0.22e-9
LDD   1  4	 0.05e-9

CBD0  8  7   50e-12
CGD0  5  7	 1E-12
CGS0  5  8	 2e-12
CGS   5 13	 75e-12
******************************************************************* 
.MODEL  NMOS   NMOS (    LEVEL  = 7             Version = 3.2  
+ TNOM   = 27            LINT   = 0.055e-6      CAPMOD = 2
+ TOX    = 3.0e-8        NCH    = 1.4525e17     NSUB   = 1.0e16
+ AGS    = 0.0           PVAG   = 1.2		U0	 = 670
+ A0     = 1.20          A1     = 0.0           A2     = 0.95
+ UA     = 0.5e-9        UB     = 1.0e-18       VBM    = -5
+ UA1    = 0.1e-9        UB1    = -2.0e-18      UTE    = -1.50
+ KT1    = -0.81         KT2    =-0.022         KT1L   = 1.0e-12
+ DVT0   = 2.5           DVT1   = 0.70	      DVT2   = -0.033
+ ETA0   = 0.050         ETAB   = -0.07         XJ     = 4e-7
+ PDIBLC1= 0.050         PDIBLC2= 0.006         NLX    = 1.75e-7
+ PSCBE1 = 1.3e8         PSCBE2 = 1e-7          PCLM   = 1.30
+ VOFF   = -0.110        NFACTOR= 2.40          JS     = 0
+ DROUT  = 0.60          DSUB   = 1.0           DELTA  = 0.06
+ CJ     = 1e-18         CJSW   = 1e-18         CF     = 0
+ CGSO   = 170e-12       CGDO   = 4.0e-12       CGBO   = 0  )
******************************************************************* 
.MODEL  PMOSd   PMOS (   LEVEL  = 1
+ TNOM   = 27            VTO    = -0.3		IS     = 6.0e-14
+ TOX    = 3.0e-8        NSUB   = 6e17          gamma  = 1.57
+ CJ     = 1e-18         CJSW   = 1e-18
+ CGSO   = 1e-18         CGDO   = 1e-18         CGBO   = 1e-18 )
********************************************************************
.MODEL DBD D (		 CJO	  = 1.325e-9 	VJ	 = 0.92
+ M	   = 0.49 		 TNOM	  = 27		FC	 = 0.5
+ TT	   = 5e-09		 XTI	  = 4.5		BV	 = 27
+ IS	   = 4.7e-13 	 N	  = 1.01 		RS     = 2.25e-3
+ TRS1   = 3.83e-3)
.MODEL DD  D (		 CJO=6.00e-11		VJ     = 0.35
+ M	   = 0.85		 IS	  = 1e-15		RS     = 0.1
+ TNOM   = 27)
*******************************************************************
.MODEL RTEMP RES (TC1={ptrc1} TC2={ptrc2})
*******************************************************************
.ENDS CSD16409Q3  
*$