* 
*****************************************************************************
* (C) Copyright 2010 Texas Instruments Incorporated. All rights reserved.
*****************************************************************************
** This model is designed as an aid for customers of Texas Instruments.
** TI and its licensors and suppliers make no warrenties, either expressed
** or implied, with respect to this model, including the warranties of 
** merchantability or fitness for a particular purpose.  The model is
** provided solely on an "as is" basis.  The entire risk as to its quality
** and performance is with the customer
*****************************************************************************
*
* Released by: Analog eLab Design Center, Texas Instruments Inc.
* Part: 
* Date: 10/26/2010 13:24:58
* Model Type: PSPICE
* Simulator: PSPICE 
* Simulator Version: 16.2.0.p001
* EVM Order Number: 
* EVM Users Guide:  
* Datasheet: {ENTER THE DATASHEET DATE HERE}
*
* Model Version: Final 1.00
*
*****************************************************************************
*
* Updates:
*
* Final 1.00
* Release to Web.
*
*****************************************************************************
*
*XI0 INP INN VREF VREF VSS SYNC VREF VDD VDD VSS VSS NET44 NET43 VDD TPA3106D1

.subckt TPA3106D1 INP INN MSTR_SLVB GAIN0 GAIN1 SYNC VREF AVCC PVCC AGND PGND OUTN OUTP SDB
R0 NET45 AGND 1
R1 NET45 AGND 1
XI0 GAIN0 GAIN1 VREF AVCC INN  INP  PGAN PGAP AGND PGA_AND_LOGIC
XI1 PGAN  PGAP  AVCC PVCC AGND PGND OUTN OUTP NTRI SDB CLASSD
XI2 MSTR_SLVB SYNC VREF AVCC AGND NTRI MASTER_S
.ends TPA3106D1
*$
*
.subckt HBRIDGE_TOPNMOS D G S VDD
EBETA NET023 0 POLY(1) VDD 0  503.490857143e-3  -31.8559524e-3  572.809524e-6
C0    NET2 0 1e-15
E1    NET03  0 POLY(2) G 0 NET023 0  0  0  0  0  1
ERES  NET039 0 POLY(2) D S NET03  0  0  0  0  0  1
GE0   D S NET2 0 1
D4    S D DDEFAULT
R2    NET039 NET2 1
R0    D S 2e3
.MODEL DDEFAULT D IS=8.000E-16   RS=5  BV=36
.ends HBRIDGE_TOPNMOS
*$
*
.subckt HBRIDGE AVDD PVDD SDB SSS VHA VHB VLA VLB VOA VOB VSS
XI3 VOA  VLA VSS PVDD HBRIDGE_TOPNMOS
XI2 VOB  VLB VSS PVDD HBRIDGE_TOPNMOS
XI1 PVDD VHB VOB PVDD HBRIDGE_TOPNMOS
XI0 PVDD VHA VOA PVDD HBRIDGE_TOPNMOS
R0  PVDD AVDD 1e6
R1  PVDD SDB 1e6
R2  PVDD SSS 1e6
.ends HBRIDGE
*$
*
.subckt DELAY_LINE VIN VOUT
R0 NET17 0 75
E1 VOUT  0 NET17 0 1
E0 NET20 0 VIN   0 1
T0 NET20 0 NET17 0 Z0=75 TD=25e-9
.ends DELAY_LINE
*$
*
.subckt HALF_HDRIVER VIN VDD VSS VOUTPOS VOUTNEG
X0 VIN NET028 VDD VSS VOUTPOS HPA_AND2
X1 NET17 NET019 VDD VSS VOUTNEG HPA_AND2
XI2 VIN NET028 DELAY_LINE
XI3 NET17 NET019 DELAY_LINE
X8 VIN VDD VSS NET17 HPA_INV
.ends HALF_HDRIVER
*$
*
.subckt HDRIVER VDD VHA VHB VIA VIB VLA VLB VSS
XIA VIA VDD VSS VHA VLA HALF_HDRIVER
XIB VIB VDD VSS VHB VLB HALF_HDRIVER
.ends HDRIVER
*$
*
.subckt BD_COMP VIP VIN REF VDD VSS VOP VON
XM VIP REF VDD VSS VOP HPA_COMP
XN VIN REF VDD VSS VON HPA_COMP
.ends BD_COMP
*$
*
.subckt FD_OPA VDD VN VP VOUTP VOUTN VSS VCM
RT2 VIBN NET029 1m
R5 NET061 VIBP 5e6
R6 NET036 VIBN 5e6
R0 VP VN 10e6
RT1 NET068 VIBP 1m
C1 VIBN 0 318e-12
C0 VIBP 0 318e-12
E4 VOUTP 0 VIBP 0 1
E5 VOUTN 0 VIBN 0 1
EG2 NET036 VCM VP VN -100e3
EG1 NET061 VCM VP VN  100e3
V1 VSSX VSS DC 100e-3
V0 VDD VDDX DC 100e-3
D3 VSSX NET029 DDEFAULT
D2 NET029 VDDX DDEFAULT
D1 VSSX NET068 DDEFAULT
D0 NET068 VDDX DDEFAULT
.MODEL DDEFAULT D IS=8.000E-16   RS=5  BV=36
.ends FD_OPA
*$
*
.subckt CLASSD VIN VIP AVDD PVDD AGND DGND OUTP OUTN VTRI SDB
XI4 AVDD PVDD SDB AVDD VHA VHB VLA VLB OUTN OUTP DGND HBRIDGE
XI3 PVDD VHA VHB COMP_N COMP_P VLA VLB DGND HDRIVER
XI2 INTEGR_N INTEGR_P VTRI PVDD DGND COMP_N COMP_P BD_COMP
XI1 PVDD NET056 NET057 INTEGR_P INTEGR_N DGND VCM FD_OPA
E3 VCM AGND POLY(2) AVDD AGND DGND AGND  0  500e-3  500e-3
C1 NET056 INTEGR_P 30e-12
C0 NET057 INTEGR_N 30e-12
R3 NET056 OUTP 200e3
R2 NET057 OUTN 200e3
R1 NET056 VIN 200e3
R0 VIP NET057 200e3
.ends CLASSD
*$
*
.subckt MASTER_S MSTR_SLV SYNC VREF VDD VSS OUT
XINV0  MSTR_SLV VREF VSS MSB HPA_INV
XINV1  MSB       VDD VSS MSS HPA_INV
XNV2   VCMP3 VDD VSS VINV1 HPA_INV
XCMP2  VREF3 MSS VDD VSS VCMP3 HPA_COMP
XCMP1  MSS VREF2 VDD VSS VCMP2 HPA_COMP
SXISW1 NET2  OUT  VCMP2 0 VSWITCH1
SXISW2 SYNC2 OUT  VCMP3 0 VSWITCH1
SXISW3 OUT   SYNC VINV1 0 VSWITCH1
E3 VMID 0 POLY(2) VDD 0 VSS 0  0  500e-3  500e-3
E1 SYNC2 VSS SYNC VSS 1
V1 NET2 VMID DC 0 AC 0 0 PULSE -833.3e-3 833.3e-3 0 1.6666666416667e-6 1.6666666416667e-6
+ 50e-15 3.3333333333333e-6
RS2 VINV1 VSS 1e9
RS1 SYNC2 VSS 1e9
RS0 OUT VSS 1e9
R5  VDD VREF3 700e3
R4  VREF3 VSS 100e3
R1  VDD VREF2 100e3
R0  VREF2 VSS 700e3
R3  NET086 VDD 399e3
R2  VDD NET086 401e3
RAA VSS MSS 50e3
RA  MSS VDD 50e3
.MODEL VSWITCH1 VSWITCH Roff=1e7 Ron=0.01 Voff=0 Von=1.1
.ends MASTER_S
*$
*
.subckt PGA_AND_LOGIC G0 G1 VREF VDD VIN VIP VON VOP VSS
XSW9 NG36  VOP     NET0135 SWITCH_PS
XSW5 N320K VOP     NET0176 SWITCH_PS
XSW1 NG20  NET0160 VIN     SWITCH_PS
XSW3 N16K  NET145  VIN     SWITCH_PS
XSW2 N16K  NET189  VIP     SWITCH_PS
XSW7 NG32  VOP     NET153  SWITCH_PS
XSW4 N320K VON     NET0188 SWITCH_PS
XSW6 NG32  VON     NET183  SWITCH_PS
XSW0 NG20  NET0101 VIP     SWITCH_PS
XSW8 NG36  VON     NET149  SWITCH_PS
XOR2 NG20  NG26 VDD VSS N320K HPA_OR2
XOR1 NG26  NG32 NG36 VDD VSS N16K HPA_OR3
XAND1 NOG1 NOG2 VDD VSS NG20 HPA_AND2
XAND4 OG1 OG2 VDD VSS NG36 HPA_AND2
XAND3 NOG1 OG2 VDD VSS NG32 HPA_AND2
XAND2 OG1 NOG2 VDD VSS NG26 HPA_AND2
XIOPA VDD NNN PPP VOP VON VSS VMID2 FD_OPA
XIV1 G0 VREF VSS NET0144 HPA_INV
XIV2 G1 VREF VSS NET0148 HPA_INV
XIV4 NET0148 VDD VSS OG2 HPA_INV
XIV3 NET0144 VDD VSS OG1 HPA_INV
XIV6 OG2 VDD VSS NOG2 HPA_INV
XIV5 OG1 VDD VSS NOG1 HPA_INV
R3 G1 VSS 1e6
R1 G0 VSS 1e6
RS6 VSS VON 1e9
RS7 VOP VSS 1e9
RS11 NET0135 VSS 1e9
RS10 NET149 VSS 1e9
RS9 NET153 VSS 1e9
RS8 NET183 VSS 1e9
RS0 NET0101 VSS 1e9
RS2 NET189 VSS 1e9
RS1 NET0160 VSS 1e9
RS3 NET145 VSS 1e9
RS5 NET0176 VSS 1e9
RS4 NET0188 VSS 1e9
RG1B NNN NET0160 32e3
RF3B NET0135 NET153 368e3
RG2B NNN NET145 16e3
RG2A NET189 PPP 16e3
RF3A NET183 NET149 368e3
RF2A NET0188 NET183 320e3
RG1A NET0101 PPP 32e3
RF1B NET0176 NNN 320e3
RF1A PPP NET0188 320e3
RF2B NET153 NET0176 320e3
E3 VMID2 0 POLY(2) VDD 0 VSS 0  0  500e-3  500e-3
.ends PGA_AND_LOGIC
*$
*
.SUBCKT HPA_COMP INP INN VDD VSS OUT 
*.SUBCKT HPA_COMP VDD INP INN OUT VSS 
E1 4 0 VALUE = { IF( (V(INP) > V(INN)), V(VDD), V(VSS) ) }
R1 4 OUT 1
.ENDS HPA_COMP
*$
*
.SUBCKT HPA_OR2 A B VDD VSS Y 
*Ra    A VSS  200k
*Rb    B VSS  200k
*Ca    A VSS  1f
*Cb    B VSS  1f
E1    4 0 VALUE = { IF( ((V(A)> 0.7 ) | (V(B)> 0.7 )), V(VDD), V(VSS) ) }
RINT  4 Y 1
Cy    Y VSS  150p
.ENDS HPA_OR2
*$
*
.subckt HPA_OR3 A B C VDD VSS Y
XLI2 NET12 C VDD VSS Y     HPA_OR2
XLI0 A     B VDD VSS NET12 HPA_OR2
.ends HPA_OR3
*$
*
.SUBCKT HPA_AND2 A B VDD VSS Y 
*Ra    A VSS  200k
*Rb    B VSS  200k
*Ca    A VSS  1f
*Cb    B VSS  1f
*(V(VDD)+V(VSS))/2
E1    4 0 VALUE = { IF( ((V(A)> 0.7 ) & (V(B)> 0.7 )), V(VDD), V(VSS) ) }
RINT  4 Y 1
Cy    Y VSS  150p
.ENDS
*$
*
.SUBCKT HPA_INV A VDD VSS Y 
*Ra    A VSS  200k
*Ca    A VSS  2f
E1    3 0 VALUE = { IF( V(A)> 0.7, V(VSS), V(VDD) ) }
R1    3 Y 1
Cy    Y VSS  150p
.ENDS
*$
.SUBCKT HPA_BUF A VDD VSS Y 
Ra    A VSS  10e6
Ca    A VSS  10f
E1    3 0 VALUE = { IF( V(A)> 0.7, V(VDD), V(VSS) ) }
R1    3 Y 1
Cy    Y VSS  150p
.ENDS
*$
*$
.SUBCKT PSW_PS D G S PARAMS: RONval=1e-3 VTHval=0.7 VCHARval=0.01 CGval=0.01pF CDval=0.01pF 
RDDUM D 0 1e11
RSDUM S 0 1e11
RGDUM G 0 1e11
CG G D {CGval}
CD D S {CDval}
Etest test 0 VALUE={IF(V(S) > V(D), V(S,G), V(D,G))}
GOUT S D VALUE={V(S,D)/(RONval*(1+EXP(-LIMIT(((V(test)-VTHval)/VCHARval),-80,80))))}
.ENDS PSW_PS
*
*$
.SUBCKT SWITCH_PS A SWD SWC PARAMS: vth=500e-3 ron=1 roff=1e6 tdelay=1e-9 trise=1e-9
+ tfall=1e-9 initval=0
VS VSUP 0 DC 1
EBUF1  Ypp 0 VALUE={IF(V(A) > ({vth}), {1-initval}, {initval})}
ROUTpp Ypp 0 1e11
XNSW1 OUTp Ypp 0    NSW_PS PARAMS: RONval={(tdelay+1e-15)/(1e-12*0.693)} VTHval=0.5
XPSW1 OUTp Ypp VSUP PSW_PS PARAMS: RONval={(tdelay+1e-15)/(1e-12*0.693)} VTHval=0.5
XNSW2 OUTr Yp  0    NSW_PS PARAMS: RONval={(trise+1e-15)/(1e-12*2.3)} VTHval=0.5
XPSW2 OUTr Yp  VSUP PSW_PS PARAMS: RONval={(tfall+1e-15)/(1e-12*2.3)} VTHval=0.5
XNSW3 SWD  VG1 SWC  NSW_PS PARAMS: RONval={ron} VTHval=0.5
CDEL1 OUTp 0 1pF
ETHRESH Yp 0 VALUE={IF(V(OUTp) > 0.5, 1, 0)}
ROUTp Yp 0 1e11
CDEL2 OUTr 0 1pF
EOUT VG1 SWC VALUE={V(OUTr)}
.ENDS SWITCH_PS
*
*$
**** NMOS SWITCH *****************************************************************************************
.SUBCKT NSW_PS D G S PARAMS: RONval=10k VTHval=0.7 VCHARval=0.01 CGval=0.01pF CDval=0.01pF 
RDDUM D 0 1e11
RSDUM S 0 1e11
RGDUM G 0 1e11
CG G D {CGval}
CD D S {CDval}
EEXP F1 0 VALUE={LIMIT(((V(G,S)-VTHval)/VCHARval),-80,80)} 
*RF1 F1 0 1e8
*GOUT S D VALUE={V(D,S)/(RONval*(1+EXP(-V(F1))))}
*GOUT S D VALUE={V(D,S)/(RONval*(2-V(F1)+(-V(F1)**2)/2+(-V(F1)**3)/6+(-V(F1)**4)/24))}
GOUT D S VALUE={V(D,S)/(RONval*(1+EXP(-LIMIT(((V(G,S)-VTHval)/VCHARval),-80,80))))}
.ENDS NSW_PS
*$


