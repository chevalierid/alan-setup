***********************************************************************
**  v1.7.5 PSpice Models Library - TI NexFET Power N/P Channel FET's
*  (C) Copyright 2010 Texas Instruments Incorporated. All rights reserved.
***********************************************************************
***********************************************************************
* Notice:
* The data and models provided herein are intended to be used by 
* customers as an aid in designing electrical circuits using 
* semiconductor devices manufactured &/or sold by Texas Instruments.
* The models and associated information contained herein are not to
* be construed as a guarantee of electrical performance and/or device 
* specification.  Therefore, Texas Instruments does not assume any 
* liability arising out of the use of said data or models, nor from
* the devices or products manufactured therefrom.  Texas Instruments
* does not convey any license under its patent rights nor the rights
* of others.  Texas Instruments reserves the right to change models 
* without prior notice.  
*
* TI is a registered trademark of Texas Instruments Inc.
*************************************************************************
********************************************************************************
*
* Released by: Analog e-Lab Design Center, Texas Instruments Inc.
* Part: CSD16321Q5
* Date: 12/08/2010
* Model Type: TRANSIENT
* Simulator: PSPICE
* Datasheet: SLPS220B - August 2009 - Revised May 2010
*
*****************************************************************************
*$
**********************************************************************
**********************************************************************
*                                                                    *
* CSD16321Q5   -  PSpice Model for use with OrCAD                    *
*                                                                    *
**********************************************************************
**********************************************************************
* SUBCKT Definition:  1=D  2=G  3=S
.SUBCKT CSD16321Q5  1 2 3
M1  4 5 6 6 NMOS W=3.09751 L=0.4u
DBD   6  4	DBD
RBD   6  4	1E12
CGD0  5  4	1E-12
M2  6 5 6 4 PMOSd W=3.09751e-1 L=0.50u
* Note:  gate oxide capacitance included in NMOS below
CGS   5 12	375E-12
RGS  12  6  145
LGG   2 11	1.31e-9
RGG  11  5	1.4
RS0   7  6	RTEMP 0.25E-3
LSS   3  7	0.20E-9
RDD   9  4	0.05e-3
LDD   1  9	0.05E-9
******************************************************************* 
.MODEL  NMOS   NMOS (    LEVEL  = 7             LINT   = 0.055e-6
+ TOX    = 1.80e-8       NCH    = 1.485e17      NSUB   = 1.0e16
+ A0     = 1.60          A1     = 0.2           A2     = 0.4
+ RD     = 0.87e-3       RS     = 0.27e-3
+ UA     = 3.30e-9       UB     = 2.7e-18       VBM    = -5
+ UA1    = 5.5e-9        UB1    = -2.5e-18      UTE    = -1.55
+ LDD    = 1.0E-6        XJ     = 3E-7          DELTA  = 0.01
+ KT1    = -0.675        KT2    = 0.022         KT1L   = 1.0e-12
+ DVT0   = 2.3           DVT1   = 0.55	      DVT2   = -0.033
+ ETA    = 0.3           ETA0   = 0.045         ETAB   = -0.07  
+ PDIBLC1= 0.08          PDIBLC2= 0.006         NLX    = 1.80e-7
+ PSCBE1 = 8.0e8         PSCBE2 = 1e-7          PCLM   = 1.4
+ VOFF   = -0.190        NFACTOR= 1.8           IS     = 0
+ DROUT  = 0.6           DSUB   = 1.0           DELTA  = 0.015
+ CJ     = 0             CJSW   = 0
+ CBD    = 0             CBS    = 0             CF     = 0
+ CGSO   = 243e-12       CGDO   = 5e-13         CGBO   = 0  )
******************************************************************* 
.MODEL  PMOSd   PMOS (     LEVEL  = 7
+ TOX    = 1.8e-8        NSUB   = 5e16          IS     = 0
+ CJ     = 0             CJSW   = 0             CF = 0
+ CGSO   = 0             CGDO   = 0             CGBO   = 0 )
******************************************************************* 
.MODEL DBD D (CJO=5250e-12 VJ=0.90 M=0.45 FC=0.5 TT=5e-09
+ IS=2.60e-11 N=1.015 RS=2.10e-3 TRS1=-2.0e-3 XTI=2.95 BV=27)
*******************************************************************
.MODEL RTEMP RES (TC1=1.05E-2   TC2=2.65E-5)
******************************************************************* 
.ENDS CSD16321Q5
